' Gambas class file

Private $encid As String
Private $Present As Boolean
Private $sValue As String[]
Private $patNo As String

Private $xclose As Boolean
' Private $NumForm As FmQuantiEntry

Public Sub Run(encid As String, sPresent As Boolean) As String[]

  Dim xval As String

  $encid = encid
  $Present = sPresent
  modGeneralMain.GetOpenModalForm(Me)
  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  $patNo = modPatient.GetPatientNoByEnc($encid)
  cmbpresent.List = ["Cephalic", "Breech", "Face", "Cord", "Shoulder"]
  cmbstatus.List = ["Latent phase", "Active phase", "Second stage", "None"]
  xval = modSettings.ShowSettingFromFIle("ClinicalRule/EDD_LMP_DayDifference")
  If xval Then
    txtexday.Value = CInt(xval)
  Else
    txtexday.Value = 277
  Endif
  pnlPresent.Visible = $Present
  pnlLabor.Visible = $Present
  pnlPast.Visible = $Present

  ObsFormat()    ''Disabled due to bug in 3.18.2
  ShowLastValues()

  If Me.ShowModal() Then Return $sValue

End

' Public Sub cmbpresent_KeyRelease()
'
'   modFillContainer.AutoFillComboBox(cmbpresent)
'   modFillContainer.RestrictComboToContent(cmbpresent)
'
' End
'
' Public Sub cmbstatus_KeyRelease()
'
'   modFillContainer.AutoFillComboBox(cmbstatus)
'   modFillContainer.RestrictComboToContent(cmbstatus)
'
' End

Public Sub dtlmp_Click()

  txtneplmp.Text = modDate.ConvertToLocaldate(dtlmp.Value)

End

Public Sub txtneplmp_Change()

  dtlmp.Value = modDate.ConvertToEnglishdate(txtneplmp.Text)

End

Public Sub dtedd_Click()

  txtnepedd.Text = modDate.ConvertToLocaldate(dtedd.Value)

End

Public Sub txtnepedd_Change()

  dtedd.Value = modDate.ConvertToEnglishdate(txtnepedd.Text)

End

Public Sub btnlmp_Click()

  GetEDDValue()

End

Private Sub GetEDDValue()

  Dim xweek As Float

  dtedd.Value = DateAdd(dtlmp.Value, gb.Day, CInt(txtexday.Value))
  txtnepedd.Text = modDate.ConvertToLocaldate(dtedd.Value)
  xweek = DateDiff(dtlmp.Value, Now(), gb.Day) / 7
  txtgesweek.Value = Fix(xweek)
  txtgesdays.Value = Round(Frac(xweek) * 7)

End

Private Function GetDiagnosisText() As String

  Dim xpresent As String
  Dim xstatus As String
  Dim xday As String
  Dim xpast As String
  Dim xx As String

  If txtgesdays.Value Then
    If rbformB.Value = True Then
      xday = CStr(txtgesweek.Value) & " + " & CStr(txtgesdays.Value) & " WOG"
    Else
      xday = CStr(txtgesweek.Value) & " weeks & " & CStr(txtgesdays.Value) & " days"
    Endif
  Else
    If rbformB.Value = True Then
      xday = CStr(txtgesweek.Value) & " WOG"
    Else
      xday = CStr(txtgesweek.Value) & " weeks"
    Endif
  Endif

  If cmbpresent.Text Then
    If cmbpresent.Text = "Cephalic" Then
    Else
      xpresent = " with " & cmbpresent.Text & " Presentation"
    Endif
  Endif

  If cmbstatus.Text Then
    If cmbstatus.Text = "None" Then
      xstatus = " Not in labor"
    Else
      xstatus = " in " & cmbstatus.Text & " of labor"
    Endif
  Endif

  If txtpast.Text Then
    xpast = " with " & txtpast.Text
  Else
    xpast = ""
  Endif

  If rbformB.Value = True Then
    xx = "G" & CStr(txtgravida.Value) & " P" & CStr(txtparity.Value) & " + " & CStr(txtabortion.Value) & " L" & CStr(txtliving.Value) & " AT " & xday & xpresent & Space(1) & xstatus & xpast
  Else
    xx = "G" & CStr(txtgravida.Value) & " P" & CStr(txtparity.Value) & " + " & CStr(txtabortion.Value) & " L" & CStr(txtliving.Value) & " with " & xday & " of pregnancy" & xpresent & Space(1) & xstatus & xpast                                                                ''
  Endif
  Return xx

End

Public Sub xdiagno_Change()

  txtdiagno.Text = GetDiagnosisText()

End

Public Sub cmbpresent_Click()

  txtdiagno.Text = GetDiagnosisText()

End

Public Sub cmbstatus_Click()

  txtdiagno.Text = GetDiagnosisText()

End

Public Sub txtpast_Change()

  txtdiagno.Text = GetDiagnosisText()

End

Private Sub ShowLastValues()

  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldgravida,fldparity,fldbortion,fldlive,fldlast,fldexpect,fldgestation,fldpresent,fldstatus,fldpast,flddelref from tblobstetrics where fldpatientval=&1 and fldlast<&2 and fldexpect>&3 ORDER BY fldtime", $patNo, Now(), DateAdd(Now(), gb.Month, -3))
  If res.Available Then
    res.MoveLast
    txtgravida.Value = res["fldgravida"]
    txtparity.Value = res["fldparity"]
    txtabortion.Value = res["fldbortion"]
    txtliving.Value = res["fldlive"]
    dtlmp.Value = res["fldlast"]
    dtedd.Value = res["fldexpect"]
    If res["fldgestation"] Then
      txtgesweek.Value = Floor(res["fldgestation"] / 7)
      txtgesdays.Value = Round(Frac(res["fldgestation"] / 7) * 7)
    Endif
    cmbpresent.Text = res["fldpresent"]
    cmbstatus.Text = res["fldstatus"]
    txtpast.Text = res["fldpast"]
    txtreference.Text = res["flddelref"]

  Endif

  txtneplmp.Text = modDate.ConvertToLocaldate(dtlmp.Value)
  txtnepedd.Text = modDate.ConvertToLocaldate(dtedd.Value)
  txtdiagno.Text = GetDiagnosisText()

End

Private Function ObstetricSUmmary() As String

  Dim xlist As String[]

  xlist = New String[]
  xlist.Add("Gravida: " & CStr(txtgravida.Value))
  If txtparity.Value Then
    xlist.Add("Parity: " & CStr(txtparity.Value))
  Endif
  If txtabortion.Value Then
    xlist.Add("Abortion: " & CStr(txtabortion.Value))
  Endif
  If txtliving.Value Then
    xlist.Add("Living: " & CStr(txtliving.Value))
  Endif
  xlist.Add("Last Menstrual Period: " & modReportVar.GetDateTimeReport(dtlmp.Value, gb.MediumDate))
  xlist.Add("Expected Delivery Date: " & modReportVar.GetDateTimeReport(dtedd.Value, gb.MediumDate))
  xlist.Add("Gestation: " & CStr(txtgesweek.Value) & " Weeks " & CStr(txtgesdays.Value) & " Days")
  If cmbpresent.Text Then
    xlist.Add("Presentation: " & cmbpresent.Text)
  Endif
  If cmbstatus.Text Then
    xlist.Add("Labor Status: " & cmbstatus.Text)
  Endif

  Return xlist.Join(gb.NewLine)

End

Public Sub btnsave_Click()

  Dim xday As Integer
  Dim xx As String[]
  Dim res As Result
  Dim xrefNo As String
  Dim xIntVal As String
  Dim xmsg As String

  xmsg = ""
  If dtlmp.Value > Now() Then
    xmsg = xmsg & "Incorrect Last Mens Period Date" & gb.NewLine
  Endif
  If dtedd.Value > DateAdd(Now(), gb.Year, 1) Then
    xmsg = xmsg & "Incorrect Expected Date of Delivery" & gb.NewLine
  Endif
  If xmsg Then
    Message.Warning(xmsg, "OK")
  Else

    $xclose = False
    If txtgesweek.Value Then
      xx = New String[]
      xx.Add(Trim(txtdiagno.Text))
      xx.Add("Obstetrics")
      xx.Add(ObstetricSUmmary())
      $sValue = xx

      xday = 7 * txtgesweek.Value + txtgesdays.Value
      If txtreference.Text Then
        res = modDatabase.$myConn.Edit("tblobstetrics", "flddelref=&1", txtreference.Text)
        res["fldgravida"] = txtgravida.Value
        res["fldparity"] = txtparity.Value
        res["fldbortion"] = txtabortion.Value
        res["fldlive"] = txtliving.Value
        res["fldlast"] = dtlmp.Value
        res["fldexpect"] = dtedd.Value
        res["fldgestation"] = xday
        res["fldpresent"] = cmbpresent.Text
        res["fldstatus"] = cmbstatus.Text
        res["fldpast"] = txtpast.Text
        res["flduserid"] = modBasic.$lbluser
        res["fldcomp"] = modBasic.$compID
        res["fldsave"] = True
        res["xyz"] = False
        res["flduptime"] = Now()
        res.Update
        $xclose = True
        Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information updated", modBasic.$BalloonDelay))

      Else
        modDatabase.$myConn.Begin
        xrefNo = modPatient.GetDeliveryHMISVal($patNo)
        If xrefNo Then
          res = modDatabase.$myConn.Create("tblobstetrics")
          res["fldencounterval"] = $encid
          res["fldpatientval"] = $patNo
          res["fldgravida"] = txtgravida.Value
          res["fldparity"] = txtparity.Value
          res["fldbortion"] = txtabortion.Value
          res["fldlive"] = txtliving.Value
          res["fldlast"] = dtlmp.Value
          res["fldexpect"] = dtedd.Value
          res["fldgestation"] = xday
          res["fldpresent"] = cmbpresent.Text
          res["fldstatus"] = cmbstatus.Text
          res["fldpast"] = txtpast.Text
          res["flduserid"] = modBasic.$lbluser
          res["fldtime"] = Now()
          res["fldcomp"] = modBasic.$compID
          res["fldsave"] = True
          res["flddelref"] = xrefNo
          res["xyz"] = False
          If MMain.$WebEntry = True Then
            xIntVal = modString.GetDateString(Now())
            res["fldid"] = CLong(xIntVal)
            res["fldrepoid"] = modMisc.GetWebIndexStr(xIntVal)
            res["fldrepodate"] = Now()
            res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
            res["fldhospcode"] = modBasic.$HospCode
          Endif
          res.Update
          modClinSub.AddGeneralParametersQuali($encid, "Pregnancy Status", GetTrimesterLabel(xday))
          modDatabase.$myConn.Commit
          txtreference.Text = xrefNo
          $xclose = True
          Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information saved", modBasic.$BalloonDelay))
        Else
          modDatabase.$myConn.Rollback
        Endif

      Endif
    Endif

  Endif

Catch
  modDatabase.$myConn.Rollback
  modHelpVariable.CreateErrorReport()

End

' Public Sub btnsave_KeyRelease()
'
'   If Key.Code = Key.Return Or If Key.Code = Key.Enter Or If Key.Code = Key.AltKey Then
'     btnsave_Click()
'   Endif
'
' End

Public Sub rbformA_Click()

  modSettings.SaveSettingsToFile("Obstetrics/Format", "A")
  txtdiagno.Text = GetDiagnosisText()

End

Public Sub rbformB_Click()

  modSettings.SaveSettingsToFile("Obstetrics/Format", "B")
  txtdiagno.Text = GetDiagnosisText()

End

Private Sub ObsFormat()

  Dim xval As String

  xval = modSettings.ShowSettingFromFIle("Obstetrics/Format")
  If xval = "B" Then
    rbformB.Value = True
  Else
    rbformA.Value = True
  Endif

End

Private Function GetTrimesterLabel(sDay As Integer) As String

  Dim xx As String

  If sDay < 84 Then
    xx = "1st trimester"
  Else If sDay >= 84 And If sDay < 189 Then
    xx = "2nd trimester"
  Else If sDay >= 189 Then
    xx = "3rd trimester"
  Endif
  Return xx

End

Public Sub btnclose_Click()

  If $xclose = True Then
    Me.Close(True)
  Else
    Me.Close()
  Endif

End

' Private Sub ClearNumPad()
'
'   txtgravida.Tag = ""
'   txtabortion.Tag = ""
'   txtparity.Tag = ""
'   txtliving.Tag = ""
'   txtgesweek.Tag = ""
'   txtgesdays.Tag = ""
'   Try $NumForm.Close
'
' End
'
' Private Sub ShowKeyPad()
'
'   If modBasic.$TabletModeEnable = "Yes" Then
'     pnlquanti.Visible = True
'   Else
'     pnlquanti.Visible = False
'   Endif
'
' End
'
' Public Sub xdiagno_GotFocus()
'
'   ClearNumPad()
'   ShowKeyPad()
'   If modBasic.$TabletModeEnable = "Yes" Then
'     $NumForm = New FmQuantiEntry(Last, pnlquanti)
'   Endif
'
' End
