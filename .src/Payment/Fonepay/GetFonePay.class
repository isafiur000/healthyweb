' Gambas class file

Private $onlineURL As String
Private $sKey As String

Private $BookStatus As String

Private $PID As String
Private $MD As String = "P"
Private $PRN As String
Private $AMT As String
Private $CRN As String = "NPR"
Private $DT As String
Private $R1 As String
Private $R2 As String
Private $RU As String

Public Sub _new(sBookId As String, sRefNo As String, sPayAmt As Float, sURL As String, retURL As String, sMerchCode As String, sKey As String, sUser As String, sHash As String)

  Dim sAmt As Float

  $R1 = sBookId
  $PRN = sRefNo
  sAmt = sPayAmt
  $onlineURL = sURL
  $RU = retURL & "?refer=" & sBookId & "&value=" & sHash
  $PID = sMerchCode
  $sKey = sKey

  $R2 = sUser
  $DT = Format(Date(), "mm/dd/yyyy")

  If $onlineURL And If sAmt > 0 Then
    If $R1 And If $PRN Then
      $AMT = CStr(sAmt)
      InsertBookingData()
    Endif
  Endif

End

Private Sub InsertBookingData()

  Dim xhash As String
  Dim xurl As String

  xhash = Trim(GetDVHash())
  $PID = URL.Encode($PID)
  $MD = URL.Encode($MD)
  $PRN = URL.Encode($PRN)
  $AMT = URL.Encode($AMT)
  $CRN = URL.Encode($CRN)
  $DT = URL.Encode($DT)
  $R1 = URL.Encode($R1)
  $R2 = URL.Encode($R2)
  xurl = $onlineURL & "?" & "PID=" & $PID & "&MD=" & $MD & "&AMT=" & $AMT & "&CRN=" & $CRN & "&DT=" & $DT & "&R1=" & $R1 & "&R2=" & $R2 & "&DV=" & xhash & "&RU=" & $RU & "&PRN=" & $PRN     ''

  If xhash Then
    modBasic.DebugAPIString("URL: " & xurl)
    WebForm.Exec("window.open('" & xurl & "', '_self'); ")
    $BookStatus = "Pending"
  Endif

End

Private Function GetDVHash() As String

  Dim asx As String[] = [$PID, $MD, $PRN, $AMT, $CRN, $DT, $R1, $R2, $RU]
  Dim arcmd As String[]
  Dim sval As String

  arcmd = ["php", modHelpVariable.$scriptDirectory &/ "fonepay.php", $sKey, asx.Join(",")]
  modBasic.DebugAPIString("Execute " & arcmd.Join(Space(1)))
  Shell arcmd.Join(Space(1)) To sval
  modBasic.DebugAPIString("Response: " & sval)
  Return sval

End
