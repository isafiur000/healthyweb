' Gambas class file

Private $strType As String

Public Sub _new()

  dtpurdate.Value = Now()
  dtexpiry.Value = Now()

  cmbvendor.List = modSupplier.FillVendorNameCombo("Active")
  cmbtype.List = ["Cash Payment", "Credit Payment"]

End

Private Sub FillListViewFromArray()

  Dim sArry As String[]

  If $strType = "Medicines" Then
    sArry = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldbrandid from tblmedbrand where fldactive=&1", "Active"))                                ''
  Else If $strType = "Surgicals" Then
    sArry = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldbrandid from tblsurgbrand where fldactive=&1", "Active"))                                ''
  Else If $strType = "Extra Items" Then
    sArry = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldbrandid from tblextrabrand where fldactive=&1", "Active"))                                ''
  Endif
  ListView1.List = sArry

End

Public Sub rbmed_Click()

  $strType = "Medicines"
  FillListViewFromArray()

End

Public Sub rbsurg_Click()

  $strType = "Surgicals"
  FillListViewFromArray()

End

Public Sub rbextra_Click()

  $strType = "Extra Items"
  FillListViewFromArray()

End

Public Sub btnpurchase_Click()

  Dim i As Integer

  If txtbillno.Text And If cmbtype.Text And If cmbvendor.Text And If txtbatch.Text And If txttotqty.Value Then

    For i = 0 To ListView1.Count - 1
      If ListView1.IsSelected(i) Then
        DoOpeningSTock(ListView1.List[i], $strType)
        Wait
      Endif
    Next

    FillListViewFromArray()
  Endif

End

Private Sub DoOpeningSTock(sItem As String, sType As String)

  Dim xstock As Boolean
  Dim StockNo As Long

  xstock = modStock.NewStock(sItem, Trim(txtbatch.Text), modBasic.$compID)
  modDatabase.$myConn.Begin
  StockNo = modStock.GetItemStockNo(sItem, Trim(txtbatch.Text), modBasic.$compID)
  If StockNo Then
    If xstock = True Then
      AddNewEntry(StockNo, sItem, sType)
    Endif
    AddPurchaseEntry(StockNo, sItem, sType)
  Endif
  modDatabase.$myConn.Commit

End

Private Sub AddNewEntry(StockNo As Long, sItem As String, ItemCategory As String)

  Dim res As Result

  If sItem And If StockNo > 0 Then
    res = modDatabase.$myConn.Create("tblentry")
    res["fldstockno"] = StockNo
    res["fldstockid"] = sItem
    res["fldbatch"] = Trim(txtbatch.Text)
    res["fldexpiry"] = modDate.StartSqlDate(dtexpiry.Value)
    res["fldqty"] = 0
    res["fldstatus"] = modStock.ItemSaleStatus(sItem, modBasic.$compID)
    res["fldcost"] = 0
    res["fldsellpr"] = Round(txttotalcost.Value / txttotqty.Value, -2)
    res["fldmrp"] = 0
    res["fldsav"] = False
    res["fldcategory"] = ItemCategory
    res["fldcomp"] = modBasic.$compID
    res["fldcode"] = 0
    res["xyz"] = True
    If MMain.$WebEntry = True Then
      res["fldrepodate"] = Now()
      res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
      res["fldhospcode"] = modBasic.$HospCode
    Endif
    res.Update()
  Endif

End

Private Sub AddPurchaseEntry(StockNo As Long, sItem As String, ItemCategory As String)

  Dim res As Result
  Dim rs As Result
  Dim xIntVal As String

  rs = modDatabase.$myConn.Exec("select fldstockid,fldbatch from tblentry where fldstockno=&1", StockNo)
  If rs.Available = True Then
    If rs["fldstockid"] = sItem And If rs["fldbatch"] = txtbatch.Text Then
      res = modDatabase.$myConn.Create("tblpurchase")
      res["fldpurtype"] = cmbtype.Text
      res["fldbillno"] = Trim(txtbillno.Text)
      res["fldsuppname"] = cmbvendor.Text
      res["fldcategory"] = ItemCategory
      res["fldstockno"] = StockNo
      res["fldstockid"] = sItem
      res["fldmrp"] = 0
      res["fldtotalcost"] = txttotalcost.Value
      res["fldtaxamt"] = 0
      res["fldmargin"] = 0
      res["fldtotalqty"] = txttotqty.Value
      res["fldreturnqty"] = 0
      res["fldcasdisc"] = 0
      res["fldcasbonus"] = 0
      res["fldqtybonus"] = 0
      res["fldcarcost"] = 0
      res["fldnetcost"] = Round(txttotalcost.Value / txttotqty.Value, -2)
      res["flsuppcost"] = Round(txttotalcost.Value / txttotqty.Value, -2)
      res["fldsellprice"] = Round(txttotalcost.Value / txttotqty.Value, -2)
      res["fldpurdate"] = dtpurdate.Value
      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
      res["fldcomp"] = modBasic.$compID
      res["fldsav"] = True
      res["fldreference"] = ""
      res["fldchk"] = False
      res["xyz"] = True
      If MMain.$WebEntry = True Then
        xIntVal = modString.GetDateString(Now())
        res["fldid"] = CLong(xIntVal)
        res["fldrepoid"] = modMisc.GetWebIndexStr(xIntVal)
        res["fldrepodate"] = Now()
        res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
        res["fldhospcode"] = modBasic.$HospCode
      Endif
      res.Update()
    Endif
  Endif

End

Public Sub btnclose_Click()

  Me.Close

End

Public Sub chkselall_Click()

  If chkselall.Value = True Then
    ListView1.SelectAll()
  Else If chkselall.Value = False Then
    ListView1.UnselectAll()
  Endif

End
