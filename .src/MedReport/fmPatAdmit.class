' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $SSQLFields As String[]

Private $newColumn As String[]
Private $ColCount As Integer
Private $RepoStr As String

Public Sub _new()

  Dim xhospfld As String

  If MMain.$WebReport = "Multiple" Then
    If modBasic.$HospCode Then
      ' cmblocation.Text = "Hospital"
      cmblocation.Text = modDataRepo.$RepositoryMode
      cmbvalue.Text = modBasic.$HospCode
      pnlocat.Enabled = False
    Else
      cmblocation.List = ["Hospital", "Municipality", "Category", "District", "Province"]
    Endif
    mnuhospsumm.Visible = True
    If MMain.$RepoReportMode = True Then
      mnusearname.Visible = False
    Endif

  Else
    pnlocat.Visible = False
  Endif

  cmbcolumn.List = ["Surname", "District", "Municipality", "Ethnic Group", "Package", "Visit Type", "Visit Mode", "Regd Department"]
  If MMain.$IsPolice = True Then
    mnuservice.Visible = True
  Else
    mnuservice.Visible = False
  Endif

  cmbstatus.List = ["Recorded", "Registered", "Follow Up", "Refer(OPD)", "Death(OPD)", "Admitted", "Exits(All)", "Discharged", "LAMA", "Refer", "Death", "Absconder", "%"]
  cmbstatus.Text = "%"
  cmbbillmode.List = modNonMedical.FillCashModeCombo()
  cmbbillmode.Add("%")
  cmbbillmode.Text = "%"
  cmbpackage.List = modNonMedical.FillDiscountCombo()
  cmbpackage.Text = "%"
  cmbgender.List = ["Male", "Female", "Other", "%"]
  cmbgender.Text = "%"
  cmbdepart.List = modGeneral.GetDepartmentAllList("%")
  cmbdepart.Add("%")
  cmbdepart.Text = "%"

  dtfir.Value = Now()
  dtlast.Value = Now()

  xhospfld = modDataRepo.HospitalField()
  $SSQLFields = ["fldencounterval", "fldencounterval", "fldencounterval", "fldencounterval", "fldpatientval", "fldregdate", "fldadmission", "fldadmitlocat", "flddisctype"]
  If xhospfld Then
    $SSQLFields.Add(xhospfld)
  Endif
  chkgrid.Value = True

End

Public Sub WebForm_Open()

  modGeneralMain.RecordFormUse(Me)

End

Private Sub SetRepoString()

  If MMain.$WebReport = "Multiple" Then
  Else
    cmbcolumn.Text = ""
    txtoption.Text = ""
  Endif

End

Public Sub dtnepfir_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtfir.Value))
  If xx Then
    dtfir.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub dtneplast_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtlast.Value))
  If xx Then
    dtlast.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Private Sub SetRepoStr()

  If MMain.$WebReport = "Multiple" Then
    $RepoStr = modDataRepo.GetWhereStringRepo(cmblocation.Text, cmbvalue.Text)
  Else
    $RepoStr = ""
  Endif

End

Public Sub mnusearch_Click()

  Dim encid As String
  Dim sql As String
  Dim xFldList As String[]

  encid = GetEncid()
  SetRepoStr()
  If encid Then
    xFldList = GetSQLColumns()
    sql = "select " & xFldList.Join(",") & " from tblencounter where fldencounterval=&1" & $RepoStr
    $rData = modDatabase.$syConn.Exec(sql, encid)
    FillPatientGrid()
  Endif

End

Public Sub mnusearname_Click()

  Dim xname As String[]
  Dim sql As String
  Dim xFldList As String[]

  xname = InputDoubleText(("Search Patient Name"), ["First Name", "SurName"], ["%", "%"], modBasic.$SurNameList)
  SetRepoStr()
  If xname Then
    xFldList = GetSQLColumns()
    sql = "select " & xFldList.Join(",") & " from tblencounter where fldpatientval in(select fldpatientval from tblpatientinfo where lower(fldptnamefir) like &1 and lower(fldptnamelast) like &2)" & $RepoStr
    $rData = modDatabase.$syConn.Exec(sql, LCase(xname[0]), LCase(xname[1]))
    FillPatientGrid()
  Endif

End

Private Function ExecuteQuery(xFldList As String[], sCon As Connection, Optional sGroup As String) As Result

  Dim sql As String
  Dim xstr As String
  Dim xfld As String
  Dim xdtstr As String
  Dim agestr As String
  Dim coltime As String
  Dim status As String
  Dim xquery As String

  Dim res As Result
  Dim xgroup As String

  Select cmbstatus.Text
    Case "Admitted"
      coltime = "flddoa"
      status = "%"
    Case "Exits(All)"
      coltime = "flddod"
      status = ["Discharged", "LAMA", "Refer", "Death", "Absconder"].Join("','")
    Case "Discharged", "LAMA", "Refer", "Death", "Absconder"
      coltime = "flddod"
      status = cmbstatus.Text
    Case Else
      coltime = "fldregdate"
      status = "%"
  End Select

  If sGroup And If chkdstpat.Value = True Then
    xgroup = " GROUP BY fldpatientval," & sGroup
  Else
    If sGroup And If chkdstpat.Value = False Then
      xgroup = " GROUP BY " & sGroup
    Else If Not sGroup And If chkdstpat.Value = True Then
      xgroup = " GROUP BY fldpatientval"
    Else
      xgroup = ""
    Endif
  Endif

  SetRepoStr()
  If mnuquery.Checked = True Then
    If txtquery.Text Then
      If modMisc.CheckRestrictedKeyStrict(txtquery.Text) = 0 Then
        xquery = " and " & Trim(txtquery.Text)
      Endif
    Else
      xquery = ""
    Endif
    sql = "select " & xFldList.Join(",") & " from tblencounter where " & coltime & ">=&1 and " & coltime & "<=&2" & xquery & $RepoStr & xgroup
    res = sCon.Exec(sql, modDate.StartSqlDate(dtfir.value), modDate.EndSqlDate(dtlast.value))

  Else
    If txtval2.Value Then
      xdtstr = modMedReports.GetAgeStringByTable("tblencounter", sCon)
      agestr = " and " & xdtstr & ">=&7 and " & xdtstr & "<&8"
    Else
      agestr = ""
    Endif
    If Not cmbcolumn.Text Then
      If cmbstatus.Text = "Exits(All)" Then
        sql = "select " & xFldList.Join(",") & " from tblencounter where " & coltime & ">=&1 and " & coltime & "<=&2 and fldadmission in ('" & status & "') and fldbillingmode like &4 and flddisctype like &5 and fldpatientval in(select fldpatientval from tblpatientinfo where fldptsex like &6" & agestr & ") and fldadmitlocat like &9" & $RepoStr & xgroup
        res = sCon.Exec(sql, modDate.StartSqlDate(dtfir.value), modDate.EndSqlDate(dtlast.value), status, cmbbillmode.Text, cmbpackage.Text, cmbgender.Text, 365 * txtval1.Value, 365 * txtval2.Value, cmbdepart.Text)
      Else
        sql = "select " & xFldList.Join(",") & " from tblencounter where " & coltime & ">=&1 and " & coltime & "<=&2 and fldadmission like &3 and fldbillingmode like &4 and flddisctype like &5 and fldpatientval in(select fldpatientval from tblpatientinfo where fldptsex like &6" & agestr & ") and fldadmitlocat like &9" & $RepoStr & xgroup
        res = sCon.Exec(sql, modDate.StartSqlDate(dtfir.value), modDate.EndSqlDate(dtlast.value), status, cmbbillmode.Text, cmbpackage.Text, cmbgender.Text, 365 * txtval1.Value, 365 * txtval2.Value, cmbdepart.Text)
      Endif
    Else
      xfld = modMedReports.GetColumnEncounter(cmbcolumn.Text)
      Select cmbcolumn.Text
        Case "Surname", "District", "Municipality"
          xstr = " and fldpatientval in(select fldpatientval from tblpatientinfo where " & xfld & " like &{10})"
        Case "Ethnic Group"
          xstr = " and fldpatientval in(select fldpatientval from tblpatientinfo where fldethniccode like &{10})"
        Case "Package", "Visit Type", "Visit Mode", "Regd Department"
          xstr = " and " & xfld & " like &{10}"
        Case Else
          xstr = ""
      End Select
      If cmbstatus.Text = "Exits(All)" Then
        sql = "select " & xFldList.Join(",") & " from tblencounter where " & coltime & ">=&1 and " & coltime & "<=&2 and fldadmission in ('" & status & "') and fldbillingmode like &4 and flddisctype like &5 and fldpatientval in(select fldpatientval from tblpatientinfo where fldptsex like &6" & agestr & ") and fldadmitlocat like &9" & xstr & $RepoStr & xgroup
        res = sCon.Exec(sql, modDate.StartSqlDate(dtfir.value), modDate.EndSqlDate(dtlast.value), status, cmbbillmode.Text, cmbpackage.Text, cmbgender.Text, 365 * txtval1.Value, 365 * txtval2.Value, cmbdepart.Text, txtoption.Text)
      Else
        sql = "select " & xFldList.Join(",") & " from tblencounter where " & coltime & ">=&1 and " & coltime & "<=&2 and fldadmission like &3 and fldbillingmode like &4 and flddisctype like &5 and fldpatientval in(select fldpatientval from tblpatientinfo where fldptsex like &6" & agestr & ") and fldadmitlocat like &9" & xstr & $RepoStr & xgroup
        res = sCon.Exec(sql, modDate.StartSqlDate(dtfir.value), modDate.EndSqlDate(dtlast.value), status, cmbbillmode.Text, cmbpackage.Text, cmbgender.Text, 365 * txtval1.Value, 365 * txtval2.Value, cmbdepart.Text, txtoption.Text)
      Endif
    Endif
  Endif
  Return res

Catch
  Message.Warning(("Database Error Code: ") & db.Error & "<br>" & Error.Text, ("OK"))

End

Private Function GetGridViewValue(Column As Integer, xVariable As Variant) As Variant

  Dim xxx As Variant
  Dim i As Integer

  If Column = 1 Then
    xxx = modPatient.GetPatientNameByEnc(xVariable, modDatabase.$syConn)
  Else If Column = 2 Then
    xxx = modPatient.GetPatientAgeString(xVariable, modPatient.GetRecordDate(xVariable))
  Else If Column = 3 Then
    xxx = modPatient.GetPatientSex(xVariable, modDatabase.$syConn)
  Else If Column = 5 Then
    xxx = modReportVar.GetDateTimeReport(xVariable, gb.GeneralDate)
  Else If Column = 9 Then
    xxx = modDataRepo.GetHospitalTextLabel(xVariable)
  Else
    xxx = xVariable
  Endif

  If $newColumn.Count Then
    For i = 0 To $newColumn.Count - 1
      If Column = $ColCount + i Then
        xxx = modCustPatient.NewColValue(Me.Tag, $newColumn[i], xVariable)
      Endif
    Next
  Endif

  Return xxx

End

Private Function GetSQLColumns() As String[]

  Dim xFldList As String[]
  Dim i As Integer

  modCustPatient.FillNewCOlumnCollection(Me.Tag)
  $newColumn = modCustPatient.CustomNewColumnsTitle(Me.Tag)
  xFldList = $SSQLFields.Copy()
  If $newColumn.Count Then
    For i = 0 To $newColumn.Count - 1
      xFldList.Add("fldencounterval")
    Next
  Endif
  Return xFldList

End

Public Sub btnrefresh_Click()

  Dim xFldList As String[]

  TabStrip1.Index = 0
  xFldList = GetSQLColumns()
  $rData = ExecuteQuery(xFldList, modDatabase.$syConn)
  If $rData.Available Then
    lblrecordcount.Text = "Count: " & $rData.Count
  Else
    lblrecordcount.Text = "Count: 0"
  Endif

  If chkgrid.Value = True Then
    FillPatientGrid()
  Endif

  If chkcount.Value = True Then
    FillChartData()
  Endif

  If chkexpense.Value = True Then
    MakeFreqDistribution()
  Endif

  ' modExternal.$ExecValueColl.Clear()

End

Private Sub FillPatientGrid()

  Dim i As Integer

  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  $ColCount = $SSQLFields.Count
  With GridView1
    .Columns[0].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(175 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"

    .Columns[0].Text = "EncID"
    .Columns[1].Text = "Name"
    .Columns[2].Text = "Age"
    .Columns[3].Text = "Gender"
    .Columns[4].Text = "PatNo"
    .Columns[5].Text = "DORec"
    .Columns[6].Text = "Status"
    .Columns[7].Text = "Depart"
    .Columns[8].Text = "Package"

    If $newColumn.Count Then
      For i = 0 To $newColumn.Count - 1
        .Columns[$ColCount + i].Text = $newColumn[i]
        .Columns[$ColCount + i].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
      Next
    Endif
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column > $SSQLFields.Count - 1 Then
    Data.Html = modString.TextToHTML(GetGridViewValue(Column, $rData[$aMyFields[Column]]))
  Else
    Data.Text = GetGridViewValue(Column, $rData[$aMyFields[Column]])
  Endif

End

''------------------------------------------- Grid view reports -------------------------------------------------
' Public Sub GridView1_Menu()
'
'   mnuhide.Popup
'
' End
'
Public Sub mnuColumns_Click()

  If Message.Question(modGridView.GetColumnsListString(GridView1), ("OK"), ("Setting")) = 2 Then
    fmChartSetting.ShowModal
  Endif

End

Public Sub mnuenchart_Click()

  Dim xx As String
  Dim xField As String
  Dim Column As Integer
  Dim res As Result
  Dim xval As Variant
  Dim hFile As CUnivariate
  Dim xFldList As String[]
  Dim sOrder As String[]

  Dim hUniv As FmAnalyzeSett

  hUniv = New FmAnalyzeSett("Univariate", modGridView.GetGridViewColumns(GridView1))
  hUniv.ShowModal

  xFldList = GetSQLColumns()
  hFile = New CUnivariate
  Column = modCustPatient.GetUnivariateColumn(0)
  xField = xFldList[Column]
  res = ExecuteQuery([xField], modDatabase.$syConn)

  If res.Available Then
    For Each res
      xval = GetGridViewValue(Column, res[xField])
      If xval Then
        hFile.Add(xval)
      Endif
    Next
  Endif

  If modSettings.ShowSettingFromFIle("UnivariateAnalysis/DataType") = "Ordinal" Then
    sOrder = ListOrder("Select Order", hFile.DistinctValues())
  Endif
  xx = modPSPP.GetEncChartGridNew(hFile.GetSPSSFile(), sOrder)
  If xx Then
    modControlSub.OpenBrowser(xx)
  Endif

End

Public Sub mnucrosstab_Click()

  Dim xx As String
  Dim Row As Integer
  Dim Column As Integer
  Dim xFieRow As String
  Dim xFieColm As String
  Dim res As Result
  Dim xval As Variant
  Dim yval As Variant

  Dim hFile As CBivariate
  Dim xFldList As String[]
  Dim sOrder As String[]
  Dim hBivar As FmAnalyzeSett

  hBivar = New FmAnalyzeSett("Bivariate", modGridView.GetGridViewColumns(GridView1))
  hBivar.ShowModal

  xFldList = GetSQLColumns()
  hFile = New CBivariate
  Row = modCustPatient.GetCrossVarRow(0)
  xFieRow = xFldList[Row]
  Column = modCustPatient.GetCrossVarColumn(0)
  xFieColm = xFldList[Column]
  res = ExecuteQuery([xFieRow, xFieColm], modDatabase.$syConn)

  If res.Available Then
    For Each res
      xval = GetGridViewValue(Row, res[xFieRow])
      yval = GetGridViewValue(Column, res[xFieColm])
      If xval And If yval Then
        hFile.Add([xval, yval])
      Endif
    Next
  Endif

  If modCustPatient.$CrossOutcomeType = "Ordinal" Then
    sOrder = ListOrder("Select Order", hFile.DistinctValues())
  Endif
  xx = modPSPP.GetCrossTabStatNew(hFile.GetSPSSFile(), hFile.GetGroups(), sOrder)
  If xx Then
    modControlSub.OpenBrowser(xx)
  Endif

End

Public Sub mnuregression_Click()

  Dim xx As String
  Dim res As Result
  Dim xFieldList As String[]

  Dim DepCol As Integer
  Dim Indep1Col As Integer
  Dim Indep2Col As Integer
  Dim Indep3Col As Integer
  Dim Indep4Col As Integer
  Dim Indep5Col As Integer

  Dim xDepField As String
  Dim xIndep1Field As String
  Dim xIndep2Field As String
  Dim xIndep3Field As String
  Dim xIndep4Field As String
  Dim xIndep5Field As String

  Dim Depvalue As Variant
  Dim Indep1Val As Variant
  Dim Indep2Val As Variant
  Dim Indep3Val As Variant
  Dim Indep4Val As Variant
  Dim Indep5Val As Variant

  Dim DepData As Variant[]
  Dim Indep1Data As Variant[]
  Dim Indep2Data As Variant[]
  Dim Indep3Data As Variant[]
  Dim Indep4Data As Variant[]
  Dim Indep5Data As Variant[]
  Dim xFldList As String[]

  xFldList = GetSQLColumns()
  Message.Info(("Generating Report ...."), ("OK"))
  xFieldList = New String[]
  DepData = New Variant[]
  Indep1Data = New Variant[]
  Indep2Data = New Variant[]
  Indep3Data = New Variant[]
  Indep4Data = New Variant[]
  Indep5Data = New Variant[]

  DepCol = modCustPatient.GetRegressionDepVal(0)
  xDepField = xFldList[DepCol]
  xFieldList.Add(xDepField)

  If modCustPatient.$RegVar1UseGridVal = "Yes" Or If modCustPatient.$RegVar1Variable Or If modCustPatient.$RegVar1SQLText Then
    Indep1Col = modCustPatient.GetRegressionVar1Val(0)
    xIndep1Field = xFldList[Indep1Col]
    xFieldList.Add(xIndep1Field)
  Endif
  If modCustPatient.$RegVar2UseGridVal = "Yes" Or If modCustPatient.$RegVar2Variable Or If modCustPatient.$RegVar2SQLText Then
    Indep2Col = modCustPatient.GetRegressionVar2Val(0)
    xIndep2Field = xFldList[Indep2Col]
    xFieldList.Add(xIndep2Field)
  Endif
  If modCustPatient.$RegVar3UseGridVal = "Yes" Or If modCustPatient.$RegVar3Variable Or If modCustPatient.$RegVar3SQLText Then
    Indep3Col = modCustPatient.GetRegressionVar3Val(0)
    xIndep3Field = xFldList[Indep3Col]
    xFieldList.Add(xIndep3Field)
  Endif
  If modCustPatient.$RegVar4UseGridVal = "Yes" Or If modCustPatient.$RegVar4Variable Or If modCustPatient.$RegVar4SQLText Then
    Indep4Col = modCustPatient.GetRegressionVar4Val(0)
    xIndep4Field = xFldList[Indep4Col]
    xFieldList.Add(xIndep4Field)
  Endif
  If modCustPatient.$RegVar5UseGridVal = "Yes" Or If modCustPatient.$RegVar5Variable Or If modCustPatient.$RegVar5SQLText Then
    Indep5Col = modCustPatient.GetRegressionVar5Val(0)
    xIndep5Field = xFldList[Indep5Col]
    xFieldList.Add(xIndep5Field)
  Endif

  res = ExecuteQuery(xFieldList, modDatabase.$syConn)

  If res.Available Then
    For Each res
      Depvalue = GetGridViewValue(DepCol, res[xDepField])
      If xIndep1Field Then
        Indep1Val = GetGridViewValue(Indep1Col, res[xIndep1Field])
      Else
        Indep1Val = "__"
      Endif
      If xIndep2Field Then
        Indep2Val = GetGridViewValue(Indep2Col, res[xIndep2Field])
      Else
        Indep2Val = "__"
      Endif
      If xIndep3Field Then
        Indep3Val = GetGridViewValue(Indep3Col, res[xIndep3Field])
      Else
        Indep3Val = "__"
      Endif
      If xIndep4Field Then
        Indep4Val = GetGridViewValue(Indep4Col, res[xIndep4Field])
      Else
        Indep4Val = "__"
      Endif
      If xIndep5Field Then
        Indep5Val = GetGridViewValue(Indep5Col, res[xIndep5Field])
      Else
        Indep5Val = "__"
      Endif

      If Depvalue And If Indep1Val And If Indep2Val And If Indep3Val And If Indep4Val And If Indep5Val Then
        modCustPatient.AddFormatRegressionDepToList(DepData, Depvalue)
        modCustPatient.AddFormatRegressionVar1ToList(Indep1Data, Indep1Val)
        modCustPatient.AddFormatRegressionVar2ToList(Indep2Data, Indep2Val)
        modCustPatient.AddFormatRegressionVar3ToList(Indep3Data, Indep3Val)
        modCustPatient.AddFormatRegressionVar4ToList(Indep4Data, Indep4Val)
        modCustPatient.AddFormatRegressionVar5ToList(Indep5Data, Indep5Val)
      Endif
    Next
  Endif

  xx = modPSPP.RegressionChartGridNew(DepData, Indep1Data, Indep2Data, Indep3Data, Indep4Data, Indep5Data)
  If xx Then
    modControlSub.OpenBrowser(xx)
  Endif

End

Public Sub mnutimeseries_Click()

  Dim xx As String
  Dim Row As Integer
  Dim Column As Integer
  Dim xFieRow As String
  Dim xFieColm As String
  Dim res As Result
  Dim xval As Variant
  Dim yval As Variant
  Dim xData As Variant[]
  Dim yData As Variant[]
  Dim xFldList As String[]

  xFldList = GetSQLColumns()
  Message.Info(("Generating Report ...."), ("OK"))
  xData = New Variant[]
  yData = New Variant[]
  Row = modCustPatient.GetTimeVarRow(0)
  xFieRow = xFldList[Row]
  Column = modCustPatient.GetTimeVarColumn(0)
  xFieColm = xFldList[Column]
  res = ExecuteQuery([xFieRow, xFieColm], modDatabase.$syConn)

  If res.Available Then
    For Each res
      xval = GetGridViewValue(Row, res[xFieRow])
      yval = GetGridViewValue(Column, res[xFieColm])
      If xval And If yval Then
        modCustPatient.AddFormatTimeVarRowToList(xData, xval)
        modCustPatient.AddFormatTimeVarColumnToList(yData, yval)
      Endif
    Next
  Endif

  xx = modPSPP.GetTimeChartGrid(xData, yData)
  If xx Then
    modControlSub.OpenBrowser(xx)
  Endif

End

Public Sub mnucrotab_Click()

  Dim Row As Integer
  Dim Column As Integer
  Dim xFieRow As String
  Dim xFieColm As String
  Dim res As Result
  Dim xval As Variant
  Dim yval As Variant

  Dim hForm As FmGridCrossTab
  Dim xForm As CCrossTab
  Dim xFldList As String[]
  Dim hGdChr As FmAnalyzeSett

  hGdChr = New FmAnalyzeSett("GridCrossTab", modGridView.GetGridViewColumns(GridView1))
  hGdChr.ShowModal

  xFldList = GetSQLColumns()
  If modCustPatient.$TabRowColumn And If modCustPatient.$TabFieldColumn Then
    xForm = New CCrossTab

    Row = CInt(modCustPatient.$TabRowColumn) - 1
    xFieRow = xFldList[Row]
    Column = CInt(modCustPatient.$TabFieldColumn) - 1
    xFieColm = xFldList[Column]
    res = ExecuteQuery([xFieRow, xFieColm], modDatabase.$syConn)

    If res.Available Then
      For Each res
        xval = GetGridViewValue(Row, res[xFieRow])
        yval = GetGridViewValue(Column, res[xFieColm])
        If xval And If yval Then
          xForm.Add([xval, yval])
        Endif
      Next
    Endif

    hForm = New FmGridCrossTab(GridView1.Columns[Row].Text, xForm.GetSPSSFile())
    hForm.ShowModal
    'modExternal.$ExecValueColl.Clear()
  Endif

End

Public Sub mnugridsummary_Click()

  Dim xx As String
  Dim ChapCol As String
  Dim GropCol As String
  Dim xChapList As String[]
  Dim xGropList As String[]

  Dim res As Result
  Dim valCol As String
  Dim xFieldList As String[]

  Dim xFinList As Variant[]
  Dim xrowVar As Variant[]
  Dim xchapval As String
  Dim xgroup As String
  Dim xPath As String
  Dim xFldList As String[]
  Dim hGdSum As FmAnalyzeSett

  hGdSum = New FmAnalyzeSett("GridSummary", modGridView.GetGridViewColumns(GridView1))
  hGdSum.ShowModal
  If Not modCustPatient.$SumGroupColumn Or If Not modCustPatient.$SumValueColumn Then Return

  xFldList = GetSQLColumns()
  xFieldList = New String[]
  If modCustPatient.$SumChapterColumn Then
    ChapCol = xFldList[modCustPatient.$SumChapterColumn - 1]
    xFieldList.Add(ChapCol)
  Endif
  GropCol = xFldList[modCustPatient.$SumGroupColumn - 1]
  valCol = xFldList[modCustPatient.$SumValueColumn - 1]
  xFieldList.Add(GropCol)
  xFieldList.Add(valCol)

  xChapList = New String[]
  xGropList = New String[]
  xFinList = New Variant[]
  res = ExecuteQuery(xFieldList, modDatabase.$syConn)
  If res.Available Then
    For Each res
      xrowVar = New Variant[]
      If modCustPatient.$SumChapterColumn Then
        xchapval = GetGridViewValue(modCustPatient.$SumChapterColumn - 1, res[ChapCol])
        xrowVar.Add(xchapval)
        xChapList.Add(xchapval)
      Endif
      xgroup = GetGridViewValue(modCustPatient.$SumGroupColumn - 1, res[GropCol])
      xrowVar.Add(xgroup)
      xGropList.Add(xgroup)

      xrowVar.Add(GetGridViewValue(modCustPatient.$SumValueColumn - 1, res[valCol]))
      xFinList.Add(xrowVar)
    Next
  Endif

  xx = "Date: " & modReportVar.GetDateTimeReport(dtfir.Value, gb.MediumDate) & " To " & modReportVar.GetDateTimeReport(dtlast.Value, gb.MediumDate)
  If modCustPatient.$SumGroupColumn And If modCustPatient.$SumValueColumn Then
    If modCustPatient.$SumChapterColumn Then
      If modCustPatient.$SummaryType = "Summation(Table)" Then
        xPath = modCHTMLReport.SummaryGridChapterTableReport("Visit Summary", xx, modString.GetDistinctStringArray(xChapList), modString.GetDistinctStringArray(xGropList), xFinList)
      Else If modCustPatient.$SummaryType = "Count(Table)" Then
        xPath = modCHTMLReport.SummaryGridChapCountTableReport("Visit Summary", xx, modString.GetDistinctStringArray(xChapList), modString.GetDistinctStringArray(xGropList), xFinList)
      Else
        xPath = modCHTMLReport.SummaryGridChapterReport("Visit Summary", xx, modString.GetDistinctStringArray(xChapList), modString.GetDistinctStringArray(xGropList), xFinList)
      Endif
    Else
      xPath = modCHTMLReport.SummaryGridReport("Visit Summary", xx, modString.GetDistinctStringArray(xGropList), xFinList)
    Endif
    modControlSub.DisplayReportExport(xPath)
  Endif

End

Public Sub mnucompreport_Click()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If modPatientSub.AllowEncIDHistory($rData["fldencounterval"], modDatabase.$syConn) = True Then
      modPatReports.GetSelectedPatientValues($rData["fldencounterval"], MMain.$defUnit)
    Endif
  Endif

End

''------------------------------------------------------ Picture 1 ---------------------------------
Private Sub FillChartData()

  Dim xLst As String[]
  Dim sFile As String
  Dim xchart As String

  xLst = New String[]
  For Each $rData
    If Not modBasic.$HospCode And If MMain.$WebReport = "Multiple" Then
      xLst.Add($rData["fldhospcode"] & gb.Tab & modPatient.GetPatientSex($rData["fldencounterval"], modDatabase.$syConn))
    Else
      xLst.Add(Format(DateDiff(dtfir.Value, $rData["fldregdate"], modChart.GetDateIntegerFromSetting()), "0#.#") & gb.Tab & modPatient.GetPatientSex($rData["fldencounterval"], modDatabase.$syConn))
    Endif
  Next

  sFile = Temp()
  File.Save(sFile, xLst.Join(gb.NewLine))
  If modBasic.$ChartExecStat = "PSPP" Then
    xchart = modChart.ProportionChart(sFile, "Visits" & " : Patient Count")
  Else
    xchart = modChart.ProportionChart(sFile, "Visits" & " : Patient Count")
  Endif
  modImage.StretchtPictureToBox(PictureBox1, xchart)
  btnstats.Tag = sFile

End

Public Sub btnexport_Click()

  ' Me.Exec("printJS('" & PictureBox1.Tag & "', 'image')")
  Me.Exec("window.open('" & PictureBox1.Image & "'); ")

End

Public Sub btnstats_Click()

  Dim hForm As FmOpenLink

  If modBasic.$ExpoRepoUser = True Then
    Message.Info(("Generating Data ...."), ("OK"))
    hForm = New FmOpenLink(modPrint.GetFileWebURL(btnstats.Tag))
    hForm.ShowModal
  Endif

End

'''------------------------------------------- Picture 2 -------------------------------------
Private Sub MakeFreqDistribution()

  Dim xLst As String[]
  Dim sFile As String
  Dim xval As Float
  Dim ximg As String

  xLst = New String[]
  For Each $rData
    xval = modNonMedical.TotalExpenseAmountbyPatient(modDatabase.$syConn, $rData["fldencounterval"])
    xLst.Add(Format(xval / modChart.GetAMTValueChart(), gb.Fixed))
  Next

  sFile = Temp()
  File.Save(sFile, xLst.Join(gb.NewLine))
  ' PictureBox2.Tag = "Expense"
  If modBasic.$ChartExecStat = "PSPP" Then
    ximg = modPSPP.CreatePSPPStatFIle("frequency", sFile)
  Else
    ximg = modChart.FrequencyChart(sFile, "Expense : Frequency Distribution")
  Endif
  modImage.StretchtPictureToBox(PictureBox2, ximg)
  btnstats2.Tag = sFile

End

Public Sub btnexport2_Click()

  ' Me.Exec("printJS('" & PictureBox2.Tag & "', 'image')")
  Me.Exec("window.open('" & PictureBox2.Image & "'); ")

End

Public Sub btnstats2_Click()

  Dim hForm As Fmgnustat

  If modBasic.$ChartExecStat = "PSPP" Then
    modControlSub.OpenBrowser(modPSPP.CreatePSPPStatFIle("statistics", btnstats2.Tag))
  Else
    hForm = New Fmgnustat(btnstats2.Tag)
    hForm.ShowModal
  Endif

End

Public Sub btncalc2_Click()

  modControlSub.OpenBrowser(modPSPP.CreatePSPPStatFIle("t-test", btnstats2.Tag))

End

''----------------------------------------- Summary ----------------------------------
Public Sub mnuagegroup_Click()

  Dim xPath As String
  Dim xdate As Date[]

  xdate = DoubleDates("Select Dates", "Registration", [dtfir.Value, dtlast.Value])
  If xdate Then
    xPath = modCHTMLDate.SummaryStatAgeGroup(modDatabase.$syConn, xdate[0], xdate[1], cmblocation.Text, cmbvalue.Text)
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Private Sub ShowSumReport(sType As String)

  Dim xPath As String
  Dim xdate As Date[]

  xdate = DoubleDates("Select Dates", "Registration", [dtfir.Value, dtlast.Value])
  If xdate Then
    xPath = modCHTMLDate.SummaryStatEncounter(modDatabase.$syConn, sType, xdate[0], xdate[1], cmblocation.Text, cmbvalue.Text)
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub mnudistrict_Click()

  ShowSumReport("District")

End

Public Sub mnusurname_Click()

  ShowSumReport("Surname")

End

Public Sub mnugender_Click()

  ShowSumReport("Gender")

End

Public Sub mnuethnic_Click()

  ShowSumReport("Ethnic Group")

End

Public Sub mnudepart_Click()

  ShowSumReport("Regd Department")

End

Public Sub mnubillgrp_Click()

  ShowSumReport("Rate Plan")

End

Public Sub mnudsccateg_Click()

  ShowSumReport("Package")

End

Public Sub mnustatus_Click()

  ShowSumReport("Last Status")

End

Public Sub mnulocat_Click()

  ShowSumReport("Regd location")

End

Public Sub mnuvisittype_Click()

  ShowSumReport("Visit Type")

End

Public Sub mnuvisitmode_Click()

  ShowSumReport("Visit Mode")

End

Public Sub mnuhospsumm_Click()

  ShowSumReport("Hospital")

End

Public Sub mnuservrank_Click()

  ShowSumReport("Service Rank")

End

Public Sub mnuservunit_Click()

  ShowSumReport("Service Unit")

End

Public Sub mnuservcateg_Click()

  ShowSumReport("Service Category")

End

Public Sub mnudemog_Click()

  Dim demog As String[]
  Dim sType As String
  Dim xPath As String
  Dim xdate As Date[]

  demog = modBasic.$DemogList
  If demog Then
    xdate = DoubleDates("Select Dates", "Registration", [dtfir.Value, dtlast.Value])
    If xdate Then
      sType = InputCombo("Select Clinical Demographic Parameter", "Summary Report", demog, "", True)
      If sType Then
        xPath = modCHTMLDate.SummaryDemographics(modDatabase.$syConn, sType, xdate[0], xdate[1], "tblexamgeneral", cmblocation.Text, cmbvalue.Text)
        modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
      Endif
    Endif
  Endif

End

Public Sub mnupatdemograp_Click()

  Dim demog As String[]
  Dim sType As String
  Dim xPath As String
  Dim xdate As Date[]

  demog = modBasic.$DemogAccList
  If demog Then
    xdate = DoubleDates("Select Dates", "Registration", [dtfir.Value, dtlast.Value])
    If xdate Then
      sType = InputCombo("Select Patient Demographic Parameter", "Summary Report", demog, "", True)
      If sType Then
        xPath = modCHTMLDate.SummaryDemographics(modDatabase.$syConn, sType, xdate[0], xdate[1], "tblpataccgeneral", cmblocation.Text, cmbvalue.Text)
        modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
      Endif
    Endif
  Endif

End

''-------------------------------------- Other Menu ----------------------
Public Sub mnuhealth_Click()

  Dim hForm As FmHealthReport

  SetRepoString()
  hForm = New FmHealthReport(cmblocation.Text, cmbvalue.Text)
  hForm.ShowModal

End

Public Sub mnuextcolumn_Click()

  Dim hForm As FmAddNewColumn

  hForm = New FmAddNewColumn(Me.Tag)
  hForm.ShowModal

End

Public Sub btnfullrep_Click()

  Dim xHeader As String[]
  Dim xhide As Integer[]
  Dim Column As Integer
  Dim xCollRow As Collection
  Dim xColum As Integer

  Dim $hGridExportTable As CExportResult
  Dim xparam1 As String
  Dim xparam2 As String
  Dim encColumn As Integer
  Dim xFldList As String[]

  xHeader = New String[]
  xhide = New Integer[]
  For Column = 0 To GridView1.Columns.Count - 1
    xHeader.Add(GridView1.Columns[Column].Text)
    If GridView1.Columns[Column].Hidden = True Then
      xhide.Add(Column)
    Endif
  Next
  xparam1 = "Visit Report"
  xparam2 = "Entry Date: " & modReportVar.GetDateTimeReport(dtfir.Value, gb.MediumDate) & " To " & modReportVar.GetDateTimeReport(dtlast.Value, gb.MediumDate)
  encColumn = 0
  $hGridExportTable = New CExportResult(Me.Tag, xHeader, xhide, xparam1, xparam2, encColumn)

  xFldList = GetSQLColumns()

  For Each $rData
    xCollRow = New Collection
    For xColum = 0 To xFldList.Count - 1
      xCollRow.Add(GetGridViewValue(xColum, $rData[xFldList[xColum]]), CStr(xColum))
    Next
    $hGridExportTable.Add($rData.Index, xCollRow)
  Next

  modControlSub.DisplayReportExport($hGridExportTable.HTMLPath())

End

Public Sub btnexpoexcel_Click()

  Me.Exec("ExportToExcel('" & GridView1.Name & "', 'xlsx')")

End

Private Sub ShowSummDatewiseReport(sField As String)

  Dim xPath As String
  Dim xdate As Date[]

  SetRepoString()
  xdate = DoubleDates("Select Dates", "Registration", [dtfir.Value, dtlast.Value], 1)
  If xdate Then
    xPath = modCHTMLDate.SummaryStatDateWiseReport(modDatabase.$syConn, "tblencounter", "fldencounterval", sField, "fldregdate", xdate[0], xdate[1], cmblocation.Text, cmbvalue.Text)
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub mnudatedepart_Click()

  ShowSummDatewiseReport("fldadmitlocat")

End

Public Sub mnudatelocat_Click()

  ShowSummDatewiseReport("fldcomp")

End

Public Sub mnudaterateplan_Click()

  ShowSummDatewiseReport("fldbillingmode")

End

Public Sub mnudatedisc_Click()

  ShowSummDatewiseReport("flddisctype")

End

Public Sub mnudatevisit_Click()

  ShowSummDatewiseReport("fldvisit")

End

Public Sub chkcount_Click()

  TabStrip1.Index = 1

End

Public Sub chkexpense_Click()

  TabStrip1.Index = 2

End

Public Sub mnudeptsummary_Click()

  Dim xPath As String
  Dim xdate As Date[]

  xdate = DoubleDates("Select Dates", "Registration", [dtfir.Value, dtlast.Value])
  If xdate Then
    xPath = modCHTMLPatSummary.ShowRegisteredNewOldAgeReport(xdate[0], xdate[1])
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub mnuipoutcome_Click()

  Dim xPath As String
  Dim xdate As Date[]

  xdate = DoubleDates("Select Dates", "IP Events", [dtfir.Value, dtlast.Value])
  If xdate Then
    xPath = modCHTMLDate.GenerStatVisitIPReport(modDatabase.$syConn, xdate[0], xdate[1], modGeneral.GetDepartmentWithBedList(), cmblocation.Text, cmbvalue.Text)
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub mnucountuni_Click()

  Dim Column As Integer
  Dim xFieColm As String
  Dim res As Result
  Dim yval As Variant
  Dim xList As String[]

  Dim xVar As Variant[]
  Dim xFldList As String[]
  Dim xPath As String

  Column = ListIndex("Column Index", modGridView.GetGridViewColumns(GridView1))
  If Column + 1 > 0 Then
    xFldList = GetSQLColumns()
    xFieColm = xFldList[Column]
    res = ExecuteQuery([xFieColm], modDatabase.$syConn)
    xList = New String[]
    If res.Available Then
      For Each res
        yval = GetGridViewValue(Column, res[xFieColm])
        If yval Then
          xList.Add(yval)
        Endif
      Next
    Endif
    If xList.Count Then
      xVar = modString.GetUnivariateSummary(xList)
      xPath = modCHTMLReport.CreateHTMLReportFromArray(["Variable", "Count"], xVar, GridView1.Columns[Column].Text, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))
    Endif

    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub mnuexpocolumn_Click()

  Dim hForm As FmCustColumnSet

  hForm = New FmCustColumnSet(Me.Tag)
  hForm.ShowModal

End
