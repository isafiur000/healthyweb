' Gambas class file

Private $colvar1 As String
Private $colvar2 As String
Private $tbl As String
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(colCategory As String, colValue As String, sTable As String, grpList As String[], itmList As String[])

  $colvar1 = colCategory
  $colvar2 = colValue
  $tbl = sTable
  txtcategory.List = grpList
  txtvariable.List = itmList

  If $tbl = "tblsuturetype" Then
    Me.Title = "Suture Codes"
    Label1.Text = ("Type")
    Label2.Text = ("Code")
  Else If $tbl = "tblextrapayers" Then
    Me.Title = "Customer Profile"
    Label1.Text = ("Name")
    Label2.Text = ("Address")
  Else If $tbl = "tbldeptgroup" Then
    Me.Title = "Department Groups"
    Label1.Text = ("Group Name")
    Label2.Text = ("Department")
  Else If $tbl = "tblreferlist" Then
    Me.Title = "Referral Hospitals"
    Label1.Text = ("Hospital")
    Label2.Text = ("Hosp Code")
  Else If $tbl = "tblgroupproc" Then
    Me.Title = "Extra Procedures"
    Label1.Text = ("Group Name")
    Label2.Text = ("Procedures")
  Else If $tbl = "tblgroupexam" Then
    Me.Title = "Examination"
    Label1.Text = ("Group Name")
    Label2.Text = ("Examination")
  Else If $tbl = "tblacledger" Then
    Me.Title = "Ledger Accounts"
    Label1.Text = ("Ledger Type")
    Label2.Text = ("Ledger A/C")
  Else If $tbl = "tblmasterdept" Then
    Me.Title = "Master Departments"
    Label1.Text = ("Master")
    Label2.Text = ("Department")
  Else If $tbl = "tblfoodmix" Then
    Me.Title = "Food Mixtures"
    Label1.Text = ("Mixture Code")
    Label2.Text = ("Mixture Name")
  Else If $tbl = "tblschemecategory" Then
    Me.Title = "Scheme Category"
    Label1.Text = ("Category")
    Label2.Text = ("Package")
  Else If $tbl = "tblipdservice" Then
    Me.Title = "Service Categories"
    Label1.Text = ("Service")
    Label2.Text = ("Diagnosis")
  Endif

  FilVarList()

End

Public Sub btnOK_Click()

  Dim res As Result

  If txtvariable.Text Then
    res = modDatabase.$myConn.Create($tbl)
    res[$colvar1] = Trim(txtcategory.Text)
    res[$colvar2] = Trim(txtvariable.Text)
    res.Update()
    SHowSelectGrid()
    txtvariable.Text = ""
    Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Variable added", modBasic.$BalloonDelay))
    txtcategory.SetFocus
  Endif

End

Public Sub btnrefresh_Click()

  If txtcategory.Text Then
    SHowSelectGrid()
  Else
    FilVarList()
  Endif

End

Private Sub SHowSelectGrid()

  Dim sql As String

  If txtcategory.Text Then
    sql = Subst("select &1,&2 from &3 where &1", $colvar1, $colvar2, $tbl) & "=&1"
    $rData = modDatabase.$myConn.Exec(sql, txtcategory.Text)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
    With GridView1
      .Columns[0].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
      .Columns[1].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
      .Columns[0].Text = Label1.Text
      .Columns[1].Text = Label2.Text
    End With

  Endif

End

Private Sub FilVarList()

  Dim sql As String

  sql = Subst("select &1,&2 from &3 ORDER BY &1", $colvar1, $colvar2, $tbl)
  $rData = modDatabase.$myConn.Exec(sql)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[0].Text = Label1.Text
    .Columns[1].Text = Label2.Text
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]

End

Public Sub btnCancel_Click()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If $rData[$colvar2] Then
      modDatabase.$myConn.Delete($tbl, $colvar1 & "=&1 and " & $colvar2 & "=&2", $rData[$colvar1], $rData[$colvar2])       ''
    Else
      modDatabase.$myConn.Delete($tbl, $colvar1 & "=&1 and " & $colvar2 & " IS &2", $rData[$colvar1], Null)       ''
    Endif
    FilVarList()
    Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Variable deleted", modBasic.$BalloonDelay))
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, Me.Title, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub GridView1_Select()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    txtcategory.Text = $rData[$colvar1]
    txtvariable.Text = $rData[$colvar2]
  Endif

End

Public Sub btnupload_Click()

  Dim xdelim As String

  If txtdelim.Text Then
    xdelim = txtdelim.Text
  Else
    xdelim = ";"
  Endif
  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, $tbl, txtcsv.Text, xdelim)
    FilVarList()
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
