' Gambas class file

Private $patNo As String
Private $tblpatient As String

Public Sub _new(patNo As String, tblPatient As String)

  $patNo = patNo
  If tblPatient Then
    $tblpatient = tblPatient
  Else
    $tblpatient = "tblpatientinfo"
  Endif

  If $tblpatient = "tblremoteusers" Then
    ShowDataRemotePatient()
  Else
    ShowDataFromPatMain()
  Endif

End

''========================= tblpatientinfo =========================
Private Sub ShowDataFromPatMain()

  txtpatno.Text = $patNo
  txtname.Text = modPatient.PatientFullNameByPatID($patNo)
  txtagesex.Text = modPatient.GetPatientAgePatNo($patNo, Now()) & "/" & Left(modPatient.PatientGenderByPatNo($patNo), 1)
  txtaddress.Text = modPatient.GetPatientAddressByPatno($patNo)
  txtcontact.Text = modPatient.GetPatientContactByPatNo($patNo)
  DisplayBlobData()
  DisplayQRcodeData()

End

Private Sub DisplayBlobData()

  Dim tempFile As String

  tempFile = modImage.DisplayPersonImageData("Patient", $patNo)
  If tempFile Then
    modImage.StretchtPictureToBox(PictureBox1, tempFile)
  Endif

End

Private Sub DisplayQRcodeData()

  Dim tempFile As String

  tempFile = modDevAll.MakeQRCode($patNo)
  If tempFile Then
    modImage.StretchtPictureToBox(PictureBox2, tempFile)
  Endif

End

''============================== tblremoteusers==========================
Private Sub ShowDataRemotePatient()

  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldpatientval,fldptnamefir,fldptnamelast,fldptsex,fldptbirday,fldptaddvill,fldptaddward,fldptadddist,fldptcontact from tblremoteusers where fldusrcode=&1", $patNo)
  If res.Available Then
    txtname.Text = res["fldptnamefir"] & Space(1) & res["fldptnamelast"]
    txtagesex.Text = modDate.GetAgeString(res["fldptbirday"], Now()) & "/" & res["fldptsex"]
    If res["fldptaddward"] Then
      txtaddress.Text = res["fldptaddvill"] & "-" & res["fldptaddward"] & ", " & res["fldptadddist"]
    Else
      txtaddress.Text = res["fldptaddvill"] & ", " & res["fldptadddist"]
    Endif
    txtcontact.Text = res["fldptcontact"]

  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
