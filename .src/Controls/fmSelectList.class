' Gambas class file

Private $typ As String
Private $tbl As String
Private $itemName As String

Public Sub _new(sTitle As String, xType As String, xItemName As String)

  $typ = xType
  $itemName = xItemName
  Me.Title = sTitle

  Select $typ
    Case "No Discount List"
      cmbcategory.List = ["Diagnostic Tests", "General Services", "Procedures", "Equipment", "Radio Diagnostics", "Other Items", "Medicines", "Surgicals", "Extra Items"]        '''
    Case "Complaints List"
      cmbcategory.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldcategory) as col from tblsymptoms"))
    Case "Essential Examinations", "Physician Examinations", "Nursing Examinations", "Triage Examinations", "Discharge Examinations"
      cmbcategory.List = ["Qualitative", "Quantitative"]
  End Select
  ShowFilledList()

End

Public Sub cmbcategory_Click()

  chkall.Value = False
  LoadSelectedListsView()

End

Private Sub LoadSelectedListsView()

  Select $typ
    Case "No Discount List"
      NonStockBillingItemListView(cmbcategory.Text)
    Case "Complaints List"
      CompComplaintsListView(cmbcategory.Text)
    Case "Essential Examinations", "Physician Examinations", "Nursing Examinations", "Triage Examinations", "Discharge Examinations"
      CompVitalExamListView(cmbcategory.Text)
  End Select

End

Public Sub chkall_Click()

  If chkall.Value = True Then
    lstfirst.SelectAll()
  Else
    lstfirst.Unselect()
  Endif

End

Private Sub ShowFilledList()

  Dim sql As String
  Dim res As Result

  Select $typ
    Case "No Discount List"
      $tbl = "tblnodiscount"
      sql = "select flditemname as col from tblnodiscount ORDER BY flditemname"
      res = modDatabase.$myConn.Exec(sql)
    Case "Complaints List"
      $tbl = "tblcomplaints"
      sql = "select fldsymptom as col from tblcomplaints where fldcomp=&1 ORDER BY fldsymptom"
      res = modDatabase.$myConn.Exec(sql, $itemName)
    Case "Essential Examinations", "Physician Examinations", "Nursing Examinations", "Triage Examinations", "Discharge Examinations"
      $tbl = "tblcompexam"
      sql = "select fldexamid as col from tblcompexam where fldcomp=&1 and fldcategory=&2 ORDER BY fldexamid"
      res = modDatabase.$myConn.Exec(sql, $itemName, $typ)
  End Select
  modFillContainer.FillListBox(lstsecond, res, False)

End

Public Sub btnsave_Click()

  Dim res As Result
  Dim i As Integer

  For i = 0 To lstfirst.List.Count - 1
    If lstfirst.IsSelected(i) = True Then
      If lstsecond.List.Exist(lstfirst.List[i]) = False Then

        res = modDatabase.$myConn.Create($tbl)
        Select $typ
          Case "No Discount List"
            res["flditemname"] = lstfirst.List[i]
            res["flditemtype"] = cmbcategory.Text
            res["flduserid"] = modBasic.$lbluser
            res["fldtime"] = Now()
            res["fldcomp"] = modBasic.$compID
          Case "Complaints List"
            res = modDatabase.$myConn.Create("tblcomplaints")
            res["fldtype"] = cmbcategory.Text
            res["fldsymptom"] = lstfirst.List[i]
            res["fldflag"] = False
            res["fldcomp"] = $itemName
          Case "Essential Examinations", "Physician Examinations", "Nursing Examinations", "Triage Examinations", "Discharge Examinations"
            res["fldtype"] = cmbcategory.Text
            res["fldcategory"] = $typ
            res["fldexamid"] = lstfirst.List[i]
            res["fldsysconst"] = modFixClinic.GetExamSysName(lstfirst.List[i])
            res["fldflag"] = False
            res["fldcomp"] = $itemName
        End Select
        res.Update()

      Endif
    Endif
  Next
  ShowFilledList()
  LoadSelectedListsView()
  cmbcategory.SetFocus

End

' ' Public Sub cmbcategory_KeyRelease()
' '
' '   modFillContainer.AutoFillComboBox(cmbcategory)
' '   modFillContainer.RestrictComboToContent(cmbcategory)
' '
' ' End

Private Sub FillGridViewSelected(res As Result)

  lstfirst.Clear
  If res.Available Then
    For Each res
      If Len(res["col"]) Then
        If lstsecond.List.Exist(res["col"]) = False Then
          lstfirst.Add(res["col"])
        Endif
      Endif
    Next
  Endif

End

Private Sub NonStockBillingItemListView(sType As String)

  Dim res As Result

  If sType = "Medicines" Then
    res = modDatabase.$myConn.Exec("select fldbrandid as col from tblmedbrand")
  Else If sType = "Surgicals" Then
    res = modDatabase.$myConn.Exec("select fldbrandid as col from tblsurgbrand")
  Else If sType = "Extra Items" Then
    res = modDatabase.$myConn.Exec("select fldbrandid as col from tblextrabrand")
  Else
    res = modDatabase.$myConn.Exec("select flditemname as col from tblservicecost where flditemtype=&1", sType) '("Diagnostic Tests","Procedures","General Services")
  Endif
  FillGridViewSelected(res)

End

Private Sub CompComplaintsListView(sType As String)

  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldsymptom as col from tblsymptoms where fldcategory like &1", sType)
  FillGridViewSelected(res)

End

Private Sub CompVitalExamListView(sType As String)

  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldexamid as col from tblexam where fldtype like &1 and fldstatus=&2", sType, "Active")
  FillGridViewSelected(res)

End

Public Sub WebButton1_Click()

  Me.Close()

End
