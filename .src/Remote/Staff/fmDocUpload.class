' Gambas class file

Private $encid As String
Private $sType As String

Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(sType As String, encid As String)

  $sType = sType
  $encid = encid

  cmbcategory.List = ["Passport Page 2", "Passport Page 3", "Passport Page 30", "Passport Page 31", "Vaccination Card", "Deposit Voucher"]
  ShowDocuments()

End

Public Sub btnrefresh_Click()

  ShowDocuments()

End

Private Sub ShowDocuments()

  $rData = modDatabase.$syConn.Exec("select fldid,fldtime,fldcateg,fldtitle,flddetail,fldsave,fldlink,fldencounterval from tblpatreport where fldencounterval like &1 and fldcateg like &2 and (flvisible=&3 or flvisible IS NULL)", $encid, $sType, "Visible")
  ResizeGridView()

End

Private Sub ResizeGridView()

  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(250 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Hidden = True
    .Columns[6].Hidden = True
    .Columns[7].Hidden = True

    .Columns[1].Text = "DateTime"
    .Columns[2].Text = "Category"
    .Columns[3].Text = "Title"
    .Columns[4].Text = "Comment"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub btnupload_Click()

  Dim sLongID As Long

  If cmbcategory.Text And If txtfilepath.Text Then
    sLongID = modImage.SavePatientFileGeneral($encid, $sType, cmbcategory.Text, txtfilepath.Text)    ''
    If sLongID Then
      Me.Exec("Toastify({text: 'File saved', duration: 3000}).showToast()")
    Endif
    ResizeGridView()
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End

''================== Activity =============================
Public Sub mnuopenfile_Click()

  Dim hForm As FmPDFView
  Dim xLink As String

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])

    xLink = modImage.GetBlobFileData($rData["fldid"])
    If xLink Then
      hForm = New FmPDFView(xLink, False, $rData["fldencounterval"])
      hForm.ShowModal
    Endif

  Endif

End

Public Sub mnuprint_Click()

  Dim xLink As String

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])

    xLink = modImage.GetBlobFileData($rData["fldid"])
    If xLink Then
      Me.Exec("printJS('" & modPrint.GetFileWebURL(xLink) & "')")
    Endif

  Endif

End

Public Sub mnuemail_Click()

  Dim xpath As String
  Dim hForm As FmRemoteMail
  Dim xx As String

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])

    xpath = modImage.GetBlobFileData($rData["fldid"])
    If xpath Then
      xx = modPatient.GetPatientEmail($rData["fldencounterval"])
      hForm = New FmRemoteMail(xx, xpath, "Report", $rData["fldtitle"])
      hForm.ShowModal()
    Endif

  Endif

End
