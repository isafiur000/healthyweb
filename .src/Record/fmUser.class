' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub _new()

  If MMain.$SISHAppMode = "REP" And If MMain.$Ayurveda = "Yes" Then
    lblcategory.Text = "Location"
    cmbcategory.List = modDataRepo.GetRepoValueListType("Hospital")
  Endif
  Select MMain.$SISHAppMode
    Case "REP", "HMIS", "Dashboard", "Registry"
      btnhospcode.Visible = True
      btnAccess.Visible = False
      btnlocat.Visible = False
      cmbcategory.List = ["Admin", "Province", "District", "Municipality", "Hospital"]
    Case "Insurance"
      btnAccess.Visible = False
      btnlocat.Visible = False
      cmbcategory.List = ["Social Security Fund", "Health Insurance"]
    Case Else
      cmbcategory.List = modGeneral.GetUsersCategory()
  End Select
  cmbstatus.List = ["Active", "Inactive"]

  MakeAllTextBlank()
  GetUserList()

End

' Public Sub Form_KeyRelease()
'
'   If Key.Code = Key.Esc Then
'     Me.Close
'   Else
'     modGeneralmain.GoToNextControlTab()
'   Endif
'
' End

Private Sub MakeAllTextBlank()

  txtuserid.Text = ""
  txtfullname.Text = ""
  txtpassword.Text = ""
  txtroot.Text = ""
  txtmobile.Text = ""
  txtemail.Text = ""
  cmbstatus.Text = ""
  txtcode.Value = 0
  dtfrom.Value = Date()
  dtto.Value = Date()
  cmbcategory.Text = ""
  chkpass.Enabled = True
  chkroot.Enabled = True

  chkfaculty.Value = False
  chkipconsult.Value = False
  chkopconsult.Value = False
  chkpayable.Value = False
  chkrefer.Value = False
  chkreport.Value = False
  chksigna.Value = False
  chkonline.Value = False

End

Public Sub txtuserid_Activate()

  txtuserid.Text = LCase(txtuserid.Text)

End

Public Sub txtsearch_Change()

  txtsearchname.Text = ""
  GetUserList()

End

Public Sub txtsearchname_Change()

  txtsearch.Text = ""
  GetUserList()

End

Public Sub GetUserList()

  If txtsearch.Text Or If txtsearchname.Text Then
    If txtsearch.Text Then
      $rData = modDatabase.$myConn.Exec("select flduserid,fldstatus,fldusername from tbluser where lower(flduserid) like &1", LCase(txtsearch.Text) & "%")
    Else If txtsearchname.Text Then
      $rData = modDatabase.$myConn.Exec("select flduserid,fldstatus,fldusername from tbluser where lower(fldusername) like &1", LCase(txtsearchname.Text) & "%")
    Endif
  Else
    $rData = modDatabase.$myConn.Exec("select flduserid,fldstatus,fldusername from tbluser")
  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(20 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[0].Text = "UserID"
    .Columns[2].Text = "User Name"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Html = modString.GetImageForHTMLGrid(modMisc.GetGridIcon(GetStatusBool($rData[$aMyFields[Column]])), "75%", "75%")
    Data.Text = ""
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Private Function GetStatusBool(sVal As String) As Boolean

  Dim xx As Boolean

  If sVal = "Active" Then
    xx = False
  Else
    xx = True
  Endif
  Return xx

End

Public Sub GridView1_Select()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    chkpass.Enabled = False
    chkroot.Enabled = False
    txtpassword.Password = True
    txtroot.Password = True
    ShowUserDetail($rData["flduserid"])
    btnOK.Text = "Add"
    btnChange.Text = "Edit"
  Endif

End

Private Sub ShowUserDetail(sUser As String)

  Dim res As Result
  Dim sql As String

  sql = "select fldusername,fldpass,fldroot,fldfromdate,fldtodate,fldstatus,fldusercode,fldcategory,fldcode,fldfaculty,fldpayable,fldreferral,fldopconsult,fldipconsult,fldsigna,fldreport,fldonline,fldcontact,fldemail from tbluser where flduserid=&1"                        ''
  res = modDatabase.$myConn.Exec(sql, sUser)
  If res.Available Then
    txtuserid.Text = sUser
    txtfullname.Text = res["fldusername"]
    txtpassword.Text = modPassword.DecryptSimple(res["fldpass"])
    txtroot.Text = UnBase64(res["fldroot"])
    cmbcategory.Text = res["fldcategory"]
    txtcode.Value = res["fldcode"]
    dtfrom.Value = res["fldfromdate"]
    dtto.Value = res["fldtodate"]
    txtusercode.Text = res["fldusercode"]
    txtmobile.Text = res["fldcontact"]
    txtemail.Text = res["fldemail"]
    cmbstatus.Text = res["fldstatus"]

    If res["fldfaculty"] Then
      chkfaculty.Value = res["fldfaculty"]
    Else
      chkfaculty.Value = False
    Endif
    If res["fldpayable"] Then
      chkpayable.Value = res["fldpayable"]
    Else
      chkpayable.Value = False
    Endif
    If res["fldreferral"] Then
      chkrefer.Value = res["fldreferral"]
    Else
      chkrefer.Value = False
    Endif
    If res["fldopconsult"] Then
      chkopconsult.Value = res["fldopconsult"]
    Else
      chkopconsult.Value = False
    Endif
    If res["fldipconsult"] Then
      chkipconsult.Value = res["fldipconsult"]
    Else
      chkipconsult.Value = False
    Endif
    If res["fldsigna"] Then
      chksigna.Value = res["fldsigna"]
    Else
      chksigna.Value = False
    Endif
    If res["fldreport"] Then
      chkreport.Value = res["fldreport"]
    Else
      chkreport.Value = False
    Endif
    If res["fldonline"] Then
      chkonline.Value = True
    Else
      chkonline.Value = False
    Endif
  Endif

End

Public Sub chkpass_Click()

  If chkpass.Value = True Then
    txtpassword.Password = False
  Else If chkpass.Value = False
    txtpassword.Password = True
  Endif

End

Public Sub chkroot_Click()

  If chkroot.Value = True Then
    txtroot.Password = False
  Else If chkroot.Value = False
    txtroot.Password = True
  Endif

End

Public Sub btnOK_Click()

  Dim rs As Result
  Dim res As Result

  If btnOK.Text = "Add" Then
    btnOK.Text = "Save"
    MakeAllTextBlank()
    rs = modDatabase.$myConn.Exec("select MAX(fldcode) as fldid from tbluser")
    txtcode.Value = rs!fldid + 1
    txtuserid.SetFocus

  Else If btnOK.Text = "Save" Then
    chkpass.Enabled = False
    chkroot.Enabled = False
    txtpassword.Password = True
    txtroot.Password = True
    If txtuserid.Text And If txtfullname.Text Then
      res = modDatabase.$myConn.Create("tbluser")
      res["flduserid"] = Trim(txtuserid.Text)
      res["fldusername"] = txtfullname.Text
      res["fldpass"] = modPassword.EncryptSimple(Trim(txtpassword.Text))
      res["fldroot"] = Base64(Trim(txtroot.Text))
      res["fldcategory"] = cmbcategory.Text
      res["fldcode"] = txtcode.Value
      res["fldfromdate"] = dtfrom.Value
      res["fldtodate"] = dtto.Value
      res["fldusercode"] = Trim(txtusercode.Text)
      res["fldcontact"] = Trim(txtmobile.Text)
      res["fldemail"] = Trim(txtemail.Text)
      res["fldstatus"] = cmbstatus.Text

      res["fldfaculty"] = chkfaculty.Value
      res["fldpayable"] = chkpayable.Value
      res["fldreferral"] = chkrefer.Value
      res["fldopconsult"] = chkopconsult.Value
      res["fldipconsult"] = chkipconsult.Value
      res["fldsigna"] = chksigna.Value
      res["fldreport"] = chkreport.Value
      res["fldonline"] = chkonline.Value
      res["xyz"] = False

      If MMain.$WebEntry = True Then
        res["fldrepodate"] = Now()
        res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
        res["fldhospcode"] = modBasic.$HospCode
      Endif

      res.Update()
      btnOK.Text = "Add"
      GetUserList()
      Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information added", modBasic.$BalloonDelay))
    Endif
  Endif

End

Public Sub btnChange_Click()

  Dim res As Result

  If btnChange.Text = "Edit" Then
    btnChange.Text = "Update"
    txtuserid.Enabled = False

  Else If btnChange.Text = "Update" Then
    If txtuserid.Text And If txtfullname.Text Then
      res = modDatabase.$myConn.Edit("tbluser", "flduserid=&1", txtuserid.Text)
      res["fldusername"] = txtfullname.Text
      res["fldpass"] = modPassword.EncryptSimple(Trim(txtpassword.Text))
      res["fldroot"] = Base64(Trim(txtroot.Text))
      res["fldfromdate"] = dtfrom.Value
      res["fldtodate"] = dtto.Value
      res["fldcategory"] = cmbcategory.Text
      res["fldusercode"] = Trim(txtusercode.Text)
      res["fldcontact"] = Trim(txtmobile.Text)
      res["fldemail"] = Trim(txtemail.Text)
      res["fldstatus"] = cmbstatus.Text

      res["fldcode"] = txtcode.Value
      res["fldfaculty"] = chkfaculty.Value
      res["fldpayable"] = chkpayable.Value
      res["fldreferral"] = chkrefer.Value
      res["fldopconsult"] = chkopconsult.Value
      res["fldipconsult"] = chkipconsult.Value
      res["fldsigna"] = chksigna.Value
      res["fldreport"] = chkreport.Value
      res["fldonline"] = chkonline.Value
      res["xyz"] = False
      res.Update()
      btnChange.Text = "Edit"
      txtuserid.Enabled = True
      GetUserList()
      Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information updated", modBasic.$BalloonDelay))
    Endif
  Endif

End

Public Sub btnCancel_Click()

  If Message.Question(("Do you want to delete this user?"), ("No"), ("Yes")) = 2 Then
    If txtuserid.Text Then
      modDatabase.$myConn.Delete("tbluser", "flduserid=&1", txtuserid.Text)
      MakeAllTextBlank()
      GetUserList()
      Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information deleted", modBasic.$BalloonDelay))
    Endif
  Endif

End

Public Sub btnreport_Click()

  Dim xx As String
  Dim xPath As String
  Dim xlist As String[]

  xlist = cmbcategory.List
  xlist.Add("%")
  xx = InputCombo("Select Category of Users", "User List", xlist, "", True)
  If xx Then
    xPath = modGENReport.ShowUserListHTML(xx)
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub btnlocat_Click()

  Dim hForm As FmAddVariableCombo

  If txtuserid.Text Then
    hform = New FmAddVariableCombo("fldcomp", "flduserid", Trim(txtuserid.Text), "tblcompaccess", modBasic.$AllCompList, True)
    hform.ShowModal
  Endif

End

Public Sub btnpic_Click()

  Dim hForm As FmPersonImage

  If txtcode.Value Then
    hForm = New FmPersonImage("Staff", txtcode.Value, False)
    hForm.ShowModal
  Endif

End

Public Sub btnprofile_Click()

  Dim hForm As FmReportUsers

  If txtuserid.Text Then
    hForm = New FmReportUsers(txtuserid.Text)
    hForm.ShowModal
  Endif

End

Public Sub btnAccess_Click()

  Dim res4 As Result
  Dim hForm As FmuserAccess

  If txtuserid.Text Then
    If MMain.$SISHAppMode = "REP" Then
      res4 = modDatabase.$myConn.Create("tbluseraccess")
      res4["flduserid"] = txtuserid.Text
      res4["fldaccess"] = Base64("Consultant")
      res4.Update()
    Else
      hForm = New FmuserAccess(txtuserid.Text)
      hForm.ShowModal
    Endif
  Endif

End

Private Sub ShowUserListAcc(sField As String, sType As String)

  Dim $BillingReport As CReportHTML
  Dim asx As New String[0]
  Dim res As Result

  $BillingReport = New CReportHTML([("User"), ("Full Name"), ("Category"), ("Status")], "", "")
  $BillingReport.UserData.Add("USER REPORT : " & sType, "PARAM1")
  $BillingReport.UserData.Add("Date: " & modReportVar.GetDateTimeReport(Now(), gb.GeneralDate), "PARAM2")

  res = modDatabase.$myConn.Exec(Subst("select flduserid,fldusername,fldcategory,fldstatus from tbluser where &1", sField) & "=&1", True)
  If res.Available Then
    For Each res
      With asx
        .Add(res["flduserid"])
        .Add(res["fldusername"])
        .Add(res["fldcategory"])
        .Add(res["fldstatus"])
      End With
      $BillingReport.Add(asx)
      asx.Clear()
    Next
  Endif

  modControlSub.OpenHTMLPreview("", $BillingReport.NewHTMLPath(), "ReportSize")

End

Public Sub btnpayable_Click()

  ShowUserListAcc("fldpayable", "Payable Users")

End

Public Sub btnrefreeal_Click()

  ShowUserListAcc("fldreferral", "Referral Users")

End

Public Sub btnconsult_Click()

  ShowUserListAcc("fldopconsult", "Consultation Users")

End

Public Sub btnpatient_Click()

  ShowUserListAcc("fldipconsult", "Medical Users")

End

Public Sub btnsigna_Click()

  ShowUserListAcc("fldsigna", "Signature Users")

End

Public Sub btnexport_Click()

  ShowUserListAcc("fldreport", "Report Export Users")

End

Public Sub btnfaculty_Click()

  ShowUserListAcc("fldfaculty", "Super Users")

End

Public Sub btnonline_Click()

  ShowUserListAcc("fldonline", "Online Users")

End

Public Sub btnrandpass_Click()

  txtpassword.Text = modPassword.GetRandomPassword()

End

Public Sub btnrandroot_Click()

  txtroot.Text = modPassword.GetRandomPassword()

End

Public Sub WebButton1_Click()

  Me.Close()

End

Public Sub btnhospcode_Click()

  Dim res As Result
  Dim xPath As String
  Dim xxx As String[]

  res = modDatabase.$myConn.Exec("select fldprovince,flddistrict,fldpality,fldhospcode,fldhospname from tblhospitals")
  xPath = modTextDB.ConvertResultToCSV(res, ";", False)
  If Exist(xPath) Then
    xxx = TreeViewCode(xPath, ";", 4, True)
    If xxx Then
      txtusercode.Text = xxx[1]
    Endif
  Endif

End
