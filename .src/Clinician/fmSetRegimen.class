' Gambas class file

Private $sType As String
Private $encid As String
Private $sValue As Long[]

Private $rData As Result
Private $aMyFields As String[]
Private $ItemList As String[]
Private sIndexLst As Long[]

Private $PatientNum As String
Private $curEncid As String
Private $lstEncid As String

Public Sub Run(sType As String, Optional encid As String) As Long[]

  $sType = sType
  If encid Then
    $encid = encid
  Endif

  If $sType = "Personal" Then
    Me.Title = "Medicine Regimen"
    lstletter.Visible = True
    WebContainer4.Visible = False
  Else If $sType = "Used Medicines" Then
    Me.Title = "Medication History"
    $curEncid = $encid
    $PatientNum = modPatient.GetPatientNoByEnc($encid)
    $lstEncid = modPatient.GetSecondLastEncidPatient($encid)
    cmbcategory.List = modPatient.GetEncListFromPatNo($PatientNum)
  Endif
  modGeneralMain.GetOpenModalForm(Me)

  rbcurrent.Value = True
  $ItemList = New String[]
  SHowGrid()
  lstletter.List = modString.GetFirstLetterArray($ItemList)
  sIndexLst = New Long[]

  If modBasic.$TabletModeEnable = "Yes" Then
  Else
    txtname.SetFocus
  Endif

  If Me.ShowModal() Then Return $sValue

End

Public Sub btnrefresh_Click()

  Dim xdate As Date

  If rbcurrent.Value = True Then
    $curEncid = $encid
  Else If rbpast.Value = True Then
    $curEncid = $lstEncid
  Else If rbselect.Value = True Then
    If cmbcategory.Text Then
      $curEncid = cmbcategory.Text
    Endif
  Endif

  If $curEncid Then
    $ItemList = New String[]
    SHowGrid()
    lstletter.List = modString.GetFirstLetterArray($ItemList)
    xdate = modPatient.GetRecordDate($curEncid)
    lbldate.Text = "DATE: " & Format(xdate, gb.ShortDate) & Space(1) & "(" & modDate.ConvertToLocaldate(xdate) & ")"
  Endif

End

Public Sub txtname_Change()

  SHowGrid()

End

Private Sub SHowGrid()

  Dim sql As String

  If $sType = "Personal" Then
    sql = "select fldid,fldid,fldroute,fldbrandid,flddose,fldfreq,fldday,fldqty,fldusage from tblmedregimen where (flduserid=&1 or flduserid=&2) and lower(fldbrandid) like &3 ORDER BY fldbrandid"
    If chkleftmain.Value = True Then
      $rData = modDatabase.$myConn.Exec(sql, modBasic.$lbluser, "%", "%" & LCase(Trim(txtname.Text)) & "%")
    Else
      $rData = modDatabase.$myConn.Exec(sql, modBasic.$lbluser, "%", LCase(Trim(txtname.Text)) & "%")
    Endif
  Else If $sType = "Used Medicines" Then
    sql = "select fldid,fldid,fldroute,flditem,flddose,fldfreq,flddays,fldqtydisp,flddirection from tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and flditemtype=&3 and fldcurval=&4 and flddose>&5 and lower(flditem) like &6 ORDER BY flditem"   ''and fldid IN (SELECT MAX(fldid) FROM tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and flditemtype=&3 and fldcurval=&4 GROUP BY flditem)
    If chkleftmain.Value = True Then
      $rData = modDatabase.$myConn.Exec(sql, $curEncid, True, "Medicines", "Continue", 0, "%" & LCase(Trim(txtname.Text)) & "%")
    Else
      $rData = modDatabase.$myConn.Exec(sql, $curEncid, True, "Medicines", "Continue", 0, LCase(Trim(txtname.Text)) & "%")
    Endif
  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  For Each $rData
    If $sType = "Personal" Then
      $ItemList.Add($rData["fldbrandid"])
    Else If $sType = "Used Medicines" Then
      $ItemList.Add($rData["flditem"])
    Endif
  Next

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Hidden = True
    .Columns[2].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"

    .Columns[2].Text = "Route"
    .Columns[3].Text = "Medicine"
    .Columns[4].Text = "Dose"
    .Columns[5].Text = "Freq"
    .Columns[6].Text = "Days"
    .Columns[7].Text = "QTY"
    .Columns[8].Text = "Direction"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]

End

Public Sub lstletter_Select()

  txtname.Text = lstletter.Text

End

Public Sub btnok_Click()

  Dim Row As Integer

  If GridView1.Selection.Count Then
    For Row = 0 To GridView1.Count - 1
      If GridView1.IsSelected(Row) = True Then
        $rData.MoveTo(Row)
        If sIndexLst.Exist($rData["fldid"]) = False Then
          sIndexLst.Add($rData["fldid"])
        Endif
      Endif
    Next
    Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information saved", modBasic.$BalloonDelay))
  Endif

End

Public Sub btnclose_Click()

  If sIndexLst.Count Then
    $sValue = sIndexLst
    Me.Close(True)
  Else
    Me.Close
  Endif

End
