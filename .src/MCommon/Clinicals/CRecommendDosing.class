' Gambas class file

Private $encid As String
Private $aAge As Float
Private $sStockId As String
Private $DoseRate As String

Private $unitDose As Float
Private $doseUnit As String
Private $frequency As String

Public Sub _new(encid As String, sAge As Float, xStockid As String)

  $encid = encid
  $aAge = sAge
  $sStockId = xStockid

  GetResults()

End

Private Sub GetResults()

  Dim sql As String
  Dim res As Result
  Dim xdose As Float

  Dim xval As Float
  Dim creatclr As Float

  If $aAge <= 12 Then
    sql = "select fldrecpeddose as dose,fldrecpeddoseunit as unit,fldrecpedfreq as freq from tblcode where fldcodename in(select fldcodename from tbldrug where flddrug in(select flddrug from tblmedbrand where fldbrandid=&1))"
  Else If $aAge > 12 Then
    sql = "select fldrecaddose as dose,fldrecaddoseunit as unit,fldrecadfreq as freq from tblcode where fldcodename in(select fldcodename from tbldrug where flddrug in(select flddrug from tblmedbrand where fldbrandid=&1))"
  Endif
  res = modDatabase.$medConn.Exec(sql, $sStockId)
  If res.Available Then
    If res["dose"] Then
      xdose = res["dose"]
    Else
      xdose = 0
    Endif
    If res["unit"] Then
      $doseUnit = res["unit"]
    Else
      $doseUnit = ""
    Endif
    If res["freq"] Then
      $frequency = CStr(res["freq"])
    Else
      $frequency = "1"
    Endif

  Else
    xdose = 0
    $doseUnit = ""
    $frequency = "1"
  Endif

  $DoseRate = CStr(xdose) & Space(1) & $doseUnit

  If xdose Then
    xval = modMedDosing.GetDoseInMg(xdose, $doseUnit, $encid)
    If xval Then
      $unitDose = xval
    Else
      $unitDose = 0
    Endif
  Else
    $unitDose = 0
  Endif

  If modBasic.$MedRenalAdjustment = "Yes" Then
    creatclr = modSysCons.GetCreatinineClearance($encid)
    If creatclr Then
      If (modMedConstant.GetDrugRenalEliminFraction($sStockId) * 100) > modBasic.$MedMinRenalElimin And If creatclr < modBasic.$MedMaxCreatClear Then                                             ''
        $unitDose = $unitDose * modMedDosing.GetDoseAdjustmentFactor($encid, $sStockId)
      Endif
    Endif
  Endif

  If $unitDose = 0 Then
    $unitDose = modMedConstant.GetDrugInitialStrength($sStockId)
  Endif

End

Public Function UnitDose() As Float

  Return $unitDose

End

Public Function DoseUnit() As String

  Return $doseUnit

End

Public Function Frequency() As String

  Return $frequency

End

Public Function Regimen() As String

  Return $DoseRate

End
