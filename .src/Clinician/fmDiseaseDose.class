' Gambas class file

Private $sFormat As String
Private $encid As String
Private $route As String
Private $stockid As String
Private $txtdose As WebValueBox
Private $cmbfreq As WebComboBox
Private $txtday As WebValueBox

Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(sFormat As String, encid As String, route As String, stockid As String, txtdose As WebValueBox, cmbfreq As WebComboBox, txtday As WebValueBox)

  $sFormat = sFormat
  $encid = encid
  $route = route
  $stockid = stockid
  $txtdose = txtdose
  $cmbfreq = cmbfreq
  $txtday = txtday
  modGeneralMain.GetOpenModalForm(Me)
  ShowStockGrid()

End

Private Sub ShowStockGrid()

  Dim sql As String

  sql = "select fldid,flddisease,fldid,fldfreq,fldday from tblregimen where fldroute=&1 and fldcodename=&2  and (fldgender=&3 or fldgender=&4) and (fldagegroup=&5 or fldagegroup=&6) and flddisease in(select fldcode from tblpatfindings where fldencounterval=&7 and fldsave=&8)"                                    ''
  $rData = modDatabase.$myConn.Exec(sql, $route, modMedConstant.GetCodeFromStockID($stockid), modPatient.GetPatientSex($encid), "Both Sex", modPatient.GetAgeGroupFIxed($encid, Now()), "All Age", $encid, True)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(300 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"

    .Columns[1].Text = "Disease"
    .Columns[2].Text = "Dose"
    .Columns[3].Text = "Freq"
    .Columns[4].Text = "Day"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 2 Then
    Data.Text = modMedDosing.GetDoseInMgFoFromRegID($rData[$aMyFields[Column]], $encid)
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub btnconsultdone_Click()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    $txtdose.Value = modMedDosing.GetDoseInMgFoFromRegID($rData["fldid"], $encid)
    $cmbfreq.Text = $rData["fldfreq"]
    $txtday.Value = $rData["fldday"]
    Me.Close
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
