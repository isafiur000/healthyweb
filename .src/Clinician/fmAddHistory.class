' Gambas class file

Private $sValue As String
Private $sType As String
Private $encid As String
Private $sVariable As String
Private $sTable As String

Private $gender As String
Private $SelAll As Variant[]
Private $SelVar As Variant[]

Private aHorzPanel As New Object[50]
Private aNameLabel As New Object[50]
Private aOptionBox As New Object[50]
Private aComboBox As New Object[50]
Private aButonBox As New Object[50]
Private aSpaceBox As New Object[50]

Public Sub Run(sType As String, encid As String, sVariable As String) As String

  $sType = sType
  $encid = encid
  $sVariable = sVariable

  If $sType = "History" Then
    $sTable = "tblhistory"
  Else If $sType = "Discharge" Then
    $sTable = "tbldischarge"
  Endif
  $gender = modPatient.GetPatientSex($encid)
  cmbformat.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec(Subst("select distinct(fldformat) as col from &1", $sTable) & " where flditem=&1 and (fldgender=&2 or fldgender=&3)", $sVariable, $gender, "Both Sex"))                              ''
  cmbformat.Text = "Common"

  If Me.ShowModal() Then Return $sValue

End

Public Sub btnfill_Click()

  If cmbformat.Text Then
    $SelAll = New Variant[]
    ShowControls()
  Endif

End

Private Sub ShowControls()

  Dim res As Result
  Dim xVar As Variant[]
  Dim aColl As Collection
  Dim aList As String[]

  res = modDatabase.$myConn.Exec(Subst("select fldid,flddetail,fldoptions from &1", $sTable) & " where flditem=&1 and fldformat like &2 and (fldgender=&3 or fldgender=&4)", $sVariable, cmbformat.Text, $gender, "Both Sex")     ''
  If res.Available Then
    res.MoveFirst
    txthistory.RichText = res["flddetail"]
    If res["fldoptions"] Then
      Try xVar = JSON.Decode(res["fldoptions"])
      If xVar Then
        $SelAll = xVar

        aList = New String[]
        For Each aColl In $SelAll
          aList.Add(aColl["Variable"])
        Next
        If aList.Count Then
          lstvariable.List = modString.GetDistinctStringArray(aList)
        Else
          lstvariable.Clear()
        Endif

      Endif
    Endif
  Endif

End

Public Sub lstvariable_Select()

  Dim aColl As Collection

  If Frame1.Children.Count Then
    Frame1.DeleteChildren()
  Endif

  $SelVar = New Variant[]
  For Each aColl In $SelAll
    If aColl["Variable"] = lstvariable.Text Then
      $SelVar.Add(aColl)
    Endif
  Next

  LoadControls()

End

Private Sub LoadControls()

  Dim i As Integer

  For i = 0 To $SelVar.Count - 1
    If i < 49 Then
      aHorzPanel[i] = New WebContainer(Frame1)
      aNameLabel[i] = New WebHtml(aHorzPanel[i])
      aOptionBox[i] = New WebComboBox(aHorzPanel[i])
      aComboBox[i] = New WebComboBox(aHorzPanel[i])
      aButonBox[i] = New WebButton(aHorzPanel[i]) As "ButtonGroup"
      aSpaceBox[i] = New WebLabel(aHorzPanel[i])

      With aHorzPanel[i]
        .Arrangement = Arrange.Horizontal
        .Class = "less-container-margin"
        .Border = True
      End With

      With aNameLabel[i]
        .Expand = True
        .Text = $SelVar[i]["Value"]
        .Tag = i
      End With

      With aOptionBox[i]
        .Width = "4em"
        .Height = "2em"
        .ReadOnly = False
        .Tag = i
      End With
      aOptionBox[i].List = ["=", ":", "is"]

      With aComboBox[i]
        .Width = "15em"
        .Height = "2em"
        .ReadOnly = False
        .Tag = i
      End With
      aComboBox[i].List = Split($SelVar[i]["Option"], ";")

      With aButonBox[i]
        .Width = "2em"
        .Height = "2em"
        .Image = "icon:/32/apply"
        .Tag = i
      End With

      With aSpaceBox[i]
        .Width = "2em"
        .Tag = $SelVar[i]["Select"]
      End With

    Endif
  Next

End

Public Sub ButtonGroup_Click()

  Dim i As Integer
  Dim xstr As String

  i = Last.Tag
  If aOptionBox[i].Text Then
    xstr = aNameLabel[i].Text & Space(1) & aOptionBox[i].Text & Space(1) & aComboBox[i].Text
  Else
    xstr = aNameLabel[i].Text & Space(1) & aComboBox[i].Text
  Endif

  If txthistory.Text Then
    If aSpaceBox[i].Tag Then
      txthistory.RichText = Replace(txthistory.RichText, aSpaceBox[i].Tag, xstr)
    Else
      txthistory.RichText = Replace(txthistory.Text, "<" & lstvariable.Text & ">", xstr)
    Endif
  Else
    txthistory.RichText = xstr
  Endif

End

Public Sub btnsave_Click()

  $sValue = txthistory.RichText
  Me.Close(True)

End

Public Sub btnclose_Click()

  Me.Close()

End
