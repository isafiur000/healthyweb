' Gambas class file

Private $sValue As String
Private $sType As String
Private $sVariable As String

Private $defValue As String
Private $SelVar As Variant[]

Private aHorzPanel As New Object[50]
Private aNameLabel As New Object[50]
Private aComboBox As New Object[50]
Private aSpaceBox As New Object[50]

Public Sub Run(sType As String, sVariable As String) As String

  $sType = sType
  $sVariable = sVariable

  cmbformat.List = ["Common", "Format A", "Format B", "Format C", "Claim Code", "Accident SSF", "Medical SSF", "Critical SSF", "Staff List"]
  cmbformat.Text = "Common"

  If Me.ShowModal() Then Return $sValue

End

Public Sub btnfill_Click()

  If Frame1.Children.Count Then
    Frame1.DeleteChildren()
  Endif
  If cmbformat.Text Then
    $SelVar = New Variant[]
    ShowControls()
  Endif

End

Private Sub ShowControls()

  Dim sTable As String
  Dim res As Result
  Dim xVar As Variant[]
  Dim i As Integer

  If $sType = "History" Then
    sTable = "tblhistory"
  Else If $sType = "Discharge" Then
    sTable = "tbldischarge"
  Endif
  res = modDatabase.$myConn.Exec(Subst("select fldid,flddetail,fldoptions from &1", sTable) & " where flditem=&1 and fldcomp=&2 and fldformat like &3", $sVariable, modBasic.$compID, cmbformat.Text)    ''
  If res.Available Then
    res.MoveFirst
    $defValue = res["flddetail"]
    If res["fldoptions"] Then
      Try xVar = JSON.Decode(res["fldoptions"])
      If xVar Then
        $SelVar = xVar

        For i = 0 To $SelVar.Count - 1
          If i < 49 Then
            aHorzPanel[i] = New WebContainer(Frame1)
            aNameLabel[i] = New WebHtml(aHorzPanel[i])
            aComboBox[i] = New WebComboBox(aHorzPanel[i])
            aSpaceBox[i] = New WebLabel(aHorzPanel[i])

            With aHorzPanel[i]
              .Arrangement = Arrange.Horizontal
              .Class = "less-container-margin"
              .Border = True
            End With

            With aNameLabel[i]
              .Expand = True
              .Text = $SelVar[i]["0"]
              .Tag = i
            End With

            With aComboBox[i]
              .Width = "25%"
              .Height = "2em"
              .ReadOnly = False
              .Tag = i
            End With
            aComboBox[i].List = Split($SelVar[i]["1"], ";")

            With aSpaceBox[i]
              .Width = "2em"
            End With

          Endif
        Next

      Endif
    Endif
  Endif

End

Public Sub btnselection_Click()

  Dim i As Integer
  Dim xList As String[]

  xList = New String[]
  For i = 0 To $SelVar.Count - 1
    If aComboBox[i].Text Then
      xList.Add(aNameLabel[i].Text & Space(1) & aComboBox[i].Text)
    Endif
  Next

  If xList.Count Then
    If txthistory.Text Then
      txthistory.RichText = txthistory.RichText & gb.NewLine & xList.Join(gb.NewLine)
    Else
      txthistory.RichText = xList.Join(gb.NewLine)
    Endif
  Endif

End

Public Sub btndefault_Click()

  If txthistory.Text Then
    txthistory.RichText = txthistory.RichText & gb.NewLine & $defValue
  Else
    txthistory.RichText = $defValue
  Endif

End

Public Sub btnsave_Click()

  $sValue = txthistory.RichText
  Me.Close(True)

End

Public Sub btnclose_Click()

  Me.Close()

End
