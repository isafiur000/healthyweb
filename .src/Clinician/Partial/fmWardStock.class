' Gambas class file

Public $DiscPackage As String

Private $encid As String
Private $PatientNum As String
Private $status As String
Private $Dept As String

Private $LedgerAC As String
Private $xBillType As String
Private $sBillMode As String
Private $sClaimState As String

Private $rData As Result
Private $aMyFields As String[]
Private $rData1 As Result
Private $aMyFields1 As String[]

Private $xNHISCode As String
Private $HiCappingVar As Collection

Public Sub _new(encid As String, sStatus As String, DiscPackage As String, sClass As String)

  Dim accLst As String[]

  $encid = encid
  $status = sStatus
  $DiscPackage = DiscPackage
  If sClass Then
    WebContainer1.Class = sClass
  Else
    WebContainer1.Class = "height-tablet"
  Endif

  Select $status
    Case "Admitted", "Discharged", "LAMA", "Death", "Refer", "Absconder"
      $Dept = "IPD"
    Case Else
      $Dept = "OPD"
  End Select

  $PatientNum = modPatient.GetPatientNoByEnc($encid)
  $xNHISCode = modPatient.GetPatientExtCOdebyEnc($encid)
  accLst = modBasic.$BillDiscountCash
  If accLst.Count = 0 Then
    accLst.Add($DiscPackage)
  Endif
  cmbdisctype.List = accLst
  cmbdisctype.Text = $DiscPackage
  ShowBillingParam($DiscPackage)
  rbroute.Value = True
  rbroute_Click()
  ShowUsedGridViewList()

End

Public Sub cmbdisctype_Click()

  If cmbdisctype.Text Then
    ShowBillingParam(cmbdisctype.Text)
    cmbgroup_Select()
  Endif

End

Public Sub SetOwnDepartment(sIndex As String)

  $Dept = sIndex
  ShowUsedGridViewList()

End

Public Sub UpdateBillingParam()

  ShowBillingParam($DiscPackage)

End

Public Sub RearrangeGrid()

  ResizeGrid()

End

Private Sub ShowBillingParam(sDisctype As String)

  Dim hForm As CPackageParams

  $HiCappingVar = New Collection
  hForm = New CPackageParams($encid, sDisctype)
  $sBillMode = hForm.GetBillingMode()
  $LedgerAC = hForm.GetLedgerAC()
  $xBillType = hForm.GetBillType()
  $sClaimState = hForm.GetClaimState()

End

Private Sub GetHImedicineCappingVar()

  Dim hForm As CimisAPICap

  If cmbdisctype.Text = modBasic.$HIPackage Or If cmbdisctype.Text = modBasic.$HIPackageER Then
    If modBasic.$IMISValidateURL Then

      If modClaim.GetChronicClaimStatus($encid) = True Then
        If $HiCappingVar.Count Then
          $HiCappingVar.Clear()
        Endif
      Else
        If Not $HiCappingVar.Count Then
          hForm = New CimisAPICap($xNHISCode)
          $HiCappingVar = hForm.GetMedicineLimits()
        Endif
      Endif

    Endif
  Endif

End

Public Sub RefreshList()

  LoadCategories()

End

Private Function GetPlannedProtocols() As String[]

  Dim sql1 As String
  Dim sql2 As String
  Dim xList As String[]

  sql1 = "select distinct(fldprotocol) as col from tblpatprotocols where fldencounterval=&1 and fldstatus=&2"
  sql2 = "select distinct(fldprotocol) as col from tblpatprotocols where fldpatientval=&3 and fldstatus=&2 and fldfuture=&4"
  xList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec(sql1 & " UNION " & sql2, $encid, "Active", $PatientNum, True))
  Return xList

End

Private Sub LoadCategories()

  If rbroute.Value = True Then
    chkall.Value = False
    chkall.Enabled = False
    If chknewall.Value = True Then
      cmbgroup.List = modMedicine.ComboRoute()
    Else
      cmbgroup.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldroute) as col from tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and fldcurval=&3", $encid, False, "Continue"))
    Endif
    btnselect.Text = "+1"

  Else If rbprot.Value = True Then
    chkall.Enabled = True
    If chknewall.Value = True Then
      cmbgroup.List = modMedicine.GetMedicineProcolsList()
    Else
      cmbgroup.List = GetPlannedProtocols()
    Endif
    btnselect.Text = ""

  Endif
  ShowUsedGridViewList()

End

Public Sub rbroute_Click()

  LoadCategories()

End

Public Sub rbprot_Click()

  LoadCategories()

End

Public Sub chknewall_Click()

  LoadCategories()

End

Public Sub cmbgroup_Select()

  If cmbgroup.Text Then
    If rbroute.Value = True Then
      ShowCurrStockGrid(cmbgroup.Text, "%")
    Else If rbprot.Value = True Then
      ShowProtocolStockGrid(cmbgroup.Text, "%")
    Endif
    If modBasic.$TabletModeEnable = "Yes" Then
    Else
      txtsearch.SetFocus
    Endif
  Endif

End

Public Sub txtsearch_Change()

  Dim xText As String

  If chkleftmain.Value = True Then
    xText = "%" & LCase(txtsearch.Text) & "%"
  Else
    xText = LCase(txtsearch.Text) & "%"
  Endif

  If cmbgroup.Text Then
    If rbroute.Value = True Then
      ShowCurrStockGrid(cmbgroup.Text, xText)
    Else If rbprot.Value = True Then
      ShowProtocolStockGrid(cmbgroup.Text, xText)
    Endif
  Endif

End

Private Sub ShowCurrStockGrid(sRoute As String, sSearch As String)

  Dim xType As String
  Dim xList As String[]

  If chknewall.Value = True Then
    xType = modNonMedical.GetBillItemCategoryFromCombo(sRoute)
    Select xType
      Case "Medicines"
        $rData = modDatabase.$syConn.Exec("select t1.fldbrandid,t1.fldbrandid as xitem,t1.fldbrandid,t2.fldroute as extra from tblmedbrand as t1 inner join tbldrug as t2 on t1.flddrug=t2.flddrug where t1.fldbrandid in(select fldstockid from tblentry where fldqty>&1 and fldcomp like &2) and t2.fldroute=&3 and lower(t1.fldbrandid) like &4 ORDER BY t1.fldbrandid ASC", 0, modBasic.$compID, sRoute, sSearch)                                ''
      Case "Surgicals"
        $rData = modDatabase.$syConn.Exec("select t1.fldbrandid,t1.fldbrandid as xitem,t1.fldbrandid,t2.fldsurgcateg as extra from tblsurgbrand as t1 inner join tblsurgicals as t2 on t1.fldsurgid=t2.fldsurgid where t1.fldbrandid in(select fldstockid from tblentry where fldqty>&1 and fldcomp like &2) and t2.fldsurgcateg=&3 and lower(t1.fldbrandid) like &4 ORDER BY t1.fldbrandid ASC", 0, modBasic.$compID, sRoute, sSearch)
      Case "Extra Items"
        xList = ["fldbrandid", "fldbrandid as xitem", "fldbrandid"]
        xList.Add(Quote("extra"))
        $rData = modDatabase.$syConn.Exec("select " & xList.Join(",") & " from tblextrabrand where fldbrandid in(select fldstockid from tblentry where fldqty>&1 and fldcomp like &2) and lower(fldbrandid) like &3 ORDER BY fldbrandid ASC", 0, modBasic.$compID, sSearch)
    End Select

  Else
    $rData = modDatabase.$syConn.Exec("select flditem,flditem,flditem,fldroute from tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and fldcurval=&3 and fldroute=&4 and lower(flditem) like &5 ORDER BY flditem ASC", $encid, False, "Continue", sRoute, sSearch)
  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView3, $rData, $aMyFields)
  ResizeFirstGrid()

End

Private Sub ShowProtocolStockGrid(sPack As String, sSearch As String)

  $rData = modDatabase.$myConn.Exec("select fldid,flditem as xitem,fldqty,fldroute as extra from tblproductgroup where fldmedgroup=&1 and (fldcomp=&2 or fldcomp=&3) and flditem like &4", sPack, modBasic.$compID, "%", sSearch)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView3, $rData, $aMyFields)
  ResizeFirstGrid()

End

Private Sub ResizeFirstGrid()

  With GridView3
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(300 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Hidden = True
    .Columns[1].Text = "Particulars"
    If rbroute.Value = True Then
      .Columns[2].Text = "Stock"
    Else If rbprot.Value = True Then
      .Columns[2].Text = "QTY"
    Endif
  End With

End

Public Sub GridView3_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 2 Then
    If rbroute.Value = True Then
      Data.Text = modStock.TotalQTYbyBrand($rData[$aMyFields[Column]], modBasic.$compID)
    Else
      Data.Text = $rData[$aMyFields[Column]]
    Endif
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView3_Select()

  If GridView3.Selection.Count Then
    $rData.MoveTo(GridView3.Selection[0])
    txtdose.Value = 0
    txtqty.Value = 0
    If modBasic.$ClinFractQuantity = "Enable" Then
      txtbreakitem.Text = $rData["xitem"]
      SHowBreakPanel($rData["xitem"])
    Endif
  Endif

End

Private Sub SHowBreakPanel(sItem As String)

  Dim xbreak As String

  If rbroute.Value = True Then
    Select cmbgroup.Text
      Case "oral", "liquid", "fluid", "injection", "resp", "topical", "eye/ear", "anal/vaginal"
        xbreak = modMedConstant.GetAllowTabBreak(sItem)
        If xbreak = "Yes" Then
          pnldose.Visible = True
          lblunit.Text = modMedConstant.GetDrugDosingUnit(sItem)
        Else
          pnldose.Visible = False
        Endif
      Case Else
        pnldose.Visible = False
    End Select
  Else
    pnldose.Visible = False
  Endif

End

Public Sub txtdose_Change()

  Dim xstr As Float
  Dim xpack As Float

  If txtdose.Value Then
    xstr = modMedConstant.GetDrugInitialStrength(txtbreakitem.Text)
    xpack = modMedConstant.GetPackVolValue(txtbreakitem.Text)
    If xstr And If xpack Then
      txtqty.Value = txtdose.Value / (xstr * xpack)
    Endif
  Endif

End

Public Sub btnselbreak_Click()

  If cmbgroup.Text Then
    If txtqty.Value Then
      EntryUseOwndata(cmbgroup.Text, txtbreakitem.Text, txtqty.Value)
      GridView3.UnselectAll()
      txtdose.Value = 0
      txtqty.Value = 0
      ShowUsedGridViewList()
    Endif
  Endif

End

Public Sub chkall_Click()

  If chkall.Value = True Then
    GridView3.SelectAll()
  Else If chkall.Value = False Then
    GridView3.UnselectAll()
  Endif

End

Public Sub btnselect_Click()

  Dim Row As Integer
  Dim sType As String
  Dim xqty As Float

  If GridView3.Count Then
    For Row = 0 To GridView3.Count - 1
      If GridView3.IsSelected(Row) = True Then
        $rData.MoveTo(Row)

        sType = modNonMedical.GetBillItemCategoryFromCombo($rData["extra"])
        xqty = 0
        If rbroute.Value = True Then
          xqty = 1
        Else If rbprot.Value = True Then
          xqty = $rData["fldqty"]
        Endif
        If xqty Then
          EntryUseOwndata(sType, $rData["xitem"], xqty)
        Endif

      Endif
    Next

    GridView3.UnselectAll()
    ShowUsedGridViewList()
  Endif

End

Private Sub EntryUseOwndata(sType As String, sItem As String, sQty As Float)

  Dim xroute As String
  Dim xtax As Float
  Dim xdisc As Float
  Dim xdose As Float
  Dim xfixrate As Float
  Dim xfixname As String

  Dim itemmode As String
  Dim CPharmFix As CFixRatePharmacy
  Dim xgo As Boolean

  xgo = False
  If modBasic.$BillPharmFixedModes.Exist($sBillMode) = True Then
    CPharmFix = New CFixRatePharmacy(sType, sItem, $sBillMode)
    xfixrate = CPharmFix.GetFixRate()
    xfixname = CPharmFix.GetFixItem()
    If xfixname Then
      xgo = True
    Else
      xgo = False
    Endif
  Else
    xgo = True
  Endif
  If xgo = True Then
    itemmode = $sBillMode
    xtax = modNonMedical.ShowTaxValues(sType, sItem)
    xdisc = modNonMedical.DiscPercentForCategoryValue($encid, $DiscPackage, sType, sItem, itemmode)
    xroute = modMedicine.GetRouteFromItem(sItem, sType)

    If sType = "Medicines" Then
      xdose = sQty * modMedConstant.GetPackVolValue(sItem) * modMedConstant.GetDrugInitialStrength(sItem)
      If $Dept = "OPD" Then
        modPharmSub.InsertDosingEntry($encid, $xBillType, $sBillMode, $DiscPackage, $LedgerAC, xroute, sItem, xdose, "stat", 1, sQty, $status, "", "", 0, xtax, xdisc, xfixname, xfixrate, "UseOwn", $Dept, "", False)
      Else
        modPharmSub.InsertDosingEntry($encid, $xBillType, $sBillMode, $DiscPackage, $LedgerAC, xroute, sItem, xdose, "SOS", 1, sQty, $status, "", "", 0, xtax, xdisc, xfixname, xfixrate, "UseOwn", $Dept, "", False)
      Endif
    Else
      modPharmSub.InsertNonMedDosingEntry(sType, $encid, $xBillType, $sBillMode, $DiscPackage, $LedgerAC, xroute, sItem, sQty, $status, 0, xtax, xdisc, xfixname, xfixrate, "UseOwn", $Dept, "", False)
    Endif
  Endif

End

Private Sub ShowUsedGridViewList()

  Dim sql As String

  If rbroute.Value = True And If chknewall.Value = False Then
    Select $Dept
      Case "OPD", "IPD"
        sql = "select fldid,fldstarttime,fldroute,flditem,flddose,fldfreq,flddays,fldqtydisp,flduserid_order,fldid,fldid,fldstarttime from tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and fldstatus=&3 and (fldorder=&4 or fldorder=&5) and fldcurval=&6"
      Case Else
        sql = "select fldid,fldstarttime,fldroute,flditem,flddose,fldfreq,flddays,fldqtydisp,flduserid_order,fldid,fldid,fldstarttime from tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and fldstatus=&3 and (fldorder=&4 or fldorder=&5) and fldcurval=&6 and flddispmode=&7"
    End Select
    $rData1 = modDatabase.$myConn.Exec(sql, $encid, False, $status, "UseOwn", "Request", "Continue", $Dept)

  Else
    Select $Dept
      Case "OPD", "IPD"
        sql = "select fldid,fldstarttime,fldroute,flditem,flddose,fldfreq,flddays,fldqtydisp,flduserid_order,fldid,fldid,fldstarttime from tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and fldstatus=&3 and fldorder=&4 and fldcurval=&5"
      Case Else
        sql = "select fldid,fldstarttime,fldroute,flditem,flddose,fldfreq,flddays,fldqtydisp,flduserid_order,fldid,fldid,fldstarttime from tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and fldstatus=&3 and fldorder=&4 and fldcurval=&5 and flddispmode=&6"
    End Select
    $rData1 = modDatabase.$myConn.Exec(sql, $encid, False, $status, "UseOwn", "Continue", $Dept)

  Endif
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(GridView4, $rData1, $aMyFields1)
  ResizeGrid()

End

Private Sub ResizeGrid()

  With GridView4
    .Columns[0].Hidden = True
    .Columns[1].Hidden = True
    .Columns[2].Hidden = True
    .Columns[3].Expand = True
    .Columns[4].Hidden = True
    .Columns[5].Hidden = True
    .Columns[6].Hidden = True
    .Columns[7].Width = CStr(35 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Hidden = True
    .Columns[9].Hidden = True
    .Columns[10].Hidden = True
    .Columns[11].Hidden = True

    .Columns[3].Text = "Particulars"
    .Columns[7].Text = "QTY"
  End With

End

Public Sub GridView4_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData1.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData1[$aMyFields1[Column]]

End

Public Sub btnownwty_Click()

  Dim xval As Float

  If GridView4.Selection.Count Then
    $rData1.MoveTo(GridView4.Selection[0])
    xval = InputValue($rData1["flditem"], ("Change Value"), $rData1["fldqtydisp"])
    If xval Then
      modPharmSub.UpdateQTYBefDispensing($rData1["fldid"], xval)
      ShowUsedGridViewList()
    Endif
  Endif

End

Public Sub btnowndelete_Click()

  Dim res As Result

  If GridView4.Selection.Count Then
    $rData1.MoveTo(GridView4.Selection[0])
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then

      res = modDatabase.$myConn.Edit("tblpatdosing", "fldid=&1 and fldsave_order=&2", $rData1["fldid"], False)
      If modGeneral.AllowClinicalEdit(res["flduserid_order"]) = True Then
        modDatabase.$myConn.Delete("tblpatdosing", "fldid=&1 and fldsave_order=&2", $rData1["fldid"], False)
      Else
        res["fldcurval"] = "Cancelled"
        res["flduserid_order"] = modBasic.$lbluser
        res["xyz"] = False
        res.Update
      Endif
      ShowUsedGridViewList()

    Endif
  Endif

End

Public Sub btnsave_Click()

  Dim Row As Integer
  Dim xstatus As String

  If chkcomplete.Value = True Then
    xstatus = "Completed"
  Else
    xstatus = "Continue"
  Endif

  If GridView4.Count Then
    If chksaveall.Value = True Then
      For Row = 0 To GridView4.Count - 1
        $rData1.MoveTo(Row)
        SaveOwnDispensing($rData1["fldid"], xstatus)
      Next
    Else
      If GridView4.Selection.Count Then
        $rData1.MoveTo(GridView4.Selection[0])
        SaveOwnDispensing($rData1["fldid"], xstatus)
      Endif
    Endif

    ShowUsedGridViewList()
  Endif

End

Private Sub SaveOwnDispensing(sID As Long, sStatus As String)

  Dim sql1 As String
  Dim res1 As Result
  Dim hForm As CPharmOwn

  sql1 = "select fldid,fldtime,fldroute,flditem,flddose,fldfreq,flddays,fldqtydisp,fldlabel,flditemtype,fldfixname,fldfixrate,fldtaxper,flddiscper,fldbillingmode,flddisctype,fldacledger,fldbilltype,fldcashincredit from tblpatdosing where fldid=&1 and fldencounterval=&2"                                                   ''
  res1 = modDatabase.$myConn.Exec(sql1, sID, $encid)
  If res1.Available = True Then
    hForm = New CPharmOwn($encid, $sBillMode, res1)
    If $sClaimState = "Consultation" Or If $sClaimState = "Patient Ward" Then
      GetHImedicineCappingVar()
    Endif
    hForm.SetCappingLimit($HiCappingVar)
    hForm.DispenseItem(sStatus)
  Endif

End
