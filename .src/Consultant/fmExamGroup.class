' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub _new()

  If modHelpVariable.$LogInCategory = "Clinician" Then
    cmbtarget.Text = modBasic.$compID
    cmbtarget.Enabled = False
    btnrefresh_Click()
  Else
    cmbtarget.List = modBasic.$AllCompPerList
    cmbtarget.Enabled = True
  Endif
  cmbgroupname.List = GetGroupList()
  cmbexamname.List = modMedicine.FillExamQualiOrQuantiCombo("%")

End

Public Sub btnrefresh_Click()

  If cmbtarget.Text Then
    ShowDataGrid()
  Endif

End

Private Function GetGroupList() As String[]

  Dim xxx As String[]

  xxx = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldgroupname) as col from tblgroupexam"))
  Return xxx

End

Private Sub ShowDataGrid()

  $rData = modDatabase.$myConn.Exec("select fldid,fldgroupname,fldexamid from tblgroupexam where fldcomp=&1", cmbtarget.Text)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(250 * modBasic.$AppWidthRatio) & "px"

    .Columns[1].Text = "GroupName"
    .Columns[2].Text = "Examination"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]

End

Public Sub GridView1_Select()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    cmbgroupname.Text = $rData["fldgroupname"]
    cmbexamname.Text = $rData["fldexamid"]
  Endif

End

Public Sub btnOK_Click()

  Dim res As Result

  If cmbexamname.Text And If cmbtarget.Text And If cmbgroupname.Text Then
    res = modDatabase.$myConn.Create("tblgroupexam")
    res["fldgroupname"] = cmbgroupname.Text
    res["fldexamid"] = cmbexamname.Text
    res["fldcomp"] = cmbtarget.Text
    res.Update
    ShowDataGrid()
    cmbgroupname.List = GetGroupList()
    cmbexamname.Text = ""
  Endif

End

Public Sub btnCancel_Click()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    modDatabase.$myConn.Delete("tblgroupexam", "fldid=&1", $rData["fldid"])
    ShowDataGrid()
    Me.Exec("Toastify({text: 'Information deleted', duration: 3000}).showToast()")
  Endif

End

Public Sub btnupload_Click()

  Dim xdelim As String

  If txtdelim.Text Then
    xdelim = txtdelim.Text
  Else
    xdelim = ";"
  Endif
  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, "tblgroupexam", txtcsv.Text, xdelim)
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
