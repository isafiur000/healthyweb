' Gambas class file

Private $encid As String
Private $rData As Result
Private $aMyFields As String[]
Private $rData1 As Result
Private $aMyFields1 As String[]
Private $CategList As String[]

Public Sub _new(encid As String)

  $encid = encid
  modGeneralMain.GetOpenModalForm(Me)

  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtgender.Text = modPatient.GetPatientSex($encid)
  If modBasic.$ClinDietApprove = "Disable" Then
    btnsave.Visible = False
  Else
    btnsave.Visible = True
  Endif

  cmbfreq.List = modMedicine.DayFrequencyCombo()
  $CategList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldfoodtype) as col from tblfoodtype ORDER BY fldfoodtype"))
  $CategList.Add("Group")
  $CategList.Add("Mixture")
  cmbsavetype.List = ["Continue", "Discontinue"]
  cmbsavetype.Text = "Continue"
  dttoday.Value = Now()
  dttodaysave.Value = Date()
  txtduration.Value = 1
  FillGridView()
  ' dttoday.SetFocus

End

Public Sub TabPanel1_Click()

  If TabPanel1.Index = 0 Then
    FillGridView()
  Else If TabPanel1.Index = 1 Then
    SHowContinuePlan()
  Endif

End

Public Sub btnseltype_Click()

  If $CategList And If $CategList.Count Then
    cmbtype.Text = GridViewNew("Select Category", $CategList, False)
  Endif

End

Public Sub chkmixture_Click()

  cmbitem.Text = ""
  If chkmixture.Value = True Then
    cmbtype.Enabled = False
    btnseltype.Enabled = False
    cmbtype.Text = "Mixture"
  Else
    cmbtype.Enabled = True
    btnseltype.Enabled = True
    cmbtype.Text = ""
  Endif

End

Private Function GetComboList() As String[]

  Dim xx As String[]

  If chkmixture.Value = True Then
    xx = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldgroup) as col from tblfoodgroup where (fldstatus=&1 or fldstatus IS NULL)", "Active"))
  Else
    If cmbtype.Text = "Mixture" Then
      xx = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldgroup) as col from tblfoodgroup"))
    Else If cmbtype.Text = "Group" Then
      xx = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldgroup) as col from tbldietgroup"))
    Else
      xx = modMedicine.GetFoodSelectedType(cmbtype.Text)
    Endif
  Endif

  Return xx

End

Public Sub btnselitem_Click()

  Dim xlist As String[]

  xlist = GetComboList()
  If xlist.Count Then
    cmbitem.Text = GridViewNew("Select Parameter", xlist, False)
    GetItemUnit()
    If modBasic.$TabletModeEnable = "Yes" Then
    Else
      txtdose.SetFocus
    Endif
  Endif

End

Private Sub GetItemUnit()

  If cmbitem.Text Then
    If cmbtype.Text = "Group" Then
      txtunit.Text = "Units"
    Else If cmbtype.Text = "Mixture" Then
      txtunit.Text = "Units"
    Else
      txtunit.Text = "Grams"
    Endif
  Endif

End

Private Function GetDateVal(xCur As Date, sHour As Date) As Date

  Dim xdate As Date

  xdate = Date(Year(xCur), Month(xCur), Day(xCur), Hour(sHour), 0, 0, 0)
  Return xdate

End

Private Function GetDateList(xStart As Date, sFreq As String, sDays As Float) As Date[]

  Dim lstdate As Date
  Dim dayLst As Date[]
  Dim xday As Integer
  Dim newday As Date

  Dim dtList As Date[]
  Dim yHr As Integer
  Dim xHr As Integer
  Dim newdt As Date
  Dim newdtLst As Date[]

  If sFreq = "stat" Then
    newdtLst = [Now()]
  Else

    lstdate = modDate.EndSqlDate(DateAdd(xStart, gb.Day, CInt(sDays - 1)))
    dayLst = New Date[]
    If sFreq = "Alt day" Then
      For xday = 0 To Floor((sDays - 1) / 2)
        newday = DateAdd(xStart, gb.Day, xday * 2)
        If newday <= lstdate Then
          dayLst.Add(newday)
        Endif
      Next
    Else If sFreq = "Third day" Then
      For xday = 0 To Floor((sDays - 1) / 3)
        newday = DateAdd(xStart, gb.Day, xday * 3)
        If newday <= lstdate Then
          dayLst.Add(newday)
        Endif
      Next
    Else If sFreq = "Fourth day" Then
      For xday = 0 To Floor((sDays - 1) / 4)
        newday = DateAdd(xStart, gb.Day, xday * 4)
        If newday <= lstdate Then
          dayLst.Add(newday)
        Endif
      Next
    Else If sFreq = "Weekly" Then
      For xday = 0 To Floor((sDays - 1) / 7)
        newday = DateAdd(xStart, gb.Day, xday * 7)
        If newday <= lstdate Then
          dayLst.Add(newday)
        Endif
      Next
    Else
      For xday = 0 To sDays - 1
        newday = DateAdd(xStart, gb.Day, xday)
        If newday <= lstdate Then
          dayLst.Add(newday)
        Endif
      Next
    Endif

    dtList = modPharmLabel.GetDosingTiming(sFreq)
    newdtLst = New Date[]
    For yHr = 0 To dayLst.Count - 1
      For xHr = 0 To dtList.Count - 1
        newdt = GetDateVal(dayLst[yHr], dtList[xHr])
        If newdt >= xStart Then
          newdtLst.Add(newdt)
        Endif
      Next
    Next

  Endif
  Return newdtLst

End

Public Sub btnadd_Click()

  Dim xstart As Date
  Dim dtList As Date[]
  Dim xdate As Date
  Dim res As Result
  Dim res1 As Result

  If cmbtype.Text And If cmbitem.Text And If txtdose.Value And If cmbfreq.Text And If txtduration.Value Then
    xstart = dttoday.Value
    dtList = GetDateList(xstart, cmbfreq.Text, txtduration.Value)

    If cmbtype.Text = "Group" Then
      res1 = modDatabase.$myConn.Exec("select flditemtype,flditemname,flditemqty from tbldietgroup where fldgroup=&1", cmbitem.Text)
      If res1.Available Then
        For Each res1
          For Each xdate In dtList

            res = modDatabase.$myConn.Create("tblextradosing")
            res["fldencounterval"] = $encid
            res["fldcategory"] = res1["flditemtype"]
            res["flditem"] = res1["flditemname"]
            res["flddose"] = txtdose.Value * res1["flditemqty"]
            res["fldfreq"] = ""
            res["fldtype"] = "Input Food/Fluid"
            res["flddosetime"] = xdate
            res["flddosecode"] = cmbitem.Text
            res["fldstatus"] = "Planned" '"Continue"
            res["flduserid"] = modBasic.$lbluser
            res["fldtime"] = Now()
            res["fldcomp"] = modBasic.$compID
            res["fldsave"] = False
            res["xyz"] = True
            res.Update

          Next
        Next
      Endif

    Else
      For Each xdate In dtList
        res = modDatabase.$myConn.Create("tblextradosing")
        res["fldencounterval"] = $encid
        res["fldcategory"] = cmbtype.Text
        res["flditem"] = cmbitem.Text
        res["flddose"] = txtdose.Value
        res["fldfreq"] = ""
        res["fldtype"] = "Input Food/Fluid"
        res["flddosetime"] = xdate
        res["flddosecode"] = ""
        res["fldstatus"] = "Planned" '"Continue"
        res["flduserid"] = modBasic.$lbluser
        res["fldtime"] = Now()
        res["fldcomp"] = modBasic.$compID
        res["fldsave"] = False
        res["xyz"] = True
        res.Update
      Next

    Endif

    cmbtype.Text = ""
    cmbitem.Text = ""
    txtdose.Value = 0
    txtdose.Tag = ""
    FillGridView()
  Endif

End

Private Sub FillGridView()

  Dim sql As String

  sql = "select fldid,fldcategory,flditem,flddose,flddosetime,fldid,flddosetime,fldstatus from tblextradosing where fldencounterval=&1 and fldsave=&2"                        ''
  $rData1 = modDatabase.$myConn.Exec(sql, $encid, False)
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(GridView1, $rData1, $aMyFields1)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Hidden = True
    .Columns[2].Width = CStr(250 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Hidden = True
    .Columns[6].Hidden = True
    .Columns[7].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"

    .Columns[2].Text = "Particulars"
    .Columns[3].Text = "Dose"
    .Columns[4].Text = "DateTime"
    .Columns[7].Text = "Status"
  End With
  GetDietArray()

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData1.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 4 Then
    Data.Text = modReportVar.GetDateTimeReport($rData1[$aMyFields1[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData1[$aMyFields1[Column]]
  Endif

End

Public Sub btndosedel_Click()

  If GridView1.Selection.Count Then
    $rData1.MoveTo(GridView1.Selection[0])
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$myConn.Delete("tblextradosing", "fldid=&1", $rData1["fldid"])
      FillGridView()
    Endif
  Endif

End

Public Sub btndoseqty_Click()

  Dim res As Result
  Dim xval As Float

  If GridView1.Selection.Count Then
    $rData1.MoveTo(GridView1.Selection[0])
    xval = InputValue(("Update Quantity of ") & $rData1["flditem"], "Diet Plan", $rData1["flddose"])
    If xval Then
      res = modDatabase.$myConn.Edit("tblextradosing", "fldid=&1", $rData1["fldid"])
      res["flddose"] = xval
      res.Update
      FillGridView()
    Endif
  Endif

End

Public Sub btndosetime_Click()

  Dim res As Result
  Dim xdate As Date

  If GridView1.Selection.Count Then
    $rData1.MoveTo(GridView1.Selection[0])
    xdate = GetDateValue(("Update Administration Date Time"), "Diet Plan", $rData1["flddosetime"])
    If xdate Then
      res = modDatabase.$myConn.Edit("tblextradosing", "fldid=&1", $rData1["fldid"])
      res["flddosetime"] = xdate
      res.Update
      FillGridView()
    Endif
  Endif

End

Public Sub btndosestatus_Click()

  Dim res As Result
  Dim xstr As String

  If GridView1.Selection.Count Then
    $rData1.MoveTo(GridView1.Selection[0])
    xstr = InputCombo(("Update Status"), "Diet Plan", ["On Hold", "Planned"], $rData1["fldstatus"], True)
    If xstr Then
      res = modDatabase.$myConn.Edit("tblextradosing", "fldid=&1", $rData1["fldid"])
      res["fldstatus"] = xstr
      res.Update
      FillGridView()
    Endif
  Endif

End

Public Sub btnsave_Click()

  Dim res As Result

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    res = modDatabase.$myConn.Edit("tblextradosing", "fldencounterval=&1 and fldstatus=&2 and flddosetime>=&3 and flddosetime<=&4 and fldsave=&5", $encid, "Planned", modDate.StartSqlDate(dttoday.value), modDate.EndSqlDate(dttoday.value), False)                   ''
    If res.Available Then
      For Each res
        res["fldstatus"] = "Continue"
        res["flduserid"] = modBasic.$lbluser
        res["fldtime"] = Now()
        res["fldcomp"] = modBasic.$compID
        res["fldsave"] = True
        res["xyz"] = False
        res.Update
      Next
      FillGridView()
    Endif
  Endif

End

Public Sub btnreport_Click()

  Dim xPath As String

  xPath = modCHTMLPatient.ShowPatientInputPlan($encid, dttoday.Value, "Planned")
  modControlSub.OpenHTMLPreview($encid, xPath, "ReportSize")

End

Public Sub btncalcul_Click()

  Dim hForm As FmDietCalc

  hForm = New FmDietCalc($encid, modDate.StartSqlDate(dttoday.value), modDate.EndSqlDate(dttoday.value), "Planned")
  hForm.ShowModal

End

''============================ Saved but pending =================================
Public Sub btnrefreshsave_Click()

  SHowContinuePlan()

End

Private Sub SHowContinuePlan()

  Dim sql As String
  Dim xcalori As Float
  Dim xfluid As Float

  If cmbsavetype.Text Then
    sql = "select fldid,fldcategory,flditem,flddose,flddosetime,fldid,flddosetime,fldstatus from tblextradosing where fldencounterval=&1 and fldstatus=&2 and flddosetime>=&3 and flddosetime<=&4 and fldsave=&5"                        ''
    $rData = modDatabase.$myConn.Exec(sql, $encid, cmbsavetype.Text, modDate.StartSqlDate(dttodaysave.value), modDate.EndSqlDate(dttodaysave.value), True)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView2, $rData, $aMyFields)

    xcalori = 0
    xfluid = 0
    For Each $rData
      xcalori = xcalori + modFixPatho.GetNutritionValue($rData["fldcategory"], $rData["flditem"], $rData["flddose"], "fldenergy")
      xfluid = xfluid + modFixPatho.GetNutritionValue($rData["fldcategory"], $rData["flditem"], $rData["flddose"], "fldfluid")
    Next
    txtcaloriesave.Value = Round(xcalori, -3)
    txtfluidsave.Value = Round(xfluid, -3)

    With GridView2
      .Columns[0].Hidden = True
      .Columns[1].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
      .Columns[2].Width = CStr(300 * modBasic.$AppWidthRatio) & "px"
      .Columns[3].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
      .Columns[4].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
      .Columns[5].Hidden = True
      .Columns[6].Hidden = True
      .Columns[7].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"

      .Columns[1].Text = "Type"
      .Columns[2].Text = "Particulars"
      .Columns[3].Text = "Dose"
      .Columns[4].Text = "Time"
      .Columns[7].Text = "Status"
    End With

  Endif

End

Public Sub GridView2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 4 Then
    Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.MediumTime)
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub btnsavedelete_Click()

  Dim res As Result

  If GridView2.Selection.Count Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      $rData.MoveTo(GridView2.Selection[0])
      res = modDatabase.$myConn.Edit("tblextradosing", "fldid=&1", $rData["fldid"])
      res["fldstatus"] = "Discontinue"
      res["xyz"] = False
      res.Update
      SHowContinuePlan()
    Endif
  Endif

End

Public Sub btnreportsave_Click()

  Dim xPath As String

  xPath = modCHTMLPatient.ShowPatientInputPlan($encid, dttodaysave.Value, "Pending")
  modControlSub.OpenHTMLPreview($encid, xPath, "ReportSize")

End

Public Sub btnsavecalc_Click()

  Dim hForm As FmDietCalc

  hForm = New FmDietCalc($encid, modDate.StartSqlDate(dttodaysave.value), modDate.EndSqlDate(dttodaysave.value), "Pending")
  hForm.ShowModal

End

''=================== Procedures =================
Private Sub GetDietArray()

  Dim xcalori As Float
  Dim xfluid As Float
  Dim xprot As Float
  Dim xsugar As Float
  Dim xfat As Float
  Dim xmineral As Float
  Dim xfiber As Float
  Dim xcalcium As Float
  Dim xphospho As Float
  Dim xiron As Float
  Dim xcarotene As Float
  Dim xthiam As Float
  Dim xribofla As Float
  Dim xniacin As Float
  Dim xpyridox As Float
  Dim xfreefoli As Float
  Dim xtotfoli As Float
  Dim xvitc As Float

  xcalori = 0
  xfluid = 0
  xcalori = 0
  xfluid = 0
  xprot = 0
  xsugar = 0
  xfat = 0
  xmineral = 0
  xfiber = 0
  xcalcium = 0
  xphospho = 0
  xiron = 0
  xcarotene = 0
  xthiam = 0
  xribofla = 0
  xniacin = 0
  xpyridox = 0
  xfreefoli = 0
  xtotfoli = 0
  xvitc = 0

  For Each $rData1
    xcalori = xcalori + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldenergy")
    xfluid = xfluid + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldfluid")
    xprot = xprot + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldprotein")
    xsugar = xsugar + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldsugar")
    xfat = xfat + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldlipid")
    xmineral = xmineral + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldmineral")
    xfiber = xfiber + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldfibre")
    xcalcium = xcalcium + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldcalcium")
    xphospho = xphospho + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldphosphorous")
    xiron = xiron + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldiron")
    xcarotene = xcarotene + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldcarotene")
    xthiam = xthiam + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldthiamine")
    xribofla = xribofla + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldriboflavin")
    xniacin = xniacin + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldniacin")
    xpyridox = xpyridox + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldpyridoxine")
    xfreefoli = xfreefoli + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldfreefolic")
    xtotfoli = xtotfoli + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldtotalfolic")
    xvitc = xvitc + modFixPatho.GetNutritionValue($rData1["fldcategory"], $rData1["flditem"], $rData1["flddose"], "fldvitaminc")
  Next
  txcalori.Value = Round(xcalori, -3)
  txfluid.Value = Round(xfluid, -3)
  txprot.Value = Round(xprot, -3)
  txsugar.Value = Round(xsugar, -3)
  txfat.Value = Round(xfat, -3)
  txmineral.Value = Round(xmineral, -3)
  txfiber.Value = Round(xfiber, -3)
  txcalcium.Value = Round(xcalcium, -3)
  txphospho.Value = Round(xphospho, -3)
  txiron.Value = Round(xiron, -3)
  txcarotene.Value = Round(xcarotene, -3)
  txthiam.Value = Round(xthiam, -3)
  txribofla.Value = Round(xribofla, -3)
  txniacin.Value = Round(xniacin, -3)
  txpyridox.Value = Round(xpyridox, -3)
  txfreefoli.Value = Round(xfreefoli, -3)
  txtotfoli.Value = Round(xtotfoli, -3)
  txvitc.Value = Round(xvitc, -3)

End

Public Sub btnclose_Click()

  Me.Close()

End
