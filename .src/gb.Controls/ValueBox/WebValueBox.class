' Gambas class file

Export
Inherits WebControl

Public Const _Properties As String = "*,Border=True,ReadOnly,PlaceHolder,Value"
Public Const _DrawWith As String = "TextBox"
Public Const _DefaultSize As String = "24,4"
Public Const _DefaultEvent As String = "Change"

Event Change
Event Activate

Property Value As Float
Property ReadOnly As Boolean
Property PlaceHolder As String
'Property Button As String

Private $sValue As Float
Private $bReadOnly As Boolean
Private $sPlaceHolder As String
'Private $sButton As String

Public Sub _new()

  Me._Proxy = ":entry"

End

Private Function Value_Read() As Float

  Return $sValue

End

Private Sub SetText(Value As Float, Optional bNoRefresh As Boolean)

  If $sValue = Value Then Return

  $sValue = Value

  If Not bNoRefresh And If Me._CanRefresh() Then WebForm._AddJavascript("$_(" & JS(Me.Name & ":entry") & ").value = " & JS($sValue))

  Raise Change

End

Private Sub Value_Write(Value As Float)

  SetText(Value)

End

Public Sub Copy()

  WebForm._AddJavascript("gw.textbox.copy(" & JS(Me.Name) & ")")

End

Public Sub _Render()

  Print "<input id=\""; Me.Name; ":entry\"";
  Print " type=\"number\""; Me._GetUpdateJS("onblur", "text", "this.value"); 'Me._GetUpdateJS("oninput", "text", "this.value"); '
  If Object.CanRaise(Me, "Change") Then Print " oninput=\"gw.textbox.onChange("; JS(Me.Name); ");\"";
  If Object.CanRaise(Me, "Activate") Then Print " onkeydown=\"gw.textbox.onActivate("; JS(Me.Name); ",event);\"";

  ' ' Me._RenderStyleSheet()
  If Not Me.Enabled Then Print " disabled";
  If $bReadOnly Then Print " readonly";
  If $sPlaceHolder Then Print " placeholder=\""; Html($sPlaceHolder); "\"";
  Print " value=\""; Html($sValue); "\"";
  Print ">"

End

Private Function ReadOnly_Read() As Boolean

  Return $bReadOnly

End

Private Sub ReadOnly_Write(Value As Boolean)

  $bReadOnly = Value
  Me.Refresh

End

Public Sub _UpdateProperty(sProp As String, vValue As Variant)

  If sProp = "text" Then
    If Not Len(vValue) Then
      SetText(0)
    Else
      SetText(vValue)
    Endif
  Else If sProp = "change" Then
    If Not Len(vValue) Then
      SetText(0, True)
    Else
      SetText(vValue, True)
    Endif
  Endif

End

Private Function PlaceHolder_Read() As String

  Return $sPlaceHolder

End

Private Sub PlaceHolder_Write(Value As String)

  $sPlaceHolder = Value
  Me.Refresh

End
