' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Private $rData1 As Result
Private $aMyFields1 As String[]

Private $Dept As String
Private $sMode As String
Private $sYear As String
Private $sValue As String

Private $tblpatlabtest As String
Private $tblpatlabsubtest As String

Public Sub Run(sDept As String, sMode As String, sYear As String) As String

  $Dept = sDept
  $sMode = sMode
  $sYear = sYear

  cmbflag.List = ["%", "Normal", "Abnormal"]
  cmbflag.Text = "%"
  dtsample.Value = Date()
  chkdate.Value = True
  modLabSub.DisplayDefaultTestUnit(rbsi, rbmetric)
  LoadTableNames()
  FillPharmGrid()

  If Me.ShowModal() Then Return $sValue

End

Public Sub Form_Open()

  txtsearch.SetFocus

End

Private Sub LoadTableNames()

  Dim res As Result

  If $sYear = "Current" Then
    $tblpatlabtest = "tblpatlabtest"
    $tblpatlabsubtest = "tblpatlabsubtest"
  Else
    res = modDatabase.$syConn.Exec("select fldpatlabtest,fldpatlabsubtest from tblfisclosing where fldindex=&1 and (fldstate=&2 or fldstate IS NULL)", $sYear, "Active")
    If res.Available Then
      If res["fldpatlabtest"] Then
        $tblpatlabtest = res["fldpatlabtest"]
      Else
        $tblpatlabtest = "tblpatlabtest"
      Endif
      If res["fldpatlabsubtest"] Then
        $tblpatlabsubtest = res["fldpatlabsubtest"]
      Else
        $tblpatlabsubtest = "tblpatlabsubtest"
      Endif
    Else
      $tblpatlabtest = "tblpatlabtest"
      $tblpatlabsubtest = "tblpatlabsubtest"
    Endif
  Endif

End

Public Sub btnmenu_Menu()

  mnufile.Popup(btnmenu)

End

Public Sub dtnepfir_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtsample.Value))
  If xx Then
    dtsample.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub chkdate_Click()

  If chkdate.Value = True Then
    dtsample.Enabled = True
    dtnepfir.Enabled = True
  Else If chkdate.Value = False Then
    dtsample.Enabled = False
    dtnepfir.Enabled = False
  Endif

End

Public Sub txtsearch_Change()

  FillPharmGrid()

End

Public Sub btnrefresh_Click()

  FillPharmGrid()

End

Private Sub FillPharmGrid()

  Dim xx As String
  Dim abnm As Boolean
  Dim xstr As String
  Dim datstr As String

  If cmbflag.Text = "%" Then
    xstr = ""
  Else
    xstr = " and fldabnormal=&5"
    If cmbflag.Text = "Normal" Then
      abnm = False
    Else If cmbflag.Text = "Abnormal" Then
      abnm = True
    Endif
  Endif

  If Not txtsearch.Text Then
    xx = "%"
  Else
    xx = Trim(txtsearch.Text)
  Endif

  If $sMode = "Test" Then
    If chkdate.Value = True Then
      datstr = " and fldtime_sample>=&6 and fldtime_sample<=&7"
    Else
      datstr = ""
    Endif
    If $Dept = "%" Then
      $rData = modDatabase.$myConn.Exec("select fldid,fldencounterval,fldencounterval,fldtestid,fldsampletype,fldabnormal,fldid,flvisible,fldtime_sample,fldstatus,fldtime_report,fldsampleid from " & $tblpatlabtest & " where lower(fldtestid) like &1 and fldsave_report=&2 and fldstatus=&3 and fldsave_verify=&4" & xstr & datstr, LCase(txtsearch.Text) & "%", True, "Reported", False, abnm, modDate.StartSqlDate(dtsample.value), modDate.EndSqlDate(dtsample.value))
    Else
      $rData = modDatabase.$myConn.Exec("select fldid,fldencounterval,fldencounterval,fldtestid,fldsampletype,fldabnormal,fldid,flvisible,fldtime_sample,fldstatus,fldtime_report,fldsampleid from " & $tblpatlabtest & " where lower(fldtestid) like &1 and fldsave_report=&2 and fldstatus=&3 and fldsave_verify=&4" & xstr & datstr & " and fldtestid in(select fldtestid from tbltest where fldcategory like &8)", LCase(txtsearch.Text) & "%", True, "Reported", False, abnm, modDate.StartSqlDate(dtsample.value), modDate.EndSqlDate(dtsample.value), $Dept)                                                   ''
    Endif
  Else If $sMode = "Radio" Then
    If chkdate.Value = True Then
      datstr = " and fldtime_report>=&6 and fldtime_report<=&7"
    Else
      datstr = ""
    Endif
    If $Dept = "%" Then
      $rData = modDatabase.$myConn.Exec("select fldid,fldencounterval,fldencounterval,fldtestid,fldsampletype,fldabnormal,fldid,flvisible,fldtime_report,fldstatus,fldtime_report,fldmethod from tblpatradiotest where lower(fldtestid) like &1 and fldsave_report=&2 and fldstatus=&3 and fldsave_verify=&4" & xstr & datstr, LCase(txtsearch.Text) & "%", True, "Reported", False, abnm, modDate.StartSqlDate(dtsample.value), modDate.EndSqlDate(dtsample.value))
    Else
      $rData = modDatabase.$myConn.Exec("select fldid,fldencounterval,fldencounterval,fldtestid,fldsampletype,fldabnormal,fldid,flvisible,fldtime_report,fldstatus,fldtime_report,fldmethod from tblpatradiotest where lower(fldtestid) like &1 and fldsave_report=&2 and fldstatus=&3 and fldsave_verify=&4" & xstr & datstr & " and fldtestid in(select fldtestid from tbltest where fldcategory like &8)", LCase(txtsearch.Text) & "%", True, "Reported", False, abnm, modDate.StartSqlDate(dtsample.value), modDate.EndSqlDate(dtsample.value), $Dept)                                                   ''
    Endif

  Endif

  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(225 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(20 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[9].Hidden = True
    If $sMode = "Test" Then
      .Columns[10].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    Else If $sMode = "Radio" Then
      .Columns[10].Hidden = True
    Endif
    .Columns[11].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"

    .Columns[1].Text = "EncID"
    .Columns[2].Text = "PatientName"
    .Columns[3].Text = "TestName"
    .Columns[4].Text = "Specimen"
    .Columns[6].Text = "Observation"
    .Columns[7].Text = "Visible"
    If $sMode = "Test" Then
      .Columns[8].Text = "SamplingTime"
      .Columns[10].Text = "ReportingTime"
      .Columns[11].Text = "SampNo"
    Else If $sMode = "Radio" Then
      .Columns[8].Text = "ReportingTime"
      .Columns[11].Text = "Method"
    Endif
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 2 Then
    Data.Text = modPatient.GetPatientNameByEnc($rData[$aMyFields[Column]])
  Else If Column = 5 Then
    Data.Html = modString.GetImageForHTMLGrid(modMisc.GetGridIcon($rData[$aMyFields[Column]]), "75%", "75%")
    Data.Text = ""
  Else If Column = 6 Then
    If $sMode = "Test" Then
      Data.Html = modLabTest.GetLabTestValueString($rData[$aMyFields[Column]], modLabSub.GetTestUnitFromButton(rbsi, rbmetric), True, "")
    Else If $sMode = "Radio" Then
      Data.Html = modRadioTest.GetRadioTestValueString($rData[$aMyFields[Column]], True)
    Endif
  Else If Column = 8 Then
    Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else If Column = 10 Then
    Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub chkselect_Click()

  If chkselect.Value = True Then
    GridView1.SelectAll()
  Else If chkselect.Value = False Then
    GridView1.UnselectAll()
  Endif

End

Public Sub mnuverify_Click()

  Dim Row As Integer

  For Row = 0 To GridView1.Count - 1
    If GridView1.IsSelected(Row) = True Then
      $rData.MoveTo(Row)
      If $sMode = "Test" Then
        modLabSub.UpdateVerifyTestReport($rData["fldid"], $tblpatlabtest)
      Else If $sMode = "Radio" Then
        modRadioSub.UpdateVerifyRadioReport($rData["fldid"])
      Endif
    Endif
  Next
  FillPharmGrid()

End

Public Sub mnumark_Click()

  Dim Row As Integer

  For Row = 0 To GridView1.Count - 1
    If GridView1.IsSelected(Row) = True Then
      $rData.MoveTo(Row)
      If $sMode = "Test" Then
        modLabSub.MarkAsChecked($rData["fldid"], $tblpatlabtest)
      Else If $sMode = "Radio" Then

      Endif
    Endif
  Next
  FillPharmGrid()

End

Public Sub mnureport_Click()

  Dim xx As String

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If $sMode = "Test" Then
      xx = GetRichTextArea($rData["fldtestid"], modLabTest.GetLabTestValueSubString($rData["fldid"], modLabSub.GetTestUnitFromButton(rbsi, rbmetric), True, $tblpatlabtest, $tblpatlabsubtest))
    Else If $sMode = "Radio" Then

    Endif
  Endif

End

Public Sub btnvisible_Click()

  Dim xx As String
  Dim res As Result
  Dim xopt As String[] = ["Visible", "Hidden"]
  Dim sTable As String

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If $sMode = "Test" Then
      sTable = $tblpatlabtest
    Else If $sMode = "Radio" Then
      sTable = "tblpatradiotest"
    Endif

    xx = InputCombo("Set Visibility", $rData["fldtestid"], xopt, $rData["flvisible"], True)
    If xx Then
      res = modDatabase.$myConn.Edit(sTable, "fldid=&1", $rData["fldid"])
      res["flvisible"] = xx
      res["xyz"] = False
      res.Update
      FillPharmGrid()
    Endif
  Endif

End

Public Sub btnsave_Click()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    $sValue = $rData["fldencounterval"]
    Me.Close(True)
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End

''================= sub test grid ================================
Public Sub GridView1_Select()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    txtagesex.Text = modPatient.GetPatientAgeString($rData["fldencounterval"], Now()) & "/" & modPatient.GetPatientSex($rData["fldencounterval"])
    txtlocation.Text = modPatient.GetPatientLocation($rData["fldencounterval"])
    SHowQualiSUbTest($rData["fldid"])
  Endif

End

Private Sub SHowQualiSUbTest(serial As Long)

  Dim sql As String

  If $sMode = "Test" Then
    sql = "select fldid,fldchk,fldsubtest,fldabnormal,fldreport,fldorder from " & $tblpatlabsubtest & " where fldtestid=&1 and fldsave=&2"
  Else If $sMode = "Radio" Then
    sql = "select fldid,fldchk,fldsubtest,fldabnormal,fldreport,fldorder from tblpatradiosubtest where fldtestid=&1 and fldsave=&2"
  Endif
  $rData1 = modDatabase.$myConn.Exec(sql, serial, True)
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(GridView2, $rData1, $aMyFields1)
  ResizeSubGrid()

End

Public Sub GridView2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData1.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 3 Then
    Data.Html = modString.GetImageForHTMLGrid(modMisc.GetGridIcon($rData1[$aMyFields1[Column]]), "75%", "75%")
    Data.Text = ""
  Else If Column = 4 Then
    Data.Html = $rData1[$aMyFields1[Column]]
  Else
    Data.Text = $rData1[$aMyFields1[Column]]
  Endif

End

Private Sub ResizeSubGrid()

  With GridView2
    .Columns[0].Hidden = True
    .Columns[1].Hidden = True
    .Columns[2].Width = CStr(250 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(25 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(500 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Hidden = True
    .Columns[2].Text = "SubTest"
    .Columns[4].Text = "Observation"
  End With

End
