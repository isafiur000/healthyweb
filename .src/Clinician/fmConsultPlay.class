' Gambas class file

Private $rData2 As Result
Private $aMyFields2 As String[]
Private $encid As String
Private $sDeprt As String

Public Sub _new(encid As String, sDeprt As String)

  $encid = encid
  $sDeprt = sDeprt
  modGeneralMain.GetOpenModalForm(Me)
  CompleteGrid()

End

Private Sub CompleteGrid()

  Dim sql As String
  Dim xFldList1 As String[]
  Dim xFldList2 As String[]

  xFldList1 = ["fldid", "fldconsulttime", "fldencounterval", "fldconsultname", "flduserid", "fldid"]
  xFldList1.Add(Quote("tblconsult"))
  xFldList2 = ["fldid", "fldconsulttime", "fldencounterval", "fldconsultname", "fldstatus", "fldid"]
  xFldList2.Add(Quote("tblopvisit"))
  If $sDeprt Then
    sql = "select " & xFldList1.Join(",") & " from tblconsult where fldencounterval=&1 and fldstatus=&2 and fldconsultname=&3" & " UNION ALL " & "select " & xFldList2.Join(",") & " from tblopvisit where fldencounterval=&1 and fldstatus=&2 and fldconsultname=&3"                        ''
    $rData2 = modDatabase.$myConn.Exec(sql, $encid, "Planned", $sDeprt)
  Else
    sql = "select " & xFldList1.Join(",") & " from tblconsult where fldencounterval=&1 and fldstatus=&2" & " UNION ALL " & "select " & xFldList2.Join(",") & " from tblopvisit where fldencounterval=&1 and fldstatus=&2"
    $rData2 = modDatabase.$myConn.Exec(sql, $encid, "Planned")
  Endif
  $aMyFields2 = New String[]
  modGridView.ReadSmallData(GridView2, $rData2, $aMyFields2)

  With GridView2
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Hidden = True
    .Columns[3].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Hidden = True
    .Columns[6].Hidden = True

    .Columns[1].Text = "DateTime"
    .Columns[3].Text = "Department"
    .Columns[4].Text = "Consultant"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData2.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Text = modReportVar.GetDateTimeReport($rData2[$aMyFields2[Column]], gb.GeneralDate)
  Else If Column = 4 Then
    Data.Text = modGeneral.GetUserFullName($rData2[$aMyFields2[Column]])
  Else
    Data.Text = $rData2[$aMyFields2[Column]]
  Endif

End

Public Sub btnconsultdone_Click()

  Dim hForm As FmOPOutcome

  If GridView2.Selection.Count Then
    $rData2.MoveTo(GridView2.Selection[0])
    If $rData2["tblconsult"] = "tblconsult" Then
      hForm = New FmOPOutcome("Consultation", $rData2["fldencounterval"], $rData2["fldid"])
    Else If $rData2["tblconsult"] = "tblopvisit" Then
      hForm = New FmOPOutcome("OP Visit", $rData2["fldencounterval"], $rData2["fldid"])
    Endif
    hForm.ShowModal
    CompleteGrid()
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
