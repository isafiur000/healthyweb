' Gambas module file

'NOTE: -------------------------------- Supplier -------------------------------------------------------
Public Function FillVendorNameCombo(xStatus As String) As String[]

  Dim xxx As String[]
  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldsuppname from tblsupplier where fldactive like &1", xStatus)
  xxx = modControlSub.GetDirectFillresult(res)

  Return xxx

End

Public Function GetAddFromSuppName(txtSupp As String) As String

  Dim sup As String
  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldsuppaddress from tblsupplier where fldsuppname=&1", txtSupp)
  If res.Available Then
    If res!fldsuppaddress Then
      sup = res!fldsuppaddress
    Else
      sup = ""
    Endif
  Else
    sup = ""
  Endif
  Return sup

End

Public Function GetSupNameByBillRef(purRefNo As String) As String

  Dim res As Result
  Dim xval As String

  res = modDatabase.$myConn.Exec("select fldsuppname from tblpurchasebill where fldreference=&1", purRefNo)
  If res.Available Then
    res.MoveLast
    If res["fldsuppname"] Then
      xval = res["fldsuppname"]
    Else
      xval = ""
    Endif
  Else
    xval = ""
  Endif
  Return xval

End

''--------------------- Purchase/payments --------------------------
Public Function GetTotalPaymentToSupplier(xsuppId As String) As Float

  Dim res As Result
  Dim xval As Float

  xval = 0
  res = modDatabase.$myConn.Exec("select SUM(fldpayamount) as xtot from tblpayment where fldsuppname=&1 and fldsave=&2", xsuppId, True)
  If res.Available Then
    For Each res
      If res["xtot"] Then
        xval = xval + res["xtot"]
      Endif
    Next
  Endif
  Return xval

End

Private Function GetPaymentTotalByRef(xType As String, xsuppId As String, srefNo As String) As Float

  Dim res As Result
  Dim xval As Float

  xval = 0
  res = modDatabase.$myConn.Exec("select SUM(fldpayamount) as xtot from tblpayment where fldcategory=&1 and fldreference=&2 and fldsuppname=&3 and fldsave=&4", xType, srefNo, xsuppId, True)
  If res.Available Then
    For Each res
      If res["xtot"] Then
        xval = xval + res["xtot"]
      Endif
    Next
  Endif
  Return xval

End

Public Function GetSuppPaymentByRef(xsuppId As String, srefNo As String) As Float

  Dim xval As Float

  xval = GetPaymentTotalByRef("Payment", xsuppId, srefNo)
  Return xval

End

Public Function GetSuppCrdNoteByRef(xsuppId As String, srefNo As String) As Float

  Dim xval As Float

  xval = GetPaymentTotalByRef("Credit Note", xsuppId, srefNo)
  Return xval

End
