' Gambas class file

Export
Inherits WebControl

Public Const _Properties As String = "*,Html,PlaceHolder"
Public Const _DrawWith As String = "WebHtml"
'Public Const _DrawWith As String = "Button"

Event Change

Property Html As String
Property PlaceHolder As String

Private $sHTML As String
Private $sPlaceHolder As String

Public Sub _new()

  Me.SetFocus

End

Private Function Html_Read() As String

  Return $sHTML

End

Private Sub Html_Write(Value As String)

  SetText(Value)

End

Public Sub _Render()

  Print $sHTML;

End

Public Sub _BeforeRender()

  Print "<div"; Me._GetClassId(); " contenteditable=\"true\"";
  If Object.CanRaise(Me, "Change") Then Print Me._GetUpdateJS("onchange", "text", "this.innerHTML")
  Print Me._GetUpdateJS("onblur", "text", "this.innerHTML");
  If $sPlaceHolder Then Print " placeholder=\""; Html($sPlaceHolder); "\"";
  Me._RenderStyleSheet()
  Print ">";

End

Public Sub _AfterRender()

  Print "</div>"

End

Public Sub _UpdateProperty(sProp As String, vValue As Variant)

  If sProp = "text" Then
    SetText(vValue, False)
  Else If sProp = "change" Then
    SetText(vValue, True)
  Endif

End

Private Sub SetText(Value As String, Optional bNoRefresh As Boolean)

  If $sHTML = Value Then Return

  $sHTML = Value
  If Not bNoRefresh Then Me.Refresh

  Raise Change

End

Private Function PlaceHolder_Read() As String

  Return $sPlaceHolder

End

Private Sub PlaceHolder_Write(Value As String)

  $sPlaceHolder = Value
  Me.Refresh

End
