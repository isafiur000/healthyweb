' Gambas class file

Private $route As String
Private $injName As String '

Public Sub _new(route As String, injName As String)

  $route = route
  $injName = injName

  If MMain.$IsDrugInfo = True Then
    pnldrugverify.Visible = True
  Endif

  lblroute.Text = $route
  lblinjname.Text = $injName
  ShowInjectionProp()

End

Public Sub btnOK_Click()

  Dim res As Result

  If btnOK.Text = "Add" Then
    txtinjid.Text = lblinjname.Text & "()"
    res = modDatabase.$medConn.Create("tblinjection")
    res["fldinjection"] = txtinjid.Text
    res["fldlabel"] = lblinjname.Text
    res["fldreconst"] = txtreconst.Text
    res["fldreconstroom"] = txtreconroom.Value
    res["fldreconstcool"] = txtreconcool.Value
    res["flddilution"] = txtdilute.Text
    res["flddilutionroom"] = txtdiluroom.Value
    res["flddilutioncool"] = txtdilucool.Value
    res["fldrateadmin"] = txtrate.Text
    res["fldsiteadmin"] = txtsite.Text
    res["fldrouteadmin"] = txtroute.Text
    If MMain.$IsDrugInfo = True Then
      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
      res["fldcomp"] = modBasic.$compID
      res["fldverifyid"] = ""
      res["fldverifytime"] = ""
      res["fldnote"] = ""
    Endif
    res.Update()
    btnOK.Text = "Edit"
    Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay

  Else If btnOK.Text = "Edit" Then
    res = modDatabase.$medConn.Edit("tblinjection", "fldlabel=&1", lblinjname.Text)
    res["fldreconst"] = txtreconst.Text
    res["fldreconstroom"] = txtreconroom.Value
    res["fldreconstcool"] = txtreconcool.Value
    res["flddilution"] = txtdilute.Text
    res["flddilutionroom"] = txtdiluroom.Value
    res["flddilutioncool"] = txtdilucool.Value
    res["fldrateadmin"] = txtrate.Text
    res["fldsiteadmin"] = txtsite.Text
    res["fldrouteadmin"] = txtroute.Text
    If MMain.$IsDrugInfo = True Then
      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
      res["fldcomp"] = modBasic.$compID
      res["fldverifyid"] = ""
      res["fldverifytime"] = ""
      res["fldnote"] = ""
    Endif
    res.Update()
    Me.Exec("Toastify({text: 'Information updated', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Private Sub ShowInjectionProp()

  Dim sql As String
  Dim res As Result

  sql = "select fldinjection,fldreconst,fldreconstroom,fldreconstcool,flddilution,flddilutionroom,flddilutioncool,fldrateadmin,fldsiteadmin,fldrouteadmin from tblinjection where fldlabel=&1"                                         '''
  res = modDatabase.$medConn.Exec(sql, lblinjname.Text)
  If res.Available Then
    txtinjid.Text = res["fldinjection"]
    txtreconst.Text = res["fldreconst"]
    txtreconroom.Value = res["fldreconstroom"]
    txtreconcool.Value = res["fldreconstcool"]
    txtdilute.Text = res["flddilution"]
    txtdiluroom.Value = res["flddilutionroom"]
    txtdilucool.Value = res["flddilutioncool"]
    txtrate.Text = res["fldrateadmin"]
    txtsite.Text = res["fldsiteadmin"]
    txtroute.Text = res["fldrouteadmin"]
    btnOK.Text = "Edit"
  Endif
  FillReconsFluid()
  FillDiluteFluid()

End

Public Sub tlbtnaddreconst_Click()

  Dim hForm As FmRefTextEntry

  If btnOK.Text = "Edit" Then
    hForm = New FmRefTextEntry("Select Reconstitution Fluids", "Reconstitution Fluids", txtinjid.Text)
    hForm.ShowModal
    FillReconsFluid()
  Endif

End

Public Sub tlbtnaddilute_Click()

  Dim hForm As FmRefTextEntry

  If btnOK.Text = "Edit" Then
    hForm = New FmRefTextEntry("Select Dilution Fluids", "Dilution Fluids", txtinjid.Text)
    hForm.ShowModal
    FillDiluteFluid()
  Endif

End

'''ZZZZZZZZZZZZZZZZZZZZZZZZZZZZZZ

Public Sub FillReconsFluid()

  Dim sql As String
  Dim res As Result

  sql = "select fldchild as col from tblreconstfluid where fldparent=&1"
  res = modDatabase.$medConn.Exec(sql, txtinjid.Text)
  modFillContainer.FillListBox(lstreconst, res, False)

End

Public Sub FillDiluteFluid()

  Dim sql As String
  Dim res As Result

  sql = "select fldchild as col from tbldilutionfluid where fldparent=&1"
  res = modDatabase.$medConn.Exec(sql, txtinjid.Text)
  modFillContainer.FillListBox(lstdilut, res, False)

End

Public Sub btnCancel_Click()

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    modDatabase.$medConn.Delete("tblinjection", "fldinjection=&1", txtinjid.Text)
    Me.Exec("Toastify({text: 'Information deleted', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btndelrecons_Click()

  modDatabase.$medConn.Delete("tblreconstfluid", "fldchild=&1 and fldparent=&2", lstreconst.Text, txtinjid.Text)
  FillReconsFluid()
  Me.Exec("Toastify({text: 'Information deleted', duration: 3000}).showToast()")
  ' Balloon.Delay = modBasic.$BalloonDelay

End

Public Sub btndeldilut_Click()

  modDatabase.$medConn.Delete("tbldilutionfluid", "fldchild=&1 and fldparent=&2", lstdilut.Text, txtinjid.Text)
  FillDiluteFluid()
  Me.Exec("Toastify({text: 'Information deleted', duration: 3000}).showToast()")
  ' Balloon.Delay = modBasic.$BalloonDelay

End

Public Sub btnverifysave_Click()

  Dim res As Result

  If lblinjname.Text Then
    res = modDatabase.$medConn.Edit("tblinjection", "fldlabel=&1", lblinjname.Text)
    If chkverify.Value = True Then
      res["fldverifyid"] = modBasic.$lbluser
      res["fldverifytime"] = Now()
      res["fldnote"] = txtverifynote.Text
    Else
      res["fldnote"] = txtverifynote.Text
    Endif
    res.Update
    Me.Exec("Toastify({text: 'Information verified', duration: 3000}).showToast()")
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
