' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $strTitle As String
Private $sCompLst As String[]

Public Sub _new(strTitle As String, aCompLst As String[])

  $strTitle = strTitle
  $sCompLst = aCompLst

  Me.Title = $strTitle
  dtlisted.Value = Now()
  DateSelectionSett()
  ShowGridView()

End

Public Sub chklist_Click()

  DateSelectionSett()

End

Private Sub DateSelectionSett()

  If chklist.Value = True Then
    Panel13.Enabled = True
  Else
    Panel13.Enabled = False
  Endif

End

Public Sub dtneplist_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtlisted.Value))
  If xx Then
    dtlisted.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub txtsearch_Change()

  ShowGridView()

End

Public Sub txtinvoice_Change()

  ShowGridView()

End

Public Sub btnrefresh_Click()

  ShowGridView()

End

Private Sub ShowGridView()

  Dim sql As String
  Dim xstr As String
  Dim sCompLst As String[]
  Dim i As Integer

  Dim xpurchase As Float
  Dim xpayment As Float

  sCompLst = $sCompLst.Copy()
  For i = 0 To sCompLst.Count - 1
    sCompLst[i] = "'" & sCompLst[i] & "'"
  Next
  xstr = " and fldcomp in(" & sCompLst.Join(",") & ")"
  If chklist.Value = True Then
    sql = "select fldid,fldreference,fldpurtype,fldpurdate,fldbillno,fldcategory,fldcredit,flddebit,fldreference,fldreference,fldpurorder,fldcomp from tblpurchasebill where fldsuppname like &1 and lower(fldreference) like &2 and lower(fldbillno) like &3 and fldpurdate>=&4 and fldpurdate<=&5" & xstr
  Else
    sql = "select fldid,fldreference,fldpurtype,fldpurdate,fldbillno,fldcategory,fldcredit,flddebit,fldreference,fldreference,fldpurorder,fldcomp from tblpurchasebill where fldsuppname like &1 and lower(fldreference) like &2 and lower(fldbillno) like &3" & xstr
  Endif
  $rData = modDatabase.$syConn.Exec(sql, $strTitle, LCase(txtsearch.Text) & "%", LCase(txtinvoice.Text) & "%", modDate.StartSqlDate(dtlisted.Value), modDate.EndSqlDate(DateAdd(dtlisted.Value, gb.Day, txtrange.Value)))
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  xpurchase = 0
  xpayment = 0
  For Each $rData
    xpurchase = xpurchase + $rData["fldcredit"]
    xpayment = xpayment + $rData["flddebit"]
  Next
  ResizeGridView()

  txtpurchase.Value = Round(xpurchase, -2)
  txtpayment.Value = Round(xpayment, -2)
  txtpending.Value = Round(xpurchase - xpayment, -2)

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 3 Then
    Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.MediumDate)
  Else If Column = 6 Then
    Data.Text = modReportVar.GetLocaleNumberFormat($rData[$aMyFields[Column]], gb.Currency)
  Else If Column = 7 Then
    Data.Text = modReportVar.GetLocaleNumberFormat($rData[$aMyFields[Column]], gb.Currency)
  Else If Column = 8 Then
    Data.Text = modReportVar.GetLocaleNumberFormat(modSupplier.GetSuppPaymentByRef($strTitle, $rData[$aMyFields[Column]]), gb.Currency)
  Else If Column = 9 Then
    Data.Text = modReportVar.GetLocaleNumberFormat(modSupplier.GetSuppCrdNoteByRef($strTitle, $rData[$aMyFields[Column]]), gb.Currency)
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Private Sub ResizeGridView()

  With GridView1
    .Columns[0].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(175 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Hidden = True
    .Columns[6].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[9].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[10].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[11].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"

    .Columns[0].Text = "SN"
    .Columns[1].Text = "Reference"
    .Columns[2].Text = "PayMode"
    .Columns[3].Text = "Date"
    .Columns[4].Text = "Invoice"
    .Columns[6].Text = "Purchase"
    .Columns[7].Text = "Payments"
    .Columns[8].Text = "Pending"
    .Columns[9].Text = "Credit Note"
    .Columns[10].Text = "Pur Order"
    .Columns[11].Text = "Location"
  End With

End

Public Sub mnudetreport_Click()

  Dim xPath As String

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    xPath = modHTMLStockReport.PurchaseEntryReport($rData["fldreference"], "PurEntry")      '
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub btnprintformat_Click()

  Dim xPath As String

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    xPath = modMaLePaFormat.PurchaseEntryMaLePa($rData["fldreference"], "PurEntry")
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub mnuexpo_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, Me.Title, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub mnueditpo_Click()

  Dim res As Result
  Dim res1 As Result
  Dim xval As String

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If Not $rData["fldpurorder"] Then

      xval = InputBox("Update Purchase Order", "Purchase", $rData["fldpurorder"])
      If xval Then
        res = modDatabase.$myConn.Edit("tblpurchasebill", "fldreference=&1 and fldbillno=&2", $rData["fldreference"], $rData["fldbillno"])
        If res.Available Then
          res1 = modDatabase.$myConn.Edit("tblpurchase", "fldreference=&1 and fldbillno=&2", $rData["fldreference"], $rData["fldbillno"])
          For Each res1
            res1["fldpurorder"] = xval
            res1.Update
          Next
          res["fldpurorder"] = xval
          res.Update
          ShowGridView()
        Endif
      Endif

    Endif
  Endif

End

Public Sub mnuedit_Click()

  Dim res As Result
  Dim res1 As Result
  Dim xval As String

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])

    xval = InputBox("Update Invoice No", "Purchase", $rData["fldbillno"])
    If xval Then
      res = modDatabase.$myConn.Edit("tblpurchasebill", "fldreference=&1 and fldbillno=&2", $rData["fldreference"], $rData["fldbillno"])
      If res.Available Then

        res1 = modDatabase.$myConn.Edit("tblpurchase", "fldreference=&1 and fldbillno=&2", $rData["fldreference"], $rData["fldbillno"])
        For Each res1
          res1["fldbillno"] = xval
          res1.Update
        Next
        res["fldbillno"] = xval
        res.Update
        ShowGridView()
      Endif
    Endif

  Endif

End

Public Sub mnuitems_Click()

  Dim sql As String
  Dim res As Result
  Dim hForm As FRequest

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    sql = "select fldpurdate,fldcategory,fldstockid,fldstockno,fldstockno,flsuppcost,fldqtybonus,fldtotalqty,fldnetcost,fldtotalcost from tblpurchase where fldreference=&1"
    res = modDatabase.$syConn.Exec(sql, $rData["fldreference"])
    If res.Available Then
      hForm = New FRequest(res, $rData["fldreference"], "PurchaseReference", False)
      hForm.ShowModal
    Endif
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, $strTitle, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End
