' Gambas class file

Export
Inherits WebContainer

Public Const _IsContainer As Boolean = False
Public Const _Properties As String = "*,HeaderText,DataValue,LeftValue,RightValue,LeftTitle,RightTitle"

Property DataValue As String
Property LeftValue As Float
Property RightValue As Float
Property HeaderText As String
Property LeftTitle As String
Property RightTitle As String

Private txtleft As WebValueBox
Private txtright As WebValueBox
Private hLeftText As WebLabel
Private hRightText As WebLabel

Public Sub _New()

  Dim hLeft As WebHBox
  Dim hRight As WebHBox

  Me.Arrangement = Arrange.Horizontal

  hRight = New WebHBox(Me)
  hRight.Width = "50%"

  hRightText = New WebLabel(hRight)
  hRightText.Text = "RIGHT"
  DesignLabel(hRightText)

  txtright = New WebValueBox(hRight)
  txtright.Expand = True
  txtright.Height = "2em"

  hLeft = New WebHBox(Me)
  hLeft.Width = "50%"

  hLeftText = New WebLabel(hLeft)
  hLeftText.Text = "LEFT"
  DesignLabel(hLeftText)

  txtleft = New WebValueBox(hLeft)
  txtleft.Expand = True
  txtleft.Height = "2em"

End

Private Sub DesignLabel(xLabel As WebLabel)

  xLabel.Font = "bold"
  xLabel.Border = True
  xLabel.Height = "2em"

End

''------------- Text value ----------------
Private Sub SetDataValue(sText As String)

  Dim valColl As Collection

  If sText Then
    Try valColl = JSON.Decode(sText)
    If valColl Then
      txtright.Value = valColl[hRightText.Text]
      txtleft.Value = valColl[hLeftText.Text]
    Endif
  Endif

End

Private Function GetDataValue() As String

  Dim xxx As String
  Dim xcoll As Collection

  If txtleft.Value Or If txtright.Value Then
    xcoll = New Collection
    xcoll.Add(txtright.Value, hRightText.Text)
    xcoll.Add(txtleft.Value, hLeftText.Text)
    xxx = JSON.Encode(xcoll)
  Else
    xxx = ""
  Endif
  Return xxx

End

Private Function DataValue_Read() As String

  Return GetDataValue()

End

Private Sub DataValue_Write(Value As String)

  SetDataValue(Value)

End

Private Function LeftValue_Read() As Float

  Return txtleft.Value

End

Private Sub LeftValue_Write(Value As Float)

  txtleft.Value = Value

End

Private Function RightValue_Read() As Float

  Return txtright.Value

End

Private Sub RightValue_Write(Value As Float)

  txtright.Value = Value

End

''------------- Text Title ----------------
Public Sub SetHeaderText(sText As String)

  Dim valColl As Collection

  If sText Then
    Try valColl = JSON.Decode(sText)
    If valColl Then
      hRightText.Text = valColl["RIGHT"]
      hLeftText.Text = valColl["LEFT"]
    Endif
  Endif

End

Public Function GetHeaderText() As String

  Dim xxx As String
  Dim xcoll As Collection

  xcoll = New Collection
  xcoll.Add(hRightText.Text, "RIGHT")
  xcoll.Add(hLeftText.Text, "LEFT")
  xxx = JSON.Encode(xcoll)
  Return xxx

End

Private Function LeftTitle_Read() As String

  Return hLeftText.Text

End

Private Sub LeftTitle_Write(Value As String)

  If Value Then
    hLeftText.Text = Value
  Endif

End

Private Function RightTitle_Read() As String

  Return hRightText.Text

End

Private Sub RightTitle_Write(Value As String)

  If Value Then
    hRightText.Text = Value
  Endif

End

Private Function HeaderText_Read() As String

  Return GetHeaderText()

End

Private Sub HeaderText_Write(Value As String)

  SetHeaderText(Value)

End
