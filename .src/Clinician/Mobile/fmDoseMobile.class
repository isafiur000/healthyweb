' Gambas class file

Private aWebFrame As New Object[50]
Private aNameLabel As New Object[50]
Private aHorizPanel As New Object[50]
Private aIndexVal As New Object[50]
Private aRegmLabel As New Object[50]
Private aQuantiBox As New Object[50]
Private aUnitLabel As New Object[50]
Private aDoseLabel As New Object[50]
Private aCountBox As New Object[50]
Private aWeighBox As New Object[50]
Private aBlankFrame As New Object[50]

Private $rData As Result

Public Sub _new(encid As String)

  txtencid.Text = encid

  modGeneralMain.GetOpenModalForm(Me)
  If txtencid.Text Then
    btnshow_Click()
  Else
    pnlfirst.Visible = True
  Endif

End

Public Sub txtencid_Activate()

  btnshow_Click()

End

Public Sub btnshow_Click()

  Dim xx As Boolean

  If txtencid.Text Then
    txtname.Text = modPatient.GetPatientNameByEnc(Trim(txtencid.Text))
    xx = modPatient.EnableClinicForm(modPatient.CurrentAdmissionStatus(Trim(txtencid.Text)))
    LoadControlsWeb()
    btnsaveall.Enabled = xx
  Endif

End

Private Sub LoadControlsWeb()

  Dim i As Integer
  Dim sql As String
  Dim xunit As String

  i = 0
  sql = "select fldid,fldroute,flditem,flddose,fldfreq,flddays,fldstatus from tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and fldstarttime<=&3 and fldendtime>=&4 and fldroute<>&5 and flditemtype=&6 and fldcurval=&7"
  $rData = modDatabase.$myConn.Exec(sql, Trim(txtencid.Text), True, Now(), DateAdd(Now(), gb.Hour, -3), "fluid", "Medicines", "Continue")
  If $rData.Available Then
    For Each $rData
      If i < 49 Then

        aWebFrame[i] = New WebContainer(Frame1)
        aNameLabel[i] = New WebHtml(aWebFrame[i])
        aHorizPanel[i] = New WebContainer(aWebFrame[i])
        aIndexVal[i] = New WebValueBox(aHorizPanel[i])
        aRegmLabel[i] = New WebLabel(aHorizPanel[i])
        aUnitLabel[i] = New WebLabel(aHorizPanel[i])
        aQuantiBox[i] = New WebValueBox(aHorizPanel[i])
        aDoseLabel[i] = New WebLabel(aHorizPanel[i])
        aCountBox[i] = New WebValueBox(aHorizPanel[i])
        aWeighBox[i] = New WebValueBox(aHorizPanel[i])
        aBlankFrame[i] = New WebSeparator(Frame1)

        With aWebFrame[i]
          .Arrangement = Arrange.Vertical
        End With

        With aNameLabel[i]
          .Font = "bold"
          .Text = "<p>" & $rData["flditem"] & "</p>"
          .Tag = i
        End With

        xunit = modPharmLabel.GetDosageFormForLabel($rData["flditem"], "Inpatient")
        With aHorizPanel[i]
          .Arrangement = Arrange.Horizontal
          .Class = "less-container-margin"
        End With

        With aIndexVal[i]
          .Width = "2em"
          .Height = "2em"
          .Value = $rData["fldid"]
          .Visible = False
          .Tag = i
        End With
        With aRegmLabel[i]
          .Expand = True
          .Height = "2em"
          .Border = True
          .Text = $rData["fldroute"] & Space(2) & modMedConstant.GetMedicineRegimenInFormat($rData["fldroute"], $rData["flditem"], $rData["flddose"], $rData["fldfreq"], $rData["flddays"])
          .Tag = $rData["fldfreq"]
        End With
        With aUnitLabel[i]
          .Width = "4em"
          .Height = "2em"
          .Border = True
          .Text = modMedConstant.GetMedicineDoseFormat($rData["fldroute"], $rData["flditem"], $rData["flddose"], modBasic.$MedicineDoseFormat)  ''Round($rData["flddose"] / modMedConstant.GetDrugInitialStrength($rData["flditem"]), -2)
          .Tag = i
        End With
        With aQuantiBox[i]
          .Width = "4em"
          .Height = "2em"
          .Tag = i
        End With
        With aDoseLabel[i]
          .Width = "5em"
          .Height = "2em"
          .Border = True
          .Text = xunit
          .Tag = i
        End With
        With aCountBox[i]
          .Width = "3em"
          .Height = "2em"
          .Enabled = False
          .Value = modPharmacy.TotalDoseCount($rData["fldid"], Now())
          .Tag = i
        End With
        With aWeighBox[i]
          .Width = "5em"
          .Height = "2em"
          .Enabled = False
          .Value = modPharmacy.TotalDoseAmount($rData["fldid"], Now())
          .Tag = i
        End With

        With aBlankFrame[i]
          .Height = "1em"
        End With

      Endif
      i = i + 1
    Next
  Endif

End

Public Sub btnsaveall_Click()

  Dim i As Integer
  Dim res As Result

  For i = 0 To $rData.Count - 1
    If i < 49 Then
      If aIndexVal[i].Value And If aQuantiBox[i].Value Then

        modPharmSub.InsertNurDosing(aIndexVal[i].Value, Trim(txtencid.Text), aQuantiBox[i].Value, aDoseLabel[i].Text, "")
        If aRegmLabel[i].Tag = "stat" Then
          res = modDatabase.$myConn.Edit("tblpatdosing", "fldid=&1", aIndexVal[i].Value)
          res["fldcurval"] = "Completed"
          res.Update
        Endif
        aQuantiBox[i].Value = 0
        aQuantiBox[i].ReadOnly = True

      Endif
    Endif
  Next
  Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information saved", modBasic.$BalloonDelay))

End

Public Sub btnclose_Click()

  Me.Close()

End
