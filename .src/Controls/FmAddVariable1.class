' Gambas class file

Private $colvar As String
Private $colfix As String
Private $tbl As String

Public Sub _new(colvar As String, colfix As String, col As String, tbl As String)

  $colvar = colvar
  $colfix = colfix
  $tbl = tbl
  txtcategory.Text = col

  FilVarList()

End

Public Sub btnOK_Click()

  Dim res As Result

  If txtvariable.Text Then
    res = modDatabase.$myConn.Create($tbl)
    res[$colvar] = Trim(txtvariable.Text)
    res[$colfix] = txtcategory.Text
    res.Update()
    FilVarList()
    txtvariable.Text = ""
    Me.Exec("Toastify({text: 'Variable added', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
    txtvariable.SetFocus
  Endif

End

Private Sub FilVarList()

  Dim sql As String
  Dim res As Result

  sql = Subst("select &1 from &2 where &3", $colvar, $tbl, $colfix)
  res = modDatabase.$myConn.Exec(sql & "=&1", txtcategory.Text)
  modFillContainer.FillListBox(lstvariable, res, False)

End

Public Sub btnCancel_Click()

  If lstvariable.Text Then
    modDatabase.$myConn.Delete($tbl, $colvar & "=&1 and " & $colfix & "=&2", lstvariable.Text, txtcategory.Text)
    FilVarList()
    Me.Exec("Toastify({text: 'Variable deleted', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnupload_Click()

  Dim xxx As String[]
  Dim xitem As String
  Dim res As Result

  If Exist(txtcsv.Text) Then
    xxx = modString.GetStringArrayFromFile(txtcsv.Text)
    For Each xitem In xxx
      If Trim(xitem) Then
        res = modDatabase.$myConn.Create($tbl)
        res[$colvar] = Trim(xitem)
        res[$colfix] = txtcategory.Text
        res.Update()
      Endif
    Next
    FilVarList()
    txtcsv.Text = ""
    Me.Exec("Toastify({text: 'Data Import completed', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
