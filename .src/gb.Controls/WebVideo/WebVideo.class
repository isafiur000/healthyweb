' Gambas class file

Export
Inherits WebControl

Public Const _Properties As String = "*,NoControls,Video{WebVideo},VideoHeight"
Public Const _Group As String = "Special"

Property NoControls As Boolean
Property Video As String
Property VideoHeight As String

Private $NoControls As Boolean
Private $sVideo As String
Private $VideoMime As String
Private $vidHeight As String

Public Sub _new()

End

Private Function Video_Read() As String

  Return $sVideo

End

Private Sub Video_Write(Value As String)

  $sVideo = Value
  $VideoMime = VideoMIMEType($sVideo)
  Me.Refresh

End

Public Sub _BeforeRender()

End

Public Sub _Render()

  If $vidHeight Then
    Print "<video "; Me._GetClassId(); " max-width=\"100%\" height=\""; $vidHeight; "\" "                 ''
  Else
    Print "<video "; Me._GetClassId(); " max-width=\"100%\" height=\"auto\" "
  Endif
  If $NoControls = True Then
    Print " >"
  Else
    Print " controls>"
  Endif

  If $VideoMime Then
    Print "<source src=\""; Html($sVideo); "\" type=\""; $VideoMime; "\">"
  Else
    Print "<source src=\""; Html($sVideo); "\">"
  Endif

  Print "</video>";

End

Private Function VideoHeight_Read() As String

  Return $vidHeight

End

Private Sub VideoHeight_Write(Value As String)

  $vidHeight = Value

End

Private Function NoControls_Read() As Boolean

  Return $NoControls

End

Private Sub NoControls_Write(Value As Boolean)

  $NoControls = Value

End

Private Function VideoMIMEType(sFile As String) As String

  Dim xmime As String

  Select File.Ext(sFile)
    Case "flv"
      xmime = "video/x-flv"
    Case "mpg", "mpeg"
      xmime = "video/mpeg"
    Case "mp4"
      xmime = "video/mp4"
    Case "m3u8"
      xmime = "application/x-mpegURL"
    Case "ts"
      xmime = "video/MP2T"
    Case "3gp"
      xmime = "video/3gpp"
    Case "mov"
      xmime = "video/quicktime"
    Case "avi"
      xmime = "video/x-msvideo"
    Case "wmv"
      xmime = "video/x-ms-wmv"
    Case "ogg"
      xmime = "application/ogg"
    Case "webm"
      xmime = "video/webm"
    Case "mkv"
      xmime = "video/x-matroska"
    Case Else
      xmime = ""
  End Select

  Return xmime

End
