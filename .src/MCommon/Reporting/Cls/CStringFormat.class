' Gambas class file

Private $encid As String
Private $Columns As String[]
Private $TableData As String
Private $TemplatePath As String
Private $RepoType As String
Private $commentText As String
Private $UserData As New Collection
Private $IndexData As String[]
Private $sHide As String

Private TextAlign As String
Private Width As Integer
Private ReportStr As String

Private $FormatColuTitle As String
Private $FormatCOlumnID As Integer

Public Sub _new(encid As String, sColumns As String[], sTableData As String, TempletePath As String, sReportType As String, sComment As String, sUserData As Collection, IndexData As String[], Optional sHide As String)

  $encid = encid
  $Columns = sColumns
  $TableData = sTableData
  $TemplatePath = TempletePath
  $RepoType = sReportType
  $commentText = sComment
  $UserData = sUserData
  $IndexData = IndexData
  $sHide = sHide

  $FormatColuTitle = modSettings.ShowSettingFromFIle("ReportColumnFormat/Title")
  If $FormatColuTitle Then
    $FormatCOlumnID = CInt(modSettings.ShowSettingFromFIle("ReportColumnFormat/ColumnID"))
  Endif
  ReportStr = MakeHTMLFile()

End

Private Function MakeHTMLFile() As String

  Dim xReport As String[]

  xReport = New String[]
  xReport.Add("<section id='printable@section' class='printable@section'>") '''used to locate printable text
  xReport.Add(modReportVar.$CSSStart)
  xReport.Add(TempleteString())
  xReport.Add(modReportVar.$CSSEnd)
  xReport.Add(modReportVar.$BodyStart)
  xReport.Insert(ReportBody())
  xReport.Add(modReportVar.$BodyEnd)
  xReport.Add("</section>")

  Return xReport.Join(gb.NewLine)

End

Public Function GetHTMLString() As String

  Return ReportStr

End

Private Function TempleteString() As String

  Dim xx As String

  If $RepoType = "LaboratoryReport" Then
    xx = File.Load($TemplatePath &/ "labtemplate.tpl")
  Else
    xx = File.Load($TemplatePath &/ "template.tpl")
  Endif
  Return xx

End

Private Function ReportBody() As String[]

  Dim counter As Integer = 0
  Dim colStr As String
  Dim xx As String
  Dim repo As String[]

  Dim xbackpic As String
  Dim xshowlabel As String
  Dim xcollabel As String
  Dim xcolwid As String
  Dim xcolwid_col As String

  repo = New String[]

  If (TextAlign = "") Then
    TextAlign = "left"
  Endif

  If (Width = 0) Then
    Width = 100
  Endif

  xbackpic = modSettings.ShowSettingFromFIle("ReportFormat/ReportBody_BackgroundPicture")
  If xbackpic Then
    xx = " background=" & xbackpic
  Else
    xx = ""
  Endif

  repo.Add(modReportVar.BlankLinesAccSetting("ReportFormat/BlankLines_Header_Body"))
  repo.Add("  <table " & xx & " class='report_table' style='text-align: " & TextAlign & "; width: " & Width & "%;' id='bodytable'>")

  repo.Add("    <thead>")     ''use when webkit bug is solved
  repo.Add("    <tr>")

  xshowlabel = modSettings.ShowSettingFromFIle("ReportFormat/ReportBody_ShowLabel")
  xcolwid = modSettings.ShowSettingFromFIle("ReportFormat/ReportBody_ColWidth")
  For counter = 0 To $Columns.Count - 1
    colStr = $Columns[counter]
    If $FormatColuTitle Then
      If $FormatCOlumnID = counter Then
        colStr = $FormatColuTitle
      Endif
    Endif

    xcollabel = modSettings.ShowSettingFromFIle("ReportFormat/ReportBody_ShowLabel_Column" & CStr(counter + 1))
    If xcollabel And If xcollabel = "No" Then
      colStr = ""
    Else
      If xshowlabel And If xshowlabel = "No" Then
        colStr = ""
      Endif
    Endif

    xcolwid_col = modSettings.ShowSettingFromFIle("ReportFormat/ReportBody_ColWidth_Column" & CStr(counter + 1))
    If xcolwid_col Then
      repo.Add("      <th class='report_table_h' style='width: " & CInt(xcolwid_col) & "%;'>" & colStr & "</th>")
    Else
      If xcolwid Then
        If counter = $Columns.Count - 1 Then
          repo.Add("      <th class='report_table_h' style='width: " & CInt(100 - ($Columns.Count - 1) * xcolwid) & "%;'>" & colStr & "</th>")
        Else
          repo.Add("      <th class='report_table_h' style='width: " & CInt(xcolwid) & "%;'>" & colStr & "</th>")
        Endif
      Else
        repo.Add("      <th class='report_table_h'>" & colStr & "</th>")
      Endif
    Endif

  Next
  repo.Add("    </tr>")
  repo.Add("    </thead>")

  repo.Add("    <tbody>")
  repo.Add($TableData)
  repo.Add("    </tbody>")
  repo.Add("  </table>")
  repo.Add(modReportVar.BlankLinesAccSetting("ReportFormat/BlankLines_Body_Footer"))

  Return repo

End
