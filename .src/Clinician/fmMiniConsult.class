' Gambas class file

Private $encid As String
Private $BillMode As String
Private $sPackage As String
Private $xFinClear As Boolean
Private $deptGroup As String

Private $rData As Result
Private $aMyFields As String[]
Private $rData2 As Result
Private $aMyFields2 As String[]
Private $ConsList As String[]

Public Sub _new(encid As String, DiscType As String)

  Dim xstatus As String
  Dim accLst As String[]

  $encid = encid
  $sPackage = DiscType
  modGeneralMain.GetOpenModalForm(Me)

  accLst = modBasic.$BillDiscountCash
  If accLst.Count = 0 Then
    accLst.Add($sPackage)
  Endif
  cmbdisctype.List = accLst
  cmbdisctype.Text = $sPackage
  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtgender.Text = modPatient.GetPatientAgeString($encid, Now()) & "/" & modPatient.GetPatientSex($encid)
  $BillMode = modNonMedical.GetDiscBindBillMode($sPackage)
  If Not $BillMode Then
    $BillMode = modpatient.GetPatBillingMode($encid)
  Endif

  xstatus = modPatient.CurrentAdmissionStatus($encid)
  txtlocation.Text = modPatient.GetLocationSetting($encid, xstatus)
  $xFinClear = modNonMedical.GetFinanceClearance($encid)
  $deptGroup = modSettings.ShowSettingFromFIle("RegistrationForm/DefaultDeptGroup")
  rbconsult.Value = True
  GetDepartmentList()

  btnconsult.Tag = modBillings.GetPayableUserSetting("Service", $encid)
  If btnconsult.Tag Then
    btnconsult.Text = modGeneral.GetUserFullName(btnconsult.Tag)
  Endif
  If modBasic.$PayableLockEntry = "Yes" Then
    btnselectuser.Enabled = False
  Endif
  dtconsult.Value = Now()
  FillLabtable()
  If $xFinClear = True Then
    btnsubOK.Visible = False
  Endif
  If modBasic.$TabletModeEnable = "Yes" Then
  Else
    txtpresummary.SetFocus
  Endif

End

Public Sub btnscheme_Click()

  If cmbdisctype.Text Then
    $BillMode = modNonMedical.GetDiscBindBillMode(cmbdisctype.Text)
    If Not $BillMode Then
      $BillMode = modpatient.GetPatBillingMode($encid)
    Endif
  Endif

End

Public Sub TabPanel1_Click()

  If TabPanel1.Index = 0 Then
    FillLabtable()
  Else If TabPanel1.Index = 1 Then
    CompleteGrid()
  Endif

End

Public Sub rbconsult_Click()

  GetDepartmentList()

End

Public Sub rbopvisit_Click()

  GetDepartmentList()

End

Private Sub GetDepartmentList()

  If $deptGroup Then
    If rbconsult.Value = True Then
      $ConsList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemname as col from tbldeptgroup where fldgroupname=&1 and flditemname in(select flddept from tbldepartment where fldcateg=&2)", $deptGroup, "Consultation"))   ''
      cmbconsult.List = $ConsList
    Else If rbopvisit.Value = True Then
      $ConsList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditemname as col from tbldeptgroup where fldgroupname=&1 and flditemname in(select flddept from tbldepartment where fldcateg=&2)", $deptGroup, "OPD Visit"))
      cmbconsult.List = $ConsList
    Endif

  Else
    If rbconsult.Value = True Then
      $ConsList = modGeneral.GetDepartForConsultOnly()
      cmbconsult.List = $ConsList
    Else If rbopvisit.Value = True Then
      $ConsList = modGeneral.GetDepartForOPVisitOnly()
      cmbconsult.List = $ConsList
    Endif

  Endif

End

Public Sub txtsearch_Change()

  cmbconsult.List = modString.SelectStringArrayToText(txtsearch.Text, $ConsList, chkleft.Value)

End

Public Sub cmbconsult_Select()

  txtdepart.Text = cmbconsult.Text
  dtconsult.Value = Now()
  txtpresummary.RichText = ""
  btnconsult.Tag = ""
  btnconsult.Text = ""

End

Public Sub btnsubOK_Click()

  Dim billItem As String
  Dim xrefer As String
  Dim xpayble As String
  Dim xgo As Boolean

  If cmbconsult.Text Then
    If $ConsList.Exist(cmbconsult.Text) Then
      xrefer = modBillings.GetReferralUserSetting("Service", $encid)
      If btnconsult.Tag Then
        xpayble = btnconsult.Tag
      Else
        xpayble = modBillings.GetPayableUserSetting("Service", $encid)
      Endif

      If modBasic.$ClinConsultComment = "Essential" Then
        If txtpresummary.RichText Then
          xgo = True
        Else
          xgo = False
        Endif
      Else
        xgo = True
      Endif

      If xgo = True Then
        If modGeneral.GetCategoryFromDept(cmbconsult.Text) = "Consultation" Then
          modPatientSub.AddConsultData($encid, cmbconsult.Text, dtconsult.Value, $BillMode, "IP Consultation", btnconsult.Tag, txtpresummary.RichText)
        Else
          modPatientSub.AddOPVisitData($encid, cmbconsult.Text, dtconsult.Value, $BillMode, "IP Consultation", txtpresummary.RichText)
        Endif

        If modBasic.$AutoBillConsult Then
          billItem = modNonMedical.GetIPDepartConsultRate(cmbconsult.Text, cmbdisctype.Text)
          If billItem Then
            If modBasic.$AutoBillConsult = "Standard" Then
            Else If modBasic.$AutoBillConsult = "Full" Then
              modBillings.GetAutoBillingEntry($encid, cmbdisctype.Text, "Service", billItem, 1, "Done", 0, True, False, xpayble, xrefer)
            Else If modBasic.$AutoBillConsult = "Partial" Then
              modBillings.GetAutoBillingEntry($encid, cmbdisctype.Text, "Service", billItem, 1, "Punched", 0, False, False, xpayble, xrefer)
            Endif
          Endif
        Endif
      Endif
      FillLabtable()

    Endif
  Endif

End

Public Sub FillLabtable()

  Dim sql As String
  Dim xFldList1 As String[] = ["fldid", "fldconsultname", "fldencounterval", "fldconsulttime", "fldid", "fldpresummary"]
  Dim xFldList2 As String[] = ["fldid", "fldconsultname", "fldencounterval", "fldconsulttime", "fldid", "fldpresummary"]

  xFldList1.Add(Quote("tblconsult"))
  xFldList2.Add(Quote("tblopvisit"))
  sql = "select " & xFldList1.Join(",") & " from tblconsult where fldencounterval=&1 and fldstatus=&2 UNION ALL select " & xFldList2.Join(",") & " from tblopvisit where fldencounterval=&1 and fldstatus=&2"                                             ''
  $rData = modDatabase.$myConn.Exec(sql, $encid, "Planned")
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(250 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Hidden = True
    .Columns[3].Width = CStr(175 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Hidden = True
    .Columns[5].Hidden = True
    .Columns[6].Hidden = True

    .Columns[1].Text = "Consultation"
    .Columns[3].Text = "DateTime"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 3 Then
    Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Select()

  Dim res As Result

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If $rData["tblconsult"] = "tblconsult" Then
      res = modDatabase.$myConn.Exec("select fldconsultname,fldconsulttime,fldpresummary,flduserid from tblconsult where fldid=&1", $rData["fldid"])
      If res.Available Then
        txtdepart.Text = res["fldconsultname"]
        dtconsult.Value = res["fldconsulttime"]
        txtpresummary.RichText = res["fldpresummary"]
        If res["flduserid"] Then
          btnconsult.Tag = res["flduserid"]
          btnconsult.Text = modGeneral.GetUserFullName(res["flduserid"])
        Else
          btnconsult.Tag = ""
          btnconsult.Text = ""
        Endif
      Endif
    Else If $rData["tblconsult"] = "tblopvisit" Then
      res = modDatabase.$myConn.Exec("select fldconsultname,fldconsulttime,fldpresummary from tblconsult where fldid=&1", $rData["fldid"])
      If res.Available Then
        txtdepart.Text = res["fldconsultname"]
        dtconsult.Value = res["fldconsulttime"]
        txtpresummary.RichText = res["fldpresummary"]
        btnconsult.Tag = ""
        btnconsult.Text = ""
      Endif
    Endif
  Endif

End

Public Sub btndelete_Click()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$myConn.Delete($rData["tblconsult"], "fldid=&1", $rData["fldid"])
      FillLabtable()
    Endif
  Endif

End

Public Sub btnselectuser_Click()

  Dim xMedUser As String[]

  xMedUser = MedicalSelectedValue(("Select Consultant"), modBasic.$OPConsulUserList)
  If xMedUser And If xMedUser.Count Then
    btnconsult.Tag = xMedUser[0]
    btnconsult.Text = xMedUser[1]
  Else
    btnconsult.Tag = ""
    btnconsult.Text = ""
  Endif

End

Public Sub btnconsult_Change()

  If btnconsult.Text = "" Then
    btnconsult.Tag = ""
  Endif

End

''=====================completed =====================
Private Sub CompleteGrid()

  Dim sql As String
  Dim xFldList1 As String[] = ["fldid", "fldconsulttime", "fldencounterval", "fldconsultname", "fldoutcome", "fldpresummary", "fldsummary"]
  Dim xFldList2 As String[] = ["fldid", "fldconsulttime", "fldencounterval", "fldconsultname", "fldoutcome", "fldpresummary", "fldsummary"]

  xFldList1.Add(Quote("tblconsult"))
  xFldList2.Add(Quote("tblopvisit"))
  sql = "select " & xFldList1.Join(",") & " from tblconsult where fldencounterval=&1 and fldstatus<>&2 UNION ALL select " & xFldList2.Join(",") & " from tblopvisit where fldencounterval=&1 and fldstatus<>&2"
  $rData2 = modDatabase.$myConn.Exec(sql, $encid, "Planned")
  $aMyFields2 = New String[]
  modGridView.ReadSmallData(GridView2, $rData2, $aMyFields2)

  With GridView2
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Hidden = True
    .Columns[3].Width = CStr(250 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Hidden = True
    .Columns[6].Hidden = True
    .Columns[7].Hidden = True

    .Columns[1].Text = "DateTime"
    .Columns[3].Text = "Consultation"
    .Columns[4].Text = "Status"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData2.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Text = modReportVar.GetDateTimeReport($rData2[$aMyFields2[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData2[$aMyFields2[Column]]
  Endif

End

Public Sub GridView2_Select()

  Dim xx As String

  If GridView2.Selection.Count Then
    $rData2.MoveTo(GridView2.Selection[0])
    xx = "<p><b>REQUEST : </b><br>" & $rData2["fldpresummary"] & "</p>"
    xx = xx & "<p><b>RESPONSE : </b><br>" & $rData2["fldsummary"] & "</p>"
    WebHtml1.Html = xx
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
