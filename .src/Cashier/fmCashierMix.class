' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $encid As String

Public Sub _new()

  dtcurr.Value = Now()

End

Public Sub btnselectuser_Click()

  Dim xMedUser As String[]

  xMedUser = MedicalSelectedValue(("Select Consultant"), modBasic.$OPConsulUserList)
  If xMedUser And If xMedUser.Count Then
    btnconsult.Tag = xMedUser[0]
    btnconsult.Text = xMedUser[1]
  Else
    btnconsult.Tag = ""
    btnconsult.Text = ""
  Endif

End

Public Sub btnconsultclear_Click()

  btnconsult.Tag = ""
  btnconsult.Text = ""

End

Public Sub btnrefresh_Click()

  FillRegistrationGrid(dtcurr.Value)

End

Private Sub FillRegistrationGrid(sDate As Date)

  Dim sql As String
  Dim xstr As String

  If chkrequest.Value = True Then
    xstr = " and fldencounterval in(select fldencounterval from tblpatbilling where fldsave=&4 and fldprint=&5 and fldstatus=&6 and flditemqty>&7)"
  Else
    xstr = ""
  Endif

  If btnconsult.Tag Then
    sql = "select fldencounterval,fldencounterval,fldadmitlocat,fldregdate,fldpatientval from tblencounter where fldregdate>=&1 and fldregdate<=&2 and flduserid=&3" & xstr
  Else
    sql = "select fldencounterval,fldencounterval,fldadmitlocat,fldregdate,fldpatientval from tblencounter where fldregdate>=&1 and fldregdate<=&2" & xstr                                     ''
  Endif
  $rData = modDatabase.$myConn.Exec(sql, modDate.StartSqlDate(sDate), modDate.EndSqlDate(sDate), btnconsult.Tag, False, False, "Punched", 0)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Expand = True
    .Columns[2].Hidden = True
    .Columns[3].Hidden = True
    .Columns[4].Hidden = True

    .Columns[0].Text = "Encounter"
    .Columns[1].Text = "Name"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Text = modPatient.GetPatientNameByEnc($rData[$aMyFields[Column]])
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Select()

  Dim encid As String
  Dim hForm As FmCashBilling
  Dim hForm1 As FmPatPharmacy

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    encid = $rData["fldencounterval"]

    If modHelpVariable.$LogInCategory = "Cashier" Then
      If pnlmain.Children.Count Then
        pnlmain.DeleteChildren()
      Endif
      hForm = New FmCashBilling(pnlmain)
      hForm.txtencid.Text = encid

    Else If modHelpVariable.$LogInCategory = "Dispensar" Then
      If pnlmain.Children.Count Then
        pnlmain.DeleteChildren()
      Endif
      hForm1 = New FmPatPharmacy(pnlmain)
      hForm1.txtencid.Text = encid

    Endif
  Endif

End
