' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub _new()

  cmboxygen.List = ["Present"]
  cmbventilator.List = ["Present"]
  cmbother.List = ["Present"]
  cmbdepart.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flddept as col from tbldepartment where fldcateg=&1", "Patient Ward"))
  cmbpackage.List = modNonMedical.FillDiscountCombo()

End

Public Sub cmbpackage_Click()

  cmbbillmode.Text = modNonMedical.GetDiscBindBillMode(cmbpackage.Text)

End

Public Sub btnselitem_Click()

  Dim xList As String[]

  If cmbbillmode.Text Then

    xList = modNonMedical.NonStockBillingItemArray("General Services", cmbbillmode.Text)
    If xList.Count Then
      cmbitem.Text = GridViewNew("Select Particulars", xList, False)
    Endif

  Endif

End

Public Sub btnselconsult_Click()

  Dim xList As String[]

  If cmbbillmode.Text Then

    xList = modNonMedical.NonStockBillingItemArray("General Services", cmbbillmode.Text)
    If xList.Count Then
      cmbconsult.Text = GridViewNew("Select Particulars", xList, False)
    Endif

  Endif

End

Public Sub btnexitcharge_Click()

  Dim xList As String[]

  If cmbbillmode.Text Then

    xList = modNonMedical.NonStockBillingItemArray("General Services", cmbbillmode.Text)
    If xList.Count Then
      cmbexitcharge.Text = GridViewNew("Select Particulars", xList, False)
    Endif

  Endif

End

Public Sub btnrefresh_Click()

  ShowGridView()

End

Private Sub BlankAllControl()

  cmbpackage.Text = ""
  cmbbillmode.Text = ""
  cmbitem.Text = ""
  cmbconsult.Text = ""
  cmbexitcharge.Text = ""
  cmboxygen.Text = ""
  cmbventilator.Text = ""
  cmbother.Text = ""

End

Public Sub ShowGridView()

  Dim sql As String

  If cmbdepart.Text Then
    sql = "select fldid,flddisctype,fldbedcharge,fldconsultcharge,fldoxyport,fldventilator,fldother,fldbillingmode,fldexititem from tbldepartbedcharge where flddept=&1"
    $rData = modDatabase.$myConn.Exec(sql, cmbdepart.Text)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

    With GridView1
      .Columns[0].Hidden = True
      .Columns[1].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
      .Columns[2].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
      .Columns[3].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
      .Columns[4].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
      .Columns[5].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
      .Columns[6].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
      .Columns[7].Hidden = True
      .Columns[8].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"

      .Columns[1].Text = "Package"
      .Columns[2].Text = "Bed Charges"
      .Columns[3].Text = "Consultation"
      .Columns[4].Text = "Oxygen"
      .Columns[5].Text = "Ventilator"
      .Columns[6].Text = "Other"
      .Columns[8].Text = "Exit Charge"
    End With

  Endif

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]

End

Public Sub btnOK_Click()

  Dim res As Result

  If cmbdepart.Text Then
    res = modDatabase.$myConn.Create("tbldepartbedcharge")
    res["flddept"] = cmbdepart.Text
    If chkdefault.Value = True Then
      res["fldoxyport"] = ""
      res["fldventilator"] = ""
      res["fldother"] = ""
    Else
      res["fldoxyport"] = cmboxygen.Text
      res["fldventilator"] = cmbventilator.Text
      res["fldother"] = cmbother.Text
    Endif
    res["flddisctype"] = cmbpackage.Text
    res["fldbillingmode"] = cmbbillmode.Text
    res["fldbedcharge"] = cmbitem.Text
    res["fldconsultcharge"] = cmbconsult.Text
    res["fldexititem"] = cmbexitcharge.Text
    res.Update()
    ShowGridView()
    BlankAllControl()
    Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
    cmbbillmode.SetFocus
  Endif

End

Public Sub btnCancel_Click()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    modDatabase.$myConn.Delete("tbldepartbedcharge", "fldid=&1", $rData["fldid"])
    ShowGridView()
    Me.Exec("Toastify({text: 'Information deleted', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, "Bed Charges", modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub GridView1_Select()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    cmbpackage.Text = $rData["flddisctype"]
    cmbbillmode.Text = $rData["fldbillingmode"]
    cmbitem.Text = $rData["fldbedcharge"]
    cmbconsult.Text = $rData["fldconsultcharge"]
    cmboxygen.Text = $rData["fldoxyport"]
    cmbventilator.Text = $rData["fldventilator"]
    cmbother.Text = $rData["fldother"]
    cmbexitcharge.Text = $rData["fldexititem"]
  Endif

End

Public Sub chkdefault_Click()

  cmboxygen.Text = ""
  cmbventilator.Text = ""
  cmbother.Text = ""
  If chkdefault.Value = True Then
    cmboxygen.Enabled = False
    cmbventilator.Enabled = False
    cmbother.Enabled = False
  Else
    cmboxygen.Enabled = True
    cmbventilator.Enabled = True
    cmbother.Enabled = True
  Endif

End
