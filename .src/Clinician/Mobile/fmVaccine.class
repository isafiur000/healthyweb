' Gambas class file

Private $patNo As String

Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(encid As String)
  
  txtencid.Text = encid
  modGeneralMain.GetOpenModalForm(Me)
  
  cmbvaccine.List = modControlSub.GetDirectFillresult(modDatabase.$syConn.Exec("select flditem as col from tblvaccine"))
  cmbschedule.List = ["First Dose", "Second Dose", "Third Dose", "Fourth Dose", "Fifth Dose", "Sixth Dose", "Seventh Dose", "Booster Dose"]
  
  If txtencid.Text Then
    btnshow_Click()
  Else
    pnlfirst.Visible = True
  Endif
  btnvacuser.Tag = modBasic.$lbluser
  btnvacuser.Text = modGeneral.GetUserFullName(btnvacuser.Tag)
  
End

Public Sub btnwebcam_Click()
  
  txtencid.Text = QRScanValue("")
  If txtencid.Text Then
    btnshow_Click()
  Endif
  
End

Public Sub btnshow_Click()
  
  Dim xx As Boolean
  
  If txtencid.Text Then
    txtpatientname.Text = modPatient.GetPatientNameByEnc(Trim(txtencid.Text))
    $patNo = modPatient.GetPatientNoByEnc(Trim(txtencid.Text))
    xx = modPatient.EnableClinicForm(modPatient.CurrentAdmissionStatus(Trim(txtencid.Text)))
    SHowVaccineRecord()
    
    btnsave.Enabled = xx
  Endif
  
End

Public Sub btnstockid_Click()
  
  Dim xList As String[]
  Dim stockno As String
  
  xList = modStock.FillVaccineDispensingArrayAllBatch()
  stockno = PharmGridList("Select Particulars", "vaccine", "Generic", xList)
  If stockno Then
    SHowInventoryAllBatch(CInt(stockno))
  Endif
  
End

Private Sub SHowInventoryAllBatch(stockNo As Integer)
  
  Dim res As Result
  
  If stockno Then
    res = modDatabase.$myConn.Exec("select fldstockid,fldcategory,fldbatch,fldexpiry,fldqty,fldsellpr from tblentry where fldstockno=&1", CInt(stockno))
    If res.Available Then
      txtstockno.Value = stockNo
      cmbstockid.Text = res["fldstockid"]
      txtbatch.Text = res["fldbatch"]
      dtexpiry.Value = res["fldexpiry"]
    Endif
  Endif
  
End

Public Sub txtencid_Activate()
  
  btnshow_Click()
  
End

Public Sub btnadd_Click()
  
  Dim hForm As FmAddVariableGrid
  
  hForm = New FmAddVariableGrid("flditem", "tblvaccine")
  hForm.ShowModal
  cmbvaccine.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flditem as col from tblvaccine"))
  
End

Private Sub BlankAll()
  
  ' modJavaScript.UpdateSelect2Combo(cmbvaccine, "")
  cmbvaccine.Text = ""
  cmbstockid.Text = ""
  txtstockno.Value = 0
  txtbatch.Text = ""
  dtexpiry.Value = ""
  txtdose.Value = 0
  txtunit.Text = ""
  ' modJavaScript.UpdateSelect2Combo(cmbschedule, "")
  cmbschedule.Text = ""
  btnvacuser.Tag = ""
  btnvacuser.Text = ""
  txtdose.Value = 0
  
End

Public Sub btnsave_Click()
  
  Dim res As Result
  Dim xIntVal As String
  
  If cmbvaccine.Text And If txtdose.Value And If cmbschedule.Text Then
    res = modDatabase.$myConn.Create("tblvaccdosing")
    res["fldencounterval"] = Trim(txtencid.Text)
    res["flditem"] = cmbvaccine.Text
    res["fldstockno"] = txtstockno.Value
    res["fldtype"] = cmbschedule.Text
    res["fldvalue"] = txtdose.Value
    res["fldunit"] = Trim(txtunit.Text)
    
    res["flduserid"] = btnvacuser.Tag
    res["fldtime"] = Now()
    res["fldcomp"] = modBasic.$compID
    res["fldsave"] = True
    res["xyz"] = False
    If MMain.$WebEntry = True Then
      xIntVal = modString.GetDateString(Now())
      res["fldid"] = CLong(xIntVal)
      res["fldrepoid"] = modMisc.GetWebIndexStr(xIntVal)
      res["fldrepodate"] = Now()
      res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
      res["fldhospcode"] = modBasic.$HospCode
    Endif
    res.Update
    SHowVaccineRecord()
    BlankAll()
  Endif
  
End

Private Sub SHowVaccineRecord()
  
  Dim sql As String
  
  sql = "select fldid,fldtime,flditem,fldtype,fldvalue,fldunit,fldencounterval from tblvaccdosing where fldencounterval in(select fldencounterval from tblencounter where fldpatientval=&1)"
  $rData = modDatabase.$myConn.Exec(sql, $patNo)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  
  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Hidden = True
    
    .Columns[1].Text = "DateTime"
    .Columns[2].Text = "Vaccine"
    .Columns[3].Text = "Schedule"
    .Columns[4].Text = "Dose"
    .Columns[5].Text = "Unit"
  End With
  
End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)
  
  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif
  
End

Public Sub btnexport_Click()
  
  modCHTMLReport.ExportGridToHTML(GridView1, "VACCINATION HISTORY", "Patient No: " & CStr($patNo),, Trim(txtencid.Text))
  
End

Public Sub btnclose_Click()
  
  Me.Close()
  
End

Public Sub btnselectuser_Click()
  
  Dim xMedUser As String[]
  
  xMedUser = MedicalSelectedValue(("Select Consultant User"), modBasic.$OPConsulUserList)
  If xMedUser And If xMedUser.Count Then
    btnvacuser.Tag = xMedUser[0]
    btnvacuser.Text = xMedUser[1]
  Else
    btnvacuser.Tag = ""
    btnvacuser.Text = ""
  Endif
  
End

' Public Sub cmbvaccine_Render()
'   
'   modJavaScript.SetSelect2Combo(cmbvaccine)
'   
' End
' 
' Public Sub cmbschedule_Render()
'   
'   modJavaScript.SetSelect2Combo(cmbschedule)
'   
' End
