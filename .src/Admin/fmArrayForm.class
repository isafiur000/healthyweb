' Gambas class file

Private aObjPanel As New Object[50]
Private aObjLabel As New Object[50]
Private aObjText As New Object[50]
Private aObjValue As New Object[50]
Private aObjDate As New Object[50]
Private aBSDate As New Object[50]
Private aObjBool As New Object[50]

Private $sTable As String
Private $sIndex As String
Private $sValue As Variant
Private $sFls As Integer

Public Sub _new(sTable As String, sIndex As String, sValue As Variant)

  $sTable = sTable
  $sIndex = sIndex
  $sValue = sValue
  CreateControls()

End

Private Sub CreateControls()

  Dim i As Integer
  Dim ref As ResultField
  Dim res As Result
  Dim AppFactor As Float

  AppFactor = modBasic.$AppScaleFactor
  res = modDatabase.$myConn.Exec(Subst("select * from &1 where &2", $sTable, $sIndex) & "=&1", $sValue)
  If res.Available Then

    i = 0
    For Each ref In res.Fields
      aObjPanel[i] = New WebHBox(Frame1)
      aObjPanel[i].Class = "less-container-margin"

      aObjLabel[i] = New WebLabel(aObjPanel[i])
      With aObjLabel[i]
        .Width = CStr(150 * AppFactor) & "px"
        .Height = "2em"
        .Border = True
        .Text = ref.Name
        .Tag = i
      End With
      If aObjLabel[i].Text = $sIndex Then
        aObjLabel[i].Font = "bold"
      Endif

      If ref.Type = gb.String Then
        aObjText[i] = New WebTextBox(aObjPanel[i])
        With aObjText[i]
          .Height = "2em"
          .Expand = True
          .Text = res[ref.Name]
          .Tag = i
        End With
        If aObjText[i].Text = $sValue Then
          aObjText[i].ReadOnly = True
        Endif

      Else If ref.Type = gb.Integer Or If ref.Type = gb.Long Or If ref.Type = gb.Float Then
        aObjValue[i] = New WebValueBox(aObjPanel[i])
        With aObjValue[i]
          .Height = "2em"
          .Expand = True
          If res[ref.Name] Then
            .Value = res[ref.Name]
          Else
            .Value = 0
          Endif
          .Tag = i
        End With
        If aObjValue[i].Value = $sValue Then
          aObjValue[i].ReadOnly = True
        Endif

      Else If ref.Type = gb.Date Then
        aObjDate[i] = New WebDateBox(aObjPanel[i])
        With aObjDate[i]
          .Height = "2em"
          .DateTime = True
          .Expand = True
          .Value = res[ref.Name]
          .Tag = i
        End With
        aBSDate[i] = New WebButton(aObjPanel[i]) As "BSButton"
        With aBSDate[i]
          .Width = "2em"
          .Height = "2em"
          .Image = "icon:/small/calendar"
          .Tag = i
        End With

      Else If ref.Type = gb.Boolean Then
        aObjBool[i] = New WebCheckBox(aObjPanel[i])
        With aObjBool[i]
          .Height = "2em"
          .Expand = True
          .Value = res[ref.Name]
          .Tag = i
        End With

      Endif

      i = i + 1
    Next
    $sFls = i

  Endif

End

Public Sub BSButton_Click()

  Dim j As Integer
  Dim xx As String

  j = Last.Tag
  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(aObjDate[j].Value))
  If xx Then
    aObjDate[j].Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub btnok_Click()

  Dim res As Result
  Dim i As Integer

  If Message.Question(("Are you Sure?"), ("No"), ("Yes")) = 2 Then
    res = modDatabase.$myConn.Edit($sTable, $sIndex & "=&1", $sValue)
    For i = 0 To $sFls - 1
      res[aObjLabel[i].Text] = GetFieldValue(aObjLabel[i].Text)
    Next
    res.Update
    Me.Exec("Toastify({text: 'Information updated', duration: 3000}).showToast()")
  Endif

End

Private Function GetFieldValue(sField As String) As Variant

  Dim xx As Variant
  Dim i As Integer

  For i = 0 To $sFls - 1
    If aObjLabel[i].Text = sField Then
      If aObjText[i] Then
        xx = aObjText[i].Text
      Else If aObjValue[i] Then
        xx = aObjValue[i].Value
      Else If aObjDate[i] Then
        xx = aObjDate[i].Value
      Else If aObjBool[i] Then
        xx = aObjBool[i].Value
      Endif
      Break
    Endif
  Next
  Return xx

End

Public Sub btnclose_Click()

  Me.Close()

End
