' Gambas class file

Private $PatientNum As String
Private $encid As String

Private $rDatamain As Result
Private $aMyFieldsmain As String[]
Private $rData1 As Result
Private $aMyFields1 As String[]
Private $rData2 As Result
Private $aMyFields2 As String[]

Private hPacsForm As FmPacsFind

Private $doa As Date

Public Sub _new(sPatNo As String)

  $PatientNum = sPatNo
  btnshowvisits.Text = "All Visits"
  rblabs.Value = True
  modBasic.$currency = "Rs "

  txtpatname.Text = modPatient.PatientFullNameByPatID($PatientNum)
  txtpataddress.Text = modPatient.GetPatientAddressByPatno($PatientNum)
  GoLastEncounter()

End

Public Sub WebForm_Open()

  modGeneralMain.RecordFormUse(Me)

End

Public Sub btnshowvisits_Click()

  If btnshowvisits.Text = "All Visits" Then
    mainGridView.Visible = True
    FillEncounterData()
    btnshowvisits.Text = "Latest Visit"
  Else If btnshowvisits.Text = "Latest Visit" Then
    mainGridView.Visible = False
    GoLastEncounter()
    btnshowvisits.Text = "All Visits"
  Endif

End

Private Sub GoLastEncounter()

  $encid = modPatient.GetLastEncoutnerFromPatNo($PatientNum)
  ShowPatientPanel()

End

Private Sub ShowPatientPanel()

  Dim hPtForm As FmClinHistory

  $doa = modPatient.GetRecordDate($encid)
  WebTabPanel1.Index = 0
  If pnlhistory.Children.Count Then
    pnlhistory.DeleteChildren()
  Endif
  hPtForm = New FmClinHistory($encid, "Encounter", True, "height-tablet-ceil", pnlhistory)

End

''============================== Main Forms =======================
Private Sub FillEncounterData()

  If MMain.$WebEntry = True Then
    $rDatamain = modDatabase.$myConn.Exec("select fldregdate,fldencounterval,fldencounterval,flddisctype,fldencounterval,fldhospcode from tblencounter where fldpatientval=&1 ORDER BY fldregdate DESC", modBasic.$lbluser)
  Else
    $rDatamain = modDatabase.$myConn.Exec("select fldregdate,fldencounterval,fldencounterval,flddisctype,fldencounterval from tblencounter where fldpatientval=&1 ORDER BY fldregdate DESC", modBasic.$lbluser)
  Endif
  $aMyFieldsmain = New String[]
  modGridView.ReadSmallData(mainGridView, $rDatamain, $aMyFieldsmain)

  With mainGridView
    .Columns[0].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(250 * modBasic.$AppWidthRatio) & "px"

    .Columns[0].Text = "Date"
    .Columns[1].Text = "Visit"
    .Columns[2].Text = "Age"
    .Columns[3].Text = "Scheme"
    .Columns[4].Text = "Diagnosis"
  End With

End

Public Sub mainGridView_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rDatamain.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 0 Then
    Data.Text = modReportVar.GetDateTimeReport($rDatamain[$aMyFieldsmain[Column]], gb.MediumDate)
  Else If Column = 2 Then
    Data.Text = modPatient.GetPatientAgeString($rDatamain[$aMyFieldsmain[Column]], modPatient.GetRecordDate($rDatamain[$aMyFieldsmain[Column]]))
  Else If Column = 4 Then
    Data.Html = modPatient.PatientDiagnoCurrentList($rDatamain[$aMyFieldsmain[Column]]).Join("<br>")
  Else
    Data.Text = $rDatamain[$aMyFieldsmain[Column]]
  Endif

End

Public Sub mainGridView_Select()

  If mainGridView.Selection.Count Then
    $rDatamain.MoveTo(mainGridView.Selection[0])
    $encid = $rDatamain["fldencounterval"]
    ShowPatientPanel()
  Endif

End

Public Sub WebTabPanel1_Click()

  If WebTabPanel1.Index = 1 Then
    FillReportData($encid, GetArchiveType())

  Else If WebTabPanel1.Index = 2 Then
    If wbPacsmainpanel.Children.Count Then
      wbPacsmainpanel.DeleteChildren()
    Endif
    hPacsForm = New FmPacsFind($encid, "Visit", True, wbPacsmainpanel)

  Else If WebTabPanel1.Index = 3 Then
    rbinvoice.Value = True
    If $encid Then
      SHowPatientInvoices($encid)
    Endif

  Endif

End

''====================== Archived REPORTS =========================
Private Function GetArchiveType() As String

  Dim xx As String

  If rblabs.Value = True Then
    xx = "Diagnostic Tests"
  Else If rbradio.Value = True Then
    xx = "Radio Diagnostics"
  Else If rbgeneral.Value = True Then
    xx = "General Reports"
  Else If rbscanned.Value = True Then
    xx = "Scanned Images"
  Endif

  Return xx

End

Public Sub rblabs_Click()

  If $encid Then
    FillReportData($encid, "Diagnostic Tests")
  Endif

End

Public Sub rbradio_Click()

  If $encid Then
    FillReportData($encid, "Radio Diagnostics")
  Endif

End

Public Sub rbgeneral_Click()

  If $encid Then
    FillReportData($encid, "General Reports")
  Endif

End

Public Sub rbscanned_Click()

  If $encid Then
    FillReportData($encid, "Scanned Images")
  Endif

End

Private Sub FillReportData(encid As String, sType As String)

  If sType = "all" Then
    If MMain.$WebEntry = True Then
      $rData1 = modDatabase.$syConn.Exec("select fldrepoid,fldcateg,fldtime,fldtitle,fldid from tblpatreport where fldencounterval like &1 and (fldcateg=&2 or fldcateg=&3 or fldcateg=&4) and (flvisible=&5 or flvisible IS NULL) ORDER BY fldid DESC", encid, "Diagnostic Tests", "Radio Diagnostics", "General Reports", "Visible")
    Else
      $rData1 = modDatabase.$syConn.Exec("select fldid,fldcateg,fldtime,fldtitle,fldid from tblpatreport where fldencounterval like &1 and (fldcateg=&2 or fldcateg=&3 or fldcateg=&4) and (flvisible=&5 or flvisible IS NULL) ORDER BY fldid DESC", encid, "Diagnostic Tests", "Radio Diagnostics", "General Reports", "Visible")                                                                 ''
    Endif
  Else
    If MMain.$WebEntry = True Then
      $rData1 = modDatabase.$syConn.Exec("select fldrepoid,fldcateg,fldtime,fldtitle,fldid from tblpatreport where fldencounterval like &1 and fldcateg=&2 and (flvisible=&3 or flvisible IS NULL) ORDER BY fldid DESC", encid, sType, "Visible")
    Else
      $rData1 = modDatabase.$syConn.Exec("select fldid,fldcateg,fldtime,fldtitle,fldid from tblpatreport where fldencounterval like &1 and fldcateg=&2 and (flvisible=&3 or flvisible IS NULL) ORDER BY fldid DESC", encid, sType, "Visible")                                                                 ''
    Endif
  Endif
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(GridView1, $rData1, $aMyFields1)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Hidden = True
    .Columns[2].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Expand = True
    .Columns[4].Hidden = True

    .Columns[2].Text = "DATE"
    .Columns[3].Text = "TITLE"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData1.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 2 Then
    Data.Text = modReportVar.GetDateTimeReport($rData1[$aMyFields1[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData1[$aMyFields1[Column]]
  Endif

End

Public Sub GridView1_Select()

  Dim xLink As String

  If GridView1.Selection.Count Then
    $rData1.MoveTo(GridView1.Selection[0])

    If MMain.$WebEntry = True Then
      xLink = modImage.GetBlobFileData($rData1["fldrepoid"])
    Else
      xLink = modImage.GetBlobFileData($rData1["fldid"])
    Endif
    If xLink Then
      WebPDFView1.Viewer = modSettings.ShowSettingFromFIle("Application/PDFViewer")
      WebPDFView1.Tag = modPrint.GetFileWebURL(xLink)
      WebPDFView1.Path = WebPDFView1.Tag
    Endif

  Endif

End

Public Sub btnprintarchive_Click()

  Dim xLink As String

  If GridView1.Selection.Count Then
    $rData1.MoveTo(GridView1.Selection[0])

    If MMain.$WebEntry = True Then
      xLink = modImage.GetBlobFileData($rData1["fldrepoid"])
    Else
      xLink = modImage.GetBlobFileData($rData1["fldid"])
    Endif
    If xLink Then
      Me.Exec("printJS('" & modPrint.GetFileWebURL(xLink) & "')")
    Endif

  Endif

End

''============================== Invoices ================
Public Sub rbreceipt_Click()

  If $encid Then
    SHowPatientInvoices($encid)
  Endif

End

Public Sub rbinvoice_Click()

  If $encid Then
    SHowPatientInvoices($encid)
  Endif

End

Private Sub SHowPatientInvoices(encid As String)

  Dim xFldList As String[]

  xFldList = ["fldtime", "fldbillno", "fldtaxamt", "flddiscountamt", "fldbilltype", "fldbankname", "fldchequeno", "flditemamt", "flddiscountamt", "fldtaxamt", "fldchargedamt", "fldreceivedamt"]
  If rbinvoice.Value = True Then
    $rData2 = modDatabase.$myConn.Exec("select " & xFldList.Join(",") & " from tblpatbilldetail where fldencounterval=&1 ORDER BY fldid DESC", encid)
  Else If rbreceipt.Value = True Then
    $rData2 = modDatabase.$myConn.Exec("select " & xFldList.Join(",") & " from tbltempbilldetail where fldencounterval=&1 ORDER BY fldid DESC", encid)
  Endif

  $aMyFields2 = New String[]
  modGridView.ReadSmallData(GridView2, $rData2, $aMyFields2)

  With GridView2
    .Columns[0].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Hidden = True
    .Columns[3].Hidden = True
    .Columns[4].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Hidden = True
    .Columns[7].Hidden = True
    .Columns[8].Hidden = True
    .Columns[9].Hidden = True
    .Columns[10].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[11].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"

    .Columns[0].Text = "DATE"
    If rbinvoice.Value = True Then
      .Columns[1].Text = "INVOICE"
    Else If rbreceipt.Value = True Then
      .Columns[1].Text = "RECEIPT"
    Endif
    .Columns[4].Text = "TYPE"
    .Columns[5].Text = "ACCOUNT"
    .Columns[10].Text = "TOTAL"
    .Columns[11].Text = "PAID"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData2.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = GetGridViewValue(Column, $rData2[$aMyFields2[Column]])

End

Private Function GetGridViewValue(Column As Integer, xVariable As Variant) As Variant

  Dim xxx As Variant

  If Column = 0 Then
    xxx = modReportVar.GetDateTimeReport(xVariable, gb.GeneralDate)
  Else If Column = 10 Then
    xxx = modReportVar.GetLocaleNumberFormat(xVariable, gb.Currency)
  Else If Column = 11 Then
    xxx = modReportVar.GetLocaleNumberFormat(xVariable, gb.Currency)
  Else
    xxx = xVariable
  Endif

  Return xxx

End

Public Sub GridView2_Select()

  Dim xLink As String
  Dim ShowTax As Boolean
  Dim ShowDisc As Boolean

  If GridView2.Selection.Count Then
    $rData2.MoveTo(GridView2.Selection[0])

    modNonMedical.$tblpatbilling = "tblpatbilling"
    modNonMedical.$tblpatbilldetail = "tblpatbilldetail"
    modNonMedical.$tbltempbilldetail = "tbltempbilldetail"
    If rbinvoice.Value = True Then
      If $rData2["fldtaxamt"] = 0 Then
        ShowTax = False
      Else
        ShowTax = True
      Endif
      If $rData2["flddiscountamt"] = 0 Then
        ShowDisc = False
      Else
        ShowDisc = True
      Endif
      xLink = modBILLFormat.GetBillingInvoicePath($rData2["fldbillno"], modBillLock.GetCopyBillTypeFromBillNo($rData2["fldbillno"]), ShowTax, ShowDisc)
    Else If rbreceipt.Value = True Then
      If $rData2["fldbillno"] Like "TDA" & "*" Then
        xLink = modBILLFormat.GetDischargeReceiptPath($rData2["fldencounterval"], $rData2["fldbillno"], "DISCHARGE SLIP (COPY)")
      Else
        xLink = modBILLFormat.GetBillingReceiptPath($rData2["fldbillno"], "RECEIPT (COPY)")
      Endif
    Endif

    If xLink Then
      WebPDFView2.Path = modPrint.GetFileWebURL(xLink)
    Endif

    modNonMedical.$tblpatbilling = ""
    modNonMedical.$tblpatbilldetail = ""
    modNonMedical.$tbltempbilldetail = ""
  Endif

End

''======================== History =============================
