' Gambas class file

Private $sValue As String[]
Private $rData As Result
Private $aMyFields As String[]

Public Sub Run() As String[]

  modGeneralMain.GetOpenModalForm(Me)
  ListBox1.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldcategory) as fldcategory from tblsyndromes"))                       ''
  If Me.ShowModal() Then Return $sValue

End

Public Sub ListBox1_Select()

  If ListBox1.Text Then
    FillSyndroGrid(ListBox1.Text)
  Endif

End

Public Sub txtsearch_Change()

  If ListBox1.Text Then
    FillSyndroGrid(ListBox1.Text)
  Endif

End

Private Sub FillSyndroGrid(sType As String)

  Dim sSearch As String

  If chkleftmain.Value = True Then
    sSearch = "%" & LCase(txtsearch.Text) & "%"
  Else
    sSearch = LCase(txtsearch.Text) & "%"
  Endif

  If txtsearch.Text Then
    $rData = modDatabase.$myConn.Exec("select fldsyndrome,fldsymcode from tblsyndromes where fldcategory=&1 and lower(fldsyndrome) like &2", sType, sSearch)
  Else
    $rData = modDatabase.$myConn.Exec("select fldsyndrome,fldsymcode from tblsyndromes where fldcategory=&1", sType)
  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = CStr(275 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[0].Text = "Syndrome"
    .Columns[1].Text = "ICD"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  Data.Text = $rData[$aMyFields[Column]]

End

Public Sub btnBrOK_Click()

  Dim xx As String[]

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])

    xx = New String[]
    xx.Add($rData["fldsyndrome"])
    xx.Add($rData["fldsymcode"])
    $sValue = xx
    Me.Close(True)
  Endif

End

Public Sub btnBrChange_Click()

  Me.Close()

End
