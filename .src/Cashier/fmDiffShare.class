' Gambas class file

Private $sid As Long
Private $encid As String
Private $rData As Result
Private $aMyFields As String[]
Private $sLevel As String

Public Sub _new(sid As Long)

  Dim res As Result

  $sid = sid
  If modBasic.$PayableManualVerify = "Disable" Then
    $sLevel = "Active"
  Else
    $sLevel = "Waiting"
  Endif
  cmbtitle.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldusertype) as col from tblprocedureshare where fldactive=&1", "Active"))

  res = modDatabase.$myConn.Exec("select fldencounterval,flditemname,fldditemamt from tblpatbilling where fldid=&1", $sid)
  If res.Available Then
    $encid = res["fldencounterval"]
    lblitemname.Text = res["flditemname"]
    txttotamt.Value = res["fldditemamt"]
    ShowGridView()
  Endif

End

Private Sub ShowGridView()

  $rData = modDatabase.$myConn.Exec("select fldid,flditemname,fldshareuser,fldtotalper,flduserper,flduseramt,fldusertax from tblpatusershares where fldbillid=&1 and fldencounterval=&2 and fldactive=&3", $sid, $encid, $sLevel)                            ''
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Hidden = True
    .Columns[4].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"

    .Columns[1].Text = "Prticulrs"
    .Columns[2].Text = "Name"
    .Columns[4].Text = "User%"
    .Columns[5].Text = "UserAmt"
    .Columns[6].Text = "TDS%"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 2 Then
    Data.Text = modGeneral.GetUserFullName($rData[$aMyFields[Column]])
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Select()

  $rData.MoveTo(GridView1.Selection[0])
  txtperpersonal.Value = $rData["fldtotalper"]
  txttotpersonal.Value = Round((txtperpersonal.Value * txttotamt.Value) / 100, -2)
  txtconsper.Value = 100 - txtperpersonal.Value
  txtconsamt.Value = txttotamt.Value - txttotpersonal.Value
  txtconsper.Enabled = False
  txtconsamt.Enabled = False

End

Public Sub btnselectuser_Click()

  Dim xMedUser As String[]

  txtconsper.Enabled = False
  txtconsamt.Enabled = False
  xMedUser = MedicalSelectedValue(("Select Payable User"), modBasic.$PayUserList)
  If xMedUser And If xMedUser.Count Then
    btnconsult.Tag = xMedUser[0]
    btnconsult.Text = xMedUser[1]
  Else
    btnconsult.Tag = ""
    btnconsult.Text = ""
  Endif
  GetUserPercent()

End

Private Sub GetUserPercent()

  Dim xtot As Float

  xtot = 0
  For Each $rData
    xtot = xtot + $rData["flduserper"]
  Next
  txtuserper.Value = 100 - xtot

End

Public Sub btnconsult_Change()

  If btnconsult.Text = "" Then
    btnconsult.Tag = ""
  Endif

End

Public Sub txtconsper_Activate()

  txtconsamt.Value = Round((txtconsper.Value * txttotamt.Value) / 100, -2)
  txtperpersonal.Value = 100 - txtconsper.Value
  txttotpersonal.Value = txttotamt.Value - txtconsamt.Value

End

Public Sub txtconsamt_Activate()

  If Not txtconsper.Value Then
    txtconsper.Value = (txtconsamt.Value * 100) / txttotamt.Value
    txtperpersonal.Value = 100 - txtconsper.Value
    txttotpersonal.Value = txttotamt.Value - txtconsamt.Value
  Endif

End

Public Sub txtuserper_Activate()

  Dim xtot As Float

  xtot = 0
  For Each $rData
    xtot = xtot + $rData["flduserper"]
  Next
  If txtuserper.Value > (100 - xtot) Then
    Message.Warning(("Not Allowed"), ("OK"))
    txtuserper.Value = 100 - xtot
  Else
    txtuseramt.Value = Round((txtuserper.Value * txttotpersonal.Value) / 100, -2)
  Endif

End

Public Sub txtuseramt_Activate()

  If Not txtuserper.Value Then
    txtuserper.Value = (txtuseramt.Value * 100) / txttotpersonal.Value
  Endif

End

Public Sub btnadd_Click()

  Dim res As Result

  res = modDatabase.$myConn.Create("tblpatusershares")
  res["fldbillid"] = $sid
  res["fldsharetype"] = "Payable"
  res["fldencounterval"] = $encid
  res["flditemname"] = lblitemname.Text
  res["fldshareuser"] = btnconsult.Tag
  res["fldsharename"] = cmbtitle.Text
  res["fldtotalper"] = txtperpersonal.Value
  res["flduserper"] = txtuserper.Value
  res["flduseramt"] = txtuseramt.Value
  res["fldusertax"] = txttaxval.Value
  res["fldactive"] = $sLevel

  res["flduserid"] = modBasic.$lbluser
  res["fldtime"] = Now()
  res["fldcomp"] = modBasic.$compID
  res.Update
  ShowGridView()
  btnconsult.Tag = ""
  btnconsult.Text = ""
  txtuserper.Value = 0
  txtuseramt.Value = 0
  btnconsult.SetFocus

End

Public Sub mnudel_Click()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    modDatabase.$myConn.Delete("tblpatusershares", "fldid=&1", $rData["fldid"])
    ShowGridView()
  Endif

End
