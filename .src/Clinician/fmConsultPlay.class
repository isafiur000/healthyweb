' Gambas class file

Private $rData2 As Result
Private $aMyFields2 As String[]
Private $sValue As String

Public Sub Run(res As Result) As String

  $rData2 = res
  modGeneralMain.GetOpenModalForm(Me)
  CompleteGrid()

  If Me.ShowModal() Then Return $sValue

End

Private Sub CompleteGrid()

  $aMyFields2 = New String[]
  modGridView.ReadSmallData(GridView2, $rData2, $aMyFields2)

  With GridView2
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Hidden = True
    .Columns[3].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Hidden = True
    .Columns[6].Hidden = True

    .Columns[1].Text = "DateTime"
    .Columns[3].Text = "Department"
    .Columns[4].Text = "Consultant"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData2.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Text = modReportVar.GetDateTimeReport($rData2[$aMyFields2[Column]], gb.GeneralDate)
  Else If Column = 4 Then
    Data.Text = modGeneral.GetUserFullName($rData2[$aMyFields2[Column]])
  Else
    Data.Text = $rData2[$aMyFields2[Column]]
  Endif

End

Public Sub btnconsultdone_Click()

  If GridView2.Selection.Count Then
    $rData2.MoveTo(GridView2.Selection[0])
    If $rData2["tblconsult"] = "tblconsult" Then
      $sValue = CStr($rData2["fldid"]) & "|" & "Consultation"
    Else If $rData2["tblconsult"] = "tblopvisit" Then
      $sValue = CStr($rData2["fldid"]) & "|" & "OP Visit"
    Endif
    Me.Close(True)
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
