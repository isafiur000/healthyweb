' Gambas class file

Private $encid As String
Private $aAge As Float
Private $sStockId As String
Private $DoseRate As String

Private $recDose As Float
Private $unitDose As Float
Private $doseUnit As String
Private $frequency As String

Public Sub _new(encid As String, sAge As Float, xStockid As String)

  $encid = encid
  $aAge = sAge
  $sStockId = xStockid

  GetResults()

End

Private Sub GetResults()

  Dim sql As String
  Dim res As Result

  If $aAge <= 12 Then
    sql = "select fldrecpeddose as dose,fldrecpeddoseunit as unit,fldrecpedfreq as freq from tblcode where fldcodename in(select fldcodename from tbldrug where flddrug in(select flddrug from tblmedbrand where fldbrandid=&1))"
  Else If $aAge > 12 Then
    sql = "select fldrecaddose as dose,fldrecaddoseunit as unit,fldrecadfreq as freq from tblcode where fldcodename in(select fldcodename from tbldrug where flddrug in(select flddrug from tblmedbrand where fldbrandid=&1))"
  Endif
  res = modDatabase.$medConn.Exec(sql, $sStockId)
  If res.Available Then
    If res["dose"] Then
      $recDose = res["dose"]
    Else
      $recDose = 0
    Endif
    If res["unit"] Then
      $doseUnit = res["unit"]
    Else
      $doseUnit = ""
    Endif
    If res["freq"] Then
      $frequency = CStr(res["freq"])
    Else
      $frequency = "1"
    Endif

  Else
    $recDose = 0
    $doseUnit = ""
    $frequency = "1"
  Endif

  $DoseRate = CStr($recDose) & Space(1) & $doseUnit

End

Public Function RecommendDose() As Float

  Return $recDose

End

Public Function DoseUnit() As String

  Return $doseUnit

End

Public Function Frequency() As String

  Return $frequency

End

Public Function Regimen() As String

  Return $DoseRate

End

Public Function UnitDose() As Float

  Dim xval As Float
  Dim creatclr As Float
  Dim xelimfrac As Float

  If $recDose Then
    xval = modMedDosing.GetDoseInMg($recDose, $doseUnit, $encid)
    If xval Then
      $unitDose = xval
    Else
      $unitDose = 0
    Endif
  Else
    $unitDose = 0
  Endif

  If modBasic.$MedRenalAdjustment = "Yes" Then
    creatclr = modSysCons.GetCreatinineClearance($encid)
    If creatclr Then
      xelimfrac = modMedConstant.GetDrugRenalEliminFraction($sStockId)
      If (xelimfrac * 100) > modBasic.$MedMinRenalElimin And If creatclr < modBasic.$MedMaxCreatClear Then                                             ''
        $unitDose = $unitDose * modMedDosing.GetDoseAdjustmentFactor(xelimfrac, creatclr)
      Endif
    Endif
  Endif

  If $unitDose = 0 Then
    $unitDose = modMedConstant.GetDrugInitialStrength($sStockId)
  Endif

  Return $unitDose

End

Public Function UnitDoseUnit() As String

  Dim xunit As String

  Select $doseUnit
    Case "mg", "mg/Kg", "mg/sqm"
      xunit = "mg"
    Case "mcg", "mcg/Kg", "mcg/sqm"
      xunit = "mcg"
    Case "gram", "gram/Kg", "gram/sqm"
      xunit = "gram"
    Case "IU", "IU/Kg", "IU/sqm"
      xunit = "IU"
    Case "Unit", "Unit/Kg", "Unit/sqm"
      xunit = "Unit"
    Case "mL", "mL/Kg", "mL/sqm"
      xunit = "mL"
  End Select
  Return xunit

End
