' Gambas module file

Public $OnlineCon As Connection
Public $OnlRoutine As String

Private $OnlineService As String
Private $OnlineHost As String
Private $OnlineUser As String
Private $OnlinePassword As String
Private $OnlineDatabase As String
Private $OnlinePort As String

Private Function GetOnlineConfigSettingFIle() As String

  Dim xfile As String

  xfile = Temp() & ".conf"
  If Exist(modHelpVariable.$appetcFolder &/ "OnlineBookBase.conf.7z") Then
    modDevAll.Get7ZExtraction(modHelpVariable.$appetcFolder &/ "OnlineBookBase.conf.7z", modHelpVariable.$CryptZip, xfile)
  Else
    File.Save(xfile, File.Load(modHelpVariable.$appetcFolder &/ "OnlineBookBase.conf"))
  Endif
  Return xfile

End

Public Sub ReadOnlineConfig()

  Dim xfile As String

  xfile = GetOnlineConfigSettingFIle()
  If Exist(xfile) Then
    $OnlineService = modControlSub.ShowSettingValue(xfile, "OnlineBooking/Service")
    $OnlineHost = modControlSub.ShowSettingValue(xfile, "OnlineBooking/Host")
    If modControlSub.ShowSettingValue(xfile, "OnlineBooking/User") Then
      $OnlineUser = UnBase64(modControlSub.ShowSettingValue(xfile, "OnlineBooking/User"))
    Endif
    If modControlSub.ShowSettingValue(xfile, "OnlineBooking/Password") Then
      $OnlinePassword = UnBase64(modControlSub.ShowSettingValue(xfile, "OnlineBooking/Password"))
    Endif
    $OnlineDatabase = modControlSub.ShowSettingValue(xfile, "OnlineBooking/Database")
    $OnlinePort = modControlSub.ShowSettingValue(xfile, "OnlineBooking/Port")
  Endif

End

Public Sub OpenOnlineDB()

  ReadOnlineConfig()
  $OnlineCon = modDBApp.OpenConnection($OnlineService, $OnlineHost, $OnlineDatabase, $OnlineUser, $OnlinePassword, $OnlinePort)      ''
  $OnlRoutine = modSettings.ShowSettingFromFIle("OnlineBooking/Routine")
  If Not $OnlRoutine Then
    $OnlRoutine = "spGetTransByToken"
  Endif

End

Public Function GetTokenStatus(sToken As String, sBookID As String, sBillNo As String) As String

  Dim res As Result
  Dim xxx As String
  Dim res1 As Result
  Dim xIntVal As String

  xxx = ""
  If $OnlineCon.Opened Then
    res = $OnlineCon.Exec("select patientId,patientName,patientMobile,refId,gateway,paidAmount,deviceType,paymentStatus,successOn from appBooking where token=&1", sToken)
    If res.Available Then
      If res["paymentStatus"] = 1 Then

        res1 = modDatabase.$myConn.Create("tblwebpayment")
        res1["fldbillno"] = sBillNo
        res1["fldencounterval"] = sBookID
        res1["flditemid"] = "Booking"
        res1["fldlink"] = ""

        res1["fldvendor"] = res["gateway"]
        res1["fldstate"] = "Successful"
        res1["fldtransid"] = res["refId"]
        res1["fldtransamt"] = res["paidAmount"]
        res1["fldtranstoken"] = sToken
        res1["fldtransdate"] = res["successOn"]

        res1["fldpayerid"] = res["patientId"]
        res1["fldpayername"] = res["patientName"]
        res1["fldpayermobile"] = res["patientMobile"]

        res1["flduserid"] = modBasic.$lbluser
        res1["fldtime"] = Now()
        res1["fldcomp"] = modBasic.$compID
        res1["fldsave"] = True
        res1["fldhostmac"] = res["deviceType"]
        res1["xyz"] = False
        If MMain.$WebEntry = True Then
          xIntVal = modString.GetDateString(Now())
          res1["fldid"] = CLong(xIntVal)
          res1["fldrepoid"] = modMisc.GetWebIndexStr(xIntVal)
          res1["fldrepodate"] = Now()
          res1["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
          res1["fldhospcode"] = modBasic.$HospCode
        Endif
        res1.Update
        xxx = "Successful"

      Else
        xxx = "Pending"
      Endif
    Else
      xxx = "Pending"
    Endif
  Endif
  Return xxx

End
