' Gambas class file

Private $strCode As String
Private $tbl As String
Private $sTitle As String
Private $mRes As Result

Public Sub _new(sTitle As String, strCode As String, tbl As String)

  $strCode = strCode
  $tbl = tbl
  $sTitle = sTitle

  If MMain.$IsDrugInfo = True Then
    pnldrugverify.Visible = True
  Endif

  lblcode.Text = $strCode
  Me.Title = $sTitle

  If $sTitle = "Hepatic Precautions" Then
    lblcondition.Text = "Condition"
    cmbcondition.List = ["Impaired", "Normal"]
  Else If $sTitle = "Pregnancy/Breast Feeding Precautions" Then
    lblcondition.Text = "Condition"
    cmbcondition.List = ["1st trimester", "2nd trimester", "3rd trimester", "Breast feeding", "Non Pregnant"]
  Else If $sTitle = "Drug-Drug Interactions" Then
    lblcondition.Text = "Drug Name"
  Endif
  ShowOutput()
  ShowSubOutput()

End

Private Sub ShowOutput()

  Dim sql As String

  sql = "Select medid,fldcondition,fldoutput,fldreference from " & $tbl & " where fldcodename=&1"
  $mRes = modDatabase.$medConn.Exec(sql, lblcode.Text)

End

Private Sub ShowSubOutput()

  If $mRes.Available Then
    txtmedid.Value = $mRes["medid"]
    cmbcondition.Text = $mRes["fldcondition"]
    txtwarn.Text = $mRes["fldoutput"]
    txtreference.Text = $mRes["fldreference"]
    btnsubOK.Text = "Add"
  Endif

End

Public Sub btnprev_Click()

  If $mRes.Index = 0 Then
    $mRes.MoveFirst
  Else
    $mRes.MovePrevious
  Endif
  ShowSubOutput()

End

Public Sub btnnext_Click()

  If $mRes.Index = $mRes.Count - 1 Then
    $mRes.MoveLast
  Else
    $mRes.MoveNext
  Endif
  ShowSubOutput()

End

Public Sub btnsubOK_Click()

  If btnsubOK.Text = "Add" Then
    btnsubOK.Text = "Save"
    txtwarn.Text = ""
    txtreference.Text = ""

  Else If btnsubOK.Text = "Save" Then
    btnsubOK.Text = "Add"
    $mRes = modDatabase.$medConn.Create($tbl)
    $mRes["fldcodename"] = lblcode.Text
    $mRes["fldcondition"] = cmbcondition.Text
    $mRes["fldoutput"] = Trim(txtwarn.Text)
    $mRes["fldreference"] = Trim(txtreference.Text)
    If MMain.$IsDrugInfo = True Then
      $mRes["flduserid"] = modBasic.$lbluser
      $mRes["fldtime"] = Now()
      $mRes["fldcomp"] = modBasic.$compID
      $mRes["fldverifyid"] = ""
      $mRes["fldverifytime"] = ""
      $mRes["fldnote"] = ""
    Endif
    $mRes.Update()
    ShowOutput()
    ShowSubOutput()
    Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay

  Endif

End

Public Sub cmbcondition_Click()

  If $sTitle = "Drug-Drug Interactions" Then
    modPharmSub.GenericMedicineList(cmbcondition)
  Endif

End

Public Sub btnsubChange_Click()

  Dim res As Result

  If btnsubChange.Text = "Edit" Then
    btnsubChange.Text = "Update"
  Else If btnsubChange.Text = "Update" Then
    btnsubChange.Text = "Edit"
    res = modDatabase.$medConn.Edit($tbl, "medid=&1", txtmedid.Value)
    If res.Available Then
      res["fldcondition"] = cmbcondition.Text
      res["fldoutput"] = Trim(txtwarn.Text)
      res["fldreference"] = Trim(txtreference.Text)
      If MMain.$IsDrugInfo = True Then
        res["flduserid"] = modBasic.$lbluser
        res["fldtime"] = Now()
        res["fldcomp"] = modBasic.$compID
        res["fldverifyid"] = ""
        res["fldverifytime"] = ""
        res["fldnote"] = ""
      Endif
      res.Update()
      Me.Exec("Toastify({text: 'Information updated', duration: 3000}).showToast()")
      ' Balloon.Delay = modBasic.$BalloonDelay
      ShowOutput()
      ShowSubOutput()
    Endif
  Endif

End

Public Sub btnsubCancel_Click()

  If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
    modDatabase.$medConn.Delete($tbl, "medid=&1", txtmedid.Value)
    txtreference.Text = ""
    txtwarn.Text = ""
    ShowOutput()
    ShowSubOutput()
    Me.Exec("Toastify({text: 'Information deleted', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Public Sub btnverifysave_Click()

  Dim res As Result

  If txtmedid.Value Then
    res = modDatabase.$medConn.Edit($tbl, "medid=&1", txtmedid.Value)
    If chkverify.Value = True Then
      res["fldverifyid"] = modBasic.$lbluser
      res["fldverifytime"] = Now()
      res["fldnote"] = txtverifynote.Text
    Else
      res["fldnote"] = txtverifynote.Text
    Endif
    res.Update
    Me.Exec("Toastify({text: 'Information verified', duration: 3000}).showToast()")
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
