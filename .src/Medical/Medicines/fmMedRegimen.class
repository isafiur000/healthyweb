' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub _new()

  cmbfreq.List = modMedicine.FrequencyCombo()
  cmbroute.List = modMedicine.ClinicalRoute()
  If modHelpVariable.$LogInCategory = "Clinician" Then
    cmbuserid.Text = modBasic.$lbluser
    cmbuserid.Enabled = False
  Else
    cmbuserid.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flduserid as col from tbluser where fldipconsult=&1 and fldstatus=&2", True, "Active"))
    cmbuserid.Add("%")
    cmbuserid.Text = "%"
  Endif
  SHowRegimenGrid()

End

Private Sub SHowRegimenGrid()

  $rData = modDatabase.$myConn.Exec("select fldid,fldroute,fldbrandid,flddose,fldfreq,fldday,fldqty,fldusage from tblmedregimen where flduserid=&1", modBasic.$lbluser)                                 ''
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(300 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"

    .Columns[1].Text = "Route"
    .Columns[2].Text = "Medicine"
    .Columns[3].Text = "Dose"
    .Columns[4].Text = "Freq"
    .Columns[5].Text = "Day"
    .Columns[6].Text = "QTY"
    .Columns[7].Text = "Usage"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]

End

Private Sub ClearControls()

  txtsnid.Value = 0
  cmbroute.Text = ""
  cmbdrug.Text = ""
  cmbdrug.Clear()
  txtunitdose.Value = 0
  cmbfreq.Text = ""
  txtdura.Value = 0
  txtqty.Value = 0
  txtusage.Text = ""

End

Public Sub btnadd_Click()

  Dim res As Result

  If cmbroute.Text And If cmbdrug.Text And If txtunitdose.Value Then
    res = modDatabase.$myConn.Create("tblmedregimen")
    res["fldroute"] = cmbroute.Text
    res["fldbrandid"] = cmbdrug.Text
    res["flddose"] = txtunitdose.Value
    res["fldfreq"] = cmbfreq.Text
    res["fldday"] = txtdura.Value
    res["fldqty"] = txtqty.Value
    res["fldusage"] = Trim(txtusage.Text)
    res["flduserid"] = cmbuserid.Text
    res.Update
    Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")
    SHowRegimenGrid()
    ClearControls()
  Endif

End

Public Sub GridView1_Select()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    txtsnid.Value = $rData["fldid"]
    cmbroute.Text = $rData["fldroute"]
    cmbdrug.Text = $rData["fldbrandid"]
    txtunitdose.Value = $rData["flddose"]
    cmbfreq.Text = $rData["fldfreq"]
    txtdura.Value = $rData["fldday"]
    txtqty.Value = $rData["fldqty"]
    txtusage.Text = $rData["fldusage"]
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result

  If cmbroute.Text And If cmbdrug.Text And If txtunitdose.Value Then
    res = modDatabase.$myConn.Edit("tblmedregimen", "fldid=&1", txtsnid.Value)
    res["fldroute"] = cmbroute.Text
    res["fldbrandid"] = cmbdrug.Text
    res["flddose"] = txtunitdose.Value
    res["fldfreq"] = cmbfreq.Text
    res["fldday"] = txtdura.Value
    res["fldqty"] = txtqty.Value
    res["fldusage"] = Trim(txtusage.Text)
    res["flduserid"] = cmbuserid.Text
    res.Update
    Me.Exec("Toastify({text: 'Information updated', duration: 3000}).showToast()")
    SHowRegimenGrid()
  Endif

End

Public Sub btndel_Click()

  If txtsnid.Value Then
    modDatabase.$myConn.Delete("tblmedregimen", "fldid=&1", txtsnid.Value)
    Me.Exec("Toastify({text: 'Information deleted', duration: 3000}).showToast()")
    SHowRegimenGrid()
  Endif

End

Public Sub btncompList_Click()

  Dim res As Result

  If Not cmbroute.Text Then
    cmbroute.SetFocus
  Else If cmbroute.Text Then
    If Not cmbdrug.Text Then
      res = modStock.ItemListForPurchaseResult(cmbroute.Text, "Generic")
      cmbdrug.Text = GridViewNew("Select Particulars", modControlSub.GetDirectFillresult(res), False)
      cmbroute.SetFocus
    Endif
  Endif

End

' ' Public Sub cmbroute_KeyRelease()
' '
' '   modFillContainer.AutoFillComboBox(cmbroute)
' '   modFillContainer.RestrictComboToContent(cmbroute)
' '
' ' End
' '
' ' Public Sub cmbfreq_KeyRelease()
' '
' '   modFillContainer.AutoFillComboBox(cmbfreq)
' '   modFillContainer.RestrictComboToContent(cmbfreq)
' '
' ' End
' '
' '

Public Sub btnclose_Click()

  Me.Close()

End
