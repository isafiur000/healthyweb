' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Private $sType As String

Public Sub _new(sType As String)

  Dim xList As String[]

  $sType = sType

  xList = modGeneral.CompList()
  cmbcomp.List = xList
  txtname.List = modGeneral.GetDepartmentAllList("%")
  txtname.Text = ""
  cmbselcomp.List = xList
  cmbselcomp.Add("%")
  cmbselcomp.Text = "%"
  txtiptype.List = ["Fixed", "Dynamic"]

  If $sType = "Admin" Then
    Panel1.Enabled = True
  Else
    Panel1.Enabled = False
  Endif

  cmbactive.List = ["Active", "Inactive"]
  ShowGridAccess()

End

Public Sub chkpass_Click()

  If chkpass.Value = True Then
    txtpass.Password = False
  Else If chkpass.Value = False Then
    txtpass.Password = True
  Endif

End

Public Sub btnrefresh_Click()

  ShowGridAccess()

End

Private Sub ShowGridAccess()

  Dim sql As String

  sql = "select fldhostmac,fldhostip,fldhostname,fldhostuser,fldcomp,fldcompname,fldaccess,fldiptype,fldhostpass,fldhostmac from tblmacaccess where fldcomp like &1 and lower(fldhostmac) like &2"
  $rData = modDatabase.$myConn.Exec(sql, cmbselcomp.Text, LCase(txtmacsearch.Text) & "%")
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(110 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(120 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Hidden = True
    .Columns[9].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"

    .Columns[0].Text = "MAC Address"
    .Columns[1].Text = "Set IP"
    .Columns[2].Text = "Host Name"
    .Columns[3].Text = "Host User"
    .Columns[4].Text = "CompID"
    .Columns[5].Text = "Department"
    .Columns[6].Text = "Access"
    .Columns[7].Text = "IPType"
    .Columns[9].Text = "Version"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 9 Then
    Data.Text = modGeneral.GetVersiononMac($rData[$aMyFields[Column]])
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Select()

  txtname.Text = ""
  chkmac.Value = False
  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    txtmac.Text = $rData["fldhostmac"]
    txtip.Text = $rData["fldhostip"]
    txthost.Text = $rData["fldhostname"]
    txtuser.Text = $rData["fldhostuser"]
    cmbcomp.Text = $rData["fldcomp"]
    txtname.Text = $rData["fldcompname"]
    cmbactive.Text = $rData["fldaccess"]
    txtiptype.Text = $rData["fldiptype"]
    txtpass.Text = UnBase64($rData["fldhostpass"])

    lblip.Text = modGeneral.GetCurrentIPonMac($rData["fldhostmac"])
    lbluser.Text = modGeneral.GetCurrentUseronMac($rData["fldhostmac"])
  Endif

End

Public Sub btnupdall_Click()

  Dim res As Result

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If Message.Question("Are you sure ?", ("No"), ("OK")) = 2 Then
      res = modDatabase.$myConn.Edit("tblmacaccess", "fldhostmac=&1", $rData["fldhostmac"])
      res["fldcomp"] = cmbcomp.Text
      res["fldhostip"] = txtip.Text
      res["fldiptype"] = txtiptype.Text
      res["fldcompname"] = Trim(txtname.Text)
      res["fldaccess"] = cmbactive.Text
      res["fldhostname"] = Trim(txthost.Text)
      res["fldhostuser"] = Trim(txtuser.Text)
      res["fldhostpass"] = Base64(Trim(txtpass.Text))
      res.Update()
      Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information updated", modBasic.$BalloonDelay))
      ShowGridAccess()
    Endif
  Endif

End

Public Sub btnadd_Click()

  Dim res As Result

  If Message.Question("Are you sure you want to add this MAC ?", ("No"), ("Yes")) = 2 Then
    res = modDatabase.$myConn.Create("tblmacaccess")
    res["fldhostmac"] = Trim(txtmac.Text)
    res["fldhostuser"] = Trim(txtuser.Text)
    res["fldhostpass"] = Base64(Trim(txtpass.Text))
    res["fldhostip"] = txtip.Text
    res["fldhostname"] = Trim(txthost.Text)
    res["fldcomp"] = cmbcomp.Text
    res["fldcompname"] = Trim(txtname.Text)
    res["fldaccess"] = cmbactive.Text
    res["fldiptype"] = txtiptype.Text
    res["fldcode"] = modGeneral.GetMaxMacAddCode()
    res.Update()
    Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information saved", modBasic.$BalloonDelay))
    ShowGridAccess()
  Endif

End

Public Sub chkmac_Click()

  If chkmac.Value = True Then
    txtmac.ReadOnly = False
  Else
    txtmac.ReadOnly = True
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
