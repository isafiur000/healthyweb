' Gambas class file

Private $PatientNum As String
Private $encid As String

Private $rDatamain As Result
Private $aMyFieldsmain As String[]
Private $rData1 As Result
Private $aMyFields1 As String[]
Private $rData2 As Result
Private $aMyFields2 As String[]

Private $doa As Date

Public Sub _new(sPatNo As String)

  $PatientNum = sPatNo
  FillEncounterData()
  cmbtype.List = ["Diagnostic Tests", "Radio Diagnostics", "General Reports", "Scanned Images", "Past Documents"]
  rbinvoice.Value = True
  modBasic.$currency = "Rs "

End

Public Sub WebForm_Open()

  modGeneralMain.RecordFormUse(Me)

End

''============================== Main Forms =======================
Private Sub FillEncounterData()

  If MMain.$WebEntry = True Then
    $rDatamain = modDatabase.$myConn.Exec("select fldregdate,fldencounterval,fldencounterval,flddisctype,fldencounterval,fldhospcode from tblencounter where fldpatientval=&1 ORDER BY fldregdate DESC", modBasic.$lbluser)
  Else
    $rDatamain = modDatabase.$myConn.Exec("select fldregdate,fldencounterval,fldencounterval,flddisctype,fldencounterval from tblencounter where fldpatientval=&1 ORDER BY fldregdate DESC", modBasic.$lbluser)
  Endif
  $aMyFieldsmain = New String[]
  modGridView.ReadSmallData(mainGridView, $rDatamain, $aMyFieldsmain)

  With mainGridView
    .Columns[0].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(250 * modBasic.$AppWidthRatio) & "px"

    .Columns[0].Text = "Date"
    .Columns[1].Text = "Visit"
    .Columns[2].Text = "Age"
    .Columns[3].Text = "Scheme"
    .Columns[4].Text = "Diagnosis"
  End With

End

Public Sub mainGridView_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rDatamain.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 0 Then
    Data.Text = modReportVar.GetDateTimeReport($rDatamain[$aMyFieldsmain[Column]], gb.MediumDate)
  Else If Column = 2 Then
    Data.Text = modPatient.GetPatientAgeString($rDatamain[$aMyFieldsmain[Column]], modPatient.GetRecordDate($rDatamain[$aMyFieldsmain[Column]]))
  Else If Column = 4 Then
    Data.Html = modPatient.PatientDiagnoCurrentList($rDatamain[$aMyFieldsmain[Column]]).Join("<br>")
  Else
    Data.Text = $rDatamain[$aMyFieldsmain[Column]]
  Endif

End

Public Sub mainGridView_Select()

  If mainGridView.Selection.Count Then
    $rDatamain.MoveTo(mainGridView.Selection[0])
    $encid = $rDatamain["fldencounterval"]
    $PatientNum = modPatient.GetPatientNoByEnc($encid)
    $doa = modPatient.GetRecordDate($encid)
    If cmbtype.Text Then
      FillReportData($encid, cmbtype.Text)
    Endif
  Endif

End

Public Sub WebTabPanel1_Click()

  Dim hForm As FmPacsFind
  Dim hPtForm As FmClinHistory

  If WebTabPanel1.Index = 1 Then
    btnbillrefresh_Click()

  Else If WebTabPanel1.Index = 2 Then
    If wbPacsmainpanel.Children.Count Then
      wbPacsmainpanel.DeleteChildren()
    Endif
    hForm = New FmPacsFind($encid, "Visit", True, wbPacsmainpanel)

  Else If WebTabPanel1.Index = 3 Then
    If pnlhistory.Children.Count Then
      pnlhistory.DeleteChildren()
    Endif
    hPtForm = New FmClinHistory($encid, "Encounter", "height-tablet-long", pnlhistory)

  Endif

End

''====================== Archived REPORTS =========================
Public Sub btnshowreport_Click()

  If $encid Then
    If cmbtype.Text Then
      FillReportData($encid, cmbtype.Text)
    Endif
  Endif

End

Private Sub FillReportData(encid As String, sType As String)

  If MMain.$WebEntry = True Then
    $rData1 = modDatabase.$syConn.Exec("select fldrepoid,fldcateg,fldtime,fldencounterval,fldencounterval,fldtitle,fldid,flddetail,fldsave,fldlink,fldcomp,flduserid from tblpatreport where fldencounterval like &1 and fldcateg=&2 and (flvisible=&3 or flvisible IS NULL) ORDER BY fldid DESC", encid, sType, "Visible")
  Else
    $rData1 = modDatabase.$syConn.Exec("select fldid,fldcateg,fldtime,fldencounterval,fldencounterval,fldtitle,fldid,flddetail,fldsave,fldlink,fldcomp,flduserid from tblpatreport where fldencounterval like &1 and fldcateg=&2 and (flvisible=&3 or flvisible IS NULL) ORDER BY fldid DESC", encid, sType, "Visible")                                                                 ''
  Endif
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(GridView1, $rData1, $aMyFields1)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Hidden = True
    .Columns[2].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Hidden = True
    .Columns[4].Hidden = True
    .Columns[5].Width = CStr(225 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Hidden = True
    .Columns[7].Width = CStr(225 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Hidden = True
    .Columns[9].Hidden = True
    .Columns[10].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[11].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"

    .Columns[2].Text = "DateTime"
    .Columns[5].Text = "Description"
    .Columns[7].Text = "Comment"
    .Columns[10].Text = "Depart"
    .Columns[11].Text = "User"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData1.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 2 Then
    Data.Text = modReportVar.GetDateTimeReport($rData1[$aMyFields1[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData1[$aMyFields1[Column]]
  Endif

End

Public Sub mnushow_Click()

  Dim xLink As String

  If GridView1.Selection.Count Then
    $rData1.MoveTo(GridView1.Selection[0])

    If MMain.$WebEntry = True Then
      xLink = modImage.GetBlobFileData($rData1["fldrepoid"])
    Else
      xLink = modImage.GetBlobFileData($rData1["fldid"])
    Endif
    If xLink Then
      WebPDFView1.Viewer = modSettings.ShowSettingFromFIle("Application/PDFViewer")
      WebPDFView1.Tag = modPrint.GetFileWebURL(xLink)
      WebPDFView1.Path = WebPDFView1.Tag
    Endif

  Endif

End

Public Sub mnuprint_Click()

  Dim xLink As String

  If GridView1.Selection.Count Then
    $rData1.MoveTo(GridView1.Selection[0])

    If MMain.$WebEntry = True Then
      xLink = modImage.GetBlobFileData($rData1["fldrepoid"])
    Else
      xLink = modImage.GetBlobFileData($rData1["fldid"])
    Endif
    If xLink Then
      Me.Exec("printJS('" & modPrint.GetFileWebURL(xLink) & "')")
    Endif

  Endif

End

''============================== Invoices ================
Public Sub btnbillrefresh_Click()

  If $encid Then
    SHowPatientInvoices($encid)
  Endif

End

Private Sub SHowPatientInvoices(encid As String)

  Dim xFldList As String[]
  Dim xcash As Float
  Dim xcred As Float

  xFldList = ["fldtime", "fldtime", "fldbillno", "fldencounterval", "fldencounterval", "fldbilltype", "fldbankname", "fldchequeno", "flditemamt", "flddiscountamt", "fldtaxamt", "fldchargedamt", "fldreceivedamt", "fldcurdeposit", "flduserid", "fldbilltype", "fldtaxamt", "flddiscountamt", "fldbillno", "flddiscountgroup", "fldcomp"]
  If rbinvoice.Value = True Then
    $rData2 = modDatabase.$myConn.Exec("select " & xFldList.Join(",") & " from tblpatbilldetail where fldencounterval=&1 ORDER BY fldid DESC", encid)
  Else If rbreceipt.Value = True Then
    $rData2 = modDatabase.$myConn.Exec("select " & xFldList.Join(",") & " from tbltempbilldetail where fldencounterval=&1 ORDER BY fldid DESC", encid)
  Endif

  xcash = 0
  xcred = 0
  For Each $rData2
    If $rData2["fldbilltype"] = "Cash" Then
      xcash = xcash + $rData2["fldreceivedamt"]
    Else If $rData2["fldbilltype"] = "Credit" Then
      xcash = xcash + $rData2["fldreceivedamt"]
      xcred = xcred + ($rData2["fldchargedamt"] - $rData2["fldreceivedamt"])
    Endif
  Next

  $aMyFields2 = New String[]
  modGridView.ReadSmallData(GridView2, $rData2, $aMyFields2)

  With GridView2
    .Columns[0].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Hidden = True
    .Columns[2].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(300 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[9].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[10].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[11].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[12].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[13].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[14].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[15].Hidden = True
    .Columns[16].Hidden = True
    .Columns[17].Hidden = True
    .Columns[18].Hidden = True
    .Columns[19].Hidden = True
    .Columns[20].Hidden = True

    .Columns[0].Text = "Date"
    .Columns[2].Text = "Invoice"
    .Columns[3].Text = "EncID"
    .Columns[4].Text = "Name"
    .Columns[5].Text = "Type"
    .Columns[6].Text = "Account"
    .Columns[7].Text = "Reference"
    .Columns[8].Text = "SubTotal"
    .Columns[9].Text = "DiscAmt"
    .Columns[10].Text = "TaxAmt"
    .Columns[11].Text = "NetTotal"
    .Columns[12].Text = "Received"
    .Columns[13].Text = "Deposit"
    .Columns[14].Text = "User"
  End With

  txtcash.Value = Format(xcash, gb.Fixed)
  txtcredit.Value = Format(xcred, gb.Fixed)

End

Public Sub GridView2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData2.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = GetGridViewValue(Column, $rData2[$aMyFields2[Column]])

End

Private Function GetGridViewValue(Column As Integer, xVariable As Variant) As Variant

  Dim xxx As Variant

  If Column = 0 Then
    xxx = modReportVar.GetDateTimeReport(xVariable, gb.GeneralDate)
  Else If Column = 4 Then
    xxx = modPatient.GetPatientNameByEnc(xVariable, modDatabase.$syConn)
  Else If Column = 8 Then
    xxx = modReportVar.GetLocaleNumberFormat(xVariable, gb.Currency)
  Else If Column = 9 Then
    xxx = modReportVar.GetLocaleNumberFormat(xVariable, gb.Currency)
  Else If Column = 10 Then
    xxx = modReportVar.GetLocaleNumberFormat(xVariable, gb.Currency)
  Else If Column = 11 Then
    xxx = modReportVar.GetLocaleNumberFormat(xVariable, gb.Currency)
  Else If Column = 12 Then
    xxx = modReportVar.GetLocaleNumberFormat(xVariable, gb.Currency)
  Else If Column = 13 Then
    xxx = modReportVar.GetLocaleNumberFormat(xVariable, gb.Currency)
  Else If Column = 18 Then
    xxx = modNonMedical.GetReturnInvoices(xVariable).Join("; ")
  Else
    xxx = xVariable
  Endif

  Return xxx

End

Public Sub mnushowinv_Click()

  Dim xLink As String
  Dim ShowTax As Boolean
  Dim ShowDisc As Boolean

  If GridView2.Selection.Count Then
    $rData2.MoveTo(GridView2.Selection[0])

    modNonMedical.$tblpatbilling = "tblpatbilling"
    modNonMedical.$tblpatbilldetail = "tblpatbilldetail"
    modNonMedical.$tbltempbilldetail = "tbltempbilldetail"
    If rbinvoice.Value = True Then
      If $rData2["fldtaxamt"] = 0 Then
        ShowTax = False
      Else
        ShowTax = True
      Endif
      If $rData2["flddiscountamt"] = 0 Then
        ShowDisc = False
      Else
        ShowDisc = True
      Endif
      xLink = modBILLFormat.GetBillingInvoicePath($rData2["fldbillno"], modBillLock.GetCopyBillTypeFromBillNo($rData2["fldbillno"]), ShowTax, ShowDisc)
    Else If rbreceipt.Value = True Then
      If $rData2["fldbillno"] Like "TDA" & "*" Then
        xLink = modBILLFormat.GetDischargeReceiptPath($rData2["fldencounterval"], $rData2["fldbillno"], "DISCHARGE SLIP (COPY)")
      Else
        xLink = modBILLFormat.GetBillingReceiptPath($rData2["fldbillno"], "RECEIPT (COPY)")
      Endif
    Endif

    If xLink Then
      WebPDFView2.Path = modPrint.GetFileWebURL(xLink)
    Endif

    modNonMedical.$tblpatbilling = ""
    modNonMedical.$tblpatbilldetail = ""
    modNonMedical.$tbltempbilldetail = ""
  Endif

End

''======================== History =============================
