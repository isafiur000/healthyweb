' Gambas class file

Private $colCateg As String
Private $colCombo As String
Private $coltext As String
Private $tbl As String
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(colcateg As String, colCombo As String, coltext As String, sTable As String, fixCateg As String, OptionList As String[])

  $colCateg = colcateg
  $colCombo = colCombo
  $coltext = coltext
  $tbl = sTable
  txtgroup.Text = fixCateg
  txtgroup.Enabled = False

  If OptionList.Count Then
    cmbtype.List = OptionList
  Endif
  ShowGridView()

End

Public Sub ShowGridView()

  Dim sql As String

  If txtgroup.Text Then
    sql = Subst("select fldid,&1,&2 from &3 where &4", $colCombo, $coltext, $tbl, $colCateg) & "=&1"
    $rData = modDatabase.$myConn.Exec(sql, txtgroup.Text)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

    With GridView1
      .Columns[0].Hidden = True
      .Columns[1].Width = CStr(250 * modBasic.$AppWidthRatio) & "px"
      .Columns[2].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
      .Columns[1].Text = "Group"
      .Columns[2].Text = "Variable"
    End With

  Endif

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]

End

Public Sub btnOK_Click()

  Dim res As Result

  If txtvariable.Text Then
    res = modDatabase.$myConn.Create($tbl)
    res[$colCateg] = Trim(txtgroup.Text)
    res[$colCombo] = cmbtype.Text
    res[$coltext] = Trim(txtvariable.Text)
    res.Update()
    Me.Exec("Toastify({text: 'Variable added', duration: 3000}).showToast()")
    ShowGridView()
    cmbtype.Text = ""
    txtvariable.Text = ""
    cmbtype.SetFocus
  Endif

End

Public Sub btnCancel_Click()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    modDatabase.$myConn.Delete($tbl, "fldid=&1", $rData["fldid"])
    ShowGridView()
    cmbtype.Text = ""
    txtvariable.Text = ""
    Me.Exec("Toastify({text: 'Variable deleted', duration: 3000}).showToast()")
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, Me.Title, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub GridView1_Select()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    cmbtype.Text = $rData[$colCombo]
    txtvariable.Text = $rData[$coltext]
  Endif

End

Public Sub btnupload_Click()

  Dim xdelim As String

  If txtdelim.Text Then
    xdelim = txtdelim.Text
  Else
    xdelim = ";"
  Endif
  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    modTextDB.ImportCSVToCurrentDB(modDatabase.$myConn, $tbl, txtcsv.Text, xdelim)
    Me.Exec("Toastify({text: 'Data Import completed', duration: 3000}).showToast()")
    ShowGridView()
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
