' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub _new()

  cmbcategory.List = ["Consultation", "OPD Visit", "Patient Ward"]
  cmbpackage.List = modNonMedical.FillDiscountCombo()
  cmbactive.List = modGeneral.GetDepartCaptionList()
  cmbcompid.List = modBasic.$AllCompList
  ShowCostGrid()

End

Public Sub btnrefresh_Click()

  ShowCostGrid()

End

Public Sub btnBrOK_Click()

  Dim res As Result
  Dim xIntVal As String

  If txtdept.Text Then
    res = modDatabase.$myConn.Create("tbldepartment")
    res["flddept"] = Trim(txtdept.Text)
    res["fldroom"] = txtroom.Text
    res["flddisctype"] = cmbpackage.Text
    res["fldcateg"] = cmbcategory.Text
    res["fldactive"] = cmbactive.Text
    res["fldcomp"] = cmbcompid.Text
    res["fldinterval"] = txtinterval.Value
    res["fldstart"] = CDate(dtstart.Text)
    If MMain.$WebEntry = True Then
      xIntVal = modString.GetDateString(Now())
      res["fldid"] = CLong(xIntVal)
      res["fldrepoid"] = modMisc.GetWebIndexStr(xIntVal)
      res["fldrepodate"] = Now()
      res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
      res["fldhospcode"] = modBasic.$HospCode
    Endif
    res.Update()
    ShowCostGrid()
    Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
    cmbactive.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldactive) as col from tbldepartment"))
    txtdept.SetFocus
  Endif

End

Public Sub btnedit_Click()

  Dim res As Result

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    res = modDatabase.$myConn.Edit("tbldepartment", "fldid=&1", $rData["fldid"])
    res["flddept"] = Trim(txtdept.Text)
    res["fldroom"] = txtroom.Text
    res["flddisctype"] = cmbpackage.Text
    res["fldcateg"] = cmbcategory.Text
    res["fldactive"] = cmbactive.Text
    res["fldcomp"] = cmbcompid.Text
    res["fldinterval"] = txtinterval.Value
    res["fldstart"] = CDate(dtstart.Text)
    res["xyz"] = False
    res.Update()
    ShowCostGrid()
    Me.Exec("Toastify({text: 'Information updated', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Private Sub ShowCostGrid()

  Dim sql As String
  Dim xcateg As String

  If cmbcategory.Text Then
    xcateg = cmbcategory.Text
  Else
    xcateg = "%"
  Endif
  sql = "select fldid,flddept,fldcateg,fldroom,flddisctype,fldactive,fldcomp,fldinterval,fldstart from tbldepartment where fldcateg like &1 ORDER BY flddept ASC"
  $rData = modDatabase.$myConn.Exec(sql, xcateg)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Hidden = True
    .Columns[3].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Hidden = True
    .Columns[7].Hidden = True
    .Columns[8].Hidden = True

    .Columns[1].Text = "Department"
    .Columns[3].Text = "RoomNo"
    .Columns[4].Text = "Package"
    .Columns[5].Text = "Caption"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]

End

Public Sub mnudel_Click()

  If GridView1.Selection.Count Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      $rData.MoveTo(GridView1.Selection[0])
      modDatabase.$myConn.Delete("tbldepartment", "fldid=&1", $rData["fldid"])
      ShowCostGrid()
    Endif
  Endif

End

Public Sub GridView1_Select()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    txtdept.Text = $rData["flddept"]
    cmbcategory.Text = $rData["fldcateg"]
    txtroom.Text = $rData["fldroom"]
    cmbpackage.Text = $rData["flddisctype"]
    cmbactive.Text = $rData["fldactive"]
    cmbcompid.Text = $rData["fldcomp"]
    If $rData["fldinterval"] Then
      txtinterval.Value = $rData["fldinterval"]
    Else
      txtinterval.Value = 0
    Endif
    If IsDate($rData["fldstart"]) Then
      dtstart.Text = CStr($rData["fldstart"])
    Else
      dtstart.Text = ""
    Endif
  Endif

End

Public Sub btnbed_Click()

  Dim hForm As FmDepartBed

  If cmbcategory.Text = "Patient Ward" Then
    hForm = New FmDepartBed(txtdept.Text)
    hForm.ShowModal
  Endif

End

Public Sub btnexport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, "DEPARTMENTS", modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub btnbedrepo_Click()

  Dim xPath As String

  xPath = modGENReport.ShowDepartmentsReport()
  modControlSub.OpenHTMLPreview("", xPath, "ReportSize")

End

Public Sub btnbedfee_Click()

  fmAddDeptBed.ShowModal

End

Public Sub btnclose_Click()

  Me.Close()

End
