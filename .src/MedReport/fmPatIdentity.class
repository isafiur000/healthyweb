' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $ColCount As Integer

Private $SSQLFields As String[]

Public Sub _new()

  Dim xhospfld As String

  If MMain.$WebReport = "Multiple" Then
    If modBasic.$HospCode Then
      cmblocation.Text = modDataRepo.$RepositoryMode
      cmbvalue.Text = modBasic.$HospCode
      pnlrep.Enabled = False
    Else
      cmblocation.List = ["Hospital", "Municipality", "Category", "District", "Province"]
    Endif
    If MMain.$RepoReportMode = True Then
      mnusearname.Visible = False
    Endif

  Else
    pnlrep.Visible = False
  Endif

  If MMain.$IsPolice = True Then
    mnuservice.Visible = True
  Else
    mnuservice.Visible = False
  Endif

  cmbgender.List = ["Male", "Female", "Other", "%"]
  cmbgender.Text = "%"
  cmbdistrict.List = modBasic.$DistrictList
  cmbdistrict.Add("%")
  cmbdistrict.Text = "%"
  cmbethnic.Text = "%"

  dtfir.Value = Now()
  dtlast.Value = Now()

  xhospfld = modDataRepo.HospitalField()
  $SSQLFields = ["fldpatientval", "fldpatientval", "fldethniccode", "fldptsex", "fldptbirday", "fldptaddvill", "fldptadddist", "fldptadmindate"]
  If xhospfld Then
    $SSQLFields.Add(xhospfld)
  Endif
  chkgrid.Value = True

End

Public Sub WebForm_Open()

  modGeneralMain.RecordFormUse(Me)

End

Public Sub cmblocation_Click()

  cmbvalue.Clear()
  If cmblocation.Text Then
    cmbvalue.List = modDataRepo.GetRepoValueListType(cmblocation.Text)
  Endif

End

Public Sub dtnepfir_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtfir.Value))
  If xx Then
    dtfir.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub dtneplast_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtlast.Value))
  If xx Then
    dtlast.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub mnusearch_Click()

  Dim patno As String
  Dim sql As String
  Dim xFldList As String[]

  xFldList = $SSQLFields.Copy()
  patno = InputBox("Patient Identification Number", "Search", "")
  If patno Then
    sql = "select " & xFldList.Join(",") & " from tblpatientinfo where fldpatientval=&1" & modDataRepo.GetWhereStringRepo(cmblocation.Text, cmbvalue.Text)
    $rData = modDatabase.$syConn.Exec(sql, patno)
    FillPatientGrid()
  Endif

End

Public Sub mnusearname_Click()

  Dim xname As String[]
  Dim sql As String
  Dim xFldList As String[]

  xFldList = $SSQLFields.Copy()
  xname = InputDoubleText(("Search Patient Name"), ["First Name", "SurName"], ["%", "%"], modBasic.$SurNameList)
  If xname Then
    sql = "select " & xFldList.Join(",") & " from tblpatientinfo where lower(fldptnamefir) like &1 and lower(fldptnamelast) like &2" & modDataRepo.GetWhereStringRepo(cmblocation.Text, cmbvalue.Text)
    $rData = modDatabase.$syConn.Exec(sql, LCase(xname[0]), LCase(xname[1]))
    FillPatientGrid()
  Endif

End

Private Function ExecuteQuery(xFldList As String[], sCon As Connection) As Result

  Dim sql As String
  Dim xdtstr As String
  Dim agestr As String
  Dim ethstr As String

  Dim res As Result

  If txtval2.Value Then
    xdtstr = modMedReports.GetAgeStringByTable("tblpatientinfo", sCon)
    agestr = " and " & xdtstr & ">=&6 and " & xdtstr & "<&7"
  Else
    agestr = ""
  Endif
  If cmbethnic.Text = "%" Then
    ethstr = ""
  Else
    ethstr = " and fldethniccode like &5"
  Endif
  If cmbdistrict.Text = "%" Then
    sql = "select " & xFldList.Join(",") & " from tblpatientinfo where fldptadmindate>=&1 and fldptadmindate<=&2 and fldptsex like &3" & ethstr & agestr & modDataRepo.GetWhereStringRepo(cmblocation.Text, cmbvalue.Text)                                                                              ''
  Else
    sql = "select " & xFldList.Join(",") & " from tblpatientinfo where fldptadmindate>=&1 and fldptadmindate<=&2 and fldptsex like &3 and fldptadddist like &4" & ethstr & agestr & modDataRepo.GetWhereStringRepo(cmblocation.Text, cmbvalue.Text)                                                                             ''
  Endif
  res = sCon.Exec(sql, modDate.StartSqlDate(dtfir.value), modDate.EndSqlDate(dtlast.value), cmbgender.Text, cmbdistrict.Text, cmbethnic.Text, 365 * txtval1.Value, 365 * txtval2.Value)

  Return res

Catch
  Message.Warning(("Database Error Code: ") & db.Error & "<br>" & Error.Text, ("OK"))

End

Private Function GetGridViewValue(Column As Integer, xVariable As Variant) As Variant

  Dim xxx As Variant

  If Column = 1 Then
    xxx = modPatient.PatientFullNameByPatID(xVariable)
  Else If Column = 4 Then
    xxx = modReportVar.GetDateTimeReport(xVariable, gb.GeneralDate)
  Else If Column = 8 Then
    xxx = modDataRepo.GetHospitalTextLabel(xVariable)
  Else
    xxx = xVariable
  Endif

  Return xxx

End

Public Sub btnrefresh_Click()

  Dim xFldList As String[]

  TabStrip1.Index = 0

  xFldList = $SSQLFields.Copy()
  $rData = ExecuteQuery(xFldList, modDatabase.$syConn)
  If $rData.Available Then
    lblrecordcount.Text = "Count: " & $rData.Count
  Else
    lblrecordcount.Text = "Count: 0"
  Endif

  If chkgrid.Value = True Then
    FillPatientGrid()
  Endif

  If chkcount.Value = True Then
    FillChartData()
  Endif

  ' modExternal.$ExecValueColl.Clear()

End

Private Sub FillPatientGrid()

  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  $ColCount = $SSQLFields.Count
  With GridView1
    .Columns[0].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(175 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Hidden = True

    .Columns[0].Text = "PatNo"
    .Columns[1].Text = "Name"
    .Columns[2].Text = "Ethnic"
    .Columns[3].Text = "Gender"
    .Columns[4].Text = "DOBirth"
    .Columns[5].Text = "Address"
    .Columns[6].Text = "District"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = GetGridViewValue(Column, $rData[$aMyFields[Column]])

End

Public Sub mnucompreport_Click()

  Dim res As Result
  Dim hForm As FRequest
  Dim xpatno As String

  If GridView1.Selection.Count > 0 Then
    $rData.MoveTo(GridView1.Selection[0])
    xpatno = $rData["fldpatientval"]
    If modPatientSub.GetPatPassCheck(modDatabase.$syConn, xpatno) = True Then
      If MMain.$WebEntry = True Then
        res = modDatabase.$syConn.Exec("select fldencounterval,fldregdate,fldadmission,fldcurrlocat,flduserid,flddisctype,fldhospcode from tblencounter where fldpatientval=&1 ORDER BY fldregdate", xpatno)
      Else
        res = modDatabase.$syConn.Exec("select fldencounterval,fldregdate,fldadmission,fldcurrlocat,flduserid,flddisctype from tblencounter where fldpatientval=&1 ORDER BY fldregdate", xpatno)
      Endif
      hForm = New FRequest(res, "Patient Encounters", "PatientEncounters", False)
      hForm.ShowModal
    Endif
  Endif

End

''------------------------------------------- Grid view reports -------------------------------------------------
Public Sub mnuenchart_Click()

  Dim xx As String
  Dim xField As String
  Dim Column As Integer
  Dim res As Result
  Dim xval As Variant
  Dim hFile As CUnivariate
  Dim sOrder As String[]

  Dim hUniv As FmAnalyzeSett

  hUniv = New FmAnalyzeSett("Univariate", modGridView.GetGridViewColumns(GridView1))
  hUniv.ShowModal

  hFile = New CUnivariate
  Column = modCustPatient.GetUnivariateColumn(0)
  xField = $SSQLFields[Column]
  res = ExecuteQuery([xField], modDatabase.$syConn)

  If res.Available Then
    For Each res
      xval = GetGridViewValue(Column, res[xField])
      If xval Then
        hFile.Add(xval)
      Endif
    Next
  Endif

  If modSettings.ShowSettingFromFIle("UnivariateAnalysis/DataType") = "Ordinal" Then
    sOrder = ListOrder("Select Order", hFile.DistinctValues())
  Endif
  xx = modPSPP.GetEncChartGridNew(hFile.GetSPSSFile(), sOrder)
  If xx Then
    modControlSub.OpenBrowser(xx)
  Endif

End

Public Sub mnucrosstab_Click()

  Dim xx As String
  Dim Row As Integer
  Dim Column As Integer
  Dim xFieRow As String
  Dim xFieColm As String
  Dim res As Result
  Dim xval As Variant
  Dim yval As Variant

  Dim hFile As CBivariate
  Dim sOrder As String[]
  Dim hBivar As FmAnalyzeSett

  hBivar = New FmAnalyzeSett("Bivariate", modGridView.GetGridViewColumns(GridView1))
  hBivar.ShowModal

  hFile = New CBivariate
  Row = modCustPatient.GetCrossVarRow(0)
  xFieRow = $SSQLFields[Row]
  Column = modCustPatient.GetCrossVarColumn(0)
  xFieColm = $SSQLFields[Column]
  res = ExecuteQuery([xFieRow, xFieColm], modDatabase.$syConn)

  If res.Available Then
    For Each res
      xval = GetGridViewValue(Row, res[xFieRow])
      yval = GetGridViewValue(Column, res[xFieColm])
      If xval And If yval Then
        hFile.Add([xval, yval])
      Endif
    Next
  Endif

  If modCustPatient.$CrossOutcomeType = "Ordinal" Then
    sOrder = ListOrder("Select Order", hFile.DistinctValues())
  Endif
  xx = modPSPP.GetCrossTabStatNew(hFile.GetSPSSFile(), hFile.GetGroups(), sOrder)
  If xx Then
    modControlSub.OpenBrowser(xx)
  Endif

End

Public Sub mnuregression_Click()

  Dim xx As String
  Dim res As Result
  Dim xFieldList As String[]

  Dim DepCol As Integer
  Dim Indep1Col As Integer
  Dim Indep2Col As Integer
  Dim Indep3Col As Integer
  Dim Indep4Col As Integer
  Dim Indep5Col As Integer

  Dim xDepField As String
  Dim xIndep1Field As String
  Dim xIndep2Field As String
  Dim xIndep3Field As String
  Dim xIndep4Field As String
  Dim xIndep5Field As String

  Dim Depvalue As Variant
  Dim Indep1Val As Variant
  Dim Indep2Val As Variant
  Dim Indep3Val As Variant
  Dim Indep4Val As Variant
  Dim Indep5Val As Variant

  Dim DepData As Variant[]
  Dim Indep1Data As Variant[]
  Dim Indep2Data As Variant[]
  Dim Indep3Data As Variant[]
  Dim Indep4Data As Variant[]
  Dim Indep5Data As Variant[]

  Message.Info(("Generating Report ...."), ("OK"))
  xFieldList = New String[]
  DepData = New Variant[]
  Indep1Data = New Variant[]
  Indep2Data = New Variant[]
  Indep3Data = New Variant[]
  Indep4Data = New Variant[]
  Indep5Data = New Variant[]

  DepCol = modCustPatient.GetRegressionDepVal(0)
  xDepField = $SSQLFields[DepCol]
  xFieldList.Add(xDepField)

  If modCustPatient.$RegVar1UseGridVal = "Yes" Or If modCustPatient.$RegVar1Variable Or If modCustPatient.$RegVar1SQLText Then
    Indep1Col = modCustPatient.GetRegressionVar1Val(0)
    xIndep1Field = $SSQLFields[Indep1Col]
    xFieldList.Add(xIndep1Field)
  Endif
  If modCustPatient.$RegVar2UseGridVal = "Yes" Or If modCustPatient.$RegVar2Variable Or If modCustPatient.$RegVar2SQLText Then
    Indep2Col = modCustPatient.GetRegressionVar2Val(0)
    xIndep2Field = $SSQLFields[Indep2Col]
    xFieldList.Add(xIndep2Field)
  Endif
  If modCustPatient.$RegVar3UseGridVal = "Yes" Or If modCustPatient.$RegVar3Variable Or If modCustPatient.$RegVar3SQLText Then
    Indep3Col = modCustPatient.GetRegressionVar3Val(0)
    xIndep3Field = $SSQLFields[Indep3Col]
    xFieldList.Add(xIndep3Field)
  Endif
  If modCustPatient.$RegVar4UseGridVal = "Yes" Or If modCustPatient.$RegVar4Variable Or If modCustPatient.$RegVar4SQLText Then
    Indep4Col = modCustPatient.GetRegressionVar4Val(0)
    xIndep4Field = $SSQLFields[Indep4Col]
    xFieldList.Add(xIndep4Field)
  Endif
  If modCustPatient.$RegVar5UseGridVal = "Yes" Or If modCustPatient.$RegVar5Variable Or If modCustPatient.$RegVar5SQLText Then
    Indep5Col = modCustPatient.GetRegressionVar5Val(0)
    xIndep5Field = $SSQLFields[Indep5Col]
    xFieldList.Add(xIndep5Field)
  Endif

  res = ExecuteQuery(xFieldList, modDatabase.$syConn)

  If res.Available Then
    For Each res
      Depvalue = GetGridViewValue(DepCol, res[xDepField])
      If xIndep1Field Then
        Indep1Val = GetGridViewValue(Indep1Col, res[xIndep1Field])
      Else
        Indep1Val = "__"
      Endif
      If xIndep2Field Then
        Indep2Val = GetGridViewValue(Indep2Col, res[xIndep2Field])
      Else
        Indep2Val = "__"
      Endif
      If xIndep3Field Then
        Indep3Val = GetGridViewValue(Indep3Col, res[xIndep3Field])
      Else
        Indep3Val = "__"
      Endif
      If xIndep4Field Then
        Indep4Val = GetGridViewValue(Indep4Col, res[xIndep4Field])
      Else
        Indep4Val = "__"
      Endif
      If xIndep5Field Then
        Indep5Val = GetGridViewValue(Indep5Col, res[xIndep5Field])
      Else
        Indep5Val = "__"
      Endif

      If Depvalue And If Indep1Val And If Indep2Val And If Indep3Val And If Indep4Val And If Indep5Val Then
        modCustPatient.AddFormatRegressionDepToList(DepData, Depvalue)
        modCustPatient.AddFormatRegressionVar1ToList(Indep1Data, Indep1Val)
        modCustPatient.AddFormatRegressionVar2ToList(Indep2Data, Indep2Val)
        modCustPatient.AddFormatRegressionVar3ToList(Indep3Data, Indep3Val)
        modCustPatient.AddFormatRegressionVar4ToList(Indep4Data, Indep4Val)
        modCustPatient.AddFormatRegressionVar5ToList(Indep5Data, Indep5Val)
      Endif
    Next
  Endif

  xx = modPSPP.RegressionChartGridNew(DepData, Indep1Data, Indep2Data, Indep3Data, Indep4Data, Indep5Data)
  If xx Then
    modControlSub.OpenBrowser(xx)
  Endif

End

Public Sub mnutimeseries_Click()

  Dim xx As String
  Dim Row As Integer
  Dim Column As Integer
  Dim xFieRow As String
  Dim xFieColm As String
  Dim res As Result
  Dim xval As Variant
  Dim yval As Variant
  Dim xData As Variant[]
  Dim yData As Variant[]

  Message.Info(("Generating Report ...."), ("OK"))
  xData = New Variant[]
  yData = New Variant[]
  Row = modCustPatient.GetTimeVarRow(0)
  xFieRow = $SSQLFields[Row]
  Column = modCustPatient.GetTimeVarColumn(0)
  xFieColm = $SSQLFields[Column]
  res = ExecuteQuery([xFieRow, xFieColm], modDatabase.$syConn)

  If res.Available Then
    For Each res
      xval = GetGridViewValue(Row, res[xFieRow])
      yval = GetGridViewValue(Column, res[xFieColm])
      If xval And If yval Then
        modCustPatient.AddFormatTimeVarRowToList(xData, xval)
        modCustPatient.AddFormatTimeVarColumnToList(yData, yval)
      Endif
    Next
  Endif

  xx = modPSPP.GetTimeChartGrid(xData, yData)
  If xx Then
    modControlSub.OpenBrowser(xx)
  Endif

End

Public Sub mnucrotab_Click()

  Dim Row As Integer
  Dim Column As Integer
  Dim xFieRow As String
  Dim xFieColm As String
  Dim res As Result
  Dim xval As Variant
  Dim yval As Variant

  Dim hForm As FmGridCrossTab
  Dim xForm As CCrossTab
  Dim hGdChr As FmAnalyzeSett

  hGdChr = New FmAnalyzeSett("GridCrossTab", modGridView.GetGridViewColumns(GridView1))
  hGdChr.ShowModal

  If modCustPatient.$TabRowColumn And If modCustPatient.$TabFieldColumn Then
    xForm = New CCrossTab

    Row = CInt(modCustPatient.$TabRowColumn) - 1
    xFieRow = $SSQLFields[Row]
    Column = CInt(modCustPatient.$TabFieldColumn) - 1
    xFieColm = $SSQLFields[Column]
    res = ExecuteQuery([xFieRow, xFieColm], modDatabase.$syConn)

    If res.Available Then
      For Each res
        xval = GetGridViewValue(Row, res[xFieRow])
        yval = GetGridViewValue(Column, res[xFieColm])
        If xval And If yval Then
          xForm.Add([xval, yval])
        Endif
      Next
    Endif

    hForm = New FmGridCrossTab(GridView1.Columns[Row].Text, xForm.GetSPSSFile())
    hForm.ShowModal
    'modExternal.$ExecValueColl.Clear()
  Endif

End

Public Sub mnugridsummary_Click()

  Dim xx As String
  Dim ChapCol As String
  Dim GropCol As String
  Dim xChapList As String[]
  Dim xGropList As String[]

  Dim res As Result
  Dim valCol As String
  Dim xFieldList As String[]

  Dim xFinList As Variant[]
  Dim xrowVar As Variant[]
  Dim xchapval As String
  Dim xgroup As String
  Dim xPath As String
  Dim hGdSum As FmAnalyzeSett

  hGdSum = New FmAnalyzeSett("GridSummary", modGridView.GetGridViewColumns(GridView1))
  hGdSum.ShowModal
  If Not modCustPatient.$SumGroupColumn Or If Not modCustPatient.$SumValueColumn Then Return

  xFieldList = New String[]
  If modCustPatient.$SumChapterColumn Then
    ChapCol = $SSQLFields[modCustPatient.$SumChapterColumn - 1]
    xFieldList.Add(ChapCol)
  Endif
  GropCol = $SSQLFields[modCustPatient.$SumGroupColumn - 1]
  valCol = $SSQLFields[modCustPatient.$SumValueColumn - 1]
  xFieldList.Add(GropCol)
  xFieldList.Add(valCol)

  xChapList = New String[]
  xGropList = New String[]
  xFinList = New Variant[]
  res = ExecuteQuery(xFieldList, modDatabase.$syConn)
  If res.Available Then
    For Each res
      xrowVar = New Variant[]
      If modCustPatient.$SumChapterColumn Then
        xchapval = GetGridViewValue(modCustPatient.$SumChapterColumn - 1, res[ChapCol])
        xrowVar.Add(xchapval)
        xChapList.Add(xchapval)
      Endif
      xgroup = GetGridViewValue(modCustPatient.$SumGroupColumn - 1, res[GropCol])
      xrowVar.Add(xgroup)
      xGropList.Add(xgroup)

      xrowVar.Add(GetGridViewValue(modCustPatient.$SumValueColumn - 1, res[valCol]))
      xFinList.Add(xrowVar)
    Next
  Endif

  xx = "Date: " & modReportVar.GetDateTimeReport(dtfir.Value, gb.MediumDate) & " To " & modReportVar.GetDateTimeReport(dtlast.Value, gb.MediumDate)
  If modCustPatient.$SumGroupColumn And If modCustPatient.$SumValueColumn Then
    If modCustPatient.$SumChapterColumn Then
      If modCustPatient.$SummaryType = "Summation(Table)" Then
        xPath = modCHTMLReport.SummaryGridChapterTableReport("Report Summary", xx, modString.GetDistinctStringArray(xChapList), modString.GetDistinctStringArray(xGropList), xFinList)
      Else If modCustPatient.$SummaryType = "Count(Table)" Then
        xPath = modCHTMLReport.SummaryGridChapCountTableReport("Report Summary", xx, modString.GetDistinctStringArray(xChapList), modString.GetDistinctStringArray(xGropList), xFinList)
      Else
        xPath = modCHTMLReport.SummaryGridChapterReport("Visit Summary", xx, modString.GetDistinctStringArray(xChapList), modString.GetDistinctStringArray(xGropList), xFinList)
      Endif
    Else
      xPath = modCHTMLReport.SummaryGridReport("Visit Summary", xx, modString.GetDistinctStringArray(xGropList), xFinList)
    Endif
    modControlSub.DisplayReportExport(xPath)
  Endif

End

''------------------------------------------------------ Picture 1 ---------------------------------
Private Sub FillChartData()

  Dim xLst As String[]
  Dim sFile As String

  xLst = New String[]
  For Each $rData
    If Not modBasic.$HospCode And If MMain.$WebReport = "Multiple" Then
      xLst.Add($rData["fldhospcode"] & gb.Tab & $rData["fldptsex"])
    Else
      xLst.Add(Format(DateDiff(dtfir.Value, $rData["fldptadmindate"], modChart.GetDateIntegerFromSetting()), "0#.#") & gb.Tab & $rData["fldptsex"])
    Endif
  Next

  sFile = Temp()
  File.Save(sFile, xLst.Join(gb.NewLine))
  If modBasic.$ChartExecStat = "PSPP" Then
    modImage.StretchtPictureToBox(PictureBox1, modChart.ProportionChart(sFile, "Registration" & " : Patient Count"))
  Else
    modImage.StretchtPictureToBox(PictureBox1, modChart.ProportionChart(sFile, "Registration" & " : Patient Count"))
  Endif
  btnstats.Tag = sFile

End

Public Sub btnexport_Click()

  ' Me.Exec("printJS('" & PictureBox1.Tag & "', 'image')")
  Me.Exec("window.open('" & PictureBox1.Image & "'); ")

End

Public Sub btnstats_Click()

  Dim hForm As FmOpenLink

  If modBasic.$ExpoRepoUser = True Then
    Message.Info(("Generating Data ...."), ("OK"))
    hForm = New FmOpenLink(modPrint.GetFileWebURL(btnstats.Tag))
    hForm.ShowModal
  Endif

End

''----------------------------------------- Summary ----------------------------------
Public Sub mnuagegroup_Click()

  ' Dim xPath As String
  Dim xdate As Date[]

  xdate = DoubleDates("Select Dates", "Registration", [dtfir.Value, dtlast.Value])
  ' If xdate Then
  '   xPath = modCHTMLDate.SummaryStatAgeGroup(modDatabase.$syConn, xdate[0], xdate[1], cmblocation.Text, cmbvalue.Text)
  '   modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  ' Endif

End

Private Sub ShowSumReport(sField As String)

  Dim xPath As String
  Dim xdate As Date[]

  xdate = DoubleDates("Select Dates", "Patient List", [dtfir.Value, dtlast.Value])
  If xdate Then
    xPath = modCHTMLDate.SummaryStatReport(modDatabase.$syConn, "tblpatientinfo", "fldpatientval", sField, "fldptadmindate", xdate[0], xdate[1], cmblocation.Text, cmbvalue.Text)
    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub mnudistrict_Click()

  ShowSumReport("fldptadddist")

End

Public Sub mnusurname_Click()

  ShowSumReport("fldptnamelast")

End

Public Sub mnugender_Click()

  ShowSumReport("fldptsex")

End

Public Sub mnuethnic_Click()

  ShowSumReport("fldethniccode")

End

Public Sub mnuservrank_Click()

  ShowSumReport("fldrank")

End

Public Sub mnuservunit_Click()

  ShowSumReport("fldunit")

End

Public Sub mnuservcateg_Click()

  ShowSumReport("fldcategory")

End

''-------------------------------------- Other Menu ---------------------
Public Sub btnfullrep_Click()

  Dim xHeader As String[]
  Dim xhide As Integer[]
  Dim Column As Integer
  Dim xCollRow As Collection
  Dim xColum As Integer

  Dim $hGridExportTable As CExportResult
  Dim xparam1 As String
  Dim xparam2 As String
  Dim encColumn As Integer

  xHeader = New String[]
  xhide = New Integer[]
  For Column = 0 To GridView1.Columns.Count - 1
    xHeader.Add(GridView1.Columns[Column].Text)
    If GridView1.Columns[Column].Hidden = True Then
      xhide.Add(Column)
    Endif
  Next
  xparam1 = "Patient List"
  xparam2 = "Entry Date: " & modReportVar.GetDateTimeReport(dtfir.Value, gb.MediumDate) & " To " & modReportVar.GetDateTimeReport(dtlast.Value, gb.MediumDate)
  encColumn = 0
  $hGridExportTable = New CExportResult(Me.Tag, xHeader, xhide, xparam1, xparam2, encColumn)

  For Each $rData
    xCollRow = New Collection
    For xColum = 0 To $SSQLFields.Count - 1
      xCollRow.Add(GetGridViewValue(xColum, $rData[$SSQLFields[xColum]]), CStr(xColum))
    Next
    $hGridExportTable.Add($rData.Index, xCollRow)
  Next
  modControlSub.DisplayReportExport($hGridExportTable.HTMLPath())

End

Public Sub btnexpoexcel_Click()

  Me.Exec("ExportToExcel('" & GridView1.Name & "', 'xlsx')")

End

Public Sub chkcount_Click()

  TabStrip1.Index = 1

End

Public Sub mnucolumns_Click()

  If Message.Question(modGridView.GetColumnsListString(GridView1), ("OK"), ("Setting")) = 2 Then
    fmChartSetting.ShowModal
  Endif

End

Public Sub mnucountuni_Click()

  Dim Column As Integer
  Dim xFieColm As String
  Dim res As Result
  Dim yval As Variant
  Dim xList As String[]

  Dim xVar As Variant[]
  Dim xFldList As String[]
  Dim xPath As String

  Column = ListIndex("Column Index", modGridView.GetGridViewColumns(GridView1))
  If Column + 1 > 0 Then
    xFldList = $SSQLFields
    xFieColm = xFldList[Column]
    res = ExecuteQuery([xFieColm], modDatabase.$syConn)
    xList = New String[]
    If res.Available Then
      For Each res
        yval = GetGridViewValue(Column, res[xFieColm])
        If yval Then
          xList.Add(yval)
        Endif
      Next
    Endif
    If xList.Count Then
      xVar = modString.GetUnivariateSummary(xList)
      xPath = modCHTMLReport.CreateHTMLReportFromArray(["Variable", "Count"], xVar, GridView1.Columns[Column].Text, modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))
    Endif

    modControlSub.OpenHTMLPreview("", xPath, "ReportSize")
  Endif

End

Public Sub mnuexpocolumn_Click()

  Dim hForm As FmCustColumnSet

  hForm = New FmCustColumnSet(Me.Tag)
  hForm.ShowModal

End
