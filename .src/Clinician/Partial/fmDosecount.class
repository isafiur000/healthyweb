' Gambas class file

Private $encid As String
Private $sTatus As String

Private $rData1 As Result
Private $aMyFields1 As String[]
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(encid As String, sClass As String)

  $encid = encid

  WebContainer1.Class = sClass
  $sTatus = modPatient.CurrentAdmissionStatus($encid)
  FillMedicineList()
  ' ListBox1.SetFocus

End

Private Sub FillMedicineList()

  Dim sql As String

  sql = "select fldid,fldroute,flditem,flddose,fldfreq,flddays,fldstatus,flddirection,fldmixing,flddosecount from tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and fldstarttime<=&3 and fldendtime>=&4 and fldroute<>&5 and flditemtype=&6 and fldcurval=&7"                    ''
  $rData1 = modDatabase.$myConn.Exec(sql, $encid, True, Now(), DateAdd(Now(), gb.Hour, -3), "fluid", "Medicines", "Continue")
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(ListBox1, $rData1, $aMyFields1)
  With ListBox1
    .Columns[0].Hidden = True
    .Columns[1].Hidden = True
    .Columns[1].Expand = True
    .Columns[3].Hidden = True
    .Columns[4].Hidden = True
    .Columns[5].Hidden = True
    .Columns[6].Hidden = True
    .Columns[7].Hidden = True
    .Columns[8].Hidden = True
    .Columns[9].Hidden = True
  End With

End

Public Sub ListBox1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData1.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData1[$aMyFields1[Column]]

End

Public Sub ListBox1_Select()

  ShowSelectedDosing()

End

Private Sub ShowSelectedDosing()

  If ListBox1.Selection.Count Then
    $rData1.MoveTo(ListBox1.Selection[0])
    txtid.Value = $rData1["fldid"]
    lblmedicine.Text = $rData1["flditem"]
    txtmedname.Text = modMedConstant.GetDrugFromStockID(lblmedicine.Text)

    btninjection.Tag = $rData1["fldroute"]
    txtinitstren.Value = modMedConstant.GetDrugInitialStrength($rData1["flditem"])
    lblregimen.Text = $rData1["fldroute"] & Space(2) & $rData1["flddose"] & Space(1) & modMedConstant.GetMedicineDoseUnit($rData1["flditem"]) & " X " & $rData1["fldfreq"] & " X " & $rData1["flddays"] & " day(s)"
    lblregimen.Tag = $rData1["fldfreq"]
    lblunit.Text = modPharmLabel.GetDosageFormForLabel($rData1["flditem"], "Inpatient")
    txtdirection.Html = "<p>" & $rData1["flddirection"] & "</p>"
    txtmixture.Html = "<p>" & $rData1["fldmixing"] & "</p>"
    txtdose.Value = Round($rData1["flddose"] / txtinitstren.Value, -2)

    FillGridView(txtid.Value)
    btnsave.Enabled = True
    If modBasic.$TabletModeEnable = "Yes" Then
    Else
      ' txtdose.SetFocus
    Endif

  Endif

End

Private Sub FillGridView(dosno As Long)

  $rData = modDatabase.$syConn.Exec("select fldid,fldtime,fldid,fldvalue,fldunit,fldid,fldvalue from tblnurdosing where fldencounterval=&1 and flddoseno=&2", $encid, dosno)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(225 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(25 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"

    .Columns[1].Text = "DateTime"
    .Columns[2].Text = "Particulars"
    .Columns[3].Text = "QTY"
    .Columns[4].Text = "Unit"
    .Columns[6].Text = "Dose"
  End With

  txtcounttoday.Value = modPharmacy.TotalDoseCount(dosno, Now())
  txtdosetoday.Value = modPharmacy.TotalDoseAmount(dosno, Now())

  txtcountsum.Value = modPharmacy.TotalDoseCount(dosno)
  txtdosesum.Value = modPharmacy.TotalDoseAmount(dosno)

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Text = modPatient.GetDayTimeFromAdmission($encid, $rData[$aMyFields[Column]])
  Else If Column = 2 Then
    Data.Text = txtmedname.Text
  Else If Column = 5 Then
    Data.Html = modString.GetImageForHTMLGrid(modGeneralMain.GetSystemIcon(GetPictureDosing($rData[$aMyFields[Column]])), CStr(20 * modBasic.$AppWidthRatio) & "px", "auto")
    Data.Text = ""
  Else If Column = 6 Then
    Data.Text = $rData[$aMyFields[Column]] * txtinitstren.Value
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Private Function GetPictureDosing(dosid As Long) As String

  Dim res As Result
  Dim xx As String
  Dim xrout As String

  res = modDatabase.$myConn.Exec("select flddoseno,fldfromtime,fldtotime from tblnurdosing where fldid=&1", dosid)
  If res.Available Then
    xrout = modPharmacy.GetRouteFromDosing(res["flddoseno"])
    If xrout = "injection" Or If xrout = "fluid" Or If xrout = "CIV" Or If xrout = "IIV" Then
      If Not res["fldfromtime"] Then
        xx = "icon:/small/play"
      Else If res["fldfromtime"] And If Not res["fldtotime"] Then
        xx = "icon:/small/stop"
      Else If res["fldfromtime"] And If res["fldtotime"] Then
        xx = "icon:/small/lock"
      Endif
    Else
      xx = "icon:/small/lock"
    Endif
  Endif
  Return xx

End

Public Sub btnsave_Click()

  Dim res As Result

  If txtid.Value And If txtdose.Value Then
    modPharmSub.InsertNurDosing(txtid.Value, $encid, txtdose.Value, lblunit.Text, "")
    If lblregimen.Tag = "stat" Then
      res = modDatabase.$myConn.Edit("tblpatdosing", "fldid=&1", txtid.Value)
      res["fldcurval"] = "Completed"
      res.Update
    Endif
    FillGridView(txtid.Value)
    btnsave.Enabled = False
    txtdose.Value = 0

  Endif

End

Public Sub btnexpo_Click()

  If lblmedicine.Text Then
    modCHTMLReport.ExportGridToHTML(GridView1, lblmedicine.Text, "Dosing Report",, $encid)
  Endif

End

Public Sub btndoseedit_Click()

  Dim res As Result
  Dim xval As Float

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    xval = InputValue(txtmedname.Text, ("Change Value"), $rData["fldvalue"])
    If xval Then
      res = modDatabase.$myConn.Edit("tblnurdosing", "fldid=&1", $rData["fldid"])
      If modGeneral.AllowClinicalEdit(res["flduserid"]) = True Then
        res["fldvalue"] = xval
        res["xyz"] = False
        res["flduserid"] = modBasic.$lbluser
        res.Update
        FillGridView(txtid.Value)
      Else
        Message.Warning("Authorization with " & res["flduserid"], ("OK"))
      Endif
    Endif
  Endif

End

Public Sub btndosestop_Click()

  Dim res As Result

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    res = modDatabase.$myConn.Edit("tblnurdosing", "fldid=&1", $rData["fldid"])
    If GetPictureDosing($rData["fldid"]) = "icon:/small/play" Then
      res["fldfromtime"] = Now()
      res["xyz"] = False
      res.Update
      FillGridView(txtid.Value)
    Else If GetPictureDosing($rData["fldid"]) = "icon:/small/stop" Then
      res["fldtotime"] = Now()
      res["xyz"] = False
      res.Update
      FillGridView(txtid.Value)
    Endif
  Endif

End

Public Sub btninjection_Click()

  Dim res As Result
  Dim hForm As FmInjection

  If btninjection.Tag Then
    res = modDatabase.$medConn.Exec("select fldlabel from tbllabel where flddrug=&1 and fldsubroute=&2", modMedConstant.GetDrugFromStockID(lblmedicine.Text), btninjection.Tag)
    If res.Available Then
      res.MoveFirst
      hForm = New FmInjection(btninjection.Tag, res["fldlabel"])
      hForm.ShowModal
    Endif
  Endif

End
