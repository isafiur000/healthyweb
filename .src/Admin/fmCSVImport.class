' Gambas class file

Private $toCon As Connection

Public Sub _new()

  cmbtypeB.List = ["sqlite3", "mysql", "postgresql", "odbc"]

End

Public Sub chkcurB_Click()

  cmbtypeB.Text = modDatabase.$myConn.Type
  txtserverB.Text = modDatabase.$myConn.Host
  txtdbB.Text = modDatabase.$myConn.Name
  txtuserB.Text = modDatabase.$myConn.User
  txtpassB.Text = modDatabase.$myConn.Password

End

Public Sub btnconB_Click()

  $toCon = modDBApp.OpenConnection(cmbtypeB.Text, txtserverB.Text, txtdbB.Text, txtuserB.Text, txtpassB.Text)
  If $toCon.Opened = True Then
    conB.Value = True
    TabPanel1.Enabled = True
    FillTableList()
    Me.Exec("Toastify({text: 'Connected', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

Private Sub FillTableList()

  Dim hTable1 As Table

  cmbtablecsv.Clear
  cmbtablecsv2.Clear
  cmbtablejson.Clear
  For Each hTable1 In $toCon.Tables
    If hTable1.System = True Then
    Else If hTable1.Name = "tblkey" Then
    Else If hTable1.Name = "tblnewkey" Then
    Else
      cmbtablecsv.Add(hTable1.Name)
      cmbtablecsv2.Add(hTable1.Name)
      cmbtablejson.Add(hTable1.Name)
    Endif
  Next

End

''---------------------------------------- CSV Import ------------------------------
Public Sub btntransfercsv_Click()

  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    modTextDB.ImportCSVToCurrentDB($toCon, cmbtablecsv.Text, txtimport.Text, txtdelim.Text)
    Me.Exec("Toastify({text: 'Information updated', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
  Endif

End

''---------------------------------- CSV Update -------------------------
Public Sub cmbtablecsv2_Click()

  Dim xList As String[]

  cmbprifield.Clear
  cmbtargetfield.Clear
  xList = modDBApp.GetFIeldsinTable(cmbtablecsv2.Text, True, $toCon)
  cmbprifield.List = xList
  cmbtargetfield.List = xList

End

Public Sub btntransfercsv2_Click()

  Dim hCSV As CsvFile
  Dim xcoll As Collection
  Dim res As Result

  If Exist(txtimport2.Text) And If cmbprifield.Text And If cmbtargetfield.Text And If txtdelim2.Text Then
    If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then

      hCSV = New CsvFile(txtimport2.Text, txtdelim2.Text)
      While hCSV.Eof = False
        xcoll = hCSV.Read()

        res = $toCon.Edit(cmbtablecsv2.Text, cmbprifield.Text & "=&1", xcoll[cmbprifield.Text])
        If res.Available Then
          res[cmbtargetfield.Text] = xcoll[cmbtargetfield.Text]
          Try res.Update()
        Endif

      Wend
      hCSV.Close()

    Endif
  Endif

End

''=========================== JSON Import ====================
Public Sub btntransferjson_Click()

  If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
    modTextDB.ImportJSONToCurrentDB($toCon, cmbtablejson.Text, txtimpojson.Text)
    Me.Exec("Toastify({text: 'Information updated', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
  Endif

End
