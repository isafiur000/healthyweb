' Gambas class file

Private $sCon As Connection
Private $rData1 As Result
Private $aMyFields1 As String[]
Private $rData2 As Result
Private $aMyFields2 As String[]

Public Sub _new()

  $sCon = modDatabase.$myConn
  cmbdesk.List = ["Medical", "Record", "Cashier", "Account", "Technologist", "Technician", "Consultant", "Clinician", "Research", "Purchase", "Store", "Pharmacist", "Dispensar"]
  cmbsideposition.List = ["Right", "Left"]
  cmbformat.List = ["PDF", "Image", "URL"]
  dttarget.Value = Now()

End

Public Sub dtneptarg_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dttarget.Value))
  If xx Then
    dttarget.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub btnrefresh_Click()

  If dttarget.Value And If cmbdesk.Text Then
    ShowBottomGrid()
    SHowSideGrid()
  Endif

End

''-------------------- Bottom -----------------------
Public Sub btndownsave_Click()

  Dim res As Result

  If txtdownlink.Text Then
    res = $sCon.Create("tbladvertise")
    res["advdate"] = Format(dttarget.Value, "yyyymmdd")
    res["advdepartment"] = cmbdesk.Text
    res["advcontent"] = txtdowncontent.Text
    res["advlink"] = Base64(txtdownlink.Text)
    res.Update
    ShowBottomGrid()
    txtdownlink.Text = ""
  Endif

End

Private Sub ShowBottomGrid()

  $rData1 = $sCon.Exec("select advid,advcontent,advlink from tbladvertise where advdepartment=&1 and advdate=&2", cmbdesk.Text, Format(dttarget.Value, "yyyymmdd"))                                  ''
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(WebTable1, $rData1, $aMyFields1)

  With WebTable1
    .Columns[0].Hidden = True
    .Columns[1].Expand = True
    .Columns[2].Expand = True

    .Columns[1].Text = "Content"
    .Columns[2].Text = "Link"
  End With

End

Public Sub WebTable1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData1.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 2 Then
    Data.Text = UnBase64($rData1[$aMyFields1[Column]])
  Else
    Data.Text = $rData1[$aMyFields1[Column]]
  Endif

End

''----------------------- Side -----------------------------
Public Sub btnsidesave_Click()

  Dim res As Result

  If txtsidelink.Text Then
    res = $sCon.Create("tbladlinks")
    res["advdate"] = Format(dttarget.Value, "yyyymmdd")
    res["advdepartment"] = cmbdesk.Text
    res["advposition"] = cmbsideposition.Text
    res["advlink"] = Base64(txtsidelink.Text)
    res["advformat"] = cmbformat.Text
    res.Update
    SHowSideGrid()
    txtsidelink.Text = ""
  Endif

End

Private Sub SHowSideGrid()

  $rData2 = $sCon.Exec("select advid,advposition,advlink,advformat from tbladlinks where advdepartment=&1 and advdate=&2", cmbdesk.Text, Format(dttarget.Value, "yyyymmdd"))
  $aMyFields2 = New String[]
  modGridView.ReadSmallData(WebTable2, $rData2, $aMyFields2)

  With WebTable2
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Expand = True
    .Columns[3].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"

    .Columns[1].Text = "Position"
    .Columns[2].Text = "Link"
  End With

End

Public Sub WebTable2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData2.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 2 Then
    Data.Text = UnBase64($rData2[$aMyFields2[Column]])
  Else
    Data.Text = $rData2[$aMyFields2[Column]]
  Endif

End

Public Sub btnclose_Click()

  Me.Close

End
