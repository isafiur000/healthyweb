' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $selDate As Boolean

Private $sValue As String
Private $ImgPath As String

Public Sub Run() As String

  cmbsex.Text = "Female"
  chknull.Value = False

  cmbdistrict.List = modBasic.$DistrictList

  btnyear.Text = "Year"
  btnmon.Text = "Mon"
  lblstatus.Text = "NEW PATIENT"
  cmbrelation.Text = "Husband"
  cmbdistrict.Text = modBasic.$RegDistrict

  If modBasic.$FixedDepartment Then
    cmbconsult.Text = modBasic.$FixedDepartment
    cmbconsult.Enabled = False
  Else
    cmbconsult.List = modGeneral.GetDepartmentAllList("OPD Visit")
  Endif
  If Not cmbconsult.Text Then
    cmbconsult.Text = "Obstetrics"
  Endif

  FillRegistrationGrid(Now())
  SetRequiredVariables()
  txtpatno.SetFocus

  If Me.ShowModal() Then Return $sValue

End

''-------------------Date --------------

Public Sub btnyear_Click()

  If btnyear.Text = "Year" Then
    btnyear.Text = "Mon"
    btnmon.Text = "Day"
  Else If btnyear.Text = "Mon" Then
    btnyear.Text = "Day"
    btnmon.Text = "Hour"
  Else If btnyear.Text = "Day" Then
    btnyear.Text = "Year"
    btnmon.Text = "Mon"
  Endif

End

Private Sub ShowTearBox()

  If txtyears.Value = 0 And If txtmonths.Value = 0 Then
    chknull.Value = True
  Else
    chknull.Value = False
  Endif

  If txtmonths.Value > 12 And If btnmon.Text = "Mon" Then
    txtmonths.Value = 0
    Message.Warning(("Wrong Age value"), ("OK"))
  Else If txtmonths.Value > 30 And If btnmon.Text = "Day" Then
    txtmonths.Value = 0
    Message.Warning(("Wrong Age value"), ("OK"))
  Else If txtmonths.Value > 24 And If btnmon.Text = "Hour" Then
    txtmonths.Value = 0
    Message.Warning(("Wrong Age value"), ("OK"))
  Endif

End

Public Sub txtyears_Activate()

  ShowTearBox()

End

Public Sub txtmonths_Activate()

  ShowTearBox()

End

Public Sub dtdobmain_Click()

  Dim xdate As Date

  xdate = GetDateValue(("Select Date Time of Birth"), "DateTime", dtdobmain.Tag)
  If xdate Then
    dtdobmain.Tag = xdate
  Endif
  If dtdobmain.Tag Then
    If dtdobmain.Tag > modDate.EndSqlDate(Now()) Then
      dtdobmain.Tag = ""
      Message.Warning("Incorrect Date and Time", ("OK"))
    Else
      $selDate = True
      modDateSub.DateToYearandMonth(dtdobmain.Tag, txtyears, txtmonths, btnyear, btnmon)
    Endif
  Endif

End

Public Sub btnnepdate_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtdobmain.Tag))
  If xx Then
    dtdobmain.Tag = modDate.ConvertToEnglishdate(xx)
  Endif
  If dtdobmain.Tag Then
    If dtdobmain.Tag > modDate.EndSqlDate(Now()) Then
      dtdobmain.Tag = ""
      Message.Warning("Incorrect Date and Time", ("OK"))
    Else
      $selDate = True
      modDateSub.DateToYearandMonth(dtdobmain.Tag, txtyears, txtmonths, btnyear, btnmon)
    Endif
  Endif

End

Public Sub chknull_Click()

  If chknull.Value = True Then
    txtyears.Value = 0
    txtmonths.Value = 0
  Endif

End

''----------------End Button ---------------
Private Function AllowDataSave() As Boolean

  Dim xx As Boolean

  xx = True
  If modBasic.$RegistEssAddress = "Yes" Then
    If Not txtaddfir.Text Then
      xx = False
    Endif
  Endif

  If modBasic.$RegistEssDistrict = "Yes" Then
    If Not cmbdistrict.Text Then
      xx = False
    Endif
  Endif

  If modBasic.$RegistEssAge = "Yes" Then
    If Not dtdobmain.Tag And If Not txtyears.Value And If Not txtmonths.Value Then
      xx = False
    Else If chknull.Value = True
      xx = False
    Endif
  Endif

  If modBasic.$RegistEssGuardian = "Yes" Then
    If Not txtguardian.Text Then
      xx = False
    Endif
  Endif

  If modBasic.$RegistEssRelation = "Yes" Then
    If Not cmbrelation.Text Then
      xx = False
    Endif
  Endif

  If modBasic.$RegistEssContact = "Yes" Then
    If Not txtcontact.Text Then
      xx = False
    Endif
  Endif

  If modBasic.$RegistEssEMail = "Yes" Then
    If Not txtemail.Text Then
      xx = False
    Endif
  Endif

  If modBasic.$RegistEssCode = "Yes" Then
    If Not txtpatcode.Text Then
      xx = False
    Endif
  Endif

  If modBasic.$RegistEssWardNo = "Yes" Then
    If Not txtward.Text Then
      xx = False
    Endif
  Endif

  If modBasic.$RegistEssEthnic = "Yes" Then
    If Not txtethcode.Text Then
      xx = False
    Endif
  Endif

  Return xx

End

Private Sub RegisterFirstTime()

  Dim res As Result
  Dim $DOBirth As Date

  If $selDate = True Then
    $DOBirth = dtdobmain.Tag
  Else
    If txtmonths.Value = 0 Then
      If btnyear.Text = "Year" Then
        $DOBirth = DateAdd(Now(), gb.Year, 0 - CInt(txtyears.Value))
      Else If btnyear.Text = "Mon" Then
        $DOBirth = DateAdd(Now(), gb.Month, 0 - CInt(txtyears.Value))
      Else If btnyear.Text = "Day" Then
        $DOBirth = DateAdd(Now(), gb.Day, 0 - CInt(txtyears.Value))
      Endif

    Else
      If btnyear.Text = "Year" And If btnmon.Text = "Mon" Then
        $DOBirth = DateAdd(Now(), gb.Month, 0 - CInt(12 * txtyears.Value + txtmonths.Value))
      Else If btnyear.Text = "Mon" And If btnmon.Text = "Day" Then
        $DOBirth = DateAdd(Now(), gb.Day, 0 - CInt(30 * txtyears.Value + txtmonths.Value))
      Else If btnyear.Text = "Day" And If btnmon.Text = "Hour" Then
        $DOBirth = DateAdd(Now(), gb.Day, 0 - CInt(24 * txtyears.Value + txtmonths.Value))
      Endif

    Endif
  Endif

  If DateDiff($DOBirth, Now(), gb.Day) > 3285 And If DateDiff($DOBirth, Now(), gb.Day) < 21900 Then

    If txtpatno.Text Then
    Else
      txtpatno.Text = modBillLock.PatientNoValue()
    Endif

    If txtpatno.Text Then
      txtvisit.Text = modPatient.GetPatientHMISStattus(Trim(txtpatno.Text))
      lblstatus.Text = "RECORDED PATIENT"
      res = modDatabase.$myConn.Create("tblpatientinfo")
      res["fldpatientval"] = Trim(txtpatno.Text)
      res["fldptnamefir"] = modPassword.EncryptPatData(String.UCaseFirst(Trim(txtnamefir.text)))
      res["fldptnamelast"] = modPassword.EncryptPatData(String.UCaseFirst(Trim(txtnamelast.text)))
      res["fldethniccode"] = txtethcode.Text
      res["fldptcontact"] = modPassword.EncryptPatData(Trim(txtcontact.Text))
      res["fldemail"] = modPassword.EncryptPatData(Trim(txtemail.Text))
      res["fldptaddvill"] = String.UCaseFirst(Trim(txtaddfir.Text))
      res["fldptaddward"] = Trim(txtward.Text)
      res["fldptadddist"] = String.UCaseFirst(Trim(cmbdistrict.Text))
      res["fldptsex"] = String.UCaseFirst(cmbsex.text)
      res["fldptguardian"] = String.UCaseFirst(Trim(txtguardian.Text))
      res["fldrelation"] = String.UCaseFirst(Trim(cmbrelation.Text))
      If chknull.Value = False Then
        res["fldptbirday"] = $DOBirth
      Endif
      res["fldptadmindate"] = Now()
      res["fldptcode"] = Trim(txtpatcode.Text)
      res["flddiscount"] = ""
      res["fldptadmindate"] = Now()
      res["fldadmitfile"] = ""
      res["fldcomment"] = ""
      res["fldencrypt"] = modBasic.$PatInfoEncrypt
      res["fldpassword"] = ""

      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
      res["fldupuser"] = ""
      res["flduptime"] = ""
      res["xyz"] = False

      If MMain.$WebEntry = True Then
        res["fldrepodate"] = Now()
        res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
        res["fldhospcode"] = modBasic.$HospCode
      Endif
      res.Update()
    Endif

  Else
    Message.Warning("Incorrect age", "OK")

  Endif

End

Private Sub SaveRegistrationData()

  Dim res As Result
  Dim follow As String
  Dim idLock As Boolean

  If chkfollow.Value Then
    follow = "FollowUp"
  Else
    follow = "Recorded"
  Endif

  If txtnamefir.Text And If txtnamelast.Text And If cmbsex.Text And If cmbconsult.Text Then
    If AllowDataSave() = True Then

      idLock = modBillLock.LockTableForID("Registration")
      If idLock = True Then

        modDatabase.$myConn.Begin
        If lblstatus.Text = "NEW PATIENT" Then
          RegisterFirstTime()
        Endif

        If txtpatno.Text Then
          txtencounter.Text = modBillLock.EncounterIDValue()
          If txtencounter.Text Then
            res = modDatabase.$myConn.Create("tblencounter")
            res["fldencounterval"] = txtencounter.Text
            res["fldpatientval"] = Trim(txtpatno.Text)
            res["fldadmitlocat"] = cmbconsult.Text
            If modBasic.$RegistDefaultDisc Then
              res["flddisctype"] = modBasic.$RegistDefaultDisc
            Else
              res["flddisctype"] = "General"
            Endif
            If modBasic.$RegistDefaultMode Then
              res["fldbillingmode"] = modBasic.$RegistDefaultMode
            Else
              res["fldbillingmode"] = "General"
            Endif
            res["fldadmission"] = follow
            res["fldregdate"] = Now()
            res["fldcomp"] = modBasic.$compID
            res["fldvisit"] = txtvisit.Text
            res["fldfollow"] = "New"

            res["fldcurrlocat"] = ""
            res["flddoa"] = ""
            res["flddod"] = ""
            res["fldheight"] = ""
            res["flduserid"] = ""
            res["fldcashdeposit"] = 0
            res["fldcashcredit"] = 0
            res["fldcharity"] = 0
            res["fldvalidity"] = DateAdd(Now(), gb.Year, 1)
            res["fldfollowdate"] = ""
            res["fldreferto"] = ""

            res["fldregistid"] = modPatient.GetPatientHMISRegVal(Trim(txtpatno.Text))
            res["fldadmitid"] = ""
            res["xyz"] = False
            If MMain.$WebEntry = True Then
              res["fldrepodate"] = Now()
              res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
              res["fldhospcode"] = modBasic.$HospCode
            Endif
            res.Update()
            modPatientSub.EnterPatientImpDate(txtencounter.Text, "Recorded", "")
          Endif
          modDatabase.$myConn.Commit
          btnOK.Enabled = False

        Else
          modDatabase.$myConn.Rollback
          txtencounter.Text = ""
        Endif ''checking pat no

        modBillLock.ReleaseLockTable("Registration")
      Endif

    Endif
  Endif

Catch
  modDatabase.$myConn.Rollback
  If idLock = True Then
    modBillLock.ReleaseLockTable("Registration")
  Endif
  txtencounter.Text = ""
  txtpatno.Text = ""
  modHelpVariable.CreateErrorReport()

End

Public Sub btnOK_Click()

  SaveRegistrationData()
  FillRegistrationGrid(Now())

End

Public Sub btnChange_Click()

  Dim res As Result
  Dim $DOBirth As Date

  If $selDate = True Then
    $DOBirth = dtdobmain.Tag
  Else
    If txtmonths.Value = 0 Then
      If btnyear.Text = "Year" Then
        $DOBirth = DateAdd(Now(), gb.Year, 0 - CInt(txtyears.Value))
      Else If btnyear.Text = "Mon" Then
        $DOBirth = DateAdd(Now(), gb.Month, 0 - CInt(txtyears.Value))
      Else If btnyear.Text = "Day" Then
        $DOBirth = DateAdd(Now(), gb.Day, 0 - CInt(txtyears.Value))
      Endif

    Else
      If btnyear.Text = "Year" And If btnmon.Text = "Mon" Then
        $DOBirth = DateAdd(Now(), gb.Month, 0 - CInt(12 * txtyears.Value + txtmonths.Value))
      Else If btnyear.Text = "Mon" And If btnmon.Text = "Day" Then
        $DOBirth = DateAdd(Now(), gb.Day, 0 - CInt(30 * txtyears.Value + txtmonths.Value))
      Else If btnyear.Text = "Day" And If btnmon.Text = "Hour" Then
        $DOBirth = DateAdd(Now(), gb.Day, 0 - CInt(24 * txtyears.Value + txtmonths.Value))
      Endif

    Endif
  Endif

  If txtpatno.Text Then
    If txtnamefir.Text And If txtnamelast.Text And If cmbsex.Text Then
      res = modDatabase.$myConn.Edit("tblpatientinfo", "fldpatientval=&1 and fldadmitfile IS &2", Trim(txtpatno.Text), Null)
      If res.Available Then

        res["fldptnamefir"] = String.UCaseFirst(Trim(txtnamefir.text))
        res["fldptnamelast"] = String.UCaseFirst(Trim(txtnamelast.text))
        res["fldptcontact"] = Trim(txtcontact.text)
        res["fldemail"] = Trim(txtemail.Text)
        res["fldethniccode"] = txtethcode.Text
        res["fldptaddvill"] = String.UCaseFirst(Trim(txtaddfir.text))
        res["fldptaddward"] = Trim(txtward.Text)
        res["fldptadddist"] = String.UCaseFirst(Trim(cmbdistrict.text))
        res["fldptsex"] = String.UCaseFirst(cmbsex.text)
        res["fldptguardian"] = String.UCaseFirst(Trim(txtguardian.text))
        res["fldrelation"] = String.UCaseFirst(Trim(cmbrelation.Text))
        If chknull.Value = False Then
          res["fldptbirday"] = $DOBirth
        Endif
        res["fldptcode"] = txtpatcode.Text
        res["fldupuser"] = modBasic.$lbluser
        res["flduptime"] = Now()
        res["xyz"] = False
        res.Update()

        FillRegistrationGrid(Now())
        Me.Exec("Toastify({text: 'Information updated', duration: 3000}).showToast()")
        ' Balloon.Delay = modBasic.$BalloonDelay
      Endif
    Endif
  Else
    Message.Warning(("Patient No is NULL"), ("OK"))
  Endif

End

Public Sub btnCancel_Click()

  If txtencounter.Text Then
    $sValue = txtencounter.Text
    Me.Close(True)
  Else
    Me.Close()
  Endif

End

''-------------------- Find button ---------------------------
Public Sub txtpatno_Activate()

  btngetpatno_Click()

End

Public Sub btngetpatno_Click()

  GetPatientInfoAll()

End

Private Sub GetPatientInfoAll()

  Dim res As Result
  Dim sql As String
  Dim lstdat As Date
  Dim xaddr As String[]

  $selDate = False
  $ImgPath = ""
  txtnamefir.Enabled = True
  txtnamelast.Enabled = True
  If txtpatno.Text Then

    sql = "select fldptnamefir,fldptnamelast,fldethniccode,fldptaddvill,fldptaddward,fldptadddist,fldptsex,fldptcontact,fldptguardian,fldptbirday,fldptadmindate,fldemail,fldrelation,fldptcode,fldencrypt from tblpatientinfo where fldpatientval=&1"
    res = modDatabase.$myConn.Exec(sql, Trim(txtpatno.Text))
    If res.Available = False Then
      modLinkDB.CheckPatientDataRemote(Trim(txtpatno.Text))
      txtpatno.SetFocus
    Else
      txtvisit.Text = modPatient.GetPatientHMISStattus(Trim(txtpatno.Text))
      cmbdistrict.Text = res["fldptadddist"]  ''district always before address
      cmbsex.Text = res["fldptsex"]
      If res["fldptaddward"] Then
        txtward.Text = res["fldptaddward"]
        txtaddfir.Text = res["fldptaddvill"]
      Else
        If res["fldptaddvill"] Then
          xaddr = Split(res["fldptaddvill"], "-")
          If xaddr.Count > 1 Then
            If IsInteger(xaddr[xaddr.Count - 1]) Then
              txtward.Text = xaddr[xaddr.Count - 1]
              txtaddfir.Text = xaddr.Copy(0, xaddr.Count - 1).Join("-")
            Else
              txtward.Text = ""
              txtaddfir.Text = xaddr.Join("-")
            Endif
          Else
            txtward.Text = ""
            txtaddfir.Text = xaddr.Join("-")
          Endif
        Endif
      Endif
      txtguardian.Text = res["fldptguardian"]
      cmbrelation.Text = res["fldrelation"]
      If modBasic.$SuperUser = True Then
        txtnamefir.Text = modPassword.DecryptPatData(res["fldptnamefir"], res["fldencrypt"])
        txtnamelast.Text = modPassword.DecryptPatData(res["fldptnamelast"], res["fldencrypt"])
        txtcontact.Text = modPassword.DecryptPatData(res["fldptcontact"], res["fldencrypt"])
        txtemail.Text = modPassword.DecryptPatData(res["fldemail"], res["fldencrypt"])
      Else
        txtnamefir.Text = res["fldptnamefir"]
        txtnamelast.Text = res["fldptnamelast"]
        txtcontact.Text = res["fldptcontact"]
        txtemail.Text = res["fldemail"]
      Endif
      txtethcode.Text = res["fldethniccode"]
      txtpatcode.Text = res["fldptcode"]
      If res["fldptbirday"] Then
        chknull.Value = False
        dtdobmain.Tag = res["fldptbirday"]
        $selDate = True
        modDateSub.DateToYearandMonth(res["fldptbirday"], txtyears, txtmonths, btnyear, btnmon)
      Else
        dtdobmain.Tag = ""
        chknull.Value = True
      Endif
      chkfollow.Enabled = True
      lblstatus.Text = "RECORDED PATIENT"
      lstdat = modPatient.GetRecordDate(modPatient.GetLastEncoutnerFromPatNo(Trim(txtpatno.Text)))
    Endif

    If Not txtnamefir.Text Then
      If modBasic.$AllowPatNoEntry = "Yes" Then
        txtnamefir.SetFocus
      Else
        txtpatno.SetFocus
      Endif
    Endif

  Endif

End

''------------------------------ form procedures ------------------------------------------------
Public Sub txtethcode_Activate()

  txtaddfir.SetFocus

End

Public Sub cmbdistrict_Click()

  txtaddfir.Clear()
  If cmbdistrict.Text Then
    txtaddfir.List = modDataRepo.GetMunicipalsByDistrict(cmbdistrict.Text)
  Endif

End

Public Sub cmbdistrict_Activate()

  Dim xbool As Boolean

  If txtnamelast.Text Then
    If Not txtethcode.Text Then
      txtethcode.Text = modGeneral.GetEthnicCode(Trim(txtnamelast.Text))
    Endif
  Endif

  If modBasic.$UnLockDistrict = "Yes" Then
    xbool = True
  Else
    xbool = False
  Endif

  If Not cmbdistrict.Text Then
    If modBasic.$DistrictList Then

      If modBasic.$RegistGridFormat = "Yes" Then
        cmbdistrict.Text = GridViewNew("Select District", modBasic.$DistrictList, xbool)
      Endif

    Endif
  Endif

End

Public Sub cmbrelation_Activate()

  Dim xbool As Boolean

  If modBasic.$UnLockRelation = "Yes" Then
    xbool = True
  Else
    xbool = False
  Endif

  If Not cmbrelation.Text Then
    If modBasic.$RelationList Then

      If modBasic.$RegistGridFormat = "Yes" Then
        cmbrelation.Text = GridViewNew("Select Relation", modBasic.$RelationList, xbool)
      Endif

    Endif
  Endif

End

''------------------------------ grid view -------------------------------
Private Sub FillRegistrationGrid(sDate As Date)

  Dim sql As String

  sql = "select fldencounterval,fldencounterval,fldencounterval,fldencounterval,fldadmitlocat,fldregdate,fldpatientval,fldencounterval,fldencounterval from tblencounter where fldregdate>=&1 and fldregdate<=&2 and fldcomp=&3"                                      ''
  $rData = modDatabase.$myConn.Exec(sql, modDate.StartSqlDate(sDate), modDate.EndSqlDate(sDate), modBasic.$compID)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = CStr(85 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(225 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Hidden = True
    .Columns[6].Hidden = True
    .Columns[7].Width = CStr(25 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Width = CStr(25 * modBasic.$AppWidthRatio) & "px"

    .Columns[0].Text = "Encounter"
    .Columns[1].Text = "Name"
    .Columns[2].Text = "Gender"
    .Columns[3].Text = "Address"
    .Columns[4].Text = "Department"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Text = modPatient.GetPatientNameByEnc($rData[$aMyFields[Column]])
  Else If Column = 2 Then
    Data.Text = modPatient.GetPatientSex($rData[$aMyFields[Column]])
  Else If Column = 3 Then
    Data.Text = modPatient.GetPatientAddressByEnc($rData[$aMyFields[Column]])
  Else If Column = 7 Then
    Data.Control = btnattach
  Else If Column = 8 Then
    Data.Control = btnedit
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

''--------------------------------------enable/disable textbox ------------------------------
Private Sub SetRequiredVariables()

  If modBasic.$RegistEssAddress = "Yes" Then
    lbladdress.Text = lbladdress.Text & "*"
  Endif

  If modBasic.$RegistEssDistrict = "Yes" Then
    lbldistrict.Text = lbldistrict.Text & "*"
  Endif

  If modBasic.$RegistEssAge = "Yes" Then
    chknull.Value = False
    lblage.Text = lblage.Text & "*"
  Endif

  If modBasic.$RegistEssGuardian = "Yes" Then
    lblguardian.Text = lblguardian.Text & "*"
  Endif

  If modBasic.$RegistEssContact = "Yes" Then
    lblcontact.Text = lblcontact.Text & "*"
  Endif

  If modBasic.$RegistEssEMail = "Yes" Then
    lblemail.Text = lblemail.Text & "*"
  Endif

  If modBasic.$RegistEssCode = "Yes" Then
    lblpatcode.Text = lblpatcode.Text & "*"
  Endif

End

Public Sub btnsearch_Click()

  txtpatno.Text = PatSearch("PatientNo")

End

Public Sub btnsearchdate_Click()

  Dim sDate As Date

  sDate = GetDateValue("Select Date", "Registration", Date())
  If sDate Then
    FillRegistrationGrid(sDate)
  Endif

End

Public Sub btnindex_Click()

  Dim hForm As FmIndexForm

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    hForm = New FmIndexForm($rData["fldencounterval"], True, True)
    hForm.ShowModal
  Endif

End
