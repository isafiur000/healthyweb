' Gambas class file

Private $encid As String
Private $categList As String[]

Public Sub _new(encid As String)

  $encid = encid
  OpenForm()

End

Public Sub OpenForm()

  Dim xgender As String
  Dim xage As String
  Dim xstr As String

  If modBasic.$CloudAIURL = "https://api.groq.com/openai/v1/chat/completions" Then
    cmblanguage.List = modCloudAI.$GroqCloudModels
  Endif
  rbfirst.Value = True

  xgender = modPatient.GetPatientSex($encid)
  xage = modPatient.GetPatientAgeString($encid, Now())

  If xgender Then
    xstr = "A " & xgender & " patient "
  Endif
  If xage Then
    If xage = "?" Then
    Else
      xstr = xstr & " of age " & xage
    Endif
  Endif
  If xstr Then
    TextArea1.Text = xstr
  Endif

End

Private Function GetPatientReport(sCateg As String) As String

  Dim xpos As String
  Dim xList As String[]
  Dim xitem As String
  Dim xnewtext As String
  Dim xnewval As String[]
  Dim xval As String
  Dim xrepo As String[]

  If rbfirst.Value = True Then
    xpos = "FitsrVal"
  Else If rblast.Value = True Then
    xpos = "LastVal"
  Endif

  If sCateg Then
    If sCateg = "Medicine Dispensed" Then
      xrepo = modPharmacy.GetMedicineDispRegimens($encid)
      If xrepo.Count Then
        xnewtext = sCateg & gb.NewLine & xrepo.Join(gb.NewLine) & gb.NewLine
      Else
        xnewtext = ""
      Endif

    Else If sCateg = "Laboratory Tests" Then
      xList = modLabTest.PaientAllTestNameArray($encid)
      xrepo = New String[]
      For Each xitem In xList
        xnewval = modLabTest.GetLabotarotyValuePosition($encid, xitem, "Metric", xpos)
        If xnewval[2] Then
          xrepo.Add(xitem & " (" & xnewval[2] & ")" & " : " & xnewval[1])
        Else
          xrepo.Add(xitem & " : " & xnewval[1])
        Endif
      Next
      If xrepo.Count Then
        xnewtext = sCateg & gb.NewLine & xrepo.Join(gb.NewLine) & gb.NewLine
      Else
        xnewtext = ""
      Endif

    Else If sCateg = "Radiological Findings" Then
      xList = modRadioTest.PaientAllRadioNameArray($encid)
      xrepo = New String[]
      For Each xitem In xList
        xnewval = modRadioTest.GetRadioValuePosition($encid, xitem, xpos)
        xrepo.Add(xitem & " : " & xnewval[1])
      Next
      If xrepo.Count Then
        xnewtext = sCateg & gb.NewLine & xrepo.Join(gb.NewLine) & gb.NewLine
      Else
        xnewtext = ""
      Endif

    Else
      xval = Trim(modReportVar.GetColumnValueRaw(sCateg, $encid))
      If xval Then
        xnewtext = sCateg & gb.NewLine & xval & gb.NewLine
      Else
        xnewtext = ""
      Endif
    Endif
  Endif

  Return xnewtext

End

Public Sub btncategory_Click()

  Dim xList As String[]

  $categList = New String[]
  xList = GridListView("Select Variables", modMedicine.GetCloudAIVariables())
  If xList And If xList.Count Then
    $categList.Insert(xList)
  Endif

End

Public Sub btnadd_Click()

  Dim xval As String
  Dim xcateg As String
  Dim xnewtext As String

  If $categList And If $categList.Count Then
    For Each xcateg In $categList
      xval = GetPatientReport(xcateg)
      If xval Then
        xnewtext = xnewtext & gb.NewLine & gb.NewLine & modString.ConvertRichTextToPlain(GetPatientReport(xcateg))
      Endif
    Next
    TextArea1.Text = TextArea1.Text & gb.NewLine & xnewtext
  Endif

End

Public Sub Button1_Click()

  Dim cForm As CGroqAPI

  txtnewtext.Text = ""
  If TextArea1.Text And If cmblanguage.Text Then
    cForm = New CGroqAPI(cmblanguage.Text, Trim(TextArea1.Text))
    txtnewtext.Text = cForm.GetAPIAnswer()
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub btngetlist_Click()

  If modBasic.$CloudAIKey Then
    cmblanguage.List = modCloudAI.GetGroqModelList(modBasic.$CloudAIKey)
  Else
    cmblanguage.List = modCloudAI.$GroqCloudModels
  Endif

End
