' Gambas class file

Private $examList As String[]

Private aWebFrame As New Object[50]
Private aIndexLabel As New Object[50]
Private aNameLabel As New Object[50]
Private aCheckAbn As New Object[50]
Private aQuantiBox As New Object[50]
Private sButtonPlus As New Object[50]
Private sButtonMinus As New Object[50]
Private aMinBox As New Object[50]
Private aMaxBox As New Object[50]
Private aQualiBox As New Object[50]
Private aComboBox As New Object[50]
Private aDichoValue As New Object[50]
Private aHelpButton As New Object[50]

Private aWebBottom As New Object[50]
Private aValSlider As New Object[50]
Private aBlankFrame As New Object[50]

Private $enableControl As Boolean
Private $viewSlider As Boolean
Private $TriageColor As Integer

Private $xOption As String[]
Private $xOpList As String[]
Private $xSysCons As String[]
Private $xType As String[]

Public Sub _new(encid As String)

  txtencid.Text = encid
  modGeneralMain.GetOpenModalForm(Me)
  If txtencid.Text Then
    btnshow_Click()
  Else
    pnlfirst.Visible = True
  Endif

End

Public Sub txtencid_Activate()

  btnshow_Click()

End

Public Sub btnshow_Click()

  Dim xx As Boolean

  If txtencid.Text Then
    txtname.Text = modPatient.GetPatientNameByEnc(Trim(txtencid.Text))
    $TriageColor = modPatient.GetPatientColorStatic(Trim(txtencid.Text))
    $examList = modClinic.GetFullVitalList(Trim(txtencid.Text))
    xx = modPatient.EnableClinicForm(modPatient.CurrentAdmissionStatus(Trim(txtencid.Text)))
    If modBasic.$ClinSliderValue = "Enable" Then
      $viewSlider = True
    Else
      $viewSlider = False
    Endif
    $enableControl = False
    LoadControlsWeb()
    $enableControl = True

    btnsaveall.Enabled = xx
    btncolor.Enabled = xx
  Endif

End

Public Sub btncolor_Click()

  Dim xcol As String

  xcol = InputColor("Triage", CStr($TriageColor))
  If xcol Then
    $TriageColor = CInt(xcol)
    modPatient.SetPatientColor(Trim(txtencid.Text), xcol)
  Endif

End

Public Sub btnwebcam_Click()

  txtencid.Text = QRScanValue("")
  If txtencid.Text Then
    btnshow_Click()
  Endif

End

Private Sub LoadControlsWeb()

  Dim i As Integer
  Dim res As Result
  Dim xopList As String[]
  Dim xlimit As Float[]

  Dim xcrit As Float
  Dim aCrlower As Float
  Dim aCrupper As Float

  $xOption = New String[]
  $xOpList = New String[]
  $xSysCons = New String[]
  $xType = New String[]

  For i = 0 To $examList.Count - 1
    If i < 49 Then

      res = modDatabase.$myConn.Exec("select fldexamid,fldtype,fldoption,fldsysconst,fldcritical from tblexam where fldexamid=&1", $examList[i])
      If res.Available Then
        If res["fldoption"] = "Clinical Scale" Then
          xopList = modAllExam.GetCLinicalScaleOptions("Exam", $examList[i])
        Else
          xopList = modAllExam.SelectExamOptionList("Exam", $examList[i])
        Endif

        If res["fldoption"] Then
          $xOption.Add(res["fldoption"])
        Else
          $xOption.Add("__")
        Endif
        If xopList And If xopList.Count Then
          $xOpList.Add(xopList.Join(";"))
        Else
          $xOpList.Add("__")
        Endif
        If res["fldsysconst"] Then
          $xSysCons.Add(res["fldsysconst"])
        Else
          $xSysCons.Add("__")
        Endif
        If res["fldtype"] Then
          $xType.Add(res["fldtype"])
        Else
          $xType.Add("__")
        Endif

        aWebFrame[i] = New WebContainer(Frame1)
        aIndexLabel[i] = New WebLabel(aWebFrame[i])
        aNameLabel[i] = New WebHtml(aWebFrame[i])
        aCheckAbn[i] = New WebCheckBox(aWebFrame[i])
        If res["fldtype"] = "Quantitative" Then
          aQuantiBox[i] = New WebValueBox(aWebFrame[i]) As "QuantiGroup"
          sButtonPlus[i] = New WebButton(aWebFrame[i]) As "PlusGroup"
          sButtonMinus[i] = New WebButton(aWebFrame[i]) As "MinusGroup"
          aMinBox[i] = New WebValueBox(aWebFrame[i])
          aMaxBox[i] = New WebValueBox(aWebFrame[i])
          aWebBottom[i] = New WebContainer(Frame1)
          aValSlider[i] = New WebSlider(aWebBottom[i]) As "SliderGroup"
        Else
          Select res["fldoption"]
            Case "Single Selection"
              aComboBox[i] = New WebComboBox(aWebFrame[i])
            Case "Dichotomous"
              aDichoValue[i] = New DichotomousValue(aWebFrame[i])
            Case "Clinical Scale"
              aQualiBox[i] = New WebTextBox(aWebFrame[i])
              aQuantiBox[i] = New WebValueBox(aWebFrame[i])
              aHelpButton[i] = New WebButton(aWebFrame[i]) As "ButtonBoxgroup"
            Case Else
              aQualiBox[i] = New WebTextBox(aWebFrame[i])
          End Select
        Endif
        aBlankFrame[i] = New WebSeparator(Frame1)

        With aWebFrame[i]
          .Arrangement = Arrange.Horizontal
          .Class = "less-container-margin"
        End With

        ''create index label
        With aIndexLabel[i]
          .Width = "2em"
          .Height = "2em"
          .Text = i + 1
          .Visible = False
          .Tag = res["fldsysconst"]
        End With
        ''create Name Label
        With aNameLabel[i]
          .Expand = True
          .Height = "2em"
          .Text = $examList[i]
          .Tag = i
        End With

        ''Value entry
        If res["fldtype"] = "Quantitative" Then
          xlimit = modClinic.GetBothQuantiExamVal($examList[i], Trim(txtencid.Text))
          If res["fldcritical"] Then
            xcrit = res["fldcritical"]
          Endif
          If xcrit Then
            aCrlower = xlimit[0] - xcrit * (xlimit[1] - xlimit[0])
            aCrupper = xlimit[1] + xcrit * (xlimit[1] - xlimit[0])
            If aCrupper = 0 Then
              aCrupper = 500
            Endif
          Else
            aCrlower = 0
            aCrupper = 500
          Endif
          With aCheckAbn[i]
            .Width = "4em"
            .Height = "2em"
            .Text = "Abn"
            .Enabled = False
            .Tag = i
          End With
          With aQuantiBox[i]
            .Width = "5em"
            .Height = "2em"
            .Tag = i
          End With
          With sButtonPlus[i]
            .Class = "transparent-control"
            .Width = "2em"
            .Height = "2em"
            .Image = "icon:/small/add"
            .Tag = i
          End With
          With sButtonMinus[i]
            .Class = "transparent-control"
            .Width = "2em"
            .Height = "2em"
            .Image = "icon:/small/remove"
            .Tag = i
          End With
          With aMinBox[i]
            .Width = "4em"
            .Height = "2em"
            If xlimit Then
              .Value = xlimit[0]
            Endif
            .Enabled = False
            .Tag = i
          End With
          With aMaxBox[i]
            .Width = "4em"
            .Height = "2em"
            If xlimit Then
              .Value = xlimit[1]
            Endif
            .Enabled = False
            .Tag = i
          End With

          With aWebBottom[i]
            .Arrangement = Arrange.Horizontal
            .Class = "less-container-margin"
            .Visible = $viewSlider
          End With
          With aValSlider[i]
            .Expand = True
            .Height = "1em"
            .Tag = i
          End With
          aValSlider[i].MinValue = aCrlower
          aValSlider[i].MaxValue = aCrupper

        Else
          With aCheckAbn[i]
            .Width = "4em"
            .Height = "2em"
            .Text = "Abn"
            .Tag = i
            If modBasic.$ClinFlagAbnormExam = "Disable" Then
              .Visible = False
            Else
              .Visible = True
            Endif
          End With

          Select res["fldoption"]

            Case "Single Selection"
              With aComboBox[i]
                .Width = "17em"
                .Height = "2em"
                .ReadOnly = False
                .List = xopList
                .Tag = i
              End With

            Case "Dichotomous"
              With aDichoValue[i]
                .Width = "17em"
                .Height = "2em"
                .List = xopList
                .Tag = i
              End With

            Case "Clinical Scale"
              With aQualiBox[i]
                .Width = "10em"
                .Height = "2em"
                .Tag = i
              End With
              With aQuantiBox[i]
                .Width = "5em"
                .Height = "2em"
                .Tag = i
              End With
              With aHelpButton[i]
                .Class = "toolButton"
                .Width = "2em"
                .Height = "2em"
                .Tag = i
                .Text = ""
                .Image = "icon:/small/info"
              End With

            Case Else
              With aQualiBox[i]
                .Width = "17em"
                .Height = "2em"
                .Tag = i
              End With

          End Select
        Endif

        With aBlankFrame[i]
          .Height = "1em"
        End With

      Else
        Message.Info("Coudnot set controls for " & $examList[i])
      Endif

    Endif
  Next

End

Public Sub PlusGroup_Click()

  Dim i As Integer

  i = Last.Tag
  aQuantiBox[i].Value = aQuantiBox[i].Value + 1

End

Public Sub MinusGroup_Click()

  Dim i As Integer

  i = Last.Tag
  aQuantiBox[i].Value = aQuantiBox[i].Value - 1

End

Public Sub ButtonBoxgroup_Click()

  Dim i As Integer
  Dim sVal As String[]
  Dim xopt As String[]

  i = Last.Tag
  If $xOpList[i] Then
    xopt = Split($xOpList[i], ";")
    If $xOption[i] = "Clinical Scale" Then
      sVal = SubChoose(xopt, "Clinical Scale", $examList[i])
      If sVal Then
        aQuantiBox[i].Value = sVal[0]
        aQualiBox[i].Text = sVal[1]
      Endif
    Endif
  Endif

End

Public Sub SliderGroup_Change()

  Dim j As Integer

  j = Last.Tag
  If aQuantiBox[j] Then
    If $enableControl = True Then
      aQuantiBox[j].Value = aValSlider[j].Value
    Endif
  Endif

End

Public Sub QuantiGroup_Change()

  Dim j As Integer

  j = Last.Tag

  If aQuantiBox[j].Value Then
    If aMinBox[j].Value <> aMaxBox[j].Value Then
      If aQuantiBox[j].Value < aMinBox[j].Value Or If aQuantiBox[j].Value > aMaxBox[j].Value Then
        aCheckAbn[j].Value = True
      Else
        aCheckAbn[j].Value = False
      Endif
    Else
      aCheckAbn[j].Value = False
    Endif
  Else
    aCheckAbn[j].Value = False
  Endif

  If aCheckAbn[j].Value = True Then
    aCheckAbn[j].Foreground = Color.Red
  Else
    aCheckAbn[j].Foreground = Color.Default
  Endif

End

Public Sub btnsaveall_Click()

  Dim i As Integer

  For i = 0 To $examList.Count - 1
    If i < 49 Then
      If $xType[i] = "Quantitative" Then
        If aQuantiBox[i].Value Then
          modClinSub.AddQuantiData(Trim(txtencid.Text), "", $examList[i], $xOption[i], aQuantiBox[i].Value, aCheckAbn[i].Value, "Essential Examinations", aIndexLabel[i].Tag)
          aQuantiBox[i].Value = 0
          aQuantiBox[i].ReadOnly = True
        Endif

      Else
        If $xOption[i] = "Single Selection" Then
          If aComboBox[i].Text Then
            modClinSub.AddQualiData(Trim(txtencid.Text), "", $examList[i], $xOption[i], aComboBox[i].Text, aCheckAbn[i].Value, "Essential Examinations", aIndexLabel[i].Tag)
            aComboBox[i].Text = ""
          Endif
        Else If $xOption[i] = "Dichotomous" Then
          If aDichoValue[i].Value Then
            modClinSub.AddQualiData(Trim(txtencid.Text), "", $examList[i], $xOption[i], aDichoValue[i].Value, aCheckAbn[i].Value, "Essential Examinations", aIndexLabel[i].Tag)
          Endif
        Else If $xOption[i] = "Clinical Scale" Then
          If aQualiBox[i].Text Then
            modClinSub.AddClinicExam(Trim(txtencid.Text), "", $examList[i], $xOption[i], aQualiBox[i].Text, aQuantiBox[i].Value, aCheckAbn[i].Value, "Essential Examinations", aIndexLabel[i].Tag, "Regular")     ''
            aQualiBox[i].Text = ""
            aQuantiBox[i].Value = 0
            aQuantiBox[i].ReadOnly = True
          Endif
        Else
          If aQualiBox[i].Text Then
            modClinSub.AddQualiData(Trim(txtencid.Text), "", $examList[i], $xOption[i], aQualiBox[i].Text, aCheckAbn[i].Value, "Essential Examinations", aIndexLabel[i].Tag)
            aQualiBox[i].Text = ""
            aQualiBox[i].ReadOnly = True
          Endif
        Endif

      Endif
    Endif
  Next
  Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")

End

Public Sub btnclose_Click()

  Me.Close()

End
