' Gambas class file

Private $ItemList As String[]
Private $CustText As Boolean

Private $sCategoryList As String[]
Private $sValueList As String[]

Private $aMyFields As String[]
Private $sValue As String

Public Sub Run(Prompt As String, ItemList As String[], CustomText As Boolean) As String

  Me.Title = Prompt
  $ItemList = ItemList
  $CustText = CustomText
  modGeneralMain.GetOpenModalForm(Me)

  GetItemListData()
  ListBox1.List = $sCategoryList
  lstletter.List = modString.GetFirstLetterArray($sValueList)
  chkleftmain.Value = modBasic.$SearchBothSide
  FillItemGrid($sValueList)
  If modBasic.$TabletModeEnable = "Yes" Then
  Else
    txtname.SetFocus
  Endif

  If Me.ShowModal() Then Return $sValue

End

Public Sub chkleftmain_Click()

  modBasic.$SearchBothSide = chkleftmain.Value
  txtname.SetFocus

End

Public Sub ListBox1_Select()

  Dim xval As String[]
  Dim xx As String
  Dim asx As String[]

  If ListBox1.Text Then
    xval = New String[]
    For Each xx In $ItemList

      If xx Then
        asx = Split(xx, "@")
        If asx[0] = ListBox1.Text Then
          If xval.Count = 0 Then
            xval.Add(asx[1])
          Else
            If xval.Exist(asx[1]) = False Then
              xval.Add(asx[1])
            Endif
          Endif
        Endif
      Endif

    Next
    If modBasic.$TabletModeEnable = "Yes" Then
    Else
      txtname.SetFocus
    Endif

  Endif
  $sValueList = xval
  lstletter.List = modString.GetFirstLetterArray($sValueList)
  FillItemGrid($sValueList)

End

Public Sub chkallcateg_Click()

  GetItemListData()
  FillItemGrid($sValueList)
  ListBox1.Index = -1

End

Private Sub GetItemListData()

  Dim xx As String
  Dim asx As String[]
  Dim xval As String[]

  $sCategoryList = New String[]
  xval = New String[]
  For Each xx In $ItemList
    If xx Then
      asx = Split(xx, "@")

      If asx[0] Then
        If $sCategoryList.Count = 0 Then
          $sCategoryList.Add(asx[0])
        Else
          If $sCategoryList.Exist(asx[0]) = False Then
            $sCategoryList.Add(asx[0])
          Endif
        Endif
      Endif

      If asx[1] Then
        If xval.Count = 0 Then
          xval.Add(asx[1])
        Else
          If xval.Exist(asx[1]) = False Then
            xval.Add(asx[1])
          Endif
        Endif
      Endif

    Endif
  Next
  $sValueList = xval

End

Private Sub FillItemGrid(sList As String[])

  GridView1.Clear()
  $aMyFields = modString.SelectStringArrayToText(txtname.Text, sList, chkleftmain.Value)
  GridView1.Columns.Count = 1
  GridView1.Count = $aMyFields.Count

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $aMyFields[Row]

End

Public Sub txtname_Change()

  FillItemGrid($sValueList)

End

Public Sub btnok_Click()

  If GridView1.Selection.Count Then
    $sValue = $aMyFields[GridView1.Selection[0]]
    Me.Close(True)
  Else
    If $CustText = True Then
      If Trim(txtname.Text) Then
        $sValue = Trim(txtname.Text)
        Me.Close(True)
      Endif
    Endif
  Endif

End

''--------------
Public Sub txtname_Activate()

  btnok_Click()

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub lstletter_Select()

  chkleftmain.Value = False
  txtname.Text = lstletter.Text
  GridView1.SetFocus

End
