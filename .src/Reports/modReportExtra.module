' Gambas module file

'NOTE: ----------------------- MEDICATION AUDIT --------------------------------------------
Public Function ShowMedicationAuditReportHTML(encid As String[], $categ As String[], $drug As String[], $condi As String[], $warn As String[], tot As Integer) As String

  Dim $BillingReport As CReportHTML
  Dim asx As New String[0]
  Dim i As Integer
  Dim newcateg As String[]
  Dim xx As String
  Dim yy As Integer

  $BillingReport = New CReportHTML([("ENCID"), ("Medicine"), ("Observation"), ("Recommendation")], "", "")
  $BillingReport.UserData.Add("MEDICATION AUDIT", "PARAM1")
  $BillingReport.UserData.Add("PATIENT COUNT=" & CStr(tot), "PARAM2")

  newcateg = modString.BinaryDistinctStringArray($categ)
  For Each xx In newcateg

    $BillingReport.AddSection(xx, True)

    yy = 0
    For i = 0 To $categ.Count - 1
      If $categ[i] = xx Then
        With asx
          .Add(encid[i])
          .Add($drug[i])
          .Add($condi[i])
          .Add($warn[i])
        End With
        $BillingReport.Add(asx)
        asx.Clear()
        yy = yy + 1
      Endif
    Next

    With asx
      .Add("..")
      .Add("Total")
      .Add(yy)
      .Add("..")
    End With
    $BillingReport.Add(asx)
    asx.Clear()

  Next

  Return $BillingReport.NewHTMLPath()

End

''-------------------------------------------- VOUCHER REPORTS --------------------------------------------
Public Function GetVOucherSummaryListBetween($con As Connection, bill1 As String, bill2 As String) As String

  Dim $BillingReport As CReportHTML
  Dim bilid1 As Long
  Dim bilid2 As Long
  Dim res2 As Result
  Dim asx As New String[0]
  Dim xrecv As Float

  $BillingReport = New CReportHTML([("User"), ("RecAmt"), ("Comment")], "", "")
  $BillingReport.UserData.Add("Voucher Report", "PARAM1")
  $BillingReport.UserData.Add(bill1 & " TO " & bill2, "PARAM2")

  bilid1 = modNonMedical.GetVOucherIDFromBillNo(bill1)
  bilid2 = modNonMedical.GetVOucherIDFromBillNo(bill2)

  If bilid1 > 0 And If bilid2 > 0 Then
    res2 = $con.Exec("select flduserid,SUM(fldpayamount) as recvamt from tblextrareceipt where fldid>=&1 and fldid<=&2 GROUP BY flduserid", bilid1, bilid2)
    If res2.Available Then
      For Each res2
        xrecv = xrecv + res2!recvamt
        With asx
          .Add(res2["flduserid"])
          .Add(modReportVar.GetLocaleNumberFormat(res2["recvamt"], gb.Currency))
          .Add("")
        End With
        $BillingReport.Add(asx)
        asx.Clear()
      Next
    Endif

    With asx
      .Add("***")
      .Add(modReportVar.GetLocaleNumberFormat(xrecv, gb.Currency))
      .Add("***")
    End With
    $BillingReport.Add(asx)
    asx.Clear()
  Endif

  Return $BillingReport.NewHTMLPath()

End