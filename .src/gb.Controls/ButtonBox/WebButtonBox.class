' Gambas class file

Export
Inherits WebContainer

Public Const _IsContainer As Boolean = False
Public Const _Properties As String = "*,ProgressControl,Text"
Public Const _DrawWith As String = "TextBox"
Public Const _DefaultSize As String = "24,4"
Public Const _DefaultEvent As String = "Click"
Public Const _Similar As String = "WebTextBox"

Event Click

Property Text As String
Property ProgressControl As WebProgressBar

Private hTextBox As WebTextBox
Private hFileButton As WebFileButton
Private $WebProgressBar1 As WebProgressBar

Public Sub _new()

  Dim hBox As WebHBox

  Me.Arrangement = Arrange.Vertical
  hBox = New WebHBox(Me)

  hTextBox = New WebTextBox(hBox) As "hTextBox"
  hTextBox.Expand = True
  ' hTextBox.Height = "2em"
  hTextBox.Border = True

  hFileButton = New WebFileButton(hBox) As "hFileButton"
  hFileButton.Width = "2em"
  hFileButton.Height = "2em"
  hFileButton.Image = "icon:/small/up"

End

Public Sub hFileButton_Click()

  hFileButton.Upload()

End

Public Sub hFileButton_Progress()

  If $WebProgressBar1 Then
    $WebProgressBar1.Value = hFileButton.Progress
  Endif

End

Public Sub hFileButton_Finish()

  Dim xPath As String

  xPath = modPrint.GetCopyTempPath(hFileButton.Path)
  If xPath Then
    hTextBox.Text = xPath
  Endif

End

Public Sub hFileButton_Abort(Reason As String)

  Message.Error(Html(Reason), ("OK"))

End

Private Function Text_Read() As String

  Return hTextBox.Text

End

Private Sub Text_Write(Value As String)

  hTextBox.Text = Value

End

Private Function ProgressControl_Read() As WebProgressBar

  Return $WebProgressBar1

End

Private Sub ProgressControl_Write(Value As WebProgressBar)

  $WebProgressBar1 = Value

End
