' Gambas class file

Private $sValue As Variant

Private $bookID As String
Private $hospCode As String
Private $PatName As String
Private $sTitle As String
Private $sDetail As String
Private $sCharge As Float

Public Sub Run(sTarget As String, sBookID As String, sPatient As String, sTitle As String, sDetail As String, sAmt As Float) As Variant

  $bookID = sBookID
  $hospCode = sTarget
  $PatName = sPatient
  $sTitle = sTitle
  $sDetail = sDetail
  $sCharge = sAmt
  PictureBox2.Image = modPrint.GetFixedWebURL(modHelpVariable.$appetcFolder &/ "qrcode.jpg")

  If Me.ShowModal() Then Return $sValue

End

Public Sub btnOpen_Click()

  btnOpen.Upload()

End

Public Sub btnOpen_Progress()

  WebProgressBar1.Value = btnOpen.Progress

End

Public Sub btnOpen_Finish()

  Dim xPath As String

  Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Image upload completed", modBasic.$BalloonDelay))
  xPath = modPrint.GetCopyTempPath(btnOpen.Path)
  If Exist(xPath) Then
    modImage.StretchtPictureToBox(PictureBox1, xPath)
  Endif

End

Public Sub btnOpen_Abort(Reason As String)

  Message.Error("Unable to upload file.<br>Reason: " & Html(Reason))
  WebProgressBar1.Value = 0

End

Public Sub btnedit_Click()

  Dim sPath As String

  If PictureBox1.Tag Then
    sPath = PictureBox1.Tag
    If Exist(sPath) Then
      $sValue = modImage.AddVoucherImageBook($hospCode, $bookID, $PatName, $sTitle, $sDetail, $sCharge, sPath)
      Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information updated", modBasic.$BalloonDelay))
    Endif
  Endif

End

Public Sub btnclear_Click()

  If PictureBox1.Tag Then
    PictureBox1.Tag = ""
    Wait
  Endif

End

Public Sub btnclose_Click()

  Me.Close(True)

End
