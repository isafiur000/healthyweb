' Gambas class file

Private $encid As String
Private $sText As String
Private $sExtra As String
Private $sValue As String

Public Sub Run(encid As String, sText As String, sExtra As String) As String

  Dim xgender As String
  Dim xage As String
  Dim xstr As String

  $encid = encid
  $sText = sText
  $sExtra = sExtra

  If modBasic.$CloudAIURL = "https://api.groq.com/openai/v1/chat/completions" Then
    cmblanguage.List = modCloudAI.$GroqCloudModels
  Endif
  cmbquestion.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldquery as col from tblaiquery where fldtype=&1", modHelpVariable.$LogInCategory))

  xgender = modPatient.GetPatientSex($encid)
  xage = modPatient.GetPatientAgeString($encid, Now())

  If xgender Then
    xstr = "A " & xgender & " patient "
  Endif
  If xage Then
    If xage = "?" Then
    Else
      xstr = xstr & " of age " & xage
    Endif
  Endif
  If xstr Then
    TextArea1.Text = xstr
  Endif
  rbfirst.Value = True

  If Me.ShowModal() Then Return $sValue

End

Public Sub btnaddquery_Click()

  Dim hForm As FmAddVariable1

  hForm = New FmAddVariable1("fldquery", "fldtype", modHelpVariable.$LogInCategory, "tblaiquery")
  hForm.ShowModal
  cmbquestion.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select fldquery as col from tblaiquery where fldtype=&1", modHelpVariable.$LogInCategory))

End

Public Sub btnpaste_Click()

  TextArea1.Text = TextArea1.Text & gb.NewLine & $sText

End

Public Sub btnquestion_Click()

  If cmbquestion.Text Then
    TextArea1.Text = TextArea1.Text & gb.NewLine & cmbquestion.Text
  Endif

End

Public Sub btncategory_Click()

  Dim xList As String[]

  xList = GridListView("Select Variables", modMedicine.GetCloudAIVariables())
  If xList And If xList.Count Then
    AddCategories(xList)
  Endif

End

Private Sub AddCategories(sList As String[])

  Dim xval As String
  Dim xcateg As String
  Dim xnewtext As String

  If sList And If sList.Count Then
    For Each xcateg In sList
      xval = GetPatientReport(xcateg)
      If xval Then
        xnewtext = xnewtext & gb.NewLine & gb.NewLine & GetPatientReport(xcateg)
      Endif
    Next
    TextArea1.Text = TextArea1.Text & gb.NewLine & xnewtext
  Endif

End

Private Function GetPatientReport(sCateg As String) As String

  Dim xpos As String
  Dim xList As String[]
  Dim xitem As String
  Dim xnewtext As String
  Dim xnewval As String[]
  Dim xval As String
  Dim xrepo As String[]

  If rbfirst.Value = True Then
    xpos = "FitsrVal"
  Else If rblast.Value = True Then
    xpos = "LastVal"
  Endif

  If sCateg Then
    If sCateg = "Medicine Dispensed" Then
      xrepo = modPharmacy.GetMedicineDispRegimens($encid)
      If xrepo.Count Then
        xnewtext = sCateg & gb.NewLine & xrepo.Join(gb.NewLine) & gb.NewLine
      Else
        xnewtext = ""
      Endif

    Else If sCateg = "Laboratory Tests" Then
      xList = modLabTest.PaientAllTestNameArray($encid)
      xrepo = New String[]
      For Each xitem In xList
        xnewval = modLabTest.GetLabotarotyValuePosition($encid, xitem, "Metric", xpos)
        If xnewval[2] Then
          xrepo.Add(xitem & " (" & xnewval[2] & ")" & " : " & xnewval[1])
        Else
          xrepo.Add(xitem & " : " & xnewval[1])
        Endif
      Next
      If xrepo.Count Then
        xnewtext = sCateg & gb.NewLine & xrepo.Join(gb.NewLine) & gb.NewLine
      Else
        xnewtext = ""
      Endif

    Else If sCateg = "Radiological Findings" Then
      xList = modRadioTest.PaientAllRadioNameArray($encid)
      xrepo = New String[]
      For Each xitem In xList
        xnewval = modRadioTest.GetRadioValuePosition($encid, xitem, xpos)
        xrepo.Add(xitem & " : " & xnewval[1])
      Next
      If xrepo.Count Then
        xnewtext = sCateg & gb.NewLine & xrepo.Join(gb.NewLine) & gb.NewLine
      Else
        xnewtext = ""
      Endif

    Else
      xval = Trim(modReportVar.GetColumnValueRaw(sCateg, $encid))
      If xval Then
        xnewtext = sCateg & gb.NewLine & xval & gb.NewLine
      Else
        xnewtext = ""
      Endif
    Endif
  Endif

  Return xnewtext

End

Public Sub Button1_Click()

  Dim cForm As CGroqAPI

  txtnewtext.Text = ""
  If TextArea1.Text And If cmblanguage.Text Then
    cForm = New CGroqAPI(cmblanguage.Text, Trim(TextArea1.Text))
    txtnewtext.Text = cForm.GetAPIAnswer()
  Endif

End

Public Sub btngetlist_Click()

  If modBasic.$CloudAIKey Then
    cmblanguage.List = modCloudAI.GetGroqModelList(modBasic.$CloudAIKey)
  Else
    cmblanguage.List = modCloudAI.$GroqCloudModels
  Endif

End

Public Sub btnclose_Click()

  $sValue = txtnewtext.Text
  Me.Close(True)

End
