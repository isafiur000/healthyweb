' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $sType As String
Private $mainList As String[]
Private $ItemList As String[]

Private aWebPanel As New Object[50]
Private aXpanel As New Object[50]
Private aIndexLabel As New Object[50]
Private aNameLabel As New Object[50]
Private aButtonBox As New Object[50]
Private aButtonTip As New Object[50]
Private aDateBox As New Object[50]
Private aBSDate As New Object[50]
Private aComboBox As New Object[50]
Private aDichoValue As New Object[50]
Private aValueBox As New Object[50]
Private aLineText As New Object[50]
Private aSpacePanel As New Object[50]

Private $xType As String[]
Private $xOption As String[]
Private AppFactor As Float

Public Sub _new(encid As String, sType As String)

  txtencid.Text = encid
  $sType = sType
  modGeneralMain.GetOpenModalForm(Me)
  If txtencid.Text Then
    btnshow_Click()
  Else
    pnlfirst.Visible = True
  Endif

End

Public Sub btnwebcam_Click()

  txtencid.Text = QRScanValue("")
  If txtencid.Text Then
    btnshow_Click()
  Endif

End

Public Sub txtencid_Activate()

  btnshow_Click()

End

Public Sub btnshow_Click()

  Dim xx As Boolean

  If txtencid.Text Then
    txtpatientname.Text = modPatient.GetPatientNameByEnc(Trim(txtencid.Text))
    txtpataddress.Text = modPatient.GetPatientAddressByEnc(Trim(txtencid.Text))
    txtgender.Text = modPatient.GetPatientSex(Trim(txtencid.Text))
    xx = modPatient.EnableClinicForm(modPatient.CurrentAdmissionStatus(Trim(txtencid.Text)))

    ShowExamGrid()
    LoadListData()
    btnBrOK.Enabled = xx
  Endif

End

Public Sub WebTabPanel1_Click()

  If txtencid.Text Then
    If WebTabPanel1.Index = 0 Then
      ShowExamGrid()
    Else If WebTabPanel1.Index = 1 Then
      If Frame1.Children.Count Then
        Frame1.DeleteChildren()
      Endif
      GetRemainingList()
      LoadControls()
    Endif
  Endif

End

' Public Sub Form_KeyRelease()
'
'   If Key.Code = Key.Esc Then
'     Me.Close
'   Else
'     modGeneralmain.GoToNextControlTab()
'   Endif
'
' End

Private Sub LoadListData()

  If $sType = "Clinical" Then
    $mainList = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flddemoid as col from tbldemographic where (fldcomp=&1 or fldcomp=&2) ORDER BY fldindex", modBasic.$compID, "%"))
  Else If $sType = "Acount" Then
    $mainList = modBasic.$DemogAccList
  Endif

End

Private Sub GetRemainingList()

  Dim prList As String[]

  If $rData.Available Then
    prList = New String[]
    For Each $rData
      prList.Add($rData["flditem"])
    Next
    $ItemList = modString.GetRemainingArray($mainList, prList)
  Else
    $ItemList = $mainList
  Endif

End

'''========================= Grouped ======================
Private Sub LoadControls()

  Dim i As Integer
  Dim sType As String
  Dim opt As String[]

  $xType = New String[]
  $xOption = New String[]
  For i = 0 To $ItemList.Count - 1
    If i < 49 Then
      If $sType = "Clinical" Then
        sType = modFixPatho.GetDemographicType($ItemList[i])
      Else If $sType = "Acount" Then
        sType = modPatient.GetAccDemographicType($ItemList[i])
      Endif
      $xType.Add(sType)

      If sType = "Single Selection" Or If sType = "Dichotomous" Or If sType = "Multiple Selection" Then
        If $sType = "Clinical" Then
          opt = modFixPatho.GetDemographicOptionList($ItemList[i], sType)
        Else If $sType = "Acount" Then
          opt = modPatient.GetAccDemographicOptionList($ItemList[i], sType)
        Endif
        $xOption.Add(opt.Join(";"))
      Else
        $xOption.Add("")
      Endif

      aWebPanel[i] = New WebHBox(Frame1)
      aIndexLabel[i] = New WebLabel(aWebPanel[i])
      aNameLabel[i] = New WebHtml(aWebPanel[i])

      aXpanel[i] = New WebHBox(Frame1)
      Select sType
        Case "Multiple Selection"
          aButtonBox[i] = New WebTextBox(aXpanel[i])
          aButtonTip[i] = New WebButton(aXpanel[i]) As "ButtonBoxgroup"
        Case "Date Time", "BS Date"
          aDateBox[i] = New WebDateBox(aXpanel[i])
          aBSDate[i] = New WebButton(aXpanel[i]) As "BSButton"
        Case "Single Selection"
          aComboBox[i] = New WebComboBox(aXpanel[i])
        Case "Dichotomous"
          aDichoValue[i] = New DichotomousValue(aXpanel[i])
        Case "Quantitative"
          aValueBox[i] = New WebValueBox(aXpanel[i])
        Case Else
          aLineText[i] = New WebTextBox(aXpanel[i])
      End Select
      aSpacePanel[i] = New WebContainer(Frame1)
    Endif
  Next
  DIsplayTextForm()

End

Private Sub DIsplayTextForm()

  Dim i As Integer

  AppFactor = modBasic.$AppScaleFactor
  ' Frame1.Height = ($ItemList.Count * 25 + 30) * AppFactor
  ' Frame1.Width = 875 * AppFactor

  For i = 0 To $ItemList.Count - 1
    If i < 49 Then

      ''create index label
      With aIndexLabel[i]
        .Width = CStr(25 * AppFactor) & "px"
        .Height = "2em"
        .Text = i + 1
        .Tag = i
      End With

      ''create Name Label
      With aNameLabel[i]
        ' .Expand = True
        ' .Width = CStr(300 * AppFactor) & "px"
        ' .Height = "2em"
        .Font = "bold"
        .Text = "<p>" & $ItemList[i] & "</p>"
        .Tag = i
        .ToolTip = $ItemList[i]
      End With

      With aXpanel[i]
        .Class = "less-container-margin"
      End With

      If $xType[i] = "Multiple Selection" Then
        With aButtonBox[i]
          .Expand = True
          ' .Width = CStr(290 * AppFactor) & "px"
          .Height = "2em"
          .Tag = i
        End With
        With aButtonTip[i]
          .Width = "2em"
          .Height = "2em"
          .Image = "icon:/small/attach"
          .Tag = i
        End With

      Else If $xType[i] = "Single Selection" Then
        With aComboBox[i]
          .Width = "100%"
          .Height = "2em"
          .ReadOnly = False
          .List = Split($xOption[i], ";")
          .Tag = i
        End With

      Else If $xType[i] = "Dichotomous" Then
        With aDichoValue[i]
          .Width = "100%"
          .Height = "2em"
          .List = Split($xOption[i], ";")
          .Tag = i
        End With

      Else If $xType[i] = "Date Time" Then
        With aDateBox[i]
          .Width = "90%"
          .Height = "2em"
          .DateTime = True
          .Tag = i
        End With
        With aBSDate[i]
          .Width = "10%"
          .Height = "2em"
          .Image = "icon:/small/calendar"
          .Tag = i
        End With

      Else If $xType[i] = "BS Date" Then
        With aDateBox[i]
          .Width = "90%"
          .Height = "2em"
          .DateTime = True
          .Tag = i
        End With
        With aBSDate[i]
          .Width = "10%"
          .Height = "2em"
          .Image = "icon:/small/calendar"
          .Tag = i
        End With

      Else If $xType[i] = "Quantitative" Then
        With aValueBox[i]
          .Width = "100%"
          .Height = "2em"
          .Tag = i
        End With

      Else
        With aLineText[i]
          .Width = "100%"
          .Height = "2em"
          .Tag = i
        End With

      Endif

      With aSpacePanel[i]
        .Height = "2em"
      End With

    Endif
  Next

End

Public Sub BSButton_Click()

  Dim j As Integer
  Dim xx As String

  j = Last.Tag
  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(aDateBox[j].Value))
  If xx Then
    aDateBox[j].Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub ButtonBoxgroup_Click()

  Dim j As Integer
  Dim xList As String[]
  Dim strLst As String[]

  j = Last.Tag
  xList = Split($xOption[j], ";")
  If xList Then
    strLst = SubChoose(xList, "Multiple Selection", $ItemList[j])
    If strLst Then
      aButtonBox[j].Text = strLst.Join(";")
    Endif
  Endif

End

Public Sub btnBrOK_Click()

  Dim i As Integer
  Dim xboolean As Boolean

  If modBasic.$ClinHistoryInput = "Single" Then
    xboolean = True
  Else
    xboolean = False
  Endif

  For i = 0 To $ItemList.Count - 1
    If i < 49 Then

      If $xType[i] = "Multiple Selection" Then
        If aButtonBox[i].Text Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQualiData(Trim(txtencid.Text), "Demographics", $ItemList[i], aButtonBox[i].Text, "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQualiData(Trim(txtencid.Text), "Demographics", $ItemList[i], aButtonBox[i].Text, "", Now(), True)
          Endif
        Endif

      Else If $xType[i] = "Single Selection" Then
        If aComboBox[i].Text Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQualiData(Trim(txtencid.Text), "Demographics", $ItemList[i], aComboBox[i].Text, "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQualiData(Trim(txtencid.Text), "Demographics", $ItemList[i], aComboBox[i].Text, "", Now(), True)
          Endif
        Endif

      Else If $xType[i] = "Dichotomous" Then
        If aDichoValue[i].Value Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQualiData(Trim(txtencid.Text), "Demographics", $ItemList[i], aDichoValue[i].Value, "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQualiData(Trim(txtencid.Text), "Demographics", $ItemList[i], aDichoValue[i].Value, "", Now(), True)
          Endif
        Endif

      Else If $xType[i] = "Date Time" Then
        If aDateBox[i].Value Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQualiData(Trim(txtencid.Text), "Demographics", $ItemList[i], modDate.DateStringForExam(aDateBox[i].Value), "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQualiData(Trim(txtencid.Text), "Demographics", $ItemList[i], modDate.DateStringForExam(aDateBox[i].Value), "", Now(), True)
          Endif
        Endif

      Else If $xType[i] = "BS Date" Then
        If aDateBox[i].Value Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQualiData(Trim(txtencid.Text), "Demographics", $ItemList[i], modDate.ConvertToLocaldate(aDateBox[i].Value), "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQualiData(Trim(txtencid.Text), "Demographics", $ItemList[i], modDate.ConvertToLocaldate(aDateBox[i].Value), "", Now(), True)
          Endif
        Endif

      Else If $xType[i] = "Quantitative" Then
        If aValueBox[i].Value Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQuantiData(Trim(txtencid.Text), "Demographics", $ItemList[i], aValueBox[i].Value, "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQuantiData(Trim(txtencid.Text), "Demographics", $ItemList[i], aValueBox[i].Value, "", Now(), True)
          Endif
        Endif

      Else
        If aLineText[i].Text Then
          If $sType = "Clinical" Then
            modPatientGeneral.AddExamGeneralQualiData(Trim(txtencid.Text), "Demographics", $ItemList[i], aLineText[i].Text, "", xboolean)
          Else If $sType = "Acount" Then
            modPatientSub.AddAccGeneralQualiData(Trim(txtencid.Text), "Demographics", $ItemList[i], aLineText[i].Text, "", Now(), True)
          Endif
        Endif

      Endif

    Endif
  Next
  ShowExamGrid()
  btnBrOK.Enabled = False
  Me.Exec("Toastify({text: 'Informaton saved', duration: 3000}).showToast()")

End

Public Sub btnBrChange_Click()

  Me.Close()

End

''============================ Manual =====================
Private Sub ShowExamGrid()

  If $sType = "Clinical" Then
    $rData = modDatabase.$myConn.Exec("select fldid,flditem,fldreportquali,flduserid from tblexamgeneral where fldencounterval=&1 and fldinput=&2 ORDER by fldid DESC", Trim(txtencid.Text), "Demographics")                                                                                 ''
  Else If $sType = "Acount" Then
    $rData = modDatabase.$myConn.Exec("select fldid,flditem,fldreportquali,flduserid from tblpataccgeneral where fldencounterval=&1 and fldinput=&2 ORDER by fldid DESC", Trim(txtencid.Text), "Demographics")                                                                                 ''
  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(325 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"

    .Columns[1].Text = "Variable"
    .Columns[2].Text = "Value"
    .Columns[3].Text = "User"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]

End

Public Sub btnedit_Click()

  Dim sType As String
  Dim opt As String[]
  Dim strLst As String[]
  Dim xx As String
  Dim yy As Variant[]
  Dim xdate As Date

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])

    If $sType = "Clinical" Then
      sType = modFixPatho.GetDemographicType($rData["flditem"])
      If sType = "Single Selection" Then
        opt = modFixPatho.GetDemographicOptionList($rData["flditem"], sType)
        strLst = SubChoose(opt, "Single Selection", $rData["flditem"])
        If strLst Then
          modPatientGeneral.UpdateExamGeneralQualiData($rData["fldid"], $rData["flditem"], strLst.Join(";"), "")
        Endif

      Else If sType = "Multiple Selection" Then
        opt = modFixPatho.GetDemographicOptionList($rData["flditem"], sType)
        strLst = SubChoose(opt, "Multiple Selection", $rData["flditem"])
        If strLst Then
          For Each xx In strLst
            modPatientGeneral.UpdateExamGeneralQualiData($rData["fldid"], $rData["flditem"], xx, "")
          Next
        Endif

      Else If sType = "Quantitative" Then
        yy = GetQuantiValues("Demographics", Trim(txtencid.Text), $rData["flditem"], 0, 0, CFloat($rData["fldreportquali"]))
        If yy Then
          modPatientGeneral.UpdateExamGeneralQuantiData($rData["fldid"], $rData["flditem"], yy[0], "")
        Endif

      Else If sType = "Date Time" Then
        xdate = GetDateValue($rData["flditem"], "Date Value", Val($rData["fldreportquali"]))
        If xdate Then
          modPatientGeneral.UpdateExamGeneralQualiData($rData["fldid"], $rData["flditem"], modDate.DateStringForExam(xdate), "")
        Endif

      Else If sType = "BS Date" Then
        xdate = GetDateValue($rData["flditem"], "Date Value", modDate.ConvertToEnglishdate($rData["fldreportquali"]))
        If xdate Then
          modPatientGeneral.UpdateExamGeneralQualiData($rData["fldid"], $rData["flditem"], modDate.ConvertToLocaldate(xdate), "")
        Endif

      Else
        xx = GetTextArea($rData["flditem"], $rData["fldreportquali"])
        If xx Then
          modPatientGeneral.UpdateExamGeneralQualiData($rData["fldid"], $rData["flditem"], xx, "")
        Endif
      Endif

    Else If $sType = "Acount" Then
      sType = modPatient.GetAccDemographicType($rData["flditem"])
      If sType = "Single Selection" Then
        opt = modPatient.GetAccDemographicOptionList($rData["flditem"], sType)
        strLst = SubChoose(opt, "Single Selection", $rData["flditem"])
        If strLst Then
          modPatientSub.UpdateAccGeneralQualiData($rData["fldid"], $rData["flditem"], strLst.Join(";"), "", Now(), True)
        Endif

      Else If sType = "Multiple Selection" Then
        opt = modPatient.GetAccDemographicOptionList($rData["flditem"], sType)
        strLst = SubChoose(opt, "Multiple Selection", $rData["flditem"])
        If strLst Then
          For Each xx In strLst
            modPatientSub.UpdateAccGeneralQualiData($rData["fldid"], $rData["flditem"], xx, "", Now(), True)
          Next
        Endif

      Else If sType = "Quantitative" Then
        yy = GetQuantiValues("Demographics", Trim(txtencid.Text), $rData["flditem"], 0, 0, CFloat($rData["fldreportquali"]))
        If yy Then
          modPatientSub.UpdateAccGeneralQuantiData($rData["fldid"], $rData["flditem"], yy[0], "", Now(), True)
        Endif

      Else
        xx = GetTextArea($rData["flditem"], $rData["fldreportquali"])
        If xx Then
          modPatientSub.UpdateAccGeneralQualiData($rData["fldid"], $rData["flditem"], xx, "", Now(), True)
        Endif
      Endif

    Endif

    ShowExamGrid()
  Endif

End

Public Sub mnudelete_Click()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If Message.Question(("Are you sure to delete this variable ?"), ("No"), ("Yes")) = 2 Then
      If $sType = "Clinical" Then
        If modGeneral.AllowClinicalEdit($rData["flduserid"]) = True Then
          modDatabase.$myConn.Delete("tblexamgeneral", "fldid=&1", $rData["fldid"])
        Else
          Message.Warning("Authorization with " & $rData["flduserid"], ("OK"))
        Endif
      Else If $sType = "Acount" Then
        modDatabase.$myConn.Delete("tblpataccgeneral", "fldid=&1", $rData["fldid"])
      Endif
      ShowExamGrid()
    Endif
  Endif

End

Public Sub btnreport_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, "DEMOGRAPHIC VALUES", modReportVar.GetDateTimeReport(Now(), gb.GeneralDate))

End

Public Sub btndemoreport_Click()

  Dim hCls As CReportCustom

  If txtencid.Text Then
    If modSettings.ShowSettingFromFIle("Diagnostic Help/Name") Then
      hCls = New CReportCustom(Trim(txtencid.Text), "Diagnostic Help", "ReportSize", MMain.$defUnit)
      hCls.Preview()
    Endif
  Endif

End

Public Sub btndefaiult_Click()

  Dim j As Integer
  Dim res As Result
  Dim sql As String

  For j = 0 To $ItemList.Count - 1
    If j < 49 Then

      If $sType = "Clinical" Then
        sql = "select flddefault from tbldemographic where flddemoid=&1 and (fldcomp=&2 or fldcomp=&3)"
      Else If $sType = "Acount" Then
        sql = "select flddefault from tblaccdemograp where flddemoid=&1 and (fldcomp=&2 or fldcomp=&3)"
      Endif
      res = modDatabase.$myConn.Exec(sql, $ItemList[j], modBasic.$compID, "%")
      If res.Available Then
        For Each res
          If aComboBox[j] Then
            If Not aComboBox[j].Text Then
              aComboBox[j].Text = res["flddefault"]
            Endif
          Else If aValueBox[j] Then
            If Not aValueBox[j].Value Then
              If res["flddefault"] Then
                aValueBox[j].Value = CFloat(res["flddefault"])
              Endif
            Endif
          Else If aLineText[j] Then
            If Not aLineText[j].Text Then
              aLineText[j].Text = res["flddefault"]
            Endif
          Else If aDichoValue[j] Then
            If Not aDichoValue[j].Value Then
              aDichoValue[j].Value = res["flddefault"]
            Endif
          Endif
        Next
      Endif
    Endif

  Next

End

Public Sub btnlastval_Click()

  Dim j As Integer
  Dim stVal As String
  Dim encid As String

  encid = modPatient.GetSecondLastEncidPatient(Trim(txtencid.Text))
  For j = 0 To $ItemList.Count - 1
    If j < 49 Then

      If $sType = "Clinical" Then
        stVal = modPatPatho.GetDemographicValue(encid, $ItemList[j])
      Else If $sType = "Acount" Then
      Endif

      If aComboBox[j] Then
        If Not aComboBox[j].Text Then
          aComboBox[j].Text = stVal
        Endif
      Else If aValueBox[j] Then
        If Not aValueBox[j].Value Then
          If stVal Then
            aValueBox[j].Value = CFloat(stVal)
          Endif
        Endif
      Else If aLineText[j] Then
        If Not aLineText[j].Text Then
          aLineText[j].Text = stVal
        Endif
      Else If aDichoValue[j] Then
        If Not aDichoValue[j].Value Then
          aDichoValue[j].Value = stVal
        Endif
      Endif

    Endif
  Next

End
