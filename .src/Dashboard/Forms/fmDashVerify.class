' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub _new()

  cmbcategory.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldchapter) as col from tblpopulation"))                           '''
  cmbhospdistrict.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(flddistrict) as col from tblmunicipals where fldprovince=&1", modPublicDash.$ProvName))
  SetLocations()

End

Private Sub SetLocations()

  If modDataRepo.$RepositoryMode = "Hospital" Then

  Else If modDataRepo.$RepositoryMode = "Municipality" Then
    cmbpality.Text = modBasic.$HospCode
    cmbpality.Enabled = False
    cmbhospdistrict.Text = modDataRepo.GetDistrictFromMunicipal(modBasic.$HospCode)
    cmbhospdistrict.Enabled = False
  Else If modDataRepo.$RepositoryMode = "District" Then
    cmbpality.List = modDataRepo.GetMunicipalsByDistrict(modBasic.$HospCode)
    cmbhospdistrict.Text = modBasic.$HospCode
    cmbhospdistrict.Enabled = False
  Else If modDataRepo.$RepositoryMode = "Province" Then
    cmbhospdistrict.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select flddistrict as col from tblmunicipals where fldprovince=&1", modBasic.$HospCode))
  Else
    cmbhospdistrict.List = modBasic.$DistrictList
  Endif

End

Public Sub cmbhospdistrict_Click()

  If cmbhospdistrict.Text Then
    cmbpality.List = modDataRepo.GetMunicipalsByDistrict(cmbhospdistrict.Text)
  Endif

End

Public Sub btnshow_Click()

  $rData = modDatabase.$myConn.Exec("select fldid,fldcategory,fldmale,fldfemale,fldother from tblpopulation where fldchapter=&1 and fldpality=&2 and flddistrict=&3 and fldtime>=&4 and fldtime<=&5", cmbcategory.Text, cmbpality.Text, cmbhospdistrict.Text, dtfir.Value, dtlast.Value)                           ''
  $aMyFields = New String[]
  modGridView.ReadSmallData(WebTable1, $rData, $aMyFields)
  With WebTable1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"

    .Columns[1].Text = "Category"
    .Columns[2].Text = "Male"
    .Columns[3].Text = "Female"
    .Columns[4].Text = "Others"
  End With

End

Public Sub WebTable1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]

End

Public Sub btnenable_Click()

  Dim Row As Integer

  If cmbcategory.Text Then
    modPopDash.DisablePopulationParam(cmbcategory.Text, cmbhospdistrict.Text, cmbpality.Text)
    For Row = 0 To WebTable1.Count - 1
      If WebTable1.IsSelected(Row) Then
        $rData.MoveTo(Row)
        modPopDash.GetPopulationVerify($rData["fldid"])
      Endif
    Next
    Me.Exec("Toastify({text: 'Information updated', duration: 3000}).showToast()")
  Endif

End
