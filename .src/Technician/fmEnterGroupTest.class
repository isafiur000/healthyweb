' Gambas class file

''controls
Private aWebRowAll As New Object[50]
Private aWebFrame As New Object[50]
Private aWebPanel As New Object[50]
Private aIndexLabel As New Object[50]
Private aSpecLabel As New Object[50]
Private aNameLabel As New Object[50]
Private aAbnormCheck As New Object[50]
Private aHTMLText As New Object[50]
Private aLineText As New Object[50]
Private aUnitText As New Object[50]
Private aValueBox As New Object[50]
Private aQuantiBox As New Object[50]
Private aQuantiMax As New Object[50]
Private aQuantiMin As New Object[50]
Private aQuantiUnit As New Object[50]
Private aDateBox As New Object[50]
Private aBSDate As New Object[50]
Private aComboBox As New Object[50]
Private aDichoValue As New Object[50]
Private aLeftRight As New Object[50]
Private aGenTextArea As New Object[50]
Private aWebSpace As New Object[50]

Private aWebBottom As New Object[50]
Private aValSlider As New Object[50]

''other variables
Private $encid As String
Private $sTable As String
Private AppFactor As Float

Private $xData As Variant[]
Private $enableControl As Boolean
Private $viewSlider As Boolean

''----------------------------------------- create controls -----------------------------------------
Public Sub _new(encid As String, sDepartment As String[], sTable As String)

  Dim ht As String

  $encid = encid
  cmbcategory.List = sDepartment
  $sTable = sTable

  AppFactor = modBasic.$AppScaleFactor
  If modBasic.$ClinSliderValue = "Enable" Then
    $viewSlider = True
  Else
    $viewSlider = False
  Endif
  ht = modSettings.ShowSettingFromFIle("MultipleTextBox/Height")
  If ht Then
    Slider1.Value = CInt(ht)
  Else
    Slider1.Value = 3
  Endif

  rbmetric.Value = True
  If cmbcategory.List.Count = 1 Then
    cmbcategory.Text = cmbcategory.List[0]
  Endif

End

Public Sub btnshowall_Click()

  ShowDepartWise()

End

Private Sub ShowDepartWise()

  If cmbcategory.Text Then

    Try $xData.Clear()
    Try Frame1.DeleteChildren()
    Wait
    GetAllListData()
    LoadFormData()

  Endif

End

Private Sub GetAllListData()

  Dim sql As String
  Dim xcomp As String
  Dim xstr As String
  Dim res As Result
  Dim xFldList As String[]

  If modBasic.$LabReportingComp Then
    xcomp = modBasic.$LabReportingComp
  Else
    xcomp = modBasic.$compID
  Endif
  If modBasic.$LabReportingFrom = "Not From" Then
    xstr = "fldcomp_sample not like "
  Else
    xstr = "fldcomp_sample like "
  Endif
  xFldList = ["fldid", "fldsampleid", "fldtestid", "fldsampletype", "fldtime_sample", "fldabnormal", "fldid", "fldtest_type", "fldreportquali", "fldreportquanti"]
  sql = "select " & xFldList.Join(",") & " from tblpatlabtest where fldencounterval=&1 and fldstatus=&2 and fldtestid in(select fldtestid from tbltest where fldcategory like &3) and " & xstr & "&4"
  res = modDatabase.$myConn.Exec(sql, $encid, "Sampled", cmbcategory.Text, xcomp)

  $xData = New Variant[]
  If res.Available Then
    For Each res
      If res["fldtest_type"] = "Quantitative" Then
        $xData.Add([res["fldid"], res["fldtestid"], res["fldsampleid"], res["fldtest_type"], "Quantitative"])
      Else
        $xData.Add([res["fldid"], res["fldtestid"], res["fldsampleid"], res["fldtest_type"], modFixLab.GetLabTestOptionType(res["fldtestid"])])
      Endif
    Next
  Endif

End

Private Sub LoadFormData()

  Dim i As Integer

  For i = 0 To $xData.Count - 1
    If i < 49 Then

      aWebRowAll[i] = New WebContainer(Frame1)

      aWebFrame[i] = New WebContainer(aWebRowAll[i])
      aIndexLabel[i] = New WebValueBox(aWebFrame[i])
      aSpecLabel[i] = New WebHtml(aWebFrame[i])
      aNameLabel[i] = New WebHtml(aWebFrame[i])

      aWebPanel[i] = New WebContainer(aWebRowAll[i])
      aAbnormCheck[i] = New WebCheckBox(aWebPanel[i])
      If $xData[i][3] = "Quantitative" Then
        aQuantiBox[i] = New WebValueBox(aWebPanel[i]) As "QuantiGroup"
        aQuantiMin[i] = New WebValueBox(aWebPanel[i])
        aQuantiMax[i] = New WebValueBox(aWebPanel[i])
        aQuantiUnit[i] = New WebLabel(aWebPanel[i])
        aWebBottom[i] = New WebContainer(Frame1)
        aValSlider[i] = New WebSlider(aWebBottom[i]) As "SliderGroup"
      Else
        Select $xData[i][4]
          Case "Single Selection"
            aComboBox[i] = New WebComboBox(aWebPanel[i])
          Case "Dichotomous"
            aDichoValue[i] = New DichotomousValue(aWebPanel[i])
          Case "Left and Right"
            aLeftRight[i] = New LeftRightTextArea(aWebPanel[i])
          Case "Date Time", "BS Date"
            aDateBox[i] = New WebDateBox(aWebPanel[i])
            aBSDate[i] = New WebButton(aWebPanel[i]) As "BSButton"
          Case "Quantitative"
            aValueBox[i] = New WebValueBox(aWebPanel[i])
            aUnitText[i] = New WebTextBox(aWebPanel[i])
          Case "Qualitative"
            aLineText[i] = New WebTextBox(aWebPanel[i])
          Case "RichText Area"
            aHTMLText[i] = New WebTextHTML(aWebPanel[i])
          Case "No Selection"
            aGenTextArea[i] = New WebTextArea(aWebPanel[i])
          Case Else
        End Select
      Endif
      aWebSpace[i] = New WebSeparator(Frame1)

    Endif
  Next
  $enableControl = False
  DIsplayTextForm()
  $enableControl = True

End

Private Sub DIsplayTextForm()

  Dim i As Integer
  Dim xlimit As Float[]

  Dim xcrit As Float
  Dim aCrlower As Float
  Dim aCrupper As Float

  For i = 0 To $xData.Count - 1
    If i < 49 Then

      With aWebRowAll[i]
        .Arrangement = Arrange.Row
      End With

      With aWebFrame[i]
        .Arrangement = Arrange.Horizontal
        .Class = "size-tablet size-tablet"
      End With

      ''create index label
      With aIndexLabel[i]
        .Width = "10%"
        .Height = "2em"
        .Value = $xData[i][0]
        .Visible = False
        .Tag = i
      End With
      ''create specimen
      With aSpecLabel[i]
        .Width = "30%"
        .Height = "2em"
        .Text = $xData[i][2]
        .Tag = i
        ' .Wrap = True
      End With
      ''create Name Label
      With aNameLabel[i]
        .Expand = True
        .Font = "bold"
        .Text = "<p>" & $xData[i][1] & "</p>"
        .Tag = i
      End With

      With aWebPanel[i]
        .Arrangement = Arrange.Horizontal
        .Class = "size-tablet less-container-margin"
      End With

      If $xData[i][3] = "Quantitative" Then
        xlimit = modLabTest.GetTestBothLimits($xData[i][0], modLabSub.GetTestUnitFromButton(rbsi, rbmetric))
        xcrit = modFixLab.GetCritValueTest($xData[i][1])
        If xcrit Then
          aCrlower = xlimit[0] - xcrit * (xlimit[1] - xlimit[0])
          aCrupper = xlimit[1] + xcrit * (xlimit[1] - xlimit[0])
          If aCrupper = 0 Then
            aCrupper = 500
          Endif
        Else
          aCrlower = 0
          aCrupper = 500
        Endif
        ''create abnormal checkbox
        With aAbnormCheck[i]
          .Width = "20%"
          .Height = "2em"
          .Text = "Abn"
          .Enabled = False
          .Tag = i
        End With

        ''create observation entry
        With aQuantiBox[i]
          .Width = "20%"
          .Height = "2em"
          .Tag = i
        End With
        'min value
        With aQuantiMin[i]
          .Width = "17.5%"
          .Height = "2em"
          .Enabled = False
          .Value = xlimit[0]
          .Tag = i
        End With
        ''max value
        With aQuantiMax[i]
          .Width = "17.5%"
          .Height = "2em"
          .Enabled = False
          .Value = xlimit[1]
          .Tag = i
        End With
        With aQuantiUnit[i]
          .Width = "20%"
          .Height = "2em"
          .Border = True
          .Text = modLabTest.GetTestUnitSrting($xData[i][0], modLabSub.GetTestUnitFromButton(rbsi, rbmetric))
          .Tag = i
        End With

        With aWebBottom[i]
          .Arrangement = Arrange.Horizontal
          .Class = "less-container-margin"
          .Visible = $viewSlider
        End With

        With aValSlider[i]
          .Expand = True
          .Height = "1em"
          .Tag = i
        End With
        aValSlider[i].MinValue = aCrlower
        aValSlider[i].MaxValue = aCrupper

      Else
        ''create abnormal checkbox
        With aAbnormCheck[i]
          .Width = "20%"
          .Height = "2em"
          .Text = "Abn"
          .Tag = i
        End With

        ''create observation entry
        If $xData[i][4] = "Single Selection" Then
          With aComboBox[i]
            .Width = "75%"
            .Height = "2em"
            .ReadOnly = False
            .List = modAllExam.SelectExamOptionList("Test", $xData[i][1], $xData[i][4])
            .Tag = i
          End With

        Else If $xData[i][4] = "Dichotomous" Then
          With aDichoValue[i]
            .Width = "75%"
            .Height = "2em"
            .List = modAllExam.SelectExamOptionList("Test", $xData[i][1], $xData[i][4])
            .Tag = i
          End With

        Else If $xData[i][4] = "Left and Right" Then
          With aLeftRight[i]
            .Width = "75%"
            .Height = CStr(2 * Slider1.Value) & "em"
            .Tag = i
          End With

        Else If $xData[i][4] = "Date Time" Then
          With aDateBox[i]
            .Width = "65%"
            .Height = "2em"
            .DateTime = True
            .Tag = i
          End With
          With aBSDate[i]
            .Width = "10%"
            .Height = "2em"
            .Image = "icon:/small/calendar"
            .Tag = i
          End With

        Else If $xData[i][4] = "BS Date" Then
          With aDateBox[i]
            .Width = "65%"
            .Height = "2em"
            .DateTime = True
            .Tag = i
          End With
          With aBSDate[i]
            .Width = "10%"
            .Height = "2em"
            .Image = "icon:/small/calendar"
            .Tag = i
          End With

        Else If $xData[i][4] = "Quantitative" Then
          With aValueBox[i]
            .Width = "40%"
            .Height = "2em"
            .Tag = i
          End With
          With aUnitText[i]
            .Width = "35%"
            .Height = "2em"
            .Tag = i
          End With

        Else If $xData[i][4] = "Qualitative" Then
          With aLineText[i]
            .Width = "75%"
            .Height = "2em"
            .Tag = i
          End With

        Else If $xData[i][4] = "RichText Area" Then
          With aHTMLText[i]
            .Width = "75%"
            .Height = CStr(2 * Slider1.Value) & "em"
            ' .DictionaryPath = modBasic.$dictPathList
            .Tag = i
          End With

        Else If $xData[i][4] = "No Selection" Then
          With aGenTextArea[i]
            .Width = "75%"
            .Height = CStr(2 * Slider1.Value) & "em"
            .Wrap = True
            .Tag = i
          End With

        Else
        Endif
      Endif

      With aWebSpace[i]
        .Height = "1em"
      End With

    Endif
  Next

End

Public Sub btnrefresh_Click()

  Try Frame1.DeleteChildren()
  LoadFormData()

End

Public Sub btnBrChange_Click()

  Me.Close()

End

Public Sub btnBrOK_Click()

  FillGenExams()
  Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")
  ' Balloon.Delay = modBasic.$BalloonDelay

End

Public Sub BSButton_Click()

  Dim j As Integer
  Dim xx As String

  j = Last.Tag
  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(aDateBox[j].Value))
  If xx Then
    aDateBox[j].Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

Public Sub SliderGroup_Change()

  Dim j As Integer

  j = Last.Tag
  If aQuantiBox[j] Then
    If $enableControl = True Then
      aQuantiBox[j].Value = aValSlider[j].Value
    Endif
  Endif

End

Public Sub QuantiGroup_Change()

  Dim j As Integer

  j = Last.Tag

  If aQuantiBox[j].Value Then
    If aQuantiMin[j].Value <> aQuantiMax[j].Value Then
      If aQuantiBox[j].Value < aQuantiMin[j].Value Or If aQuantiBox[j].Value > aQuantiMax[j].Value Then
        aAbnormCheck[j].Value = True
      Else
        aAbnormCheck[j].Value = False
      Endif
    Else
      aAbnormCheck[j].Value = False
    Endif
  Else
    aAbnormCheck[j].Value = False
  Endif

  If aAbnormCheck[j].Value = True Then
    aAbnormCheck[j].Foreground = Color.Red
  Else
    aAbnormCheck[j].Foreground = Color.Default
  Endif

End

Private Sub FillGenExams()

  Dim i As Integer

  For i = 0 To $xData.Count - 1
    If i < 49 Then
      If $xData[i][3] = "Quantitative" Then
        If aQuantiBox[i].Value Then
          modLabSub.UpdateLabTestReportQuanti(aIndexLabel[i].Value, aQuantiBox[i].Value, aAbnormCheck[i].Value, modLabSub.GetTestUnitFromButton(rbsi, rbmetric), $sTable)
        Endif
      Else
        If $xData[i][4] = "Single Selection" Then
          If aComboBox[i].Text Then
            modLabSub.UpdateLabTestReportQuali(aIndexLabel[i].Value, aComboBox[i].Text, aAbnormCheck[i].Value, "", $sTable)
          Endif
        Else If $xData[i][4] = "Dichotomous" Then
          If aDichoValue[i].Value Then
            modLabSub.UpdateLabTestReportQuali(aIndexLabel[i].Value, aDichoValue[i].Value, aAbnormCheck[i].Value, "", $sTable)
          Endif
        Else If $xData[i][4] = "Left and Right" Then
          If aLeftRight[i].DataText Then
            modLabSub.UpdateLabTestReportQuali(aIndexLabel[i].Value, aLeftRight[i].DataText, aAbnormCheck[i].Value, "", $sTable)
          Endif
        Else If $xData[i][4] = "Date Time" Then
          If aDateBox[i].Value Then
            modLabSub.UpdateLabTestReportQuali(aIndexLabel[i].Value, modDate.DateStringForExam(aDateBox[i].Value), aAbnormCheck[i].Value, "", $sTable)
          Endif
        Else If $xData[i][4] = "BS Date" Then
          If aDateBox[i].Value Then
            modLabSub.UpdateLabTestReportQuali(aIndexLabel[i].Value, modDate.ConvertToLocaldate(aDateBox[i].Value), aAbnormCheck[i].Value, "", $sTable)
          Endif
        Else If $xData[i][4] = "Quantitative" Then
          If aValueBox[i].Value Then
            modLabSub.UpdateLabTestReportQuali(aIndexLabel[i].Value, CStr(aValueBox[i].Value) & Space(1) & aUnitText[i].Text, aAbnormCheck[i].Value, "", $sTable)
          Endif
        Else If $xData[i][4] = "Qualitative" Then
          If aLineText[i].Text Then
            modLabSub.UpdateLabTestReportQuali(aIndexLabel[i].Value, aLineText[i].Text, aAbnormCheck[i].Value, "", $sTable)
          Endif
        Else If $xData[i][4] = "RichText Area" Then
          If aHTMLText[i].Text Then
            modLabSub.UpdateLabTestReportQuali(aIndexLabel[i].Value, aHTMLText[i].RichText, aAbnormCheck[i].Value, "", $sTable)
          Endif
        Else If $xData[i][4] = "No Selection" Then
          If aGenTextArea[i].Text Then
            modLabSub.UpdateLabTestReportQuali(aIndexLabel[i].Value, aGenTextArea[i].Text, aAbnormCheck[i].Value, "", $sTable)
          Endif
        Endif
      Endif
    Endif
  Next

End

Public Sub btnfill_Click()

  Dim i As Integer
  Dim res As Result

  For i = 0 To $xData.Count - 1
    If i < 49 Then
      res = modDatabase.$myConn.Exec("select flddetail from tbltest where fldtestid=&1", $xData[i][1])
      If res.Available Then

        If $xData[i][4] = "Single Selection" Then
          If Not aComboBox[i].Text Then
            aComboBox[i].Text = res["flddetail"]
          Endif
        Else If $xData[i][4] = "RichText Area" Then
          If Not aHTMLText[i].Text Then
            aHTMLText[i].RichText = res["flddetail"]
          Endif
        Else If $xData[i][4] = "No Selection" Then
          If Not aGenTextArea[i].Text Then
            aGenTextArea[i].Text = res["flddetail"]
          Endif
        Else If $xData[i][4] = "Dichotomous" Then
          If Not aDichoValue[i].Value Then
            aDichoValue[i].Value = res["flddetail"]
          Endif
        Endif

      Endif
    Endif
  Next

End
