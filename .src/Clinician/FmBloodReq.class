' Gambas class file

Private $encid As String
Private $PatientNum As String

Private $rData As Result
Private $aMyFields As String[]
Private $rData2 As Result
Private $aMyFields2 As String[]

Public Sub _new(encid As String)

  Dim xstatus As String
  Dim def As String
  Dim xList As String[]

  $encid = encid
  modGeneralMain.GetOpenModalForm(Me)

  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  $PatientNum = modPatient.GetPatientNoByEnc($encid)
  xstatus = modPatient.CurrentAdmissionStatus($encid)
  txtlocation.Text = modPatient.GetLocationSetting($encid, xstatus)

  cmbitem.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(flditem) as col from tblbloodstore"))
  cmbgroup.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldgroup) as col from tblbloodstore"))
  def = modSettings.ShowSettingFromFIle("BloodBank/GroupingCode")
  If def Then
    xList = GetBloodTypeList($PatientNum, modFixLab.GetLabTestIDFromSysConst(def))
    If xList And If xList.Count Then
      txtbloodgroup.Text = xList.Join(";")
    Endif
  Endif

  rbpresent.Value = True
  FillRequestGrid()
  CompleteGrid()

End

Public Sub TabPanel1_Click()

  If TabPanel1.Index = 0 Then
    FillRequestGrid()
  Else If TabPanel1.Index = 1 Then
    CompleteGrid()
  Endif

End

Private Function GetBloodTypeList(xPatNo As String, strTest As String) As String[]

  Dim res As Result
  Dim xList As String[]

  res = modDatabase.$myConn.Exec("select distinct(tblpatlabtest.fldreportquali) as col from tblpatlabtest inner join tblencounter on tblpatlabtest.fldencounterval=tblencounter.fldencounterval where tblencounter.fldpatientval=&1 and (tblpatlabtest.fldstatus=&2 or tblpatlabtest.fldstatus=&3) and tblpatlabtest.fldtestid=&4 and tblpatlabtest.fldreportquali IS NOT &5", xPatNo, "Reported", "Verified", strTest, Null)
  xList = modControlSub.GetDirectFillresult(res)

  Return xList

End

Public Sub btnshow_Click()

  Dim res As Result

  If cmbitem.Text And If cmbgroup.Text Then
    res = modDatabase.$myConn.Exec("select flditemcode from tblbloodstore where flditem=&1 and fldgroup=&2", cmbitem.Text, cmbgroup.Text)
    If res.Available Then
      lblcode.Text = res["flditemcode"]
      txtqty.ReadOnly = False
      btnsubOK.Enabled = True
    Else
      Message.Warning("Item not coded", "OK")
    Endif
  Endif

End

Public Sub btnsubOK_Click()

  Dim i As Integer

  If cmbitem.Text And If cmbgroup.Text And If txtqty.Value Then
    If lblcode.Text Then
      For i = 1 To txtqty.Value
        modPatientGeneral.PatMedicalItemRequest("Blood", $encid, lblcode.Text, TextArea1.Text)
      Next
      FillRequestGrid()
      btnsubOK.Enabled = False
      lblcode.Text = ""
      cmbitem.Text = ""
      cmbgroup.Text = ""
      txtqty.Value = 0
    Endif
  Endif

End

Public Sub FillRequestGrid()

  Dim sql As String

  sql = "select fldid,fldtime_order,fldcategory,flditem,fldpatinfo,fldid from tblpatmeditem where fldencounterval=&1 and fldorder=&2"                                             ''
  $rData = modDatabase.$myConn.Exec(sql, $encid, "Requested")
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Hidden = True
    .Columns[3].Width = CStr(300 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Hidden = True

    .Columns[1].Text = "DateTime"
    .Columns[3].Text = "Particulars"
    .Columns[4].Text = "Details"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub btnpharm_Click()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$myConn.Delete("tblpatmeditem", "fldid=&1", $rData["fldid"])
      FillRequestGrid()
    Endif
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End

'''===========completed requests =========================
Public Sub rbready_Click()

  CompleteGrid()

End

Public Sub rbpresent_Click()

  CompleteGrid()

End

Private Sub CompleteGrid()

  Dim sql As String

  If rbpresent.Value = True Then
    sql = "select fldid,fldcode,flditem,fldid,fldfirsttime,fldsecondtime,fldcomment,fldexpiry,fldstatus from tblmedinventory where fldreceiver=&1 and fldcategory=&2 and (fldstatus=&3 or fldstatus=&4)"            '
    $rData2 = modDatabase.$myConn.Exec(sql, $encid, "Blood", "Transferred", "Administered")
  Else If rbready.Value = True Then
    sql = "select fldid,fldcode,flditem,fldid,fldfirsttime,fldsecondtime,fldcomment,fldexpiry,fldstatus from tblmedinventory where fldreceiver=&1 and fldcategory=&2 and (fldstatus=&3 or fldstatus=&4)"            '
    $rData2 = modDatabase.$myConn.Exec(sql, $encid, "Blood", "Allocated", "Requested")
  Endif
  $aMyFields2 = New String[]
  modGridView.ReadSmallData(GridView2, $rData2, $aMyFields2)

  With GridView2
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(275 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(35 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Hidden = True
    .Columns[5].Hidden = True
    .Columns[6].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"

    .Columns[1].Text = "Bag No"
    .Columns[2].Text = "Particulars"
    .Columns[6].Text = "Comment"
    .Columns[7].Text = "Expiry"
    .Columns[8].Text = "Status"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData2.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 3 Then
    If rbpresent.Value = True Then
      Data.Html = modString.GetImageForHTMLGrid(modGeneralMain.GetSystemIcon(GetStateIcon($rData2[$aMyFields2[Column]])), CStr(20 * modBasic.$AppWidthRatio) & "px", "auto")
    Endif
    Data.Text = ""
  Else If Column = 7 Then
    Data.Text = modReportVar.GetDateTimeReport($rData2[$aMyFields2[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData2[$aMyFields2[Column]]
  Endif

End

Private Function GetStateIcon(sIdx As Long) As String

  Dim res As Result
  Dim xicon As String

  res = modDatabase.$myConn.Exec("select fldfirsttime,fldsecondtime from tblmedinventory where fldid=&1", sIdx)
  If res.Available Then
    If res["fldfirsttime"] And If res["fldsecondtime"] Then
      xicon = "icon:/32/lock"
    Else If Not res["fldfirsttime"] And If Not res["fldsecondtime"] Then
      xicon = "icon:/32/play"
    Else
      xicon = "icon:/32/stop"
    Endif
  Else
    xicon = ""
  Endif

  Return xicon

End

Public Sub GridView2_Select()

  If GridView2.Selection.Count Then
    $rData2.MoveTo(GridView2.Selection[0])
    txtidval.Value = $rData2["fldid"]
    btnplay.Image = GetStateIcon($rData2["fldid"])
  Endif

End

Public Sub btnplay_Click()

  Dim res As Result

  If txtidval.Value Then
    res = modDatabase.$myConn.Edit("tblmedinventory", "fldid=&1 and (fldstatus=&2 or fldstatus=&3)", txtidval.Value, "Transferred", "Administered")
    If res.Available Then
      If Not res["fldfirsttime"] And If Not res["fldsecondtime"] Then
        res["fldfirsttime"] = Now()
        res["fldstatus"] = "Administered"
        res["flduserid_start"] = modBasic.$lbluser
        res["fldcomp_start"] = modBasic.$compID
        res["xyz"] = False
        res.Update
        btnplay.Image = "icon:/32/stop"
        CompleteGrid()
      Else If Not res["fldsecondtime"] Then
        res["fldsecondtime"] = Now()
        res["xyz"] = False
        res.Update
        btnplay.Image = "icon:/32/lock"
        CompleteGrid()
      Endif
    Endif
  Endif

End

Public Sub btnrequest_Click()

  Dim res As Result

  If txtidval.Value Then
    res = modDatabase.$myConn.Edit("tblmedinventory", "fldid=&1 and fldstatus=&2 and flduserid_start IS NULL", txtidval.Value, "Allocated")
    If res.Available Then
      res["fldstatus"] = "Requested"
      res["xyz"] = False
      res.Update
      CompleteGrid()
    Endif
  Endif

End

Public Sub btnreturn_Click()

  Dim res As Result

  If txtidval.Value Then
    res = modDatabase.$myConn.Edit("tblmedinventory", "fldid=&1 and fldstatus=&2 and flduserid_start IS NULL", txtidval.Value, "Transferred")
    If res.Available Then
      res["fldstatus"] = "Returned"
      res["xyz"] = False
      res.Update
      CompleteGrid()
    Endif
  Endif

End

Public Sub btnsetgroup_Click()

  Dim xx As String

  xx = InputBox("Provide Laboratory Codes for Blood Grouping", "")
  If xx Then
    modSettings.SaveSettingsToFile("BloodBank/GroupingCode", xx)
  Endif

End
