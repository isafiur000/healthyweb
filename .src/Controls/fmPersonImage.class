' Gambas class file

Private $categ As String
Private $imgName As String
Private $Lock As Boolean

Public Sub _new(categ As String, imgName As String, sLock As Boolean)

  $categ = categ
  $imgName = imgName
  $Lock = sLock
  If $Lock = True Then
    btnedit.Visible = False
  Endif
  DisplayBlobData()

End

Private Sub DisplayBlobData()

  Dim tempFile As String

  tempFile = modImage.DisplayPersonImageData($categ, $imgName)
  If tempFile Then
    modImage.StretchtPictureToBox(PictureBox1, tempFile)
  Endif

End

' Public Sub Form_KeyRelease()
'
'   If Key.Code = Key.Esc Then
'     Me.Close
'   Endif
'
' End

Public Sub btnOpen_Click()

  btnOpen.Upload()

End

Public Sub btnOpen_Progress()

  WebProgressBar1.Value = btnOpen.Progress

End

Public Sub btnOpen_Finish()

  Dim xPath As String

  Me.Exec("Toastify({text: 'Upload Completed', duration: 3000}).showToast()")
  xPath = modPrint.GetCopyTempPath(btnOpen.Path)
  If Exist(xPath) Then
    modImage.StretchtPictureToBox(PictureBox1, xPath)
  Endif

End

Public Sub btnOpen_Abort(Reason As String)

  Message.Error("Unable to upload file.<br>Reason: " & Html(Reason))
  WebProgressBar1.Value = 0

End

Public Sub btnedit_Click()

  Dim res As Result
  Dim sPath As String

  If PictureBox1.Tag Then
    sPath = PictureBox1.Tag
    If Exist(sPath) Then
      res = modDatabase.$myConn.Edit("tblpersonimage", "fldcateg=&1 and fldname=&2", $categ, $imgName)
      If res.Available = False Then
        modImage.InsertPatientImage($categ, $imgName, sPath)
      Else
        res["fldpic"] = File.Load(sPath)
        res["fldlink"] = ""
        res["flduserid"] = modBasic.$lbluser
        res["fldcomp"] = modBasic.$compID
        res["flduptime"] = Now()
        res.Update()
      Endif
      Me.Exec("Toastify({text: 'Information updated', duration: 3000}).showToast()")
      ' Balloon.Delay = modBasic.$BalloonDelay
    Endif
  Endif

End

Public Sub btnclear_Click()

  If PictureBox1.Tag Then
    PictureBox1.Tag = ""
    ' PictureBox1.Image.Clear()
    ' PictureBox1.Refresh()
    Wait
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
