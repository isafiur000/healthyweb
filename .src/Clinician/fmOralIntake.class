' Gambas class file

Private $encid As String
Private $rData As Result
Private $aMyFields As String[]
Private $rData1 As Result
Private $aMyFields1 As String[]

Public Sub _new(encid As String)

  $encid = encid
  modGeneralMain.GetOpenModalForm(Me)

  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtgender.Text = modPatient.GetPatientSex($encid)
  cmbtype.List = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldfoodtype) as col from tblfoodtype"))
  cmbtype.Add("Mixture")
  cmbtype.Add("Group")
  cmbtype.Text = ""
  rbpackage.Value = True
  ShowAdministerdFood()
  FillRequestedPlan()

End

Private Sub AddFoodIntakeData(encid As String, strItem As String, strReport As String, strValue As Float)

  Dim res As Result
  Dim xIntVal As String

  res = modDatabase.$myConn.Create("tblexamgeneral")
  res["fldencounterval"] = encid
  res["fldinput"] = "Input Food/Fluid"
  res["fldtype"] = "Qualitative"
  res["flditem"] = strItem
  res["fldreportquali"] = strReport
  res["fldreportquanti"] = strValue
  res["flddetail"] = ""
  res["flduserid"] = modBasic.$lbluser
  res["fldtime"] = Now()
  res["fldcomp"] = modBasic.$compID
  res["fldsave"] = False
  res["flduptime"] = ""
  res["xyz"] = False
  If MMain.$WebEntry = True Then
    xIntVal = modString.GetDateString(Now())
    res["fldid"] = CLong(xIntVal)
    res["fldrepoid"] = modMisc.GetWebIndexStr(xIntVal)
    res["fldrepodate"] = Now()
    res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
    res["fldhospcode"] = modBasic.$HospCode
  Endif
  res.Update()
  'don't catch error

End

Public Sub btnselitem_Click()

  Dim xlist As String[]

  xlist = GetComboList()
  If xlist And If xlist.Count Then
    cmbitem.Text = GridViewNew("Select Parameter", xlist, False)
    GetItemUnit()
    If modBasic.$TabletModeEnable = "Yes" Then
    Else
      txtdose.SetFocus
    Endif
  Endif

End

Private Function GetComboList() As String[]

  Dim xx As String[]

  If cmbtype.Text = "Mixture" Then
    xx = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldgroup) as col from tblfoodgroup"))
  Else If cmbtype.Text = "Group" Then
    xx = modControlSub.GetDirectFillresult(modDatabase.$myConn.Exec("select distinct(fldgroup) as col from tbldietgroup"))
  Else
    xx = modMedicine.GetFoodSelectedType(cmbtype.Text)
  Endif

  Return xx

End

Private Sub GetItemUnit()

  If cmbitem.Text Then
    If cmbtype.Text = "Group" Then
      txtunit.Text = "Units"
    Else If cmbtype.Text = "Mixture" Then
      txtunit.Text = "Units"
    Else
      txtunit.Text = "Grams"
    Endif
  Endif

End

Public Sub btnBrOK_Click()

  If cmbitem.Text And If txtdose.Value And If cmbtype.Text Then
    AddFoodIntakeData($encid, cmbitem.Text, cmbtype.Text, txtdose.Value)
    txtdose.Value = 0
    txtdose.Tag = ""
    cmbtype.Text = ""
    Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
    ShowAdministerdFood()
  Endif

End

''======================== Requested Diet================================
Private Sub FillRequestedPlan()

  Dim sql As String
  Dim xdate As Date

  xdate = Now()
  If rbpackage.Value = True Then
    sql = "select flddosecode,flddosetime,flddosecode,flddosetime from tblextradosing where fldencounterval=&1 and fldstatus=&2 and flddosetime>=&3 and flddosetime<=&4 and fldsave=&5 and flddosecode like &6 GROUP BY flddosetime"                                   ''
    $rData = modDatabase.$myConn.Exec(sql, $encid, "Continue", modDate.StartSqlDate(xdate), modDate.EndSqlDate(xdate), True, "%")
  Else If rbnutrition.Value = True Then
    sql = "select fldid,flddosetime,flditem,flddosetime from tblextradosing where fldencounterval=&1 and fldstatus=&2 and flddosetime>=&3 and flddosetime<=&4 and fldsave=&5 and flddosecode IS &6"                                   ''
    $rData = modDatabase.$myConn.Exec(sql, $encid, "Continue", modDate.StartSqlDate(xdate), modDate.EndSqlDate(xdate), True, Null)
  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView2, $rData, $aMyFields)
  With GridView2
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Hidden = True

    .Columns[1].Text = "Time"
    .Columns[2].Text = "Particulars"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    GridView2.Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.MediumTime)
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub rbpackage_Click()

  FillRequestedPlan()

End

Public Sub rbnutrition_Click()

  FillRequestedPlan()

End

Public Sub btndiscont_Click()

  Dim res As Result

  If GridView2.Selection.Count Then
    $rData.MoveTo(GridView2.Selection[0])
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      If rbpackage.Value = True Then
        res = modDatabase.$myConn.Edit("tblextradosing", "fldencounterval=&1 and fldstatus=&2 and flddosetime=&3 and flddosecode=&4 and fldsave=&5", $encid, "Continue", $rData["flddosetime"], $rData["flddosecode"], True)
        For Each res
          res["fldstatus"] = "Discontinue"
          res["xyz"] = False
          res.Update
        Next
      Else If rbnutrition.Value = True Then
        res = modDatabase.$myConn.Edit("tblextradosing", "fldid=&1", $rData["fldid"])
        res["fldstatus"] = "Discontinue"
        res["xyz"] = False
        res.Update
      Endif
      FillRequestedPlan()
    Endif
  Endif

End

Public Sub btnadmin_Click()

  Dim res As Result

  If GridView2.Selection.Count Then
    $rData.MoveTo(GridView2.Selection[0])
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      If rbpackage.Value = True Then
        modDatabase.$myConn.Begin()
        res = modDatabase.$myConn.Edit("tblextradosing", "fldencounterval=&1 and fldstatus=&2 and flddosetime=&3 and flddosecode=&4 and fldsave=&5", $encid, "Continue", $rData["flddosetime"], $rData["flddosecode"], True)
        For Each res
          AddFoodIntakeData(res["fldencounterval"], res["flditem"], res["fldcategory"], res["flddose"])
          res["fldstatus"] = "Completed"
          res["xyz"] = False
          res.Update
        Next
        modDatabase.$myConn.Commit()
      Else If rbnutrition.Value = True Then
        modDatabase.$myConn.Begin()
        res = modDatabase.$myConn.Edit("tblextradosing", "fldid=&1", $rData["fldid"])
        AddFoodIntakeData(res["fldencounterval"], res["flditem"], res["fldcategory"], res["flddose"])
        res["fldstatus"] = "Completed"
        res["xyz"] = False
        res.Update
        modDatabase.$myConn.Commit()
      Endif
      FillRequestedPlan()
      ShowAdministerdFood()
    Endif
  Endif

Catch
  modDatabase.$myConn.Rollback()
  modHelpVariable.CreateErrorReport()

End

''======================= GridView ========================
Private Sub ShowAdministerdFood()

  Dim sql As String

  sql = "select fldid,fldtime,flditem,fldreportquanti from tblexamgeneral where fldencounterval=&1 and fldinput=&2 and fldtype=&3 and fldsave=&4"
  $rData1 = modDatabase.$myConn.Exec(sql, $encid, "Input Food/Fluid", "Qualitative", False)
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(GridView1, $rData1, $aMyFields1)
  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(225 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"

    .Columns[1].Text = "DateTime"
    .Columns[2].Text = "Particulars"
    .Columns[3].Text = "Dose"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData1.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Text = modReportVar.GetDateTimeReport($rData1[$aMyFields1[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData1[$aMyFields1[Column]]
  Endif

End

Public Sub btneditgrid_Click()

  Dim res As Result
  Dim xval As Float

  If GridView1.Selection.Count Then
    $rData1.MoveTo(GridView1.Selection[0])
    xval = InputValue(("Update Administered Quantity"), "Input Food/Fluid", $rData1["fldreportquanti"])
    If xval Then
      res = modDatabase.$myConn.Edit("tblexamgeneral", "fldid=&1", $rData1["fldid"])
      res["fldreportquanti"] = xval
      res.Update
      ShowAdministerdFood()
    Endif
  Endif

End

Public Sub btnsaveall_Click()

  Dim res As Result

  res = modDatabase.$myConn.Edit("tblexamgeneral", "fldencounterval=&1 and fldinput=&2 and fldtype=&3 and fldsave=&4", $encid, "Input Food/Fluid", "Qualitative", False)
  If res.Available Then
    For Each res
      res["fldsave"] = True
      res.Update
    Next
    Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")
    ' Balloon.Delay = modBasic.$BalloonDelay
    ShowAdministerdFood()
  Endif

End
