' Gambas class file

Private $DataList As Variant[]
Private $DataJson As Variant[]
Private $sType As String

Public Sub _new(sType As String)

  $sType = sType

  If MMain.$RepoReportMode = True Then
    cmbconsult.List = modDataRepo.GetRepoValueListType("Hospital")
    btnimport.Enabled = False
  Else
    cmbconsult.Text = modBasic.$HospCode
    cmbconsult.Enabled = False
  Endif
  dtsel.Value = Now()

End

Public Sub btnrefresh_Click()

  Dim xval As String

  modRepository.ReadRepoConfig()
  If $sType = "Referrals" Then
    xval = modRepository.GetRepositoryJson("tblencounter", "fldreferto", cmbconsult.Text)
  Else If $sType = "Bookings" Then
    xval = modRepository.GetRepositoryJsonMulti("tblonlinebook", ["fldhospital", "fldconsultdate"], [modBasic.$HospCode, Date()])
  Endif
  $DataJson = JSON.Decode(xval)

  GridView1.Clear()
  If $sType = "Referrals" Then
    If chkdate.Value = True Then
      $DataList = modString.SelectVarJSONToDate($DataJson, "fldfollowdate", dtsel.Value)
    Else
      $DataList = $DataJson
    Endif
    GridView1.Count = $DataList.Count
    GridView1.Columns.Count = 3
    With GridView1
      .Columns[0].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
      .Columns[1].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
      .Columns[2].Width = CStr(225 * modBasic.$AppWidthRatio) & "px"
      .Columns[0].Text = "Date"
      .Columns[1].Text = "PatientNo"
      .Columns[2].Text = "Name"
    End With

  Else If $sType = "Bookings" Then
    $DataList = $DataJson
    GridView1.Count = $DataList.Count
    GridView1.Columns.Count = 30
  Endif

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  modGridView.GridViewDecoration(Data, Row)
  If $sType = "Referrals" Then
    If Column = 0 Then
      Data.Text = $DataList[Row]["fldfollowdate"]
    Else If Column = 1 Then
      Data.Text = $DataList[Row]["fldpatientval"]
    Else If Column = 2 Then
      Data.Text = modPatient.PatientFullNameByPatID($DataList[Row]["fldpatientval"])
    Endif

  Else If $sType = "Bookings" Then
    Data.Text = $DataList[Row][Column]
  Endif

End

Public Sub btnimport_Click()

  If GridView1.Selection.Count Then
    modRepository.CopyPatInfoFromRepository($DataList[GridView1.Selection[0]]["fldpatientval"])
  Endif

End

Public Sub chkdate_Click()

  If chkdate.Value = True Then
    dtsel.Enabled = True
  Else If chkdate.Value = False Then
    dtsel.Enabled = False
  Endif

End

Public Sub WebForm_Close()

  MMain.InitialAppMode()

End
