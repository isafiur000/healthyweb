' Gambas class file

Private $encid As String
Private $sReceipt As String
Private $sTotal As Float
Private $xbillType As String

Private $TrsnsFee As Float
Private $xLocation As String
Private $xUser As String
Private $xmobileNo As String
Private $xComment As String
Private $xCompID As String

Private $QRScanURL As String
Private $QRScanUser As String
Private $QRScanPass As String
Private $QRScanQuote As String
Private $QRScanStr As String
Private $QRScanID As String

Public Sub _new(encid As String, sReceipt As String, sTotal As Float, sBillType As String)

  $encid = encid
  $sReceipt = sReceipt
  $sTotal = sTotal
  $xbillType = sBillType

  $QRScanUser = Trim(UnBase64(modSettings.ShowSettingFromFIle("QRScan/UserName")))
  $QRScanPass = Trim(UnBase64(modSettings.ShowSettingFromFIle("QRScan/Password")))
  $QRScanURL = Trim(modSettings.ShowSettingFromFIle("QRScan/URL"))
  $QRScanQuote = Trim(modSettings.ShowSettingFromFIle("QRScan/QuotePass"))
  ExecuteAPI()

End

Private Sub ExecuteAPI()

  Dim xcmd As String
  Dim xauth As String
  Dim xcontent As String
  Dim xurl As String
  Dim xcoll As Collection
  Dim xoutput As String
  Dim aColl As Collection

  xurl = Trim($QRScanURL)
  If $QRScanQuote = "Yes" Then
    xauth = "'" & $QRScanUser & ":" & $QRScanPass & "'"
  Else
    xauth = $QRScanUser & ":" & $QRScanPass
  Endif
  xcontent = "Content-Type: application/json"
  xcoll = GetQRScanJson()

  If $QRScanUser Then
    xcmd = "curl -u " & xauth & " -H " & Quote(xcontent) & " -d '" & JSON.Encode(xcoll) & "' -X POST " & xurl
  Else
    xcmd = "curl" & " -H " & Quote(xcontent) & " -d '" & JSON.Encode(xcoll) & "' -X POST " & xurl
  Endif
  modBasic.DebugAPIString("Execute " & Replace(xcmd, $QRScanPass, "***"))
  Shell xcmd To xoutput
  modBasic.DebugAPIString("Response: " & xoutput)

  If xoutput Then
    Try aColl = JSON.Decode(xoutput)
    If aColl Then
      $QRScanStr = aColl["qrString"]
      $QRScanID = aColl["validationTraceId"]
    Endif
  Endif

End

Public Function QRScanOutput() As String

  Return $QRScanStr

End

Public Function QRScanOutID() As String

  Return $QRScanID

End

Private Function GetQRScanJson() As Collection

  Dim aColl As Collection

  $TrsnsFee = 0
  $xLocation = modHelpVariable.$MACAddress
  $xUser = modBasic.$lbluser
  $xmobileNo = modPatient.GetPatientContactByEnc($encid)
  $xComment = ""
  $xCompID = modBasic.$compID

  aColl = New Collection
  aColl.Add(Format($sTotal, gb.Fixed), "transactionAmount")
  aColl.Add(Format($TrsnsFee, gb.Fixed), "valueOfConvenienceFeeFixed")
  aColl.Add($sReceipt, "billNumber")
  aColl.Add($encid, "referenceLabel")
  aColl.Add($xmobileNo, "mobileNo")
  aColl.Add($xUser, "storeLabel")
  aColl.Add($xLocation, "terminalLabel")
  aColl.Add($xbillType, "purposeOfTransaction")
  aColl.Add($xComment, "additionalConsumerDataRequest")
  aColl.Add("", "loyaltyNumber")
  aColl.Add($xCompID, "addnField2")

  Return aColl

End
