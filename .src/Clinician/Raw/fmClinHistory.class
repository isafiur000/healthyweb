' Gambas class file

Private $patId As String
Private $sType As String
Private $HistoryList As String[]
Private $DischargeList As String[]

Private $xFinList As String[]
Private $PatNum As String
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(patId As String, sType As String, sHistory As String[], sDischarge As String[], HideList As Boolean, PanelClass As String)

  $patId = patId
  $sType = sType
  $HistoryList = sHistory
  $DischargeList = sDischarge
  WebContainer1.Class = PanelClass
  If HideList = True Then
    WebContainer2.Visible = False
  Endif

  If $sType = "PatientID" Then
    WebContainer3.Visible = True
    WebContainer7.Visible = False
    $PatNum = $patId
    ShowEncounterList()
  Else If $sType = "Encounter" Then
    WebContainer3.Visible = False
    txtnewencid.Text = $patId
    $PatNum = modPatient.GetPatientNoByEnc($patId)
  Endif
  $xFinList = GetSelectedItemList()
  If Not $xFinList.Count Then
    If MMain.$SISHAppMode = "Portal" Then
      $xFinList = ["Provisional Diagnosis", "Final Diagnosis", "Laboratory Tests", "Radiological Findings", "Treatment Advised", "Initial Planning", "Unavailable Medicines", "Orders for New Visit"]         ''
    Else
      $xFinList = modMedicine.GetCustReportVariables(chkselect.Value, $HistoryList, $DischargeList)
    Endif
  Endif
  rbmetric.Value = True

  If modBasic.$WebZoomValue Then
    Slider1.Value = modBasic.$WebZoomValue * 100
    WebView1.Font = CStr(Slider1.Value / 100) & "em"
  Endif

  If HideList = True Then
    If $sType = "Encounter" Then
      ShowReportEncid($xFinList)
    Endif
  Else
    AddListBiewContent($xFinList)
  Endif

End

Public Sub Slider1_Change()

  WebView1.Font = CStr(Slider1.Value / 100) & "em"

End

Private Sub ShowEncounterList()

  $rData = modDatabase.$myConn.Exec("select fldregdate,fldencounterval from tblencounter where fldpatientval=&1 ORDER BY fldregdate DESC", $patId)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[0].Text = "Date"
    .Columns[1].Text = "Encounter"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 0 Then
    Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.MediumDate)
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub btnselect_Click()

  Dim i As Integer

  $xFinList = SelectMedBody("PatientHistory")
  If $xFinList And If $xFinList.Count Then
    For i = 0 To $xFinList.Count - 1
      modSettings.SaveSettingsToFile("PatientHistory/content_" & CStr(i + 1), $xFinList[i])                   ''
    Next
    AddListBiewContent($xFinList)
  Endif

End

Public Sub chkselect_Click()

  $xFinList = modMedicine.GetCustReportVariables(chkselect.Value, $HistoryList, $DischargeList)
  AddListBiewContent($xFinList)

End

Private Sub AddListBiewContent(xList As String[])

  Dim xitem As String
  Dim i As Integer

  lstcontent.Clear()
  i = 1
  For Each xitem In xList
    lstcontent.Add(xitem, i)
    i = i + 1
  Next

End

Public Sub chkall_Click()

  If chkall.Value = True Then
    lstcontent.SelectAll()
  Else If chkall.Value = False Then
    lstcontent.UnselectAll()
  Endif

End

Public Sub GridView1_Select()

  $rData.MoveTo(GridView1.Selection[0])
  txtnewencid.Text = $rData["fldencounterval"]
  ShowReportEncid(GetReportItems())

End

Public Sub btnsave_Click()

  ShowReportEncid(GetReportItems())

End

Private Function GetReportItems() As String[]

  Dim xx As String[]
  Dim i As Integer

  xx = New String[]
  If lstcontent.Selection.Count Then
    For i = 0 To lstcontent.List.Count - 1
      If lstcontent.IsSelected(i) Then
        xx.Add(lstcontent.List[i])
      Endif
    Next
  Else
    xx = lstcontent.List
  Endif

  Return xx

End

Private Sub ShowReportEncid(xList As String[])

  Dim xstring As String
  Dim xdate As Date

  If txtnewencid.Text Then
    xdate = modPatient.GetRecordDate(txtnewencid.Text)
    lbldate.Text = "DATE: " & Format(xdate, gb.ShortDate) & Space(1) & "(" & modDate.ConvertToLocaldate(xdate) & ")"
    If xList.Count Then
      xstring = GetSelectHistoryResultStr(xList, txtnewencid.Text)
      WebView1.Html = xstring
    Endif
  Endif

End

Private Sub GetSelectHistoryResultStr(xList As String[], encid As String) As String

  Dim xvarList As Variant[]
  Dim examlist As String[]
  Dim lablist As String[]
  Dim radiolist As String[]
  Dim sUnit As String
  Dim xstring As String

  If encid Then
    If modPatientSub.GetPatPassCheck(modDatabase.$myConn, $PatNum) = True Then
      If xList Then
        If xList.Exist("Selected Investigations") Then
          xvarList = ListViewMulti(encid)
          If xvarList Then
            examlist = xvarList[0]
            lablist = xvarList[1]
            radiolist = xvarList[2]
          Endif
        Endif
      Endif
      sUnit = modLabSub.GetTestUnitFromButton(rbsi, rbmetric)
      xstring = modPatReports.ShowCompleteReport(encid, xList, examlist, lablist, radiolist, sUnit, True)

    Else
      xstring = "You do not have permission to view"
    Endif
  Endif

  Return xstring

End

Public Sub btnsaverepohist_Click()

  mnuprint_Click()

End

Public Sub mnuprint_Click()

  If WebView1.Html Then
    Me.Exec("printJS({printable: 'printable@section', type: 'html', scanStyles: false})")
  Endif

End

Private Function GetSelectedItemList() As String[]

  Dim xx As String[]
  Dim i As Integer
  Dim xitem As String

  xx = New String[]
  For i = 0 To 99
    xitem = modSettings.ShowSettingFromFIle("PatientHistory/content_" & CStr(i + 1))
    If xitem Then
      xx.Add(xitem)
      xitem = ""
    Endif
  Next

  Return xx

End

Public Sub btnup_Click()

  Dim xList As String[]
  Dim aa As Integer
  Dim asx As String
  Dim bb As Integer
  Dim bsx As String

  Dim newList As String[]

  xList = $xFinList
  bb = lstcontent.Selection[0]
  If bb Then
    bsx = xList[bb]
    If bb = 0 Then Return

    aa = bb - 1
    asx = xList[aa]

    newList = New String[]
    newList.Insert(xList.Copy(0, aa))
    newList.Add(bsx)
    newList.Add(asx)
    newList.Insert(xList.Copy(bb + 1, xList.Count - (bb + 1)))

    $xFinList = newList
    AddListBiewContent($xFinList)
    lstcontent.Index = aa
  Endif

End

Public Sub btndown_Click()

  Dim xList As String[]
  Dim aa As Integer
  Dim asx As String
  Dim bb As Integer
  Dim bsx As String

  Dim newList As String[]

  xList = $xFinList
  aa = lstcontent.Selection[0]
  If aa Then
    asx = xList[aa]
    If aa = xList.Count - 1 Then Return

    bb = aa + 1
    bsx = xList[bb]

    newList = New String[]
    newList.Insert(xList.Copy(0, aa))
    newList.Add(bsx)
    newList.Add(asx)
    newList.Insert(xList.Copy(bb + 1, xList.Count - (bb + 1)))

    $xFinList = newList
    AddListBiewContent($xFinList)
    lstcontent.Index = bb
  Endif

End

Public Sub btncopy_Click()

  Dim hForm As FmPatCliNew
  Dim hForm1 As FmPatientMain
  Dim encid As String
  Dim xform As String

  encid = txtnewencid.Text
  xform = Me.Parent.Parent.Parent.Tag
  If encid Then
    If modHelpVariable.$LogInCategory = "Clinician" Then
      Me.Parent.Parent.Parent.Parent.DeleteChildren()
      If xform = "Clinician Form" Then
        hForm = New FmPatCliNew(encid, 0, "", fmOfficer.Workspace1)
      Else If xform = "InPatient Form" Then
        hForm1 = New FmPatientMain(encid, fmOfficer.Workspace1)
      Endif
    Endif
  Endif

End
