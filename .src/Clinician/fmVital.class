' Gambas class file

Private $encid As String
Private $rData As Result
Private $aMyFields As String[]
Private xFinal As Variant[]
Private $ExamList As String[]

Public Sub _new(encid As String)

  $encid = encid
  modGeneralMain.GetOpenModalForm(Me)

  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtcolor.Background = modPatient.GetPatientColorStatic($encid)
  $ExamList = modClinic.GetFullVitalList($encid)
  ListBox1.List = $ExamList

End

Public Sub TabPanel1_Click()

  If TabPanel1.Index = 1 Then
    FillDailyGrid()
  Endif

End

Public Sub btncolor_Click()

  Dim xcol As String

  xcol = InputColor("Triage", CStr(txtcolor.Background))
  If xcol Then
    txtcolor.Background = CInt(xcol)
    modPatient.SetPatientColor($encid, xcol)
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub ListBox1_Select()

  If ListBox1.Text Then
    ShowVitalExamEntry()
  Endif

End

Private Sub ShowVitalExamEntry()

  Dim sType As String
  Dim xlimit As Float[]

  txtexamval.Value = 0
  txtexamval.Tag = ""
  txtquali.Clear
  txtquali.Text = ""

  sType = modFixClinic.GetExaminationType(ListBox1.Text)
  If sType = "Quantitative" Then
    txtquali.Enabled = False
    btnselquali.Enabled = False
    txtexamval.Enabled = True

    xlimit = modClinic.GetBothQuantiExamVal(ListBox1.Text, $encid)
    txtmin.Value = xlimit[0]
    txtmax.Value = xlimit[1]
    lblunit.Text = modClinic.GetExamUnit(ListBox1.Text, $encid)

  Else
    txtquali.Enabled = True
    btnselquali.Enabled = True
    txtexamval.Enabled = False
    lblunit.Text = ""

    txtquali.Tag = modFixClinic.GetExamtOptionType(ListBox1.Text)
    If txtquali.Tag = "Single Selection" Or If txtquali.Tag = "Dichotomous" Then
      txtquali.List = modAllExam.SelectExamOptionList("Exam", ListBox1.Text)
    Endif

  Endif
  ShowExamGrid(ListBox1.Text)

  If modBasic.$TabletModeEnable = "Yes" Then
  Else
    If sType = "Quantitative" Then
      txtexamval.SetFocus
    Else If sType = "Qualitative" Then
      txtquali.SetFocus
    Endif
  Endif

End

Public Sub btnselquali_Click()

  ShowDetails()

End

Private Sub ShowDetails()

  Dim xType As String
  Dim xscle As String[]

  If Not txtquali.Text Then
    If ListBox1.Text Then

      xType = txtquali.Tag
      If xType = "Clinical Scale" Then
        xscle = modExamOption.GetClinScalePopUp("Exam", ListBox1.Text)
        If xscle Then
          txtexamval.Value = CFloat(xscle[0])
          txtquali.Text = xscle[1]
          txtexamval.Enabled = True
          txtquali.Enabled = False
        Endif
      Else If xType = "Visual Input" Then
        txtquali.Text = modExamOption.GetVisualDataExamVal(ListBox1.Text)
      Endif

    Endif
  Endif

End

Public Sub txtquali_Activate()

  If Not txtquali.Text Then
    ShowDetails()
  Else
    btnsave_Click()
  Endif

End

Public Sub txtexamval_Activate()

  btnsave_Click()

End

Public Sub btnsave_Click()

  Dim sType As String
  Dim xsys As String
  Dim OptionType As String
  Dim hForm As CGetExamParam

  If ListBox1.Text Then
    hForm = New CGetExamParam(ListBox1.Text, "Exam")
    sType = hForm.GetExamDataType()
    xsys = hForm.GetExamConstant()
    OptionType = hForm.GetExamOption()

    If sType = "Quantitative" Then
      If txtmin.Value = 0 And If txtmax.Value = 0 Then
        chkabnormal.Value = False
      Else
        If txtexamval.Value < txtmin.Value Or If txtexamval.Value > txtmax.Value Then
          chkabnormal.Value = True
        Else
          chkabnormal.Value = False
        Endif
      Endif
      modClinSub.AddQuantiData($encid, "", ListBox1.Text, OptionType, txtexamval.Value, chkabnormal.Value, "Essential Examinations", xsys)
      ' FillVitalExamChart(PictureBox1, $encid, lblexamname.Text)

    Else If sType = "Qualitative" Then
      If txtquali Then
        modClinSub.AddClinicExam($encid, "", ListBox1.Text, OptionType, txtquali.Text, txtexamval.Value, chkabnormal.Value, "Essential Examinations", xsys, "Regular")
      Endif
    Endif
    ShowExamGrid(ListBox1.Text)
    FillDailyGrid()
    txtquali.Text = ""
    txtquali.Tag = ""
    If txtquali.List.Count Then
      txtquali.Clear()
    Endif
    txtexamval.Value = 0
    txtexamval.Tag = ""
    lblunit.Text = ""

  Endif

End

Private Sub ShowExamGrid(sExam As String)

  $rData = modDatabase.$myConn.Exec("select fldid,fldid,fldtype,fldhead,fldabnormal,fldid,fldtime,fldsave from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldhead=&3 ORDER by fldid DESC", $encid, True, sExam)                                                                                 ''
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(25 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Hidden = True
    .Columns[3].Width = CStr(175 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(25 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Hidden = True

    .Columns[3].Text = "Examination"
    .Columns[5].Text = "Observation"
    .Columns[6].Text = "DateTime"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Background = modPathoSub.GetPatientColorByTriage($rData[$aMyFields[Column]])
    Data.Text = ""
  Else If Column = 4 Then
    Data.Html = modString.GetImageForHTMLGrid(modMisc.GetGridIcon($rData[$aMyFields[Column]]), "75%", "75%")
    Data.Text = ""
  Else If Column = 5 Then
    Data.Html = modClinic.GetExamValueString($encid, $rData[$aMyFields[Column]], False)
  Else If Column = 6 Then
    Data.Text = modPatient.GetDayTimeFromAdmission($encid, $rData[$aMyFields[Column]])
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub btnflag_Click()

  Dim xx As String

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    xx = InputCombo("Select Flag for observation", "Change Flag", ["Normal", "Abnormal"], modMisc.GetIconValue($rData["fldabnormal"]), True)
    If xx Then
      modClinSub.UpdateExamFlag($rData["fldid"], xx)
      ShowExamGrid(ListBox1.Text)
    Endif
  Endif

End

Public Sub btnedit_Click()

  Dim xquantival As Variant[]
  Dim yqualival As Variant[]
  Dim sType As String
  Dim xlimit As Float[]
  Dim xType As String

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    sType = modFixClinic.GetExaminationType($rData["fldhead"])
    If sType = "Quantitative" Then
      xlimit = modClinic.GetBothQuantiExamVal($rData["fldhead"], $encid)
      xquantival = GetQuantiValues("Exam", $encid, $rData["fldhead"], xlimit[0], xlimit[1], modClinic.GetExamValueByID($rData["fldid"]))                                       '
      If xquantival Then
        modClinSub.UpdateQuantiData($rData["fldid"], xquantival[0], xquantival[1])
        ' FillVitalExamChart(PictureBox1, $encid, lblexamname.Text)
      Endif
    Else If sType = "Qualitative" Then
      xType = modFixClinic.GetExamtOptionType($rData["fldhead"])
      If xType = "Clinical Scale" Then
        yqualival = modExamOption.GetClinScalePopUp("Exam", $rData["fldhead"])
        If yqualival Then
          modClinSub.UpdateQualiQuantiData($rData["fldid"], yqualival[1], CFloat(yqualival[0]), False)
        Endif
      Else If xType = "RichText Area" Then
        yqualival = GetQualiRich($rData["fldhead"], modClinic.GetExamValueString($encid, $rData["fldid"], False), "Exam")
        If yqualival Then                                  '
          modClinSub.UpdateQualiData($rData["fldid"], yqualival[0], yqualival[1], yqualival[2])
        Endif
      Else
        yqualival = GetQualiValues($rData["fldhead"], modClinic.GetExamValueString($encid, $rData["fldid"], False), "Exam")
        If yqualival Then                                  '
          modClinSub.UpdateQualiData($rData["fldid"], yqualival[0], yqualival[1], yqualival[2])
        Endif
      Endif
    Endif
    ShowExamGrid(ListBox1.Text)
  Endif

End

Public Sub mnudel_Click()

  Dim res As Result

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      res = modDatabase.$myConn.Edit("tblpatientexam", "fldid=&1", $rData["fldid"])
      If modGeneral.AllowClinicalEdit(res["flduserid"]) = True Then
        res["fldsave"] = False
        res["flduserid"] = modBasic.$lbluser
        res.Update
        ShowExamGrid(ListBox1.Text)
      Else
        Message.Warning("Authorization with " & res["flduserid"], ("OK"))
      Endif
    Endif
  Endif

End

''==================== table =======================
Private Sub FillDailyGrid()

  Dim xRowVal As Collection
  Dim xfir As Date
  Dim xlast As Date

  Dim i As Integer
  Dim j As Integer

  xFinal = New Variant[]

  xfir = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, -18))
  xlast = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, 1))

  GridView2.Clear()
  GridView2.Count = $ExamList.Count
  GridView2.Columns.Count = DateDiff(xfir, xlast, gb.Hour) + 1

  For i = 0 To GridView2.Count - 1
    xRowVal = New Collection
    For j = 0 To GridView2.Columns.Count - 1
      If j = 0 Then
        xRowVal.Add($ExamList[i], CStr(j))
      Else
        xRowVal.Add(GetExamValueByDate($encid, $ExamList[i], DateAdd(xfir, gb.Hour, j)), CStr(j))
      Endif
    Next
    xFinal.Add(xRowVal)
  Next

  With GridView2
    For j = 0 To GridView2.Columns.Count - 1
      If j = 0 Then
        .Columns[j].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
        .Columns[j].Text = "Examination"
      Else
        .Columns[j].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
        .Columns[j].Text = modReportVar.GetDateTimeReport(DateAdd(xfir, gb.Hour, j), gb.ShortTime)
      Endif
    Next
  End With
  ' GridView2.Scroll(GridView2.X + GridView2.Width, GridView2.Y)

End

Public Sub GridView2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  modGridView.GridViewDecoration(Data, Row)
  Data.Text = xFinal[Row][CStr(Column)]

End

Private Function GetExamValueByDate(encid As String, xexam As String, xDate As Date) As String

  Dim res As Result
  Dim xx As String

  res = modDatabase.$myConn.Exec("select fldrepquanti from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldhead=&3 and fldtime>=&4 and fldtime<=&5", encid, True, xexam, modDate.StartSqlHour(xDate), modDate.EndSqlHour(xDate))
  If res.Available Then
    res.MoveFirst
    If res["fldrepquanti"] Then
      xx = CStr(res["fldrepquanti"])
    Else
      xx = ""
    Endif
  Else
    xx = ""
  Endif
  Return xx

End
