' Gambas class file

Private $encid As String
Private $sBillMode As String
Private res1 As Result

Private $HiCappingVar As Collection

Public Sub _new(encid As String, sBillMode As String, res As Result)

  $encid = encid
  $sBillMode = sBillMode
  res1 = res

End

Public Sub SetCappingLimit(sColl As Collection)

  $HiCappingVar = sColl

End

Public Sub DispenseItem(sStatus As String)

  Dim res2 As Result
  Dim sql2 As String

  Dim tax As Float
  Dim disc As Float
  Dim xrate As Float
  Dim xtot As Float
  Dim xitem As String

  Dim qtynew As Float
  Dim xcateg As String
  Dim xdose As Float
  Dim xunit As String
  Dim xerr As String

  Dim xrefer As String
  Dim xpayble As String
  Dim xallow As Boolean
  Dim xCshCrd As Float
  Dim xfixcode As String

  xrefer = modBillings.GetReferralUserSetting("Pharmacy", $encid)
  xpayble = modBillings.GetPayableUserSetting("Pharmacy", $encid)

  xerr = ""
  If res1.Available = True Then
    'get stockno, rate and quantity based on expiry check
    If modBasic.$SkipExpiry = "Expired" Then
      sql2 = "select fldstockno,fldqty from tblentry where fldstockid=&1 and fldcomp=&2 and fldstatus=&3 and fldexpiry>&4 and fldqty>&5"
      res2 = modDatabase.$myConn.Exec(sql2, res1["flditem"], modBasic.$compID, 1, modDate.StartSqlDate(Now()), 0)
    Else
      sql2 = "select fldstockno,fldqty from tblentry where fldstockid=&1 and fldcomp=&2 and fldstatus=&3 and fldqty>&4"
      res2 = modDatabase.$myConn.Exec(sql2, res1["flditem"], modBasic.$compID, 1, 0)
    Endif
    If res2.Available Then
      res2.MoveFirst

      'get tax and discount percentages
      xcateg = res1["flditemtype"]
      If res1["fldtaxper"] Then
        tax = res1["fldtaxper"]
      Else
        tax = 0
      Endif
      If res1["flddiscper"] Then
        disc = res1["flddiscper"]
      Else
        disc = 0
      Endif
      If res1["fldcashincredit"] Then
        xCshCrd = res1["fldcashincredit"]
      Else
        xCshCrd = 0
      Endif

      If res1["fldfixrate"] Then
        xrate = res1["fldfixrate"]
      Else
        xrate = modStock.GetCurrentSellingPrice(res1["flditem"], modBasic.$compID)
      Endif

      ' xallow = modNonMedical.AllowPharmProceedPreBill($encid, cmbdisctype.Text, res1["fldqtydisp"], xrate, res1["flddiscper"], res1["fldbilltype"], xCshCrd, res1["fldroute"], res1["flditem"])
      xallow = False
      If modBasic.$AutoBillPharmacy = "Standard" Then
        xtot = modNonMedical.GetCashBillAmount(xrate, res1["fldqtydisp"], res1["fldbilltype"], disc, tax, xCshCrd)
        If modNonMedical.AllowPreCheckDeposit($encid, "Pharmacy", res1["flditem"], xtot, res1["fldbilltype"]) = True Then
          xallow = True
        Endif
      Else If modBasic.$AutoBillPharmacy = "Full" Then
        xtot = modNonMedical.GetCashBillAmount(xrate, res1["fldqtydisp"], res1["fldbilltype"], disc, tax, xCshCrd)
        If modNonMedical.AllowPreEntryWithDeposit($encid, "Pharmacy", res1["flditem"], xtot, res1["fldbilltype"]) = True Then
          xallow = True
        Endif
      Else
        If modBasic.$AutoBillSaveZero = "Yes" And If xrate = 0 Then
          xallow = True
        Else If modBasic.$AutoBillSaveZero = "Yes" And If res1["flddiscper"] = 100 Then
          xallow = True
        Else If modBasic.$AutoBillSaveFullCredit = "Yes" And If res1["fldbilltype"] = "Credit" And If xCshCrd = 0 Then
          xallow = True
        Endif
      Endif
      If xallow = True Then
        xfixcode = modNonMedical.GetBillItemHIAbbv(res1["fldfixname"], res1["flditemtype"])
        If GetHICapGo(xfixcode, res1["fldfixname"], res1["fldqtydisp"], res1["flddisctype"]) = True Then
          If xfixcode And If $HiCappingVar.Exist(xfixcode) Then
            $HiCappingVar[xfixcode] = $HiCappingVar[xfixcode] - res1["fldqtydisp"]
          Endif

          qtynew = res1["fldqtydisp"]
          modDatabase.$myConn.Begin
          While qtynew > 0
            xitem = res1["flditem"]
            If res1["fldfixname"] Then
              xitem = res1["fldfixname"]
              If res1["fldfixrate"] Then
                xrate = res1["fldfixrate"]
              Else
                xrate = 0
              Endif
            Else
              xrate = modStock.GetSellingPriceByStockNo(res2["fldstockno"], $sBillMode)
            Endif

            If qtynew <= res2["fldqty"] Then
              If modBasic.$AutoBillUseOwn = "Enable" Then
                modBillings.InsertBlankBillItemNewApp($encid, res1["fldbilltype"], res1["fldbillingmode"], res1["flddisctype"], res1["fldacledger"], res1["flditemtype"], res2["fldstockno"], xitem, xrate, qtynew, tax, disc, xCshCrd, "Done", res1["fldid"], True, False, "", xpayble, xrefer)
              Endif
              modStockSub.AddToExistEntry(res2["fldstockno"], (0 - qtynew), modBasic.$compID)
              qtynew = 0
            Else If qtynew > res2["fldqty"] Then
              If modBasic.$AutoBillUseOwn = "Enable" Then
                modBillings.InsertBlankBillItemNewApp($encid, res1["fldbilltype"], res1["fldbillingmode"], res1["flddisctype"], res1["fldacledger"], res1["flditemtype"], res2["fldstockno"], xitem, xrate, res2["fldqty"], tax, disc, xCshCrd, "Done", res1["fldid"], True, False, "", xpayble, xrefer)
              Endif
              modStockSub.AddToExistEntry(res2["fldstockno"], (0 - res2["fldqty"]), modBasic.$compID)
              qtynew = qtynew - res2["fldqty"]
            Endif
            Wait
          Wend

          If res1["fldqtydisp"] > qtynew Then
            If qtynew > 0 Then
              modPharmSub.DuplicateRecordWithQTY(res1["fldid"], qtynew)
              modPharmSub.UpdateQTYBefDispensing(res1["fldid"], res1["fldqtydisp"] - qtynew)
            Endif

            If sStatus = "Completed" Then
              modPharmSub.UpdateDispensing(res1["fldid"], True, True)
              If modNonMedical.GetBillItemCategoryFromCombo(res1["fldroute"]) = "Medicines" Then
                xdose = Round(res1["flddose"] / modMedConstant.GetDrugInitialStrength(res1["flditem"]), -2)
                xunit = modPharmLabel.GetDosageFormForLabel(res1["flditem"], "Inpatient")
                modPharmSub.InsertNurDosing(res1["fldid"], $encid, xdose, xunit, "")
              Endif
            Else
              modPharmSub.UpdateDispensing(res1["fldid"], True, False)
            Endif
          Endif
          modDatabase.$myConn.Commit

        Else
          xerr = xerr & "Cap Limit exceeded for " & res1["fldfixname"] & gb.NewLine
        Endif
      Else
        xerr = xerr & res1["fldfixname"] & gb.NewLine
      Endif

    Endif
  Endif
  If xerr Then
    Message.Warning("<b>Over Set Discount/Credit Limit:</b>" & gb.NewLine & xerr, ("OK"))
  Endif

Catch
  modDatabase.$myConn.Rollback
  modHelpVariable.CreateErrorReport()

End

Private Function GetHICapGo(sFixCode As String, sFixItem As String, sQty As Float, sDiscType As String) As Boolean

  Dim xgo As Boolean
  Dim xmax As Float

  xgo = True
  If sFixCode Then
    If $HiCappingVar.Exist(sFixCode) Then

      If Not $HiCappingVar[sFixCode] Then
        xgo = False
      Else
        xmax = $HiCappingVar[sFixCode] - modNonMedical.GetPatientItemSavedSalesPackage($encid, sFixItem, sDiscType)
        If xmax < sQty Then
          xgo = False
        Endif
      Endif

    Endif
  Endif

  Return xgo

End
