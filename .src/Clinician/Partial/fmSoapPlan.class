' Gambas class file

Private $encid As String
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(encid As String, sClass As String)

  $encid = encid
  WebContainer1.Class = sClass

  dtnote.Value = Now()
  ShowProblemList()

End

Private Sub ShowProblemList()

  Dim sql As String

  If chksoap.Value Then
    sql = "select fldid,fldtime,fldproblem,fldsubjective,fldobjective,fldassess,fldplan,flduserid,fldtime from tblpatplanning where fldencounterval=&1 and fldplancategory=&2 ORDER BY fldtime DESC"
  Else
    sql = "select fldid,fldtime,fldproblem,fldsubjective,fldobjective,fldassess,fldplan,flduserid,fldtime from tblpatplanning where fldencounterval=&1 and fldplancategory=&2 and fldtime>=&3 and fldtime<=&4 ORDER BY fldtime DESC"
  Endif
  $rData = modDatabase.$myConn.Exec(sql, $encid, "Clinician Plan", modDate.StartSqlDate(dtnote.Value), modDate.EndSqlDate(dtnote.Value))
  $aMyFields = New String[]
  modGridView.ReadSmallData(grdplan, $rData, $aMyFields)

  With grdplan
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Expand = True
    .Columns[3].Hidden = True
    .Columns[4].Hidden = True
    .Columns[5].Hidden = True
    .Columns[6].Hidden = True
    .Columns[7].Hidden = True
    .Columns[8].Hidden = True
  End With

End

Public Sub grdplan_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub grdplan_Select()

  If grdplan.Selection.Count Then
    $rData.MoveTo(grdplan.Selection[0])
    txtplanid.Value = $rData["fldid"]
    txtproblem.Text = $rData["fldproblem"]
    txtsubject.RichText = $rData["fldsubjective"]
    txtobject.RichText = $rData["fldobjective"]
    txtmanage.RichText = $rData["fldassess"]
    txtplan.RichText = $rData["fldplan"]
    lblnoteuser.Text = modGeneral.GetUserFullName($rData["flduserid"]) & " [" & modReportVar.GetDateTimeReport($rData["fldtime"], gb.GeneralDate) & "]"
  Endif

End

Public Sub btnsaveall_Click()

  Dim res As Result
  Dim xIntVal As String

  If $encid Then
    If txtproblem.Text Then
      res = modDatabase.$myConn.Create("tblpatplanning")
      res["fldencounterval"] = $encid
      res["fldplancategory"] = "Clinician Plan"
      res["fldproblem"] = txtproblem.Text
      res["fldsubjective"] = txtsubject.RichText
      res["fldobjective"] = txtobject.RichText
      res["fldassess"] = txtmanage.RichText
      res["fldplan"] = txtplan.RichText
      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
      res["fldcomp"] = modBasic.$compID
      res["fldsave"] = True
      res["flduptime"] = ""
      res["xyz"] = False
      If MMain.$WebEntry = True Then
        xIntVal = modString.GetDateString(Now())
        res["fldid"] = CLong(xIntVal)
        res["fldrepoid"] = modMisc.GetWebIndexStr(xIntVal)
        res["fldrepodate"] = Now()
        res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
        res["fldhospcode"] = modBasic.$HospCode
      Endif
      res.Update()
      ShowProblemList()
      Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information saved", modBasic.$BalloonDelay))
    Endif
  Endif

End

Public Sub btnupdate_Click()

  Dim res As Result

  If txtplanid.Value Then
    If txtproblem.Text Then
      res = modDatabase.$myConn.Edit("tblpatplanning", "fldid=&1", txtplanid.Value)
      If modGeneral.AllowClinicalEdit(res["flduserid"]) = True Then
        res["fldproblem"] = txtproblem.Text
        res["fldsubjective"] = txtsubject.RichText
        res["fldobjective"] = txtobject.RichText
        res["fldassess"] = txtmanage.RichText
        res["fldplan"] = txtplan.RichText
        res["flduserid"] = modBasic.$lbluser
        res["flduptime"] = Now()
        res["fldcomp"] = modBasic.$compID
        res["xyz"] = False
        res.Update()
        ShowProblemList()
        Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information saved", modBasic.$BalloonDelay))
      Else
        Message.Warning("Authorization with " & res["flduserid"], ("OK"))
      Endif
    Endif
  Endif
  'don't catch error

End

''----------------- pen
Public Sub btnpansubj_Click()

  txtsubject.RichText = txtsubject.RichText & Space(1) & modFillContainer.GetExtraTextArea("Subjective Parameters", txtsubject.Text)

End

Public Sub btnpanobject_Click()

  txtobject.RichText = txtobject.RichText & Space(1) & modFillContainer.GetExtraTextArea("Objective Parameters", txtobject.Text)

End

Public Sub btnpanassess_Click()

  txtmanage.RichText = txtmanage.RichText & Space(1) & modFillContainer.GetExtraTextArea("Clinical Assessment", txtmanage.Text)

End

Public Sub btnpanplan_Click()

  txtplan.RichText = txtplan.RichText & Space(1) & modFillContainer.GetExtraTextArea("Clinical Planning", txtplan.Text)

End

''------------- attach
Public Sub btnattsubj_Click()

  txtsubject.RichText = txtsubject.RichText & DictionaryVIew("Subjective Parameters")

End

Public Sub btnattobject_Click()

  txtobject.RichText = txtobject.RichText & DictionaryVIew("Objective Parameters")

End

Public Sub btnattassess_Click()

  txtmanage.RichText = txtmanage.RichText & DictionaryVIew("Clinical Assessment")

End

Public Sub btnattplan_Click()

  txtplan.RichText = txtplan.RichText & DictionaryVIew("Clinical Planning")

End

''---------------exec
Public Sub btnexecsubj_Click()

  txtsubject.RichText = txtsubject.RichText & modString.TextToHTML(modCloudAI.GetPatCloudAIResponse($encid, txtsubject.Text))

End

Public Sub btnexecobject_Click()

  txtobject.RichText = txtobject.RichText & modString.TextToHTML(modCloudAI.GetPatCloudAIResponse($encid, txtobject.Text))

End

Public Sub btnexecassess_Click()

  txtmanage.RichText = txtmanage.RichText & modString.TextToHTML(modCloudAI.GetPatCloudAIResponse($encid, txtmanage.Text))

End

Public Sub btnexecplan_Click()

  txtplan.RichText = txtplan.RichText & modString.TextToHTML(modCloudAI.GetPatCloudAIResponse($encid, txtplan.Text))

End

Public Sub btnclearsunj_Click()

  txtsubject.RichText = ""

End

Public Sub btnclearobj_Click()

  txtobject.RichText = ""

End

Public Sub btnclearasses_Click()

  txtmanage.RichText = ""

End

Public Sub btnclearplan_Click()

  txtplan.RichText = ""

End
