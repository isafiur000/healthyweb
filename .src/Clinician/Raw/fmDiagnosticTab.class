' Gambas class file

Private $encid As String
Private $rData As Result
Private $aMyFields As String[]

Private xFinal As Variant[]
Private $ExamList As String[]

Private $GenHeaders As String[] = ["Index", "Parameter", "Observation", "Comment", "Time"]
Private $StructHeaders As String[] = ["Index", "Parameter", "Observation", "DateTime"]

Public Sub _new(encid As String, PanelClass As String)

  $encid = encid
  WebContainer1.Class = PanelClass

  rbmetric.Value = True
  rblabs.Value = True
  rbdesc.Value = True
  rbdate.Value = True
  rbtext.Value = True

  If modBasic.$WebZoomValue Then
    Slider1.Value = modBasic.$WebZoomValue * 100
    WebView1.Font = CStr(Slider1.Value / 100) & "em"
  Endif

End

Public Sub Slider1_Change()

  WebView1.Font = CStr(Slider1.Value / 100) & "em"

End

Private Function LabUnitForm() As String

  Dim xx As String

  If rbsi.Value = True Then
    xx = "SI"
  Else
    xx = "Metric"
  Endif
  Return xx

End

Public Sub rbdate_Click()

  btnshowexams_Click()

End

Public Sub rbname_Click()

  btnshowexams_Click()

End

''======================= List ============================
Private Sub ShowGridViewList()

  Dim xorder As String

  If rbdesc.Value = True Then
    If rblabs.Value = True Then
      xorder = " ORDER BY fldtime_sample DESC"
    Else If rbradio.Value = True Then
      xorder = " ORDER BY fldtime_report DESC"
    Else
      xorder = " ORDER BY fldtime DESC"
    Endif
  Else
    If rblabs.Value = True Then
      xorder = " ORDER BY fldtime_sample ASC"
    Else If rbradio.Value = True Then
      xorder = " ORDER BY fldtime_report ASC"
    Else
      xorder = " ORDER BY fldtime ASC"
    Endif
  Endif

  If rbname.Value = True Then
    If rblabs.Value = True Then
      $rData = modDatabase.$myConn.Exec("select fldtestid,fldtestid as col from tblpatlabtest where fldencounterval=&1 and (fldstatus=&2 or fldstatus=&3) and flvisible=&4 GROUP BY fldtestid ORDER BY fldtestid", $encid, "Reported", "Verified", "Visible")
    Else If rbradio.Value = True Then
      $rData = modDatabase.$myConn.Exec("select fldtestid,fldtestid as col from tblpatradiotest where fldencounterval=&1 and (fldstatus=&2 or fldstatus=&3) and flvisible=&4 GROUP BY fldtestid ORDER BY fldtestid", $encid, "Reported", "Verified", "Visible")
    Else If rbexam.Value = True Then
      $rData = modDatabase.$myConn.Exec("select fldhead,fldhead as col from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldserialval IS NULL GROUP BY fldhead ORDER BY fldhead", $encid, True)
    Else If rbstruct.Value = True Then
      $rData = modDatabase.$myConn.Exec("select fldinput,fldinput as col from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldserialval like &3 GROUP BY fldinput ORDER BY fldinput", $encid, True, "%")
    Endif

  Else If rbdate.Value = True Then
    If rblabs.Value = True Then
      $rData = modDatabase.$myConn.Exec("select DATE(fldtime_sample),DATE(fldtime_sample) as col from tblpatlabtest where fldencounterval=&1 and (fldstatus=&2 or fldstatus=&3) and flvisible=&4 GROUP BY DATE(fldtime_sample)" & xorder, $encid, "Reported", "Verified", "Visible")
    Else If rbradio.Value = True Then
      $rData = modDatabase.$myConn.Exec("select DATE(fldtime_report),DATE(fldtime_report) as col from tblpatradiotest where fldencounterval=&1 and (fldstatus=&2 or fldstatus=&3) and flvisible=&4 GROUP BY DATE(fldtime_report)" & xorder, $encid, "Reported", "Verified", "Visible")
    Else If rbexam.Value = True Then
      $rData = modDatabase.$myConn.Exec("select DATE(fldtime),DATE(fldtime) as col from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldserialval IS NULL GROUP BY DATE(fldtime)" & xorder, $encid, True)
    Else If rbstruct.Value = True Then
      $rData = modDatabase.$myConn.Exec("select DATE(fldtime),DATE(fldtime) as col from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldserialval like &3 GROUP BY DATE(fldtime)" & xorder, $encid, True, "%")
    Endif

  Endif

  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Expand = True
    .Columns[1].Hidden = True
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 0 Then
    If rbname.Value = True Then
      Data.Text = $rData[$aMyFields[Column]]
    Else If rbdate.Value = True Then
      Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.MediumDate)
    Endif
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Select()

  Dim xformat As String
  Dim xorder As Integer
  Dim xVar As Variant[]

  If rbdate.Value = True Then
    xformat = "Date"
  Else If rbname.Value = True Then
    xformat = "Name"
  Endif

  If rbasc.Value = True Then
    xorder = gb.Ascent
  Else If rbdesc.Value = True Then
    xorder = gb.Descent
  Endif

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If rbgrid.Value = True Then
      If rblabs.Value = True Then
        xVar = modDiagnosticReport.GetLaboratoryPanelCollection($encid, xformat, xorder, LabUnitForm(), $rData["col"])
      Else If rbradio.Value = True Then
        xVar = modDiagnosticReport.GetRadiologyPanelCollection($encid, xformat, xorder, $rData["col"])
      Else If rbexam.Value = True Then
        xVar = modDiagnosticReport.GetExaminationPanelCollection($encid, "Physician Examinations", xformat, xorder, $rData["col"])
      Else If rbstruct.Value = True Then
        xVar = modDiagnosticReport.GetStructuredPanelCollection($encid, xformat, xorder, $rData["col"])
      Endif
      GetGridFormat(xVar)

    Else
      If rblabs.Value = True Then
        WebView1.Html = modDiagnosticReport.GetLaboratoryPanelReport($encid, xformat, xorder, LabUnitForm(), $rData["col"])
      Else If rbradio.Value = True Then
        WebView1.Html = modDiagnosticReport.GetRadiologyPanelReport($encid, xformat, xorder, $rData["col"])
      Else If rbexam.Value = True Then
        WebView1.Html = modDiagnosticReport.GetExaminationPanelReport($encid, "Physician Examinations", xformat, xorder, $rData["col"])
      Else If rbstruct.Value = True Then
        WebView1.Html = modDiagnosticReport.GetStructuredPanelReport($encid, xformat, xorder, $rData["col"])
      Endif
    Endif
  Endif

End

''=============================== EXAMS ====================
Public Sub btnshowexams_Click()

  Dim xformat As String
  Dim xorder As Integer
  Dim xVar As Variant[]

  If rbdate.Value = True Then
    xformat = "Date"
  Else If rbname.Value = True Then
    xformat = "Name"
  Endif

  If rbasc.Value = True Then
    xorder = gb.Ascent
  Else If rbdesc.Value = True Then
    xorder = gb.Descent
  Endif

  If $encid Then
    ShowGridViewList()
    If rbvitals.Value = True Then
      WebContainer4.Visible = False
      GridView2.Visible = True
      $ExamList = modString.GetListFromCompareVariant(modTableCache.$ExaminationCompVar, "fldexamid", "fldcategory", "Essential Examinations", True)
      FillDailyGrid()

    Else
      If rbgrid.Value = True Then
        WebContainer4.Visible = False
        GridView2.Visible = True
        If rblabs.Value = True Then
          xVar = modDiagnosticReport.GetLaboratoryPanelCollection($encid, xformat, xorder, LabUnitForm())
        Else If rbradio.Value = True Then
          xVar = modDiagnosticReport.GetRadiologyPanelCollection($encid, xformat, xorder)
        Else If rbexam.Value = True Then
          xVar = modDiagnosticReport.GetExaminationPanelCollection($encid, "Physician Examinations", xformat, xorder)
        Else If rbstruct.Value = True Then
          xVar = modDiagnosticReport.GetStructuredPanelCollection($encid, xformat, xorder)
        Endif
        GetGridFormat(xVar)

      Else
        WebContainer4.Visible = True
        GridView2.Visible = False
        If rblabs.Value = True Then
          WebView1.Html = modDiagnosticReport.GetLaboratoryPanelReport($encid, xformat, xorder, LabUnitForm())
        Else If rbradio.Value = True Then
          WebView1.Html = modDiagnosticReport.GetRadiologyPanelReport($encid, xformat, xorder)
        Else If rbexam.Value = True Then
          WebView1.Html = modDiagnosticReport.GetExaminationPanelReport($encid, "Physician Examinations", xformat, xorder)
        Else If rbstruct.Value = True Then
          WebView1.Html = modDiagnosticReport.GetStructuredPanelReport($encid, xformat, xorder)
        Endif
      Endif

    Endif
  Endif

End

Private Sub GetGridFormat(xVar As Variant[])

  Dim xHeaders As String[]
  Dim j As Integer

  If rblabs.Value = True Then
    xHeaders = $GenHeaders
  Else If rbradio.Value = True Then
    xHeaders = $GenHeaders
  Else If rbexam.Value = True Then
    xHeaders = $GenHeaders
  Else If rbstruct.Value = True Then
    xHeaders = $StructHeaders
  Endif

  GridView2.Clear()
  GridView2.Count = xVar.Count
  GridView2.Columns.Count = xHeaders.Count
  xFinal = xVar
  With GridView2
    For j = 0 To xHeaders.Count - 1
      If j = 0 Then
        .Columns[j].Hidden = True
      Else If j = 1 Then
        .Columns[j].Width = CStr(250 * modBasic.$AppWidthRatio) & "px"
        .Columns[j].Text = xHeaders[j]
      Else If j = 2 Then
        .Columns[j].Width = CStr(400 * modBasic.$AppWidthRatio) & "px"
        .Columns[j].Text = xHeaders[j]
      Else If j = 3 Then
        .Columns[j].Width = CStr(250 * modBasic.$AppWidthRatio) & "px"
        .Columns[j].Text = xHeaders[j]
      Else
        .Columns[j].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
        .Columns[j].Text = xHeaders[j]
      Endif
    Next

  End With

End

''----------------------------- Vitals ------------------------------
Private Sub FillDailyGrid()

  Dim xRowVal As Collection
  Dim xfir As Date
  Dim xlast As Date

  Dim i As Integer
  Dim j As Integer

  xFinal = New Variant[]

  xfir = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, -18))
  xlast = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, 1))

  GridView2.Clear()
  GridView2.Count = $ExamList.Count
  GridView2.Columns.Count = DateDiff(xfir, xlast, gb.Hour) + 1

  For i = 0 To $ExamList.Count - 1
    xRowVal = New Collection
    For j = 0 To GridView2.Columns.Count - 1
      If j = 0 Then
        xRowVal.Add($ExamList[i], CStr(j))
      Else
        xRowVal.Add(GetExamValueByDate($encid, $ExamList[i], DateAdd(xfir, gb.Hour, j)), CStr(j))
      Endif
    Next
    xFinal.Add(xRowVal)
  Next

  With GridView2
    For j = 0 To GridView2.Columns.Count - 1
      If j = 0 Then
        .Columns[j].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
        .Columns[j].Text = "Examination"
      Else
        .Columns[j].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
        .Columns[j].Text = modReportVar.GetDateTimeReport(DateAdd(xfir, gb.Hour, j), gb.ShortTime)
      Endif
    Next
  End With
  ' GridView2.Scroll(GridView2.X + GridView2.Width, GridView2.Y)

End

Public Sub GridView2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  modGridView.GridViewDecoration(Data, Row)
  If Column = 2 Then
    If rbvitals.Value = True Then
      Data.Text = xFinal[Row][CStr(Column)]
    Else
      Data.Html = xFinal[Row][CStr(Column)]
    Endif
  Else
    Data.Text = xFinal[Row][CStr(Column)]
  Endif

End

Private Function GetExamValueByDate(encid As String, xexam As String, xDate As Date) As String

  Dim res As Result
  Dim xx As String

  res = modDatabase.$myConn.Exec("select fldrepquanti from tblpatientexam where fldencounterval=&1 and fldsave=&2 and fldhead=&3 and fldtime>=&4 and fldtime<=&5 ORDER BY fldtime", encid, True, xexam, modDate.StartSqlHour(xDate), modDate.EndSqlHour(xDate))
  If res.Available Then
    res.MoveLast
    If res["fldrepquanti"] Then
      xx = CStr(res["fldrepquanti"])
    Else
      xx = ""
    Endif
  Else
    xx = ""
  Endif
  Return xx

End
