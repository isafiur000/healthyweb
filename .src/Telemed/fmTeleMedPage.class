' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Private $rData1 As Result
Private $aMyFields1 As String[]

Private $encid As String
Private $PatientNum As String
Private $doa As Date

' Private $sColl As Collection
Private $aColl As Collection

Public Sub _new()

  cmburgency.List = ["Urgent", "Normal"]
  cmbtype.List = ["Diagnostic Tests", "Radio Diagnostics", "General Reports", "Scanned Images", "Past Documents"]
  cmbtype.Text = "Scanned Images"
  cmbcomment.List = ["Completed", "Incomplete", "Transferred"]
  rbclinician.Value = True
  LoadGridView()

End

''================== Main forms ===================
Public Sub btnrefresh_Click()

  LoadGridView()

End

Private Sub LoadGridView()

  Dim sql As String
  Dim xurgnt As String
  Dim xhosp As String

  If cmburgency.Text Then
    xurgnt = " and fldurgency like &4"
  Else
    xurgnt = ""
  Endif
  If cmbhospital.Text Then
    xhosp = " and fldhospital like &5"
  Else
    xhosp = ""
  Endif
  sql = "select fldtalkid,fldencounterval,fldencounterval,fldencounterval,fldpatientval,fldhospital,fldurgency,fldrequest,fldordtime,fldorduserid,fldresponse,flduserid,fldtime,fldcoment,fldvidroom,fldvidpass,fldvidserver from tbltelemedtalk where fldteleuser=&1 and fldordtime>=&2 and fldordtime<=&3" & xurgnt & xhosp    ''
  $rData = modDatabase.$myConn.Exec(sql, modBasic.$lbluser, modDate.StartSqlDate(Now()), modDate.EndSqlDate(Now()), cmburgency.Text, cmbhospital.Text)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Hidden = True
    .Columns[5].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Hidden = True
    .Columns[8].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[9].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[10].Hidden = True
    .Columns[11].Hidden = True
    .Columns[12].Hidden = True
    .Columns[13].Hidden = True
    .Columns[14].Hidden = True
    .Columns[15].Hidden = True
    .Columns[16].Hidden = True

    .Columns[1].Text = "EncID"
    .Columns[2].Text = "Name"
    .Columns[3].Text = "Age/Sex"
    .Columns[5].Text = "Hospital"
    .Columns[6].Text = "Urgency"
    .Columns[8].Text = "DateTime"
    .Columns[9].Text = "User"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]
  If Column = 2 Then
    Data.Text = modPatient.GetPatientNameByEnc($rData[$aMyFields[Column]], modDatabase.$syConn)
  Else If Column = 3 Then
    Data.Text = modPatient.GetPatientAgeString($rData[$aMyFields[Column]], Now()) & "/" & modPatient.GetPatientSex($rData[$aMyFields[Column]], modDatabase.$syConn)
  Else If Column = 8 Then
    Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub GridView1_Select()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])

    $encid = $rData["fldencounterval"]
    $PatientNum = $rData["fldpatientval"]
    txtchaturl.Text = $rData["fldvidserver"] &/ "room" &/ $rData["fldvidroom"]
    txtpass.Text = $rData["fldvidpass"]

    $doa = modPatient.GetRecordDate($encid)
    ShowChiefSymptomData()
    modPatientGeneral.SetPatientFindingTree($encid, lsticdisease, chkalldiagno.Value)
  Endif

End

Public Sub WebTabPanel1_Click()

  Dim hPtForm As FmClinHistory
  Dim hDigForm As FmDiagnosticTab
  Dim hForm As FmPacsFind

  If WebTabPanel1.Index = 0 Then
    txtchaturl.Text = $rData["fldvidserver"] &/ "room" &/ $rData["fldvidroom"]
    txtpass.Text = $rData["fldvidpass"]
    ShowChiefSymptomData()
    modPatientGeneral.SetPatientFindingTree($encid, lsticdisease, chkalldiagno.Value)

  Else If WebTabPanel1.Index = 1 Then

  Else If WebTabPanel1.Index = 2 Then
    If wbPacsmainpanel.Children.Count Then
      wbPacsmainpanel.DeleteChildren()
    Endif
    hForm = New FmPacsFind($encid, "History", True, wbPacsmainpanel)

  Else If WebTabPanel1.Index = 3 Then
    ''nothing on load

  Else If WebTabPanel1.Index = 4 Then
    If pnldiagnosis.Children.Count Then
      pnldiagnosis.DeleteChildren()
    Endif
    hDigForm = New FmDiagnosticTab($encid, "height-tablet-full-less", pnldiagnosis)

  Else If WebTabPanel1.Index = 5 Then
    If pnlhistory.Children.Count Then
      pnlhistory.DeleteChildren()
    Endif
    hPtForm = New FmClinHistory($PatientNum, "PatientID", "height-tablet-full-less", pnlhistory)

  Else If WebTabPanel1.Index = 6 Then
    If GridView1.Selection.Count Then
      $rData.MoveTo(GridView1.Selection[0])
      txtteleindex.Text = $rData["fldtalkid"]
      txtquery.RichText = $rData["fldrequest"]
      txtresponse.RichText = $rData["fldresponse"]
      lbluser.Text = $rData["flduserid"]
      lbldate.Text = modReportVar.GetDateTimeReport($rData["fldtime"], gb.GeneralDate)
      cmbcomment.Text = $rData["fldcoment"]
    Endif
  Endif

End

Private Sub ShowChiefSymptomData()

  Dim res As Result
  Dim HistLst As Collection
  Dim xvar As String
  Dim xstr As String

  txtcomplaints.Html = ""
  txthistory.Html = ""
  txtcomplaints.Html = "<p>" & modString.TextToHTML(modPatReports.GetPatientPresentingSymptomReport($encid).Join(gb.NewLine) & gb.NewLine & modPatPatho.ShowSelectedHistoryValue($encid, "General Complaints")) & "</p>"

  HistLst = New Collection
  res = modDatabase.$myConn.Exec("select flditem,flddetail from tblexamgeneral where fldencounterval=&1 and fldinput=&2 ORDER BY fldtime", $encid, "History")
  If res.Available Then
    For Each res
      If res["flditem"] = "Final Planning" Then
      Else If res["flditem"] = "General Complaints" Then
      Else
        If res["flddetail"] Then
          HistLst.Add(res["flddetail"], res["flditem"])
        Endif
      Endif
    Next
  Endif

  xstr = ""
  For Each xvar In HistLst
    xstr = xstr & "<p><b>" & HistLst.Key & "</b><br>" & modString.TextToHTML(xvar) & "</p><br>"
  Next
  txthistory.Html = xstr

End

Public Sub mnuopenlink_Click()

  Me.Exec("window.open('" & txtchaturl.Text & "'); ")

End

Public Sub btncallurl_Click()

  If txtchaturl.Text Then
    mnuopenlink_Click()
  Endif

End

Public Sub btnOK_Click()

  Dim res As Result

  If txtteleindex.Text Then
    res = modDatabase.$myConn.Edit("tbltelemedtalk", "fldtalkid=&1", txtteleindex.Text)
    res["fldresponse"] = txtresponse.RichText
    If res["flduserid"] Then
      res["fldupuser"] = modBasic.$lbluser
      res["flduptime"] = Now()
    Else
      res["flduserid"] = modBasic.$lbluser
      res["fldtime"] = Now()
    Endif
    res["fldcoment"] = cmbcomment.Text
    res.Update
    LoadGridView()
    Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information saved", modBasic.$BalloonDelay))
  Endif

End

''================ Exam menus =================
Public Sub btnclinical_Menu()

  mnuclinicdata.Popup(btnclinical)

End

Public Sub btnexams_Menu()

  mnudataview.Popup(btnexams)

End

Public Sub btnlabs_Menu()

  mnulabrepoall.Popup(btnlabs)

End

Public Sub btndrugs_Menu()

  mnumedall.Popup(btndrugs)

End

Public Sub btnimage_Menu()

  mnuimgenarl.Popup(btnimage)

End

'''------------- menu reports
Public Sub mnucompexam_Click()

  Dim hForm As FmTPR

  hForm = New FmTPR($encid, modBasic.$ClinEssentialComp)
  hForm.ShowModal()

End

Public Sub mnureqexam_Click()

  Dim hForm As FmTPR

  hForm = New FmTPR($encid, modPatPatho.GetMonitorList($encid, "Exam", "Continue", "%"))
  hForm.ShowModal()

End

Public Sub mnucomptest_Click()

  Dim res As Result
  Dim xx As String[]
  Dim yy As String[]
  Dim hForm As FmMultiChart

  If $encid Then
    yy = New String[]

    res = modDatabase.$myConn.Exec("select distinct(fldtestid) as col from tblpatlabtest where fldencounterval=&1 and fldtest_type=&2 and (fldstatus=&3 or fldstatus=&4) and flvisible=&5", $encid, "Quantitative", "Reported", "Verified", "Visible")
    xx = modControlSub.GetDirectFillresult(res)
    If xx.Count Then
      xx.Sort()
      hForm = New FmMultiChart($encid, yy, xx, LabUnitForm())
      hForm.ShowModal
    Endif
  Endif

End

''current
Public Sub mnudiagnorep_Click()

  Dim xPath As String

  If $encid Then
    xPath = modCHTMLPatient.ShowPatientLaboratoryReport($encid, LabUnitForm())
    modControlSub.OpenHTMLPreview($encid, xPath, "ReportSize")
  Endif

End

Public Sub mnuradio_Click()

  Dim xPath As String

  If $encid Then
    xPath = modCHTMLPatient.ShowPatRadioReportbyEncID($encid)
    modControlSub.OpenHTMLPreview($encid, xPath, "ReportSize")
  Endif

End

Public Sub mnuexamrepo_Click()

  Dim xPath As String

  If $encid Then
    xPath = modCHTMLPatient.ShowPatientExaminationgReport($encid)
    modControlSub.OpenHTMLPreview($encid, xPath, "ReportSize")
  Endif

End

''history
Public Sub mnulablocal_Click()

  modPatReports.GetPatHistoryLocalResult("Laboratory", $encid, LabUnitForm())

End

Public Sub mnuraiolocal_Click()

  modPatReports.GetPatHistoryLocalResult("Radiology", $encid, LabUnitForm())

End

Public Sub mnuexamhistory_Click()

  modPatReports.GetPatHistoryLocalResult("Examination", $encid, LabUnitForm())

End

''dosing
Public Sub mnudosing_Click()

  Dim xPath As String

  If $encid Then
    xPath = modCHTMLPatient.ShowNursingDosing($encid)
    modControlSub.OpenHTMLPreview($encid, xPath, "ReportSize")
  Endif

End

Public Sub mnumedicinelocal_Click()

  modPatReports.GetPatHistoryLocalResult("Medicines", $encid, LabUnitForm())

End

''images
Public Sub mnudicom_Click()

  Dim hForm As FmPatImage

  If $encid Then
    hForm = New FmPatImage("DICOM", $encid, "")
    hForm.ShowModal
  Endif

End

Public Sub mnupatimage_Click()

  Dim hForm As FmPatImage

  If $encid Then
    hForm = New FmPatImage("IMAGE", $encid, "")
    hForm.ShowModal
  Endif

End

Public Sub mnuvideodata_Click()

  Dim hForm8 As FmPatVideo

  If $encid Then
    hForm8 = New FmPatVideo("VIDEO", $encid, "")
    hForm8.ShowModal
  Endif

End

Public Sub mnuhistory_Click()

  Dim xPath As String

  If $encid Then
    xPath = modCHTMLPatient.ShowPatientHistoryReport($encid)
    modControlSub.OpenHTMLPreview($encid, xPath, "ReportSize")
  Endif

End

Public Sub mnuipnote_Click()

  Dim xPath As String

  If $encid Then
    xPath = modCHTMLPatient.ShowPatientNoteReport($encid, "Inpatient Notes")
    modControlSub.OpenHTMLPreview($encid, xPath, "ReportSize")
  Endif

End

Public Sub mnuvisits_Click()

  modPatReports.GetVisitHistoryResult($PatientNum)

End

Private Function LabUnitForm() As String

  Dim xx As String

  If rbsi.Value = True Then
    xx = "SI"
  Else
    xx = "Metric"
  Endif
  Return xx

End

Public Sub mnucomplete_Click()

  Dim xPath As String
  Dim xpatno As String
  Dim xList As String[]
  Dim xvarList As Variant[]
  Dim examlist As String[]
  Dim lablist As String[]
  Dim radiolist As String[]

  If $encid Then
    xpatno = $PatientNum
    If modPatientSub.GetPatPassCheck(modDatabase.$myConn, xpatno) = True Then
      xList = SelectMedBody("")
      Wait
      If xList Then
        If xList.Exist("Selected Investigations") Then
          xvarList = ListViewMulti($encid)
          If xvarList Then
            examlist = xvarList[0]
            lablist = xvarList[1]
            radiolist = xvarList[2]
          Endif
        Endif
      Endif

      xPath = modPatReports.ShowAllEncounterReport($encid, xList, examlist, lablist, radiolist, LabUnitForm())
      modControlSub.OpenHTMLPreview($encid, xPath, "ReportSize")
    Endif
  Endif

End

''========================= Notes ====================
Public Sub btnshownote_Click()

  FillTreeView()

End

Private Sub FillTreeView()

  Dim dtList As Date[]
  Dim xdate As Date
  Dim res As Result
  Dim xType As String
  Dim Column As Integer

  Dim xdata As String
  Dim pic2 As String
  Dim pic3 As String

  pic2 = "icons/coll2.png"
  pic3 = "icons/coll4.png"

  If rbprogress.Value = True Then
    xType = "Progress Note"
  Else If rbclinician.Value = True Then
    xType = "Clinicians Note"
  Else If rbnurses.Value = True Then
    xType = "Nurses Note"
  Else If rbtransfer.Value = True Then
    xType = "Transfer Note"
  Else If rbpatient.Value = True Then
    xType = "Patient Note"
  Endif

  dtList = modDate.GetDateArrayBetween($doa, Now())
  dtList.Sort(gb.Descent)
  ColumnView1.Clear()
  ColumnView1.Columns.Count = 2
  With ColumnView1
    .Columns[0].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Expand = True

    .Columns[0].Text = "Time"
    .Columns[1].Text = "Description"

  End With

  $aColl = New Collection
  For Each xdate In dtList
    res = modDatabase.$myConn.Exec("select fldreportquali,flddetail,flduserid,fldtime from tblexamgeneral where fldencounterval=&1 and fldinput=&2 and flditem=&3 and fldtime>=&4 and fldtime<=&5 ORDER BY fldtime DESC", $encid, "Notes", xType, modDate.StartSqlDate(xdate), modDate.EndSqlDate(xdate))
    If res.Available Then
      ColumnView1.Add(CStr(xdate), modReportVar.GetDateTimeReport(xdate, gb.MediumDate), pic2)

      Column = 0
      For Each res
        xdata = ""
        If res["flddetail"] Then
          xdata = res["flddetail"]
        Endif
        If res["fldreportquali"] Then
          xdata = xdata & "<br><b>IMPRESSION:</b>" & res["fldreportquali"]
        Endif
        $aColl.Add(xdata, CStr(xdate) & ":" & CStr(Column))
        ColumnView1.Add(CStr(xdate) & ":" & CStr(Column), modReportVar.GetDateTimeReport(res["fldtime"], gb.ShortTime), pic3, CStr(xdate))
        Column = Column + 1
      Next

    Endif
  Next

End

Public Sub ColumnView1_Data(Index As String, Column As Integer, Data As WebTreeData)

  If Column = 1 Then
    Data.Html = $aColl[Index]
  Endif

End

''====================== Archived REPORTS =========================
Public Sub btnshowreport_Click()

  If $encid Then
    If cmbtype.Text Then
      FillReportData($encid, cmbtype.Text)
    Endif
  Endif

End

Private Sub FillReportData(encid As String, sType As String)

  If MMain.$WebEntry = True Then
    $rData1 = modDatabase.$syConn.Exec("select fldrepoid,fldcateg,fldtime,fldencounterval,fldencounterval,fldtitle,fldid,flddetail,fldsave,fldlink from tblpatreport where fldencounterval like &1 and fldcateg=&2 and (flvisible=&3 or flvisible IS NULL) ORDER BY fldid DESC", encid, sType, "Visible")
  Else
    $rData1 = modDatabase.$syConn.Exec("select fldid,fldcateg,fldtime,fldencounterval,fldencounterval,fldtitle,fldid,flddetail,fldsave,fldlink from tblpatreport where fldencounterval like &1 and fldcateg=&2 and (flvisible=&3 or flvisible IS NULL) ORDER BY fldid DESC", encid, sType, "Visible")                                                                 ''
  Endif
  $aMyFields1 = New String[]
  modGridView.ReadSmallData(GridView2, $rData1, $aMyFields1)

  With GridView2
    .Columns[0].Hidden = True
    .Columns[1].Hidden = True
    .Columns[2].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Hidden = True
    .Columns[4].Hidden = True
    .Columns[5].Width = CStr(200 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Hidden = True
    .Columns[7].Width = CStr(225 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Hidden = True
    .Columns[9].Hidden = True

    .Columns[2].Text = "DateTime"
    .Columns[5].Text = "Title"
    .Columns[7].Text = "Comment"
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData1.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 2 Then
    Data.Text = modReportVar.GetDateTimeReport($rData1[$aMyFields1[Column]], gb.GeneralDate)
  Else
    Data.Text = $rData1[$aMyFields1[Column]]
  Endif

End

Public Sub mnushow_Click()

  Dim xLink As String

  If GridView2.Selection.Count Then
    $rData1.MoveTo(GridView2.Selection[0])

    If MMain.$WebEntry = True Then
      xLink = modImage.GetBlobFileData($rData1["fldrepoid"])
    Else
      xLink = modImage.GetBlobFileData($rData1["fldid"])
    Endif
    If xLink Then
      WebPDFView1.Viewer = modSettings.ShowSettingFromFIle("Application/PDFViewer")
      WebPDFView1.Tag = modPrint.GetFileWebURL(xLink)
      WebPDFView1.Path = WebPDFView1.Tag
    Endif

  Endif

End

Public Sub mnuprint_Click()

  Dim xLink As String

  If GridView2.Selection.Count Then
    $rData1.MoveTo(GridView2.Selection[0])

    If MMain.$WebEntry = True Then
      xLink = modImage.GetBlobFileData($rData1["fldrepoid"])
    Else
      xLink = modImage.GetBlobFileData($rData1["fldid"])
    Endif
    If xLink Then
      Me.Exec("printJS('" & modPrint.GetFileWebURL(xLink) & "')")
    Endif

  Endif

End

Public Sub btnprintall_Click()

  ShowReportQuery($encid)

End

Private Sub ShowReportQuery(encid As String)

  Dim xPath As String
  Dim xList As String[]
  Dim xvarList As Variant[]
  Dim examlist As String[]
  Dim lablist As String[]
  Dim radiolist As String[]
  Dim sColl As Collection

  If encid Then
    xList = SelectMedBody("")
    Wait
    If xList Then
      If xList.Exist("Selected Investigations") Then
        xvarList = ListViewMulti(encid)
        If xvarList Then
          examlist = xvarList[0]
          lablist = xvarList[1]
          radiolist = xvarList[2]
        Endif
      Endif
    Endif

    If txtquery.RichText Then
      sColl = New Collection
      sColl.Add(txtquery.RichText, "Query")
      If txtresponse.RichText Then
        sColl.Add(txtresponse.RichText, "Response")
      Endif
      If cmbcomment.Text Then
        sColl.Add(cmbcomment.Text, "Comment")
      Endif
      xPath = modPatReports.ShowCompleteReport(encid, xList, examlist, lablist, radiolist, MMain.$defUnit, False, sColl)

    Else
      xPath = modPatReports.ShowCompleteReport(encid, xList, examlist, lablist, radiolist, MMain.$defUnit, False)
    Endif
    modControlSub.OpenHTMLPreview(encid, xPath, "ReportSize")

  Endif

End
