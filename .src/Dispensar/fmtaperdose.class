' Gambas class file

Private $dosno As Long
Private $rData As Result
Private $aMyFields As String[]

Public Sub _new(dosno As Long)

  $dosno = dosno
  cmbfreq.List = modMedicine.FrequencyCombo()
  ShowDoseInfo($dosno)
  TaperingGrid()
  txtdose.SetFocus

End

Private Sub ShowDoseInfo(dosno As Long)

  Dim res As Result

  res = modDatabase.$myConn.Exec("select fldroute,flditem,fldencounterval from tblpatdosing where fldid=&1", dosno)
  txtencid.Text = res!fldencounterval
  txtroute.Text = res!fldroute
  txtmedicine.Text = res!flditem

End

Public Sub mnudel_Click()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    modDatabase.$myConn.Delete("tbltaperdose", "fldid=&1", $rData["fldid"])
    TaperingGrid()
  Endif

End

Private Sub TaperingGrid()

  $rData = modDatabase.$myConn.Exec("select fldid,flddose,fldfreq,flddays from tbltaperdose where flddoseno=&1", $dosno)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(175 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"

    .Columns[1].Text = "Unit Dose"
    .Columns[2].Text = "Frequency"
    .Columns[3].Text = "Duration"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]

End

Private Sub ShowBlank()

  txtdose.Value = 0
  cmbfreq.Text = ""
  txtday.Value = 0

End

Public Sub txtday_Activate()

  btnadd_Click()

End

Public Sub btnadd_Click()

  Dim res As Result
  Dim xIntVal As String

  If $dosno And If txtdose.Value And If cmbfreq.Text And If txtday.Value Then
    res = modDatabase.$myConn.Create("tbltaperdose")
    res["flddoseno"] = $dosno
    res["flddose"] = txtdose.Value
    res["fldfreq"] = cmbfreq.Text
    res["flddays"] = txtday.Value
    If MMain.$WebEntry = True Then
      xIntVal = modString.GetDateString(Now())
      res["fldid"] = CLong(xIntVal)
      res["fldrepoid"] = modMisc.GetWebIndexStr(xIntVal)
      res["fldrepodate"] = Now()
      res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
      res["fldhospcode"] = modBasic.$HospCode
    Endif
    res.Update
    TaperingGrid()
    ShowBlank()
    txtdose.SetFocus
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub txtdose_Activate()

  cmbfreq.SetFocus

End

Public Sub cmbfreq_Activate()

  txtday.SetFocus

End

Public Sub cmbfreq_Click()

  txtday.SetFocus

End
