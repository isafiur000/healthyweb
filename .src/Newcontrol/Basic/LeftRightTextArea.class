' Gambas class file

Export
Inherits WebContainer

Public Const _IsContainer As Boolean = False
Public Const _Properties As String = "*,HeaderText,DataText,LeftText,RightText,LeftTitle,RightTitle"

Property DataText As String
Property LeftText As String
Property RightText As String
Property HeaderText As String
Property LeftTitle As String
Property RightTitle As String

Private txtleft As WebTextArea
Private txtright As WebTextArea
Private hLeftText As WebLabel
Private hRightText As WebLabel

Public Sub _New()

  Dim hLeft As WebVBox
  Dim hRight As WebVBox

  Me.Arrangement = Arrange.Horizontal

  hLeft = New WebVBox(Me)
  hLeft.Width = "50%"

  hLeftText = New WebLabel(hLeft)
  hLeftText.Text = "LEFT"
  DesignLabel(hLeftText)

  txtleft = New WebTextArea(hLeft)
  txtleft.Expand = True
  txtleft.Wrap = True

  hRight = New WebVBox(Me)
  hRight.Width = "50%"

  hRightText = New WebLabel(hRight)
  hRightText.Text = "RIGHT"
  DesignLabel(hRightText)

  txtright = New WebTextArea(hRight)
  txtright.Expand = True
  txtright.Wrap = True

End

Private Sub DesignLabel(xLabel As WebLabel)

  xLabel.Font = "bold"
  xLabel.Border = True
  xLabel.Height = "2em"

End

''------------- Text value ----------------
Private Sub SetDataText(sText As String)

  Dim valColl As Collection

  If sText Then
    Try valColl = JSON.Decode(sText)
    If valColl Then
      txtleft.Text = valColl[hLeftText.Text]
      txtright.Text = valColl[hRightText.Text]
    Endif
  Endif

End

Private Function GetDataText() As String

  Dim xxx As String
  Dim xcoll As Collection

  If txtleft.Text Or If txtright.Text Then
    xcoll = New Collection
    xcoll.Add(Trim(txtleft.Text), hLeftText.Text)
    xcoll.Add(Trim(txtright.Text), hRightText.Text)
    xxx = JSON.Encode(xcoll)
  Else
    xxx = ""
  Endif
  Return xxx

End

Private Function DataText_Read() As String

  Return GetDataText()

End

Private Sub DataText_Write(Value As String)

  SetDataText(Value)

End

Private Function LeftText_Read() As String

  Return txtleft.Text

End

Private Sub LeftText_Write(Value As String)

  txtleft.Text = Value

End

Private Function RightText_Read() As String

  Return txtright.Text

End

Private Sub RightText_Write(Value As String)

  txtright.Text = Value

End

''------------- Text Title ----------------
Public Sub SetHeaderText(sText As String)

  Dim valColl As Collection

  If sText Then
    Try valColl = JSON.Decode(sText)
    If valColl Then
      hLeftText.Text = valColl["LEFT"]
      hRightText.Text = valColl["RIGHT"]
    Endif
  Endif

End

Public Function GetHeaderText() As String

  Dim xxx As String
  Dim xcoll As Collection

  xcoll = New Collection
  xcoll.Add(hLeftText.Text, "LEFT")
  xcoll.Add(hRightText.Text, "RIGHT")
  xxx = JSON.Encode(xcoll)
  Return xxx

End

Private Function LeftTitle_Read() As String

  Return hLeftText.Text

End

Private Sub LeftTitle_Write(Value As String)

  If Value Then
    hLeftText.Text = Value
  Endif

End

Private Function RightTitle_Read() As String

  Return hRightText.Text

End

Private Sub RightTitle_Write(Value As String)

  If Value Then
    hRightText.Text = Value
  Endif

End

Private Function HeaderText_Read() As String

  Return GetHeaderText()

End

Private Sub HeaderText_Write(Value As String)

  SetHeaderText(Value)

End
