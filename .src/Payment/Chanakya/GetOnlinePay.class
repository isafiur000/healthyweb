' Gambas class file

Private $sBookId As String
Private $sRefNo As String
Private $sPayAmt As Float

Private $onlineURL As String
Private $retURL As String
Private $BookToken As String
Private $BookStatus As String

Public Sub _new(sBookId As String, sRefNo As String, sPayAmt As Float, sURL As String, retURL As String, sHash As String)

  $sBookId = sBookId
  $sRefNo = sRefNo
  $sPayAmt = sPayAmt
  $onlineURL = sURL
  If retURL Then
    $retURL = retURL & "?refer=" & sBookId & "&value=" & sHash
  Else
    $retURL = ""
  Endif

  If $onlineURL And If $sPayAmt > 0 Then
    If $sBookId And If $sRefNo Then
      InsertBookingData()
    Endif
  Endif

End

Private Sub InsertBookingData()

  Dim res1 As Result
  Dim xkey As String
  Dim xdocid As String
  Dim xdocname As String

  Dim sql As String
  Dim res As Result

  sql = "select fldpatientval,fldptnamefir,fldptnamelast,fldptaddvill,fldptaddward,fldptadddist,fldptsex,fldptcontact,fldptguardian,fldptbirday,fldptadmindate,fldemail,fldptcode,fldrelation,fldadmitlocat,fldbillingmode,fldconsultdate,flduserid from tblonlinebook where fldbookingval=&1"
  res = modDatabase.$myConn.Exec(sql, $sBookId)
  If res.Available Then

    If res["flduserid"] Then
      xdocid = res["flduserid"]
      xdocname = modGeneral.GetUserFullName(res["flduserid"])
    Else
      xdocid = ""
      xdocname = ""
    Endif

    modOnlineDbase.OpenOnlineDB()
    If modOnlineDbase.$OnlineCon.Opened Then
      xkey = modApplication.GetMD5SumString($sRefNo)
      res1 = modOnlineDbase.$OnlineCon.Create("appBooking")
      res1["token"] = xkey
      res1["patientId"] = res["fldpatientval"]
      res1["patientName"] = res["fldptnamefir"] & Space(1) & res["fldptnamelast"]
      res1["patientMobile"] = res["fldptcontact"]
      res1["doctorId"] = xdocid
      res1["doctorName"] = xdocname
      res1["deptId"] = res["fldadmitlocat"]
      res1["deptName"] = res["fldadmitlocat"]
      res1["appDate"] = res["fldconsultdate"]
      res1["amount"] = $sPayAmt
      res1.Update
      If xkey Then
        WebForm.Exec("window.open('" & $onlineURL &/ xkey & "'); ")
        $BookStatus = "Pending"
        $BookToken = xkey
      Endif
    Endif
  Endif

End

Public Function GetBookingToken() As String

  Return $BookToken

End

Public Function GetBookingStatus() As String

  Return $BookStatus

End
