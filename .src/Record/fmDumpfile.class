' Gambas class file

Private $xFolder As String
Private $sFile As String
Private $FileVar As Variant[]

Public Sub _new()

  $xFolder = ""

End

Public Sub btncreate_Click()

  Dim sCon As Connection
  Dim xdir As String[]

  Dim sFile As String
  Dim xFile As String
  Dim aList As String[]

  sCon = modDatabase.$myConn
  If Not $xFolder Then
    If MMain.$WebEntry = True Then
      $xFolder = modDatabase.GetRepositoryDump(sCon.Host, sCon.Name, sCon.User, sCon.Password, modBasic.$HospCode)
    Else
      $xFolder = modDatabase.GetLocalDBDump(sCon.Host, sCon.Name, sCon.User, sCon.Password)
    Endif
  Endif
  If Exist($xFolder) Then
    $FileVar = New Variant[]
    xdir = Dir($xFolder, Null, gb.File)
    xdir.Sort()
    For Each sFile In xdir
      xFile = ""
      xFile = modPrint.GetFixedWebURL($xFolder &/ sFile)

      aList = New String[]
      aList.Add(File.Name(sFile))
      aList.Add(Round(Stat($xFolder &/ sFile).Size / 1024, -2) & "MB")
      aList.Add(xFile)
      $FileVar.Add(aList)
    Next
    FillGridView()
  Endif

End

Public Sub btndownall_Click()

  Dim sCon As Connection
  Dim sFile As String

  sCon = modDatabase.$myConn
  If Not $xFolder Then
    If MMain.$WebEntry = True Then
      $xFolder = modDatabase.GetRepositoryDump(sCon.Host, sCon.Name, sCon.User, sCon.Password, modBasic.$HospCode)
    Else
      $xFolder = modDatabase.GetLocalDBDump(sCon.Host, sCon.Name, sCon.User, sCon.Password)
    Endif
  Endif
  If Exist($xFolder) Then
    sFile = modHelpVariable.AppCacheDir &/ modString.GetDateString(Now()) & ".zip"
    modDevAll.GetZipArchive(sFile, $xFolder)
    $sFile = modPrint.GetFileWebURL(sFile)
  Endif

End

Private Sub FillGridView()

  WebTable1.Clear()
  WebTable1.Count = $FileVar.Count
  WebTable1.Columns.Count = 3

  With WebTable1
    .Columns[0].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Expand = True
  End With

End

Public Sub WebTable1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $FileVar[Row][Column]

End

Public Sub mnudownload_Click()

  Me.Exec("window.open('" & $sFile & "'); ")

End

Public Sub WebTable1_Select()

  If WebTable1.Selection.Count Then
    $sFile = $FileVar[WebTable1.Selection[0]][2]
  Endif

End

Public Sub btndown_Click()

  mnudownload_Click()

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub btndown2_Click()

  mnudownload_Click()

End
