' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $HashCode As String

Public Sub _new()

  modPatPortal.SetPaymentParams()
  PictureBox2.Image = modPrint.GetFixedWebURL(modHelpVariable.$appetcFolder &/ "qrcode.jpg")
  dtconsult.Value = Now()
  ShowBookTable()

End

Public Sub btnOpen_Click()

  btnOpen.Upload()

End

Public Sub btnOpen_Progress()

  WebProgressBar1.Value = btnOpen.Progress

End

Public Sub btnOpen_Finish()

  Dim xPath As String

  Me.Exec("Toastify({text: 'Upload Completed', duration: 3000}).showToast()")
  xPath = modPrint.GetCopyTempPath(btnOpen.Path)
  If Exist(xPath) Then
    modImage.StretchtPictureToBox(PictureBox1, xPath)
  Endif

End

Public Sub btnOpen_Abort(Reason As String)

  Message.Error("Unable to upload file.<br>Reason: " & Html(Reason))
  WebProgressBar1.Value = 0

End

Public Sub btnepdate_Click()

  Dim xx As String

  xx = BSDateFormat(("Enter BS Date in yyyy/mm/dd format"), ("Date Converter"), modDate.ConvertToLocaldate(dtconsult.Value))
  If xx Then
    dtconsult.Value = modDate.ConvertToEnglishdate(xx)
  Endif

End

''----------------- Telemed Request ------------------------
Public Sub btnexpert_Click()

  Dim xMedUser As String[]
  Dim res As Result
  Dim xPath As String
  Dim sCol As Collection

  res = modDatabase.$myConn.Exec("select fldcategory,fldhospital,fldteleuser,fldusername from tbltelemeduser where fldstatus=&1", "Active")
  xPath = modTextDB.ConvertResultToCSV(res, ";", False)
  If Exist(xPath) Then
    xMedUser = TreeViewCode(xPath, ";", 3, True)
    If xMedUser And If xMedUser.Count Then
      cmbexpert.Tag = xMedUser[1]
      sCol = modGeneral.GetTeleMedUserProfile(cmbexpert.Tag)
      If sCol.Count Then
        cmbexpert.Text = sCol["Name"]
        txtcategory.Text = sCol["Category"]
        txthospcode.Text = sCol["Hospital"]
        If sCol["ConsultFee"] Then
          txtcharge.Value = sCol["ConsultFee"]
        Else
          txtcharge.Value = 0
        Endif
      Endif
    Endif
  Endif

End

Private Sub ClearConsult()

  cmbexpert.Tag = ""
  cmbexpert.Text = ""
  txtcategory.Text = ""
  txtcharge.Value = 0
  txthospcode.Text = ""

End

Private Function GetPatientBookingPaid() As String

  Dim rsx As Result
  Dim res As Result
  Dim res1 As Result
  Dim sql As String
  Dim xbookID As String

  Dim aLobID As Variant
  Dim xname As String
  Dim xsurnm As String
  Dim xgo As Boolean

  xgo = False
  If txtcharge.Value Then
    If PictureBox1.Tag Then
      xgo = True
    Endif
  Else
    xgo = True
  Endif
  $HashCode = modPassword.GetRandomPassword()

  If xgo = True Then
    rsx = modDatabase.$myConn.Exec("select fldtype,fldbillingmode from tbldiscount where fldonline=&1", "Enable")
    If rsx.Available Then
      rsx.MoveFirst

      modDatabase.$myConn.Begin
      xbookID = modBillLock.BookingNoValue()
      If xbookID Then
        sql = "select fldptnamefir,fldptnamelast,fldethniccode,fldptaddvill,fldptaddward,fldptadddist,fldptsex,fldptcontact,fldptguardian,fldptbirday,fldemail,fldptcode,fldrelation,fldencrypt from tblpatientinfo where fldpatientval=&1"
        res1 = modDatabase.$myConn.Exec(sql, modBasic.$lbluser)
        If res1.Available Then
          xname = modPassword.DecryptPatData(res1["fldptnamefir"], res1["fldencrypt"])
          xsurnm = modPassword.DecryptPatData(res1["fldptnamelast"], res1["fldencrypt"])

          aLobID = modImage.AddVoucherImageBook(txthospcode.Text, xbookID, xname & Space(1) & xsurnm, "Payment Slip", cmbexpert.Text & "@" & txtcategory.Text, txtcharge.Value, PictureBox1.Tag)
          If aLobID Then
            res = modDatabase.$myConn.Create("tblonlinebook")
            res["fldpatientval"] = modBasic.$lbluser
            res["fldptnamefir"] = xname
            res["fldptnamelast"] = xsurnm
            res["fldethniccode"] = res1["fldethniccode"]
            res["fldptaddvill"] = res1["fldptaddvill"]
            res["fldptaddward"] = res1["fldptaddward"]
            res["fldptadddist"] = res1["fldptadddist"]
            res["fldptsex"] = res1["fldptsex"]
            res["fldptcontact"] = modPassword.DecryptPatData(res1["fldptcontact"], res1["fldencrypt"])
            res["fldptguardian"] = res1["fldptguardian"]
            res["fldrelation"] = res1["fldrelation"]
            res["fldptbirday"] = res1["fldptbirday"]
            res["fldptadmindate"] = Now()
            res["fldemail"] = modPassword.DecryptPatData(res1["fldemail"], res1["fldencrypt"])
            res["fldptcode"] = res1["fldptcode"]

            res["fldbookingval"] = xbookID
            res["fldconsultdate"] = dtconsult.Value
            res["fldadmitlocat"] = txtcategory.Text
            res["flduserid"] = cmbexpert.Tag
            res["fldstate"] = "Paid"
            res["flddisctype"] = rsx["fldtype"]
            res["fldbillingmode"] = rsx["fldbillingmode"]
            res["fldcomp"] = modBasic.$compID
            res["fldorduserid"] = modBasic.$lbluser
            res["fldhashcode"] = $HashCode
            res["flditemamt"] = txtcharge.Value
            res["fldpayreference"] = "PaySlip:" & CStr(aLobID)
            res["fldhospital"] = txthospcode.Text
            res["fldcomment"] = ""
            res["fldencounterval"] = ""
            res["fldbillno"] = ""
            res["fldremotemail"] = ""
            If MMain.$WebEntry = True Then
              res["fldrepodate"] = Now()
              res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
              res["fldhospcode"] = modBasic.$HospCode
            Endif
            res.Update()
            modDatabase.$myConn.Commit
            Me.Exec("Toastify({text: 'Booking Completed', duration: 3000}).showToast()")

          Else
            modDatabase.$myConn.Rollback
            xbookID = 0
          Endif

        Else
          modDatabase.$myConn.Rollback
          xbookID = 0
        Endif

      Else
        modDatabase.$myConn.Rollback
        xbookID = 0
      Endif

    Endif
  Endif
  Return xbookID

Catch
  modDatabase.$myConn.Rollback
  xbookID = 0
  modHelpVariable.CreateErrorReport()
  Return xbookID

End

Public Sub btnregister_Click()

  If txtcategory.Text Then
    txtbooking.Text = GetPatientBookingPaid()
    If txtbooking.Text Then
      ClearConsult()
      ShowBookTable()
    Endif
  Endif

End

Private Sub ShowBookTable()

  Dim sql As String

  sql = "select fldbookingval,fldbillno,fldconsultdate,flddisctype,fldadmitlocat,fldstate,flduserid,fldhospital,fldpatientval,fldpayreference,fldbillno,fldhashcode,flditemamt,fldencounterval,fldhospital from tblonlinebook where fldorduserid=&1 ORDER BY fldptadmindate DESC"
  $rData = modDatabase.$myConn.Exec(sql, modBasic.$lbluser)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(25 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Hidden = True
    .Columns[8].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[9].Hidden = True
    .Columns[10].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[11].Hidden = True
    .Columns[12].Hidden = True
    .Columns[13].Hidden = True
    .Columns[14].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"

    .Columns[0].Text = "BookID"
    .Columns[2].Text = "Date"
    .Columns[3].Text = "Scheme"
    .Columns[4].Text = "Department"
    .Columns[5].Text = "Status"
    .Columns[6].Text = "Consultant"
    .Columns[8].Text = "PatientID"
    .Columns[10].Text = "Invoice"
    .Columns[14].Text = "Hospital"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 1 Then
    Data.Control = btninvoice
  Else If Column = 2 Then
    Data.Text = modReportVar.GetDateTimeReport($rData[$aMyFields[Column]], gb.MediumDate)
  Else If Column = 6 Then
    Data.Text = modGeneral.GetUserFullName($rData[$aMyFields[Column]])
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Public Sub btninvoice_Click()

  Dim xLink As String

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])

    If $rData["fldbillno"] Then
      xLink = modBILLFormat.GetBillingInvoicePath($rData["fldbillno"], modBillLock.GetCopyBillTypeFromBillNo($rData["fldbillno"]), False, False)
      If xLink Then
        WebPDFView2.Path = modPrint.GetFileWebURL(xLink)
      Endif
    Endif
  Endif

End

' Public Sub GridView1_Click(Row As Integer, Column As Integer)
'
'   Dim xLink As String
'   Dim ShowTax As Boolean
'   Dim ShowDisc As Boolean
'
'   If Column = 1 Then
'     $rData.MoveTo(GridView1.Selection[Row])
'
'     If $rData["fldbillno"] Then
'       xLink = modBILLFormat.GetBillingInvoicePath($rData["fldbillno"], modBillLock.GetCopyBillTypeFromBillNo($rData["fldbillno"]), False, False)
'       If xLink Then
'         WebPDFView2.Path = modPrint.GetFileWebURL(xLink)
'       Endif
'     Endif
'   Endif
'
' End
