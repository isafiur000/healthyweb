' Gambas class file

Private $encid As String

Private $rFludRes As Result
Private $aFludFields As String[]
Private $rRateRes As Result
Private $aRateFields As String[]

Public Sub _new(encid As String)

  Dim xstatus As String

  $encid = encid

  modGeneralMain.GetOpenModalForm(Me)

  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  txtgender.Text = modPatient.GetPatientAgeString($encid, Now()) & "/" & modPatient.GetPatientSex($encid)

  xstatus = modPatient.CurrentAdmissionStatus($encid)
  txtlocation.Text = modPatient.GetLocationSetting($encid, xstatus)
  ShowMedFluids()

End

Private Sub ShowMedFluids()

  Dim sql As String

  sql = "select fldid,fldid,flditem,flddose,fldfreq,flddays,fldcurval,fldstarttime,fldstarttime,fldstatus from tblpatdosing where fldencounterval=&1 and fldsave_order=&2 and fldroute=&3 and fldstarttime<=&4 and fldendtime>=&5 ORDER BY fldid DESC"
  $rFludRes = modDatabase.$myConn.Exec(sql, $encid, True, "fluid", modDate.EndSqlDate(Now()), modDate.StartSqlDate(Now()))
  $aFludFields = New String[]
  modGridView.ReadSmallData(grdfluid, $rFludRes, $aFludFields)

  With grdfluid
    .Columns[0].Hidden = True
    .Columns[1].Width = CStr(25 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(225 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(40 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Hidden = True
    .Columns[6].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Hidden = True
    .Columns[9].Hidden = True

    .Columns[2].Text = "Medicine"
    .Columns[3].Text = "Dose"
    .Columns[4].Text = "Freq"
    .Columns[6].Text = "Status"
    .Columns[7].Text = "Date"
  End With

End

Public Sub grdfluid_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rFludRes.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 7 Then
    Data.Text = modReportVar.GetDateTimeReport($rFludRes[$aFludFields[Column]], gb.GeneralDate)
  Else If Column = 1 Then
    Data.Html = modString.GetImageForHTMLGrid(modGeneralMain.GetSystemIcon(GetFluidAdminStatusCode($rFludRes[$aFludFields[Column]])), "50%", "50%")
    Data.Text = ""
  Else
    Data.Text = $rFludRes[$aFludFields[Column]]
  Endif

End

Public Sub btnfluidstatus_Click()

  Dim xx As String[] = ["Continue", "Completed", "Discontinue", "On Hold", "Change", "ReWrite", "Cancelled", "Wasted"]
  Dim res As Result
  Dim xval As String

  If grdfluid.Selection.Count Then
    $rFludRes.MoveTo(grdfluid.Selection[0])
    xval = InputCombo($rFludRes["flditem"], ("Select Current Status"), xx, $rFludRes["fldcurval"], True)
    If xval Then
      res = modDatabase.$myConn.Edit("tblpatdosing", "fldid=&1", $rFludRes["fldid"])
      res["fldcurval"] = xval
      res["xyz"] = False
      res.Update()
      ShowMedFluids()
    Endif
  Endif

End

Public Sub grdfluid_Select()

  If grdfluid.Selection.Count Then
    $rFludRes.MoveTo(grdfluid.Selection[0])
    btnivclose.Visible = True
    txtfluidname.Text = $rFludRes["flditem"]
    FillFluidGridView($rFludRes["fldid"])
  Endif

End

Public Sub btnivstart_Click()

  If grdfluid.Selection.Count Then
    $rFludRes.MoveTo(grdfluid.Selection[0])

    If $rFludRes["fldcurval"] = "Continue" Then
      If GetFluidAdminStatusCode($rFludRes["fldid"]) = "icon:/small/next" Then
        If txtrate.Value Then
          modPharmSub.InsertNurDosing($rFludRes["fldid"], $encid, txtrate.Value, "mL/Hour", Now())
          FillFluidGridView($rFludRes["fldid"])
          ShowMedFluids()
          txtrate.Value = 0
        Endif
      Endif
    Endif

  Endif

End

Private Sub FillFluidGridView(dosno As Long)

  $rRateRes = modDatabase.$syConn.Exec("select fldid,fldid,fldvalue,fldunit,fldfromtime,fldtotime,fldid,flddoseno from tblnurdosing where fldencounterval=&1 and flddoseno=&2 and fldfromtime IS NOT NULL ORDER BY fldid DESC", $encid, dosno)
  $aRateFields = New String[]
  modGridView.ReadSmallData(grdfluiddata, $rRateRes, $aRateFields)

  With grdfluiddata
    .Columns[0].Width = CStr(25 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(250 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Hidden = True
    .Columns[7].Hidden = True

    .Columns[1].Text = "Particulars"
    .Columns[2].Text = "Rate"
    .Columns[3].Text = "Unit"
    .Columns[4].Text = "From"
    .Columns[5].Text = "To"
  End With

End

Public Sub grdfluiddata_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rRateRes.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 0 Then
    Data.Html = modString.GetImageForHTMLGrid(modGeneralMain.GetSystemIcon(GetPictureDosing($rRateRes[$aRateFields[Column]])), "95%", "95%")
    Data.Text = ""
  Else If Column = 1 Then
    Data.Text = txtfluidname.Text
  Else If Column = 2 Then
    Data.Text = $rRateRes[$aRateFields[Column]]
  Else If Column = 4 Then
    Data.Text = modReportVar.GetDateTimeReport($rRateRes[$aRateFields[Column]], gb.GeneralDate)
  Else If Column = 5 Then
    Data.Text = modReportVar.GetDateTimeReport($rRateRes[$aRateFields[Column]], gb.GeneralDate)
  Else
    Data.Text = $rRateRes[$aRateFields[Column]]
  Endif

End

Public Sub btnivclose_Click()

  Dim res As Result

  If grdfluiddata.Selection.Count Then
    $rRateRes.MoveTo(grdfluiddata.Selection[0])
    If GetPictureDosing($rRateRes["fldid"]) = "icon:/small/stop" Then
      res = modDatabase.$myConn.Edit("tblnurdosing", "fldid=&1", $rRateRes["fldid"])
      res["fldtotime"] = Now()
      res["xyz"] = False
      res.Update
      FillFluidGridView($rRateRes["flddoseno"])
      ShowMedFluids()
    Endif
  Endif

End

Private Function GetPictureDosing(dosid As Long) As String

  Dim res As Result
  Dim xx As String

  res = modDatabase.$myConn.Exec("select flddoseno,fldfromtime,fldtotime from tblnurdosing where fldid=&1", dosid)
  If res.Available Then
    If res["fldfromtime"] And If Not res["fldtotime"] Then
      xx = "icon:/small/stop"
    Else If res["fldfromtime"] And If res["fldtotime"] Then
      xx = "icon:/small/lock"
    Endif
  Endif
  Return xx

End

Private Function GetFluidAdminStatusCode(dosno As Long) As String

  Dim res As Result
  Dim xx As String

  res = modDatabase.$syConn.Exec("select fldid,fldfromtime,fldtotime from tblnurdosing where fldencounterval=&1 and flddoseno=&2 ORDER BY fldid DESC", $encid, dosno)
  If res.Available Then
    If res["fldfromtime"] And If Not res["fldtotime"] Then
      xx = "icon:/small/stop"
    Else
      xx = "icon:/small/next"
    Endif
  Else
    xx = "icon:/small/next"
  Endif
  Return xx

End

Public Sub btnsubOK2_Click()

  Me.Close()

End
