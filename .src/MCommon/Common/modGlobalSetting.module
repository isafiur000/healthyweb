' Gambas module file

Public $resSett As Result

Private Sub LoadCompDBSetting()

  $resSett = modDatabase.$myConn.Exec("select fldcategory,fldvalue from tblsettings where fldcomp=&1", modBasic.$compID)

End

Private Sub AddSettingToDB(strSetting As String, strVal As String, compid As String)

  Dim res As Result

  res = modDatabase.$myConn.Create("tblsettings")
  res["fldindex"] = compid & ":" & strSetting
  res["fldcomp"] = compid
  res["fldcategory"] = strSetting
  res["fldvalue"] = Trim(strVal)
  res["fldtime"] = Now()
  res["flduserid"] = modBasic.$lbluser
  res.Update

End

Private Sub EditSettingToDB(strSetting As String, strVal As String, compid As String)

  Dim res As Result

  If compid Then
    res = modDatabase.$myConn.Edit("tblsettings", "fldindex=&1", compid & ":" & strSetting)
    res["fldvalue"] = Trim(strVal)
    res["fldtime"] = Now()
    res["flduserid"] = modBasic.$lbluser
    res.Update
  Endif

End

Public Sub SaveSettingToDB(strSetting As String, strVal As String, compid As String)

  Dim res As Result

  If compid Then
    res = modDatabase.$myConn.Exec("select fldvalue from tblsettings where fldindex=&1", compid & ":" & strSetting)                      ''
    If res.Available = False Then
      AddSettingToDB(strSetting, strVal, compid)
    Else If res.Available = True Then
      EditSettingToDB(strSetting, strVal, compid)
    Endif
  Endif

End

Public Function ShowSettingFromDBAny(strSetting As String, compid As String) As String

  Dim res As Result
  Dim xx As String

  res = modDatabase.$myConn.Exec("select fldvalue from tblsettings where fldindex=&1", compid & ":" & strSetting)
  If res.Available Then
    If res!fldvalue Then
      xx = Trim(res!fldvalue)
    Else
      xx = ""
    Endif
  Else
    xx = ""
  Endif
  Return xx

End

Public Function ShowSettingFromDB(strSetting As String) As String

  Dim xx As String

  ' xx = ShowSettingFromDBAny(strSetting, modBasic.$compID)
  If Not $resSett Then
    LoadCompDBSetting()
  Endif
  For Each $resSett
    If $resSett["fldcategory"] = strSetting Then
      If $resSett["fldvalue"] Then
        xx = $resSett["fldvalue"]
      Else
        xx = ""
      Endif
      Break
    Endif
  Next

  Return xx

End

''-----------------------Table List --------------------------------
Public Function ShowTableSettingDB(compid As String, sType As String) As String[]

  Dim res As Result
  Dim xxx As String[]

  res = modDatabase.$myConn.Exec("select fldvalue as col from tbltabsettings where fldcomp=&1 and fldcategory=&2", compid, sType & "/TableList")
  xxx = modControlSub.GetDirectFillresult(res)
  Return xxx

End

Public Sub AddTableListDB(compid As String, sType As String, TableList As String[])

  Dim sitem As String
  Dim res As Result

  For Each sitem In TableList
    res = modDatabase.$myConn.Create("tbltabsettings")
    res["fldcomp"] = compid
    res["fldcategory"] = sType & "/TableList"
    res["fldvalue"] = sitem
    res["fldtime"] = Now()
    res["flduserid"] = modBasic.$lbluser
    res.Update
  Next

End

Public Function GetDBTableList(sSetting As String) As String[]

  Dim res As Result
  Dim xx As String[]

  res = modDatabase.$myConn.Exec("select fldvalue from tbltabsettings where fldcomp=&1 and fldcategory=&2", modBasic.$compID, sSetting)
  xx = modControlSub.GetDirectFillresult(res)
  Return xx

End

'NOTE:--------------------- for WebCheckBox ------------------------------
Public Sub EnterSettingFromDB(chkvar As WebCheckBox, strTitle As String, compid As String)

  If compid Then
    If chkvar.Value = True Then
      SaveSettingToDB(strTitle, "Yes", compid)
    Else If chkvar.Value = False Then
      SaveSettingToDB(strTitle, "No", compid)
    Endif
  Endif

End

Public Sub ShowCheckBoxFromDBAny(chkvar As WebCheckBox, StrTitle As String, compid As String)

  Dim xval As String

  xval = ShowSettingFromDBAny(StrTitle, compid)
  If xval Then
    If xval = "Yes" Then
      chkvar.Value = True
    Else
      chkvar.Value = False
    Endif
  Else
    chkvar.Value = False
  Endif

End

Public Sub ShowCheckBoxFromDB(chkvar As WebCheckBox, StrTitle As String)

  ShowCheckBoxFromDBAny(chkvar, StrTitle, modBasic.$compID)

End
