' Gambas class file

Private $sLink As String
Private $encid As String
Private $sType As String

Private $exportPath As String

Public Sub _new(sLink As String, sLock As Boolean, encid As String, sType As String)

  Dim xfile As String

  $sLink = sLink
  $encid = encid
  $sType = sType
  If sLock = True Then
    WebContainerLock.Visible = False
    btnsavepdf.Visible = False
    btnexpoexcel.Visible = False
    btnprint.Tag = "Lock"
  Else If sLock = False Then
    WebContainerLock.Visible = True
    btnsavepdf.Visible = True
    btnexpoexcel.Visible = True
    btnprint.Tag = ""
  Endif

  xfile = File.Load($sLink)
  If InStr(xfile, modReportVar.$HTMLStart) > 0 Then
    WebView1.Html = modString.GetSelectedText(xfile, modReportVar.$HTMLStart, modReportVar.$HTMLEnd)
  Else
    WebView1.Html = xfile
  Endif
  $exportPath = ""

End

Public Sub btnrefresh_Click()

  Me.Reload()

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub btnpdf_Click()

  Dim hForm As FmPDFView
  Dim xpath As String

  If $sLink Then
    xpath = modPrint.ConvertHTMLToPDFString($sLink, "ReportSize")
    If Exist(xpath) Then
      If modBasic.$ReportLogPDF = "Yes" Then
        modImage.SaveReportLog("PDF", xpath, $encid)
      Endif

      hForm = New FmPDFView(xpath, False, $encid)
      hForm.ShowModal
    Endif
  Endif

End

Public Sub btnemail_Click()

  Dim xemail As String
  Dim hForm As FmRemoteMail

  If $sLink Then
    If $encid Then
      xemail = modPatient.GetPatientEmail($encid)
    Else
      xemail = ""
    Endif
    If modBasic.$ReportLogSend = "Yes" Then
      modImage.SaveReportLog("Send", $sLink, $encid)
    Endif

    hForm = New FmRemoteMail(xemail, $sLink, "Report", "")
    hForm.ShowModal
  Endif

End

Public Sub btnsave_Click()

  Dim xpath As String
  Dim hForm As FmSaveFile
  Dim sInt As String

  If $sLink Then
    sInt = modString.GetNowString()
    xpath = modPrint.ConvertHTMLToPDFString($sLink, "ReportSize", sInt)
    If modBasic.$ReportLogSave = "Yes" Then
      modImage.SaveReportLog("Save", xpath, $encid)
    Endif

    If $encid Then
      hForm = New FmSaveFile(xpath, $encid, $sType)
    Else
      hForm = New FmSaveFile(xpath, "", $sType)
    Endif
    hForm.ShowModal()
  Endif

End

Public Sub btnexport_Click()

  Dim sPath As String

  If $sLink Then
    sPath = modprint.ExportToExternalOption($sLink, "ReportSize")
    If Exist(sPath) Then
      If modBasic.$ReportLogExport = "Yes" Then
        modImage.SaveReportLog("Export", sPath, $encid)
      Endif

      $exportPath = modprint.GetFileWebURL(sPath)
      mnuexport_Click()
    Endif
  Endif

End

Public Sub mnuexport_Click()

  If $exportPath Then
    Me.Exec("window.open('" & $exportPath & "'); ")
  Endif

End

Public Sub mnuprint_Click()

  If $sLink Then
    Me.Exec("printJS({printable: 'printable@report', type: 'html', scanStyles: false})")
  Endif

End

Public Sub mnupdfsave_Click()

  Dim sPrt As String
  Dim xleftmargin As String
  Dim xrightmargin As String
  Dim xtopmargin As String
  Dim xbottommargin As String
  Dim xpapsize As String
  Dim xorient As String
  Dim xout As String
  Dim aList As String

  sPrt = "Report"
  xleftmargin = modSettings.ShowSettingFromFIle(sPrt & "/PDF_LeftMargin")
  If Not xleftmargin Then
    xleftmargin = "10"
  Endif
  xrightmargin = modSettings.ShowSettingFromFIle(sPrt & "/PDF_RightMargin")
  If Not xrightmargin Then
    xrightmargin = "10"
  Endif
  xtopmargin = modSettings.ShowSettingFromFIle(sPrt & "/PDF_TopMargin")
  If Not xtopmargin Then
    xtopmargin = "10"
  Endif
  xbottommargin = modSettings.ShowSettingFromFIle(sPrt & "/PDF_BottomMargin")
  If Not xbottommargin Then
    xbottommargin = "10"
  Endif

  xpapsize = modSettings.ShowSettingFromFIle(sPrt & "/PDF_PaperSize")
  If Not xpapsize Then
    xpapsize = "a4"
  Else
    xpapsize = LCase(xpapsize)
  Endif
  xorient = modSettings.ShowSettingFromFIle(sPrt & "/PDF_Orientation")
  If Not xorient Then
    xorient = "portrait"
  Else
    xorient = LCase(xorient)
  Endif
  xout = modString.GetDateString(Now()) & ".pdf"

  aList = "'" & xout & "', '" & xpapsize & "', '" & xorient & "', " & xtopmargin & ", " & xleftmargin & ", " & xbottommargin & ", " & xrightmargin

  If $sLink Then
    Me.Exec("html2pdfdocs( " & aList & " )")
  Endif

End

Public Sub btnprint_Click()

  If btnprint.Tag = "Lock" Then
  Else
    If modBasic.$ReportLogPrint = "Yes" Then
      modImage.SaveReportLog("Print", $sLink, $encid)
    Endif
  Endif

  mnuprint_Click()
  If btnprint.Tag = "Lock" Then
    btnprint.Visible = False
    btnrefresh.Visible = False
  Endif

End

Public Sub btnsavepdf_Click()

  If $sLink Then
    If modBasic.$ReportLogSave = "Yes" Then
      modImage.SaveReportLog("Save", $sLink, $encid)
    Endif
    mnupdfsave_Click()
  Endif

End

Public Sub btnexpoexcel_Click()

  If $sLink Then
    If modBasic.$ReportLogExport = "Yes" Then
      modImage.SaveReportLog("Export", $sLink, $encid)
    Endif
    Me.Exec("ExportToExcel('bodytable', 'xlsx')")
  Endif

End

' Private Function GetPrintableHTML(sLink As String) As String
'
'   Dim xhtml As String
'
'   If sLink Then
'     xhtml = modString.GetSelectedText(File.Load(sLink), modReportVar.$CSSStart, modReportVar.$FooterEnd)
'   Endif
'   Return xhtml
'
' End

''printJS('test.pdf')
''printJS({printable: 'test', type: 'html', scanStyles: false})
''printJS({printable: 'test', type: 'html', style: style, scanStyles: false})
''printJS({printable: 'test', type: 'html', css: 'test.css', scanStyles: false})
''printJS({printable: `<h1>Print.js Raw HTML Print Test</h1>`,type: 'raw-html',style: '.blueText {color:blue;}'})
''printJS('ramdhan.jpg', 'image')
'' Me.Exec("window.print()")
