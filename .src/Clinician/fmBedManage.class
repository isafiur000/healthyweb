' Gambas class file

Private $encid As String
Private $bedNo As String
Private $depart As String
Private $DiscType As String

Private $rData As Result
Private $aMyFields As String[]

Private $sValue As String

Public Sub Run(encid As String) As String

  $encid = encid

  modGeneralMain.GetOpenModalForm(Me)
  If modBasic.$ClinDepartFormat = "Service" Then
    lstdepartment.List = modBasic.$IPDServDepartments
  Else
    lstdepartment.List = modBasic.$IPDDepartmentsAll
  Endif
  GetCurrentBedData()
  $DiscType = modNonMedical.GetAutoIPBillingPack("BedCharge", $encid)
  If modBasic.$ClinMidBedRelease = "Enable" Then
    btnclear.Visible = True
  Endif

  If Me.ShowModal() Then Return $sValue

End

Public Sub WebForm_Open()

  dtnow.Value = Now()
  If modHelpVariable.$LogInCategory = "Cashier" Then
    dtnow.Enabled = True
  Endif

End

Private Sub GetCurrentBedData()

  Dim i As Integer
  Dim res As Result

  res = modDatabase.$myConn.Exec("select flddept,fldbed from tbldepartmentbed where fldbed in(select fldcurrlocat from tblencounter where fldencounterval=&1)", $encid)
  If res.Available Then
    res.MoveLast
    $bedNo = res["fldbed"]
    $depart = res["flddept"]
  Endif

  If modBasic.$FixedDepartment Then
    For i = 0 To lstdepartment.Count - 1
      If lstdepartment.List[i] = modBasic.$FixedDepartment Then
        lstdepartment.Index = i
      Endif
    Next
  Else
    For i = 0 To lstdepartment.Count - 1
      If lstdepartment.List[i] = $depart Then
        lstdepartment.Index = i
      Endif
    Next
  Endif

End

Public Sub lstdepartment_Select()

  If lstdepartment.Text Then
    FillBedGrid()
  Endif

End

Public Sub btnshow_Click()

  If lstdepartment.Text Then
    FillBedGrid()
  Endif

End

Private Sub FillBedGrid()

  Dim sql As String

  If modBasic.$ClinDepartFormat = "Service" Then
    sql = "select fldencounterval,fldoxyport,fldventilator,fldbed,fldencounterval,fldencounterval,fldencounterval,fldfreedate,fldbed from tbldepartmentbed where (fldservice=&1 or fldaddservice=&1) and (fldstatus IS NULL or fldstatus=&2)"
  Else
    sql = "select fldencounterval,fldoxyport,fldventilator,fldbed,fldencounterval,fldencounterval,fldencounterval,fldfreedate,fldbed from tbldepartmentbed where flddept=&1 and (fldstatus IS NULL or fldstatus=&2)"
  Endif
  $rData = modDatabase.$myConn.Exec(sql, lstdepartment.Text, "Active")
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Width = CStr(20 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(20 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(20 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(225 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Width = CStr(35 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Hidden = True

    .Columns[3].Text = "Bed"
    .Columns[4].Text = "EncID"
    .Columns[5].Text = "Name"
    .Columns[6].Text = "Package"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 0 Then
    Data.Html = modString.GetImageForHTMLGrid(ShowBedColor($rData[$aMyFields[Column]]), "auto", "auto")
    Data.Text = ""
  Else If Column = 1 Then
    Data.Text = GetOxygenText($rData[$aMyFields[Column]])
  Else If Column = 2 Then
    Data.Text = GetVentilatorText($rData[$aMyFields[Column]])
  Else If Column = 5 Then
    If $rData[$aMyFields[Column]] Then
      Data.Text = modPatient.GetPatientNameByEnc($rData[$aMyFields[Column]])
    Else
      Data.Text = ""
    Endif
  Else If Column = 6 Then
    If $rData[$aMyFields[Column]] Then
      Data.Text = modPatient.ShowDiscountCategEnc($rData[$aMyFields[Column]])
    Else
      Data.Text = ""
    Endif
  Else If Column = 7 Then
    Data.Html = modString.GetImageForHTMLGrid(ShowFreeColor($rData[$aMyFields[Column]]), "auto", "auto")
    Data.Text = ""
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

Private Function ShowBedColor(encid As String) As String

  Dim xx As String

  If encid Then
    xx = "/" &/ Application.Root &/ "icons/false.svg"
  Else
    xx = "/" &/ Application.Root &/ "icons/null.svg"
  Endif
  Return xx

End

Private Function ShowFreeColor(sDischarge As Date) As String

  Dim xx As String

  If modBasic.$ClinBedHoldTime Then
    If DateDiff(sDischarge, Now(), gb.Hour) <= modBasic.$ClinBedHoldTime Then
      xx = "/" &/ Application.Root &/ "icons/coll5.png"
    Else
      xx = ""
    Endif
  Else
    xx = ""
  Endif
  Return xx

End

Private Function GetOxygenText(sBedVal As String) As String

  Dim sPath As String

  If sBedVal = "Present" Then
    sPath = "O"
  Else
    sPath = ""
  Endif
  Return sPath

End

Private Function GetVentilatorText(sBedVal As String) As String

  Dim sPath As String

  If sBedVal = "Present" Then
    sPath = "V"
  Else
    sPath = ""
  Endif
  Return sPath

End

Public Sub btnsave_Click()

  Me.Close(True)

End

' Public Sub GridView1_Menu()
'
'   mnuhide.Popup
'
' End

' Public Sub mnusel_Click()
'
'   If GridView1.Selection.Count > 0 Then
'     If modBasic.$LockToOwnDepart = "Yes" And If modBasic.$FixedDepartment Then
'       If lstdepartment.Text = modBasic.$FixedDepartment Then
'         SelectBed()
'       Endif
'     Else
'       SelectBed()
'     Endif
'   Endif
'
' End

Public Sub btnselect_Click()

  If GridView1.Selection.Count Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      $rData.MoveTo(GridView1.Selection[0])
      If modBasic.$LockToOwnDepart = "Yes" And If modBasic.$FixedDepartment Then
        If lstdepartment.Text = modBasic.$FixedDepartment Then
          SelectBed($rData["fldbed"])
        Endif
      Else
        SelectBed($rData["fldbed"])
      Endif
    Endif
  Endif

End

Public Sub btnclear_Click()

  If GridView1.Selection.Count Then
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      $rData.MoveTo(GridView1.Selection[0])
      If modBasic.$LockToOwnDepart = "Yes" And If modBasic.$FixedDepartment Then
        If lstdepartment.Text = modBasic.$FixedDepartment Then
          ReleaseBed($rData["fldbed"])
        Endif
      Else
        ReleaseBed($rData["fldbed"])
      Endif
    Endif
  Endif

End

Private Sub SelectBed(newbed As String)

  Dim res As Result
  Dim res1 As Result
  Dim hr As Float
  Dim xitem As String
  Dim unitrate As String

  Dim xrefer As String
  Dim xpayble As String
  Dim xidno As Long
  Dim xcomm As String
  Dim xTotal As Integer

  If $encid Then
    xrefer = modBillings.GetReferralUserSetting("Service", $encid)
    xpayble = modBillings.GetPayableUserSetting("Service", $encid)
    If $depart = lstdepartment.Text Then
      xcomm = "Intra Transfer"
    Else
      xcomm = newbed
    Endif

    res1 = modDatabase.$myConn.Exec("select fldencounterval from tbldepartmentbed where flddept=&1 and fldbed=&2", lstdepartment.Text, newbed)
    If res1.Available And If res1["fldencounterval"] Then
      Message.Warning(("Bed not free"), ("OK"))
    Else

      res = modDatabase.$myConn.Exec("select fldid,flditem,flddisctype,fldfirsttime,fldfirstreport from tblpattiming where fldencounterval=&1 and fldtype=&2 and fldfirstsave=&3 and fldsecondsave=&4 and fldfirstreport=&5", $encid, "General Services", True, False, "Bed")                                                                   ''
      If res.Available = False Then
        modPatientSub.UpdateAdmissionWard($encid, lstdepartment.Text, newbed)
        xidno = modPatientGeneral.AddPatientTableFirstTime($encid, "General Services", $DiscType, lstdepartment.Text, "Bed", dtnow.Value, newbed)
      Else
        res.MoveFirst

        modDatabase.$myConn.Begin
        If modBasic.$AutoBedChargeMode = "Daily" Then  ''done before update
          xTotal = modBillings.GetSavedBedChargesTotal($encid)
        Else
          xTotal = 0
        Endif
        xitem = modNonMedical.GetIPDepartChargeRate(res["flditem"], res["flddisctype"], $bedNo)
        modPatientGeneral.UpdatePatientTableSecondTime(res["fldid"], $bedNo, xitem, dtnow.Value)

        If modBasic.$AutoBillBed = "Standard" Or If modBasic.$AutoBillBed = "Full" Or If modBasic.$AutoBillBed = "Partial" Then
          If xitem Then
            unitrate = modNonMedical.GetBillingTargeDept(xitem, "General Services")
            hr = modPatPatho.GetWardBedUseDuration($encid, res["flditem"], unitrate, res["fldfirsttime"], dtnow.Value, xTotal)
            If hr > 0 Then
              modBillings.EnterBillingForInterval(modBasic.$AutoBillBed, "Service", $encid, res["flddisctype"], xitem, hr, 0, xpayble, xrefer)
            Endif
          Else
            WebForm.Exec("Toastify({text: 'Bed Charge not specified', duration: 3000}).showToast()")
          Endif
        Endif

        xidno = modPatientGeneral.AddPatientTableFirstTime($encid, "General Services", $DiscType, lstdepartment.Text, "Bed", dtnow.Value, newbed)
        modDatabase.$myConn.Commit
      Endif

      $bedNo = newbed
      $depart = lstdepartment.Text
      modPatientSub.UpdatePatLocation($encid, newbed, dtnow.Value)
      FillBedGrid()

      $sValue = newbed
    Endif
  Endif

Catch
  modDatabase.$myConn.Rollback
  modHelpVariable.CreateErrorReport()

End

Private Sub ReleaseBed(bedno As String)

  Dim res As Result
  Dim res1 As Result
  Dim hr As Float
  Dim xitem As String
  Dim unitrate As String

  Dim xrefer As String
  Dim xpayble As String
  Dim xTotal As Integer

  If $encid Then
    xrefer = modBillings.GetReferralUserSetting("Service", $encid)
    xpayble = modBillings.GetPayableUserSetting("Service", $encid)

    res1 = modDatabase.$myConn.Exec("select fldencounterval from tbldepartmentbed where flddept=&1 and fldbed=&2", lstdepartment.Text, bedno)
    If res1.Available And If res1["fldencounterval"] Then

      res = modDatabase.$myConn.Exec("select fldid,flditem,flddisctype,fldfirsttime,fldfirstreport from tblpattiming where fldencounterval=&1 and fldtype=&2 and fldfirstsave=&3 and fldsecondsave=&4 and fldfirstreport=&5", $encid, "General Services", True, False, "Bed")                                                                   ''
      If res.Available Then
        res.MoveFirst

        modDatabase.$myConn.Begin
        If modBasic.$AutoBedChargeMode = "Daily" Then  ''done before update
          xTotal = modBillings.GetSavedBedChargesTotal($encid)
        Else
          xTotal = 0
        Endif
        xitem = modNonMedical.GetIPDepartChargeRate(res["flditem"], res["flddisctype"], $bedNo)
        modPatientGeneral.UpdatePatientTableSecondTime(res["fldid"], $bedNo, xitem, dtnow.Value)

        If modBasic.$AutoBillBed = "Standard" Or If modBasic.$AutoBillBed = "Full" Or If modBasic.$AutoBillBed = "Partial" Then
          If xitem Then
            unitrate = modNonMedical.GetBillingTargeDept(xitem, "General Services")
            hr = modPatPatho.GetWardBedUseDuration($encid, res["flditem"], unitrate, res["fldfirsttime"], dtnow.Value, xTotal)
            If hr > 0 Then
              modBillings.EnterBillingForInterval(modBasic.$AutoBillBed, "Service", $encid, res["flddisctype"], xitem, hr, 0, xpayble, xrefer)
            Endif
          Else
            WebForm.Exec("Toastify({text: 'Bed Charge not specified', duration: 3000}).showToast()")
          Endif
        Endif

        modDatabase.$myConn.Commit
      Endif

      modPatientSub.UpdatePatLocationRelease($encid, dtnow.Value)
      $bedNo = ""
      $depart = ""
      FillBedGrid()

      $sValue = ""
    Endif
  Endif

Catch
  modDatabase.$myConn.Rollback
  modHelpVariable.CreateErrorReport()

End
