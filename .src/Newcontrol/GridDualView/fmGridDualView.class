' Gambas class file

Private $sList As String[]
Private $GridDataList As String[]
Private $GridWorkList As String[]
Private $sValue As String[]

Public Sub Run(sTitle As String, sList As String[]) As String[]

  Me.Title = sTitle
  $sList = sList
  modGeneralMain.GetOpenModalForm(Me)

  $sValue = New String[]
  $GridDataList = $sList
  lstletter.List = modString.GetFirstLetterArray($sList)
  $GridWorkList = $GridDataList
  chkleftmain.Value = modBasic.$SearchBothSide
  FillICDGrid()
  If modBasic.$TabletModeEnable = "Yes" Then
  Else
    txtname.SetFocus
  Endif

  If Me.ShowModal() Then Return $sValue

End

Public Sub chkleftmain_Click()

  modBasic.$SearchBothSide = chkleftmain.Value
  txtname.SetFocus

End

Public Sub FillICDGrid()

  GridView1.Clear()
  GridView1.Columns.Count = 2
  GridView1.Count = $GridWorkList.Count

  With GridView1
    .Columns[0].Expand = True
    .Columns[1].Width = CStr(50 * modBasic.$AppWidthRatio) & "px"
    .Columns[0].Text = "Particulars"
    .Columns[1].Text = "Code"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  Dim sLip As String[]

  sLip = Split($GridWorkList[Row], "@")
  modGridView.GridViewDecoration(Data, Row)
  If Column = 0 Then
    Data.Text = sLip[0]
  Else If Column = 1 Then
    Data.Text = sLip[1]
  Endif

End

Private Sub SortSelectedRows()

  Dim xx As String
  Dim asx As String[]
  Dim xfinal As String[]

  xfinal = New String[]
  If $GridDataList Then
    For Each xx In $GridDataList
      asx = Split(xx, "@")
      If txtcode.Text Then
        If LCase(asx[1]) Like LCase(txtcode.Text) & "*" Then
          xfinal.Add(asx[0] & "@" & asx[1])
        Endif
      Else If txtname.Text Then
        If chkleftmain.Value Then
          If LCase(asx[0]) Like "*" & LCase(txtname.Text) & "*" Then
            xfinal.Add(asx[0] & "@" & asx[1])
          Endif
        Else
          If LCase(asx[0]) Like LCase(txtname.Text) & "*" Then
            xfinal.Add(asx[0] & "@" & asx[1])
          Endif
        Endif
      Endif
    Next
  Endif
  If xfinal Then
    xfinal.Sort()
  Endif
  $GridWorkList = xfinal
  FillICDGrid()

End

Public Sub txtname_Change()

  txtcode.Text = ""
  If txtname.Text Then
    If chkleftmain.Value = False Then
      txtname.Text = String.UCaseFirst(txtname.Text)
    Endif
    SortSelectedRows()
  Else
    $GridWorkList = $GridDataList
    FillICDGrid()
  Endif

End

Public Sub lstletter_Select()

  chkleftmain.Value = False
  txtname.Text = lstletter.Text
  SortSelectedRows()

End

Public Sub txtcode_Change()

  txtname.Text = ""
  If txtcode.Text Then
    txtcode.Text = UCase(txtcode.Text)
    SortSelectedRows()
  Else
    $GridWorkList = $GridDataList
    FillICDGrid()
  Endif

End

Public Sub btninsert_Click()

  Dim i As Integer
  Dim sLip As String[]

  For i = 0 To GridView1.Count - 1
    If GridView1.IsSelected(i) = True Then
      sLip = Split($GridWorkList[i], "@")
      $sValue.Add(sLip[0])
    Endif
  Next
  Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")
  GridView1.UnselectAll()

End

Public Sub btnok_Click()

  If $sValue.Count Then
    Me.Close(True)
  Else
    Me.Close()
  Endif

End
