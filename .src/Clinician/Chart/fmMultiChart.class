' Gambas class file

Private aPanel As New Object[50]
Private aObj As New Object[50]
Private aObk As New Object[50]
Private aObjx As New Object[50]
Private aObj1 As New Object[50]
Private aObHBox As New Object[50]
Private aObVal As New Object[50]
Private aObBox As New Object[50]
Private aObButt As New Object[50]

Private $encid As String
Private $sTitle As String
Private $xParamVar As Variant[]

Private $xalllow As Boolean
Private $chartColl As Collection
Private $unt As String
Private AppFactor As Float
Private $UserRestrict As String[]
Private $PatientNum As String
Private $status As String
Private $input As Boolean

Private $examColl As Collection
Private $labColl As Collection
Private $xfir As Date
Private $xlast As Date

Public Sub _new(encid As String, sTitle As String, unt As String)

  Dim xstatus As String

  $encid = encid
  $sTitle = sTitle
  $unt = unt
  modGeneralMain.GetOpenModalForm(Me)

  Me.Title = "ENCID: " & $encid
  txtpatientname.Text = modPatient.GetPatientNameByEnc($encid)
  $PatientNum = modPatient.GetPatientNoByEnc($encid)
  txtlocation.Text = modPatient.GetPatientLocation($encid)
  $status = modPatient.CurrentAdmissionStatus($encid)
  $xalllow = modPatient.EnableClinicForm($status)
  $input = modPatient.EnableClinicForm($status)
  $UserRestrict = modBasic.$ClinicDisableCompo

  modBasic.LoadClinicalChartSetting()
  AppFactor = modBasic.$AppScaleFactor
  GetVariables()

  rbdaily.Value = True
  rboneday.Value = True

End

Private Sub GetVariables()

  Dim res As Result
  Dim sColl As Collection

  Dim aColl As Collection
  Dim xList As String

  $xParamVar = New Variant[]
  If $sTitle = "Essential Examinations" Then
    For Each aColl In modTableCache.$ExaminationCompVar
      If aColl["fldcategory"] = "Essential Examinations" Then
        sColl = New Collection
        sColl.Add("Examination", "Category")
        sColl.Add(aColl["fldexamid"], "Examination")
        sColl.Add("", "Component")
        sColl.Add(aColl["fldtype"], "Type")
        sColl.Add("", "Comment")
        sColl.Add(aColl["fldsysconst"], "Constant")
        sColl.Add(aColl["fldtanswertype"], "Option")
        If aColl["fldtype"] = "Qualitative" And If aColl["fldsysconst"] Then
          xList = modAllExam.SelectExamOptionList("Exam", aColl["fldexamid"]).join(";")
          sColl.Add(xList, "OptionValues")
        Else
          sColl.Add("", "OptionValues")
        Endif
        $xParamVar.Add(sColl)
      Endif
    Next

  Else
    res = modDatabase.$myConn.Exec("select fldcategory,fldexam,fldsubexam,fldtype,fldcomment,fldsysconst,fldoption,fldselection from tbldepartchart where fldtitle=&1 and (fldcomp=&2 or fldcomp=&3) ORDER BY fldexamorder", $sTitle, modBasic.$compID, "%")
    If res.Available Then
      For Each res
        sColl = New Collection
        sColl.Add(res["fldcategory"], "Category")
        sColl.Add(res["fldexam"], "Examination")
        sColl.Add(res["fldsubexam"], "Component")
        sColl.Add(res["fldtype"], "Type")
        sColl.Add(res["fldcomment"], "Comment")
        sColl.Add(res["fldsysconst"], "Constant")
        sColl.Add(res["fldoption"], "Option")
        If res["fldtype"] = "Qualitative" And If res["fldsysconst"] Then
          xList = modAllExam.SelectExamOptionList("Exam", res["fldexam"]).join(";")
          sColl.Add(xList, "OptionValues")
        Else
          sColl.Add(res["fldselection"], "OptionValues")
        Endif
        $xParamVar.Add(sColl)
      Next
    Endif

  Endif

End

''======================= Tables =============================================
Public Sub btnshowtables_Click()

  LoadTablesExam()

End

Private Sub LoadTablesExam()

  Dim j As Integer
  Dim xdiff As Integer

  xdiff = 0 - spncolumn.Value
  GridView2.Clear()
  GridView2.Count = $xParamVar.Count
  If rbminute.Value = True Then
    $xfir = modDate.StartSqlTenMinute(DateAdd(Now(), gb.Minute, xdiff * 10))
    $xlast = modDate.StartSqlTenMinute(DateAdd(Now(), gb.Minute, 10))
    GridView2.Columns.Count = (DateDiff($xfir, $xlast, gb.Minute) / 10) + 1
  Else If rbhour.Value = True Then
    $xfir = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, xdiff))
    $xlast = modDate.StartSqlHour(DateAdd(Now(), gb.Hour, 1))
    GridView2.Columns.Count = DateDiff($xfir, $xlast, gb.Hour) + 1
  Else If rbdaily.Value = True Then
    $xfir = modDate.StartSqlDate(DateAdd(Now(), gb.Day, xdiff))
    $xlast = modDate.StartSqlDate(DateAdd(Now(), gb.Day, 1))
    GridView2.Columns.Count = DateDiff($xfir, $xlast, gb.Day) + 1
  Else If rbmonth.Value = True Then
    $xfir = modDate.StartSqlMonth(DateAdd(Now(), gb.Month, xdiff))
    $xlast = modDate.StartSqlMonth(DateAdd(Now(), gb.Month, 1))
    GridView2.Columns.Count = DateDiff($xfir, $xlast, gb.Month) + 1
  Endif

  With GridView2
    For j = 0 To GridView2.Columns.Count - 1
      If j = 0 Then
        .Columns[j].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
        .Columns[j].Text = "Examination"
      Else If j = 1 Then
        If chkcomment.Value = True Then
          .Columns[j].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
          .Columns[j].Text = "Comment"
        Else
          .Columns[j].Hidden = True
        Endif
      Else
        .Columns[j].Width = CStr(64 * modBasic.$AppWidthRatio) & "px"
        If rbminute.Value = True Then
          .Columns[j].Text = modReportVar.GetDateTimeReport(DateAdd($xfir, gb.Minute, j * 10), gb.ShortTime)
        Else If rbhour.Value = True Then
          .Columns[j].Text = modReportVar.GetDateTimeReport(DateAdd($xfir, gb.Hour, j), gb.ShortTime)
        Else If rbdaily.Value = True Then
          .Columns[j].Text = modDate.GetDateOnlyInFormatForAll(DateAdd($xfir, gb.Day, j), modBasic.$DateFormat, "DateOnly")
        Else If rbmonth.Value = True Then
          .Columns[j].Text = modDate.GetDateOnlyInFormatForAll(DateAdd($xfir, gb.Month, j), modBasic.$DateFormat, "YearMonth")
        Endif
      Endif
    Next
  End With

End

Public Sub GridView2_Data(Row As Integer, Column As Integer, Data As WebTableData)

  modGridView.GridViewDecoration(Data, Row)
  If Column = 0 Then
    If $xParamVar[Row]["Component"] Then
      Data.Text = $xParamVar[Row]["Component"] & " [" & $xParamVar[Row]["Examination"] & "]"
    Else
      Data.Text = $xParamVar[Row]["Examination"]
    Endif
  Else If Column = 1 Then
    Data.Text = $xParamVar[Row]["Comment"]
  Else

    If $xParamVar[Row]["Category"] = "Examination" Then
      If rbminute.Value = True Then
        Data.Text = GetExamValueByDate($PatientNum, $xParamVar[Row], DateAdd($xfir, gb.Minute, Column * 10))
      Else If rbhour.Value = True Then
        Data.Text = GetExamValueByDate($PatientNum, $xParamVar[Row], DateAdd($xfir, gb.Hour, Column))
      Else If rbdaily.Value = True Then
        Data.Text = GetExamValueByDate($PatientNum, $xParamVar[Row], DateAdd($xfir, gb.Day, Column))
      Else If rbmonth.Value = True Then
        Data.Text = GetExamValueByDate($PatientNum, $xParamVar[Row], DateAdd($xfir, gb.Month, Column))
      Endif
    Else If $xParamVar[Row]["Category"] = "Laboratory" Then
      If rbminute.Value = True Then
        Data.Text = GetLabValueByDate($PatientNum, $xParamVar[Row], DateAdd($xfir, gb.Minute, Column * 10))
      Else If rbhour.Value = True Then
        Data.Text = GetLabValueByDate($PatientNum, $xParamVar[Row], DateAdd($xfir, gb.Hour, Column))
      Else If rbdaily.Value = True Then
        Data.Text = GetLabValueByDate($PatientNum, $xParamVar[Row], DateAdd($xfir, gb.Day, Column))
      Else If rbmonth.Value = True Then
        Data.Text = GetLabValueByDate($PatientNum, $xParamVar[Row], DateAdd($xfir, gb.Month, Column))
      Endif
    Endif

  Endif

End

Private Function GetLabValueByDate(patNo As String, xColl As Collection, xDate As Date) As String

  Dim res As Result
  Dim xx As String
  Dim xfir As Date
  Dim xlast As Date

  If rbminute.Value = True Then
    xfir = modDate.StartSqlTenMinute(xDate)
    xlast = modDate.EndSqlTenMinute(xDate)
  Else If rbhour.Value = True Then
    xfir = modDate.StartSqlHour(xDate)
    xlast = modDate.EndSqlHour(xDate)
  Else If rbdaily.Value = True Then
    xfir = modDate.StartSqlDate(xDate)
    xlast = modDate.EndSqlDate(xDate)
  Else If rbmonth.Value = True Then
    xfir = modDate.StartSqlMonth(xDate)
    xlast = modDate.EndSqlMonth(xDate)
  Endif

  If xColl["Component"] Then
    res = modDatabase.$myConn.Exec("select tblpatlabsubtest.fldreport as fldreport from (tblpatlabsubtest inner join tblpatlabtest on (tblpatlabtest.fldid=tblpatlabsubtest.fldtestid and tblpatlabtest.fldencounterval=tblpatlabsubtest.fldencounterval)) inner join tblencounter on tblpatlabtest.fldencounterval=tblencounter.fldencounterval  where tblencounter.fldpatientval=&1 and tblpatlabtest.fldsave_report=&2 and tblpatlabtest.fldtestid=&3 and tblpatlabsubtest.fldsubtest=&4 and tblpatlabtest.fldtime_sample>=&5 and tblpatlabtest.fldtime_sample<=&6 ORDER BY tblpatlabtest.fldtime_sample", patNo, True, xColl["Examination"], xColl["Component"], xfir, xlast)         ''
    If res.Available Then
      res.MoveLast
      If res["fldreport"] Then
        xx = res["fldreport"]
      Else
        xx = ""
      Endif
    Else
      xx = ""
    Endif

  Else
    res = modDatabase.$myConn.Exec("select tblpatlabtest.fldreportquali as fldreportquali,tblpatlabtest.fldreportquanti as fldreportquanti from tblpatlabtest inner join tblencounter on tblpatlabtest.fldencounterval=tblencounter.fldencounterval  where tblencounter.fldpatientval=&1 and tblpatlabtest.fldsave_report=&2 and tblpatlabtest.fldtestid=&3 and tblpatlabtest.fldtime_sample>=&4 and tblpatlabtest.fldtime_sample<=&5 ORDER BY tblpatlabtest.fldtime_sample", patNo, True, xColl["Examination"], xfir, xlast)
    If res.Available Then
      res.MoveLast
      If res["fldreportquanti"] Then
        xx = CStr(res["fldreportquanti"])
      Else
        xx = res["fldreportquali"]
      Endif
    Else
      xx = ""
    Endif

  Endif
  Return xx

End

Private Function GetExamValueByDate(patNo As String, xColl As Collection, xDate As Date) As String

  Dim res As Result
  Dim xx As String
  Dim xfir As Date
  Dim xlast As Date
  Dim aColl As Collection

  If rbminute.Value = True Then
    xfir = modDate.StartSqlTenMinute(xDate)
    xlast = modDate.EndSqlTenMinute(xDate)
  Else If rbhour.Value = True Then
    xfir = modDate.StartSqlHour(xDate)
    xlast = modDate.EndSqlHour(xDate)
  Else If rbdaily.Value = True Then
    xfir = modDate.StartSqlDate(xDate)
    xlast = modDate.EndSqlDate(xDate)
  Else If rbmonth.Value = True Then
    xfir = modDate.StartSqlMonth(xDate)
    xlast = modDate.EndSqlMonth(xDate)
  Endif

  If xColl["Component"] Then
    If xColl["Option"] = "Clinical Scale" Then
      res = modDatabase.$myConn.Exec("select tblpatientexam.fldrepquali as fldrepquali,tblpatientexam.fldrepquanti as fldrepquanti from tblpatientexam inner join tblencounter on tblpatientexam.fldencounterval=tblencounter.fldencounterval  where tblencounter.fldpatientval=&1 and tblpatientexam.fldsave=&2 and tblpatientexam.fldhead=&3 and tblpatientexam.fldtime>=&4 and tblpatientexam.fldtime<=&5 ORDER BY tblpatientexam.fldtime", patNo, True, xColl["Examination"], xfir, xlast)
      If res.Available Then
        res.MoveLast
        If res["fldrepquali"] Then
          aColl = JSON.Decode(res["fldrepquali"])
          If aColl Then
            xx = aColl[xColl["Component"]]
          Endif
        Else
          xx = ""
        Endif
      Else
        xx = ""
      Endif

    Else
      res = modDatabase.$myConn.Exec("select tblpatientsubexam.fldreport as fldreport from (tblpatientsubexam inner join tblpatientexam  on (tblpatientexam.fldid=tblpatientsubexam.fldheadid and tblpatientexam.fldencounterval=tblpatientsubexam.fldencounterval)) inner join tblencounter on tblpatientexam.fldencounterval=tblencounter.fldencounterval  where tblencounter.fldpatientval=&1 and tblpatientexam.fldsave=&2 and tblpatientexam.fldhead=&3 and tblpatientsubexam.fldsubtexam=&4 and tblpatientexam.fldtime>=&5 and tblpatientexam.fldtime<=&6 ORDER BY tblpatientexam.fldtime", patNo, True, xColl["Examination"], xColl["Component"], xfir, xlast)
      If res.Available Then
        res.MoveLast
        If res["fldreport"] Then
          xx = res["fldreport"]
        Else
          xx = ""
        Endif
      Else
        xx = ""
      Endif

    Endif

  Else
    res = modDatabase.$myConn.Exec("select tblpatientexam.fldrepquali as fldrepquali,tblpatientexam.fldrepquanti as fldrepquanti from tblpatientexam inner join tblencounter on tblpatientexam.fldencounterval=tblencounter.fldencounterval  where tblencounter.fldpatientval=&1 and tblpatientexam.fldsave=&2 and tblpatientexam.fldhead=&3 and tblpatientexam.fldtime>=&4 and tblpatientexam.fldtime<=&5 ORDER BY tblpatientexam.fldtime", patNo, True, xColl["Examination"], xfir, xlast)
    If res.Available Then
      res.MoveLast
      If res["fldrepquanti"] Then
        xx = CStr(res["fldrepquanti"])
      Else
        xx = res["fldrepquali"]
      Endif
    Else
      xx = ""
    Endif

  Endif

  Return xx

End

''====================== Chart ==========================
Public Sub btnshowchart_Click()

  LoadControls()

End

Private Function ChartHourVal() As Integer

  Dim xx As Integer

  If rboneday.Value = True Then
    xx = 24
  Else If rbtwoday.Value = True Then
    xx = 48
  Else If rbthreeday.Value = True Then
    xx = 72
  Else If rbweek.Value = True Then
    xx = 168
  Else
    xx = 0
  Endif

  Return xx

End

Private Sub LoadControls()

  Dim i As Integer
  Dim j As Integer
  Dim sColl As Collection

  If ListContainer1.Children.Count Then
    ListContainer1.DeleteChildren()
  Endif

  For i = 0 To $xParamVar.Count - 1
    If i < 49 Then
      aPanel[i] = New WebContainer(ListContainer1)
      aObHBox[i] = New WebContainer(aPanel[i])
      aObj1[i] = New WebHtml(aObHBox[i]) As "TextLabelGroup"

      If $xParamVar[i]["Category"] = "Examination" Then
        If $xParamVar[i]["Type"] = "Quantitative" Then
          If $xalllow = True Then
            If $xParamVar[i]["Component"] Then
            Else
              aObVal[i] = New WebValueBox(aObHBox[i])
              If $input = True Then
                aObButt[i] = New WebButton(aObHBox[i]) As "SaveButton"
              Endif
            Endif
          Endif
          aObjx[i] = New WebScrollView(aPanel[i]) As "ScrollGroup"
          aObj[i] = New WebCanvas(aObjx[i]) As "PictureBoxgroup"
        Else
          If $xalllow = True Then
            If $xParamVar[i]["Component"] Then
            Else
              aObBox[i] = New WebComboBox(aObHBox[i])
              If $input = True Then
                aObButt[i] = New WebButton(aObHBox[i]) As "SaveButton"
              Endif
            Endif
          Endif
          aObk[i] = New WebListBox(aPanel[i]) As "ListGroup"
        Endif
      Else If $xParamVar[i]["Category"] = "Laboratory" Then
        If $xParamVar[i]["Type"] = "Quantitative" Then
          aObjx[i] = New WebScrollView(aPanel[i]) As "ScrollGroup"
          aObj[i] = New WebCanvas(aObjx[i]) As "PictureBoxgroup"
        Else
          aObk[i] = New WebListBox(aPanel[i]) As "ListGroup"
        Endif
      Endif

    Endif
  Next

  FillCHartItems(CStr(Slider1.Value * 3) & "em")

End

Private Sub FillCHartItems(sHeight As String)

  Dim i As Integer
  Dim sPath As String

  $chartColl = New Collection

  For i = 0 To $xParamVar.Count - 1
    If i < 49 Then

      With aPanel[i]
        .Arrangement = Arrange.Vertical
      End With

      With aObHBox[i]
        .Arrangement = Arrange.Horizontal
        .Border = True
        .Class = "less-container-margin"
        .Tag = $xParamVar[i]["Constant"]
      End With

      With aObj1[i]
        .Font = "bold"
        If $xParamVar[i]["Component"] Then
          .Text = "<p><b>" & $xParamVar[i]["Component"] & " [" & $xParamVar[i]["Examination"] & "]" & "</b></p>"
        Else
          .Text = "<p><b>" & $xParamVar[i]["Examination"] & "</b></p>"
        Endif
        .Expand = True
        .Tag = i
      End With

      If $xParamVar[i]["Category"] = "Examination" Then
        If $xParamVar[i]["Type"] = "Quantitative" Then
          If $xalllow = True Then
            If $xParamVar[i]["Component"] Then
            Else
              With aObVal[i]
                .Width = "5em"
                .Height = "2em"
                .Tag = i
              End With
              If $input = True Then
                With aObButt[i]
                  .Width = "2em"
                  .Height = "2em"
                  .Image = "icon:/small/apply"
                  .Tag = i
                End With
              Endif
            Endif
          Endif

          With aObjx[i]
            .Height = sHeight
            .Arrangement = Arrange.Vertical
            .Tag = i
          End With
          With aObj[i]
            .Height = sHeight
            .Tag = i
          End With
          If rballvisit.Value = True Then
            sPath = modClinSub.FillQuantiExamChartHistory(aObj[i].Name, $PatientNum, $xParamVar[i]["Examination"], ChartHourVal())
          Else
            sPath = modClinSub.FillQuantiExamChart(aObj[i].Name, $encid, $xParamVar[i]["Examination"], ChartHourVal())
          Endif
          If sPath Then
            $chartColl.Add(sPath, $xParamVar[i]["Examination"])
          Endif

        Else  ''quali exam
          If $xalllow = True Then
            If $xParamVar[i]["Component"] Then
            Else
              With aObBox[i]
                .Width = "12em"
                .Height = "2em"
                .ReadOnly = False
                .List = Split($xParamVar[i]["OptionValues"], ";")
                .Tag = i
              End With
              If $input = True Then
                With aObButt[i]
                  .Width = "2em"
                  .Height = "2em"
                  .Image = "icon:/small/apply"
                  .Tag = i
                End With
              Endif
            Endif
          Endif

          With aObk[i]
            .List = modClinic.UniExamValueList($encid, $xParamVar[i]["Examination"])
            .Height = sHeight
            .Tag = i
          End With

        Endif

      Else If $xParamVar[i]["Category"] = "Laboratory" Then
        If $xParamVar[i]["Type"] = "Quantitative" Then
          With aObjx[i]
            .Height = sHeight
            .Arrangement = Arrange.Vertical
            .Tag = i
          End With
          With aObj[i]
            .Height = sHeight
            .Tag = i
          End With
          If rballvisit.Value = True Then
            sPath = modLabSub.FillQuantiTestChartHistory(aObj[i].Name, $PatientNum, $xParamVar[i]["Examination"], $unt, ChartHourVal())
          Else
            sPath = modLabSub.FillQuantiTestChart(aObj[i].Name, $encid, $xParamVar[i]["Examination"], $unt, ChartHourVal())
          Endif
          If sPath Then
            $chartColl.Add(sPath, $xParamVar[i]["Examination"])
          Endif

        Else
          With aObk[i]
            .List = modClinic.UniExamValueList($encid, $xParamVar[i]["Examination"])
            .Height = sHeight
            .Tag = i
          End With

        Endif

      Endif

    Endif
  Next

  For Each sPath In $chartColl
    If sPath Then
      Me.Exec(sPath)
    Endif
  Next

End

Public Sub SaveButton_Click()

  Dim i As Integer
  Dim xcateg As String
  Dim sPath As String

  i = Last.Tag

  If $sTitle = "Essential Examinations" Then
    xcateg = "Essential Examinations"
  Else
    xcateg = "Physician Examinations"
  Endif

  If aObVal[i] And If aObVal[i].Value Then
    modClinSub.AddQuantiData($encid, "", $xParamVar[i]["Examination"], "", aObVal[i].Value, False, xcateg, aObHBox[i].Tag)

    $chartColl.Remove($xParamVar[i]["Examination"])
    sPath = modClinSub.FillQuantiExamChart(aObj[i].Name, $encid, $xParamVar[i]["Examination"], ChartHourVal())
    If sPath Then
      Me.Exec(sPath)
      $chartColl.Add(sPath, $xParamVar[i]["Examination"])
    Endif
    Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information saved", modBasic.$BalloonDelay))
    aObVal[i].Value = 0

  Else If aObBox[i] And If aObBox[i].Text Then
    modClinSub.AddQualiData($encid, "", $xParamVar[i]["Examination"], "", aObBox[i].Text, False, xcateg, aObHBox[i].Tag)

    aObk[i].List = modClinic.UniExamValueList($encid, $xParamVar[i]["Examination"])
    Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information saved", modBasic.$BalloonDelay))
    aObBox[i].Text = ""

  Endif

End

Public Sub mnuexpo_Click()

  ' Dim xPath As String
  '
  ' xPath = modCHTMLPatient.ExportMultiExamTestToHTML($encid, $examColl, $labColl)
  ' modControlSub.OpenHTMLPreview($encid, xPath, "ReportSize")

End

Public Sub btnclose_Click()

  Me.Close()

End
