' Gambas module file

Public Sub SendFixFileToFTP(hFile As String, sType As String, sFolder As String) As String

  Dim sPath As String
  Dim sHost As String
  Dim sTarget As String
  Dim sUser As String
  Dim sPass As String
  Dim xx As String
  Dim xfile As String

  sHost = modHelpVariable.$SysFTPHost
  sTarget = modHelpVariable.$SysFTPFolder &/ sFolder &/ sType
  sUser = modHelpVariable.$SysFTPUser
  sPass = modHelpVariable.$SysFTPPass

  xfile = File.Name(hFile)
  If sHost Then
    sPath = sFolder &/ sType &/ xfile
    xx = modDevAll.SendFileToFTP(sHost, sTarget, sUser, sPass, xfile, hFile)
    If xx Then
      Message.Warning(xx, ("OK"))
    Else
      Return sPath
    Endif
  Endif

End

''Send Image/Blob to Local FTP Server for all types of internal file storage (acc to General Setting)
Public Sub SendBlobToFTP(hFile As String, sType As String, sFolder As String) As String

  Dim sPath As String
  Dim sHost As String
  Dim sTarget As String
  Dim sUser As String
  Dim sPass As String
  Dim xx As String
  Dim xfile As String
  Dim xext As String

  sHost = modHelpVariable.$SysFTPHost
  sTarget = modHelpVariable.$SysFTPFolder &/ sFolder &/ sType
  sUser = modHelpVariable.$SysFTPUser
  sPass = modHelpVariable.$SysFTPPass

  xext = modImageSub.GetExtFromFile(hFile)
  If xext Then
    xfile = modString.GetNowString() & "." & xext
  Else
    xfile = modString.GetNowString()
  Endif

  If sHost Then
    sPath = sFolder &/ sType &/ xfile
    xx = modDevAll.SendFileToFTP(sHost, sTarget, sUser, sPass, xfile, hFile)
    If xx Then
      Message.Warning(xx, ("OK"))
    Else
      Return sPath
    Endif
  Endif

End

Private Function GetFieFromFTPFolder(sLink As String, sHost As String, sFTP As String, sUser As String, sPass As String) As String

  Dim sPath As String
  Dim sTarget As String
  Dim xcmd As String
  Dim xtemp As String

  If sHost Then
    If modHelpVariable.$SysFTPSSL = "Yes" Then
      sPath = "ftps://" & sHost &/ sFTP &/ sLink
    Else
      sPath = "ftp://" & sHost &/ sFTP &/ sLink
    Endif
    sTarget = "/tmp/" &/ modString.GetNowString()
    If Not Exist(sTarget) Then
      Mkdir sTarget
    Endif

    If System.Exist("wget") Then
      xtemp = Temp() & ".log"
      If modHelpVariable.$SysFTPSSL = "Yes" Then
        xcmd = "wget -o " & xtemp & " --no-check-certificate" & " --ftp-user=" & Quote(sUser) & " --ftp-password=" & Quote(sPass) & Space(1) & Quote(sPath) & " -P " & sTarget
      Else
        xcmd = "wget -o " & xtemp & " --ftp-user=" & Quote(sUser) & " --ftp-password=" & Quote(sPass) & Space(1) & Quote(sPath) & " -P " & sTarget
      Endif
      modBasic.DebugString("Execute " & Replace(xcmd, sPass, "***"))
      Shell xcmd Wait
    Else
      modApplSub.InstallSelectedApplication(["wget"])
    Endif
    If Exist(sTarget &/ File.Name(sLink)) Then
      Return sTarget &/ File.Name(sLink)
    Endif
  Endif

End

''Download File from Local FTP server
Public Function GetFileFromLocalFTP(sLink As String) As String

  Dim sHost As String
  Dim sFTP As String
  Dim sUser As String
  Dim sPass As String
  Dim sTarget As String

  sHost = modHelpVariable.$SysFTPHost
  sFTP = modHelpVariable.$SysFTPFolder
  sUser = modHelpVariable.$SysFTPUser
  sPass = modHelpVariable.$SysFTPPass
  sTarget = GetFieFromFTPFolder(sLink, sHost, sFTP, sUser, sPass)

  Return sTarget

End

''Download File from Remote FTP server
Public Function GetFileFromRemoteFTP(sLink As String) As String

  Dim sHost As String
  Dim sFTP As String
  Dim sUser As String
  Dim sPass As String
  Dim sTarget As String

  sHost = modRepository.$RepoFTPHost
  sFTP = modRepository.$RepolFTPFolder
  sUser = modRepository.$RepoFTPUser
  sPass = modRepository.$RepoFTPPassword
  sTarget = GetFieFromFTPFolder(sLink, sHost, sFTP, sUser, sPass)

  Return sTarget

End

''Delete Image/Blob file present in Local FTP
Public Function DeleteBlobFIleFTP(sLink As String)

  Dim sHost As String
  Dim sFolder As String
  Dim sUser As String
  Dim sPass As String

  sHost = modHelpVariable.$SysFTPHost
  sFolder = modHelpVariable.$SysFTPFolder
  sUser = modHelpVariable.$SysFTPUser
  sPass = modHelpVariable.$SysFTPPass

  If sHost Then
    modDevAll.DeleteFTPFIle(sHost, sFolder, sUser, sPass, sLink)
  Endif

End

Public Sub GetMountFTPFolder(sTarget As String, sLocal As String)

  Dim sHost As String
  Dim sFolder As String
  Dim sUser As String
  Dim sPass As String
  Dim xcmd As String

  sHost = modHelpVariable.$SysFTPHost
  sFolder = modHelpVariable.$SysFTPFolder
  sUser = modHelpVariable.$SysFTPUser
  sPass = modHelpVariable.$SysFTPPass

  If System.Exist("curlftpfs") Then
    xcmd = "curlftpfs " & sUser & ":" & sPass & "@" & sHost &/ sFolder &/ sTarget & " " & sLocal
    modBasic.DebugString("Execute " & Replace(xcmd, sPass, "***"))
    Shell xcmd Wait
  Else
    modApplSub.InstallSelectedApplication(["curlftpfs"])
  Endif

End
