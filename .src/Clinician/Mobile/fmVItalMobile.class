' Gambas class file

Private $sVitalsVar As Variant[]

Private aWebFrame As New Object[50]
Private aIndexLabel As New Object[50]
Private aNameLabel As New Object[50]
Private aCheckAbn As New Object[50]
Private aQuantiKey As New Object[50]
Private aQuantiBox As New Object[50]
Private sButtonPlus As New Object[50]
Private sButtonMinus As New Object[50]
Private aMinBox As New Object[50]
Private aMaxBox As New Object[50]
Private aQualiBox As New Object[50]
Private aComboBox As New Object[50]
Private aDichoValue As New Object[50]
Private aHelpButton As New Object[50]

Private aWebBottom As New Object[50]
Private aValSlider As New Object[50]
Private aBlankFrame As New Object[50]

Private $enableControl As Boolean
Private $viewSlider As Boolean
Private $TriageColor As Integer

Private $hKeyBoard As FmQuantiEntry

Public Sub _new(encid As String)

  txtencid.Text = encid
  modGeneralMain.GetOpenModalForm(Me)
  If txtencid.Text Then
    btnshow_Click()
  Else
    pnlfirst.Visible = True
  Endif

End

Public Sub txtencid_Activate()

  btnshow_Click()

End

Public Sub btnshow_Click()

  Dim xx As Boolean

  If txtencid.Text Then
    If modBasic.$TabletModeEnable = "Yes" Then
      $hKeyBoard = New FmQuantiEntry(pnlkeyboard)
    Else
      pnlkeyboard.Visible = False
    Endif
    txtname.Text = modPatient.GetPatientNameByEnc(Trim(txtencid.Text))
    $TriageColor = modPatient.GetPatientColorStatic(Trim(txtencid.Text))
    $sVitalsVar = modPathoSub.GetAllEssentialExams(Trim(txtencid.Text))
    xx = modPatient.EnableClinicForm(modPatient.CurrentAdmissionStatus(Trim(txtencid.Text)))
    If modBasic.$ClinSliderValue = "Enable" Then
      $viewSlider = True
    Else
      $viewSlider = False
    Endif
    $enableControl = False
    GetControlParams()
    $enableControl = True

    btnsaveall.Enabled = xx
    btncolor.Enabled = xx
  Endif

End

Public Sub btncolor_Click()

  Dim xcol As String

  xcol = InputColor("Triage", CStr($TriageColor))
  If xcol Then
    $TriageColor = CInt(xcol)
    modPatient.SetPatientColor(Trim(txtencid.Text), xcol)
  Endif

End

Public Sub btnwebcam_Click()

  txtencid.Text = QRScanValue("")
  If txtencid.Text Then
    btnshow_Click()
  Endif

End

Private Sub GetControlParams()

  Dim i As Integer
  Dim xlimit As Float[]
  Dim xcrit As Float
  Dim aCrlower As Float
  Dim aCrupper As Float

  For i = 0 To $sVitalsVar.Count - 1
    aWebFrame[i] = New WebContainer(Frame1)
    aIndexLabel[i] = New WebLabel(aWebFrame[i])
    aNameLabel[i] = New WebHtml(aWebFrame[i])
    aCheckAbn[i] = New WebCheckBox(aWebFrame[i])
    If $sVitalsVar[i]["DataType"] = "Quantitative" Then
      aQuantiKey[i] = New WebButton(aWebFrame[i]) As "QuantiKey"
      aQuantiBox[i] = New WebValueBox(aWebFrame[i]) As "QuantiGroup"
      sButtonPlus[i] = New WebButton(aWebFrame[i]) As "PlusGroup"
      sButtonMinus[i] = New WebButton(aWebFrame[i]) As "MinusGroup"
      aMinBox[i] = New WebValueBox(aWebFrame[i])
      aMaxBox[i] = New WebValueBox(aWebFrame[i])
      aWebBottom[i] = New WebContainer(Frame1)
      aValSlider[i] = New WebSlider(aWebBottom[i]) As "SliderGroup"
    Else
      Select $sVitalsVar[i]["OptionType"]
        Case "Single Selection"
          aComboBox[i] = New WebComboBox(aWebFrame[i])
        Case "Dichotomous"
          aDichoValue[i] = New DichotomousValue(aWebFrame[i])
        Case "Clinical Scale"
          aQualiBox[i] = New WebTextBox(aWebFrame[i])
          aQuantiBox[i] = New WebValueBox(aWebFrame[i])
          aHelpButton[i] = New WebButton(aWebFrame[i]) As "ButtonBoxgroup"
        Case Else
          aQualiBox[i] = New WebTextBox(aWebFrame[i])
      End Select
    Endif
    aBlankFrame[i] = New WebSeparator(Frame1)

    With aWebFrame[i]
      .Arrangement = Arrange.Horizontal
      .Class = "less-container-margin"
    End With

    ''create index label
    With aIndexLabel[i]
      .Width = "2em"
      .Height = "2em"
      .Text = i + 1
      .Visible = False
      .Tag = $sVitalsVar[i]["SysConstant"]
    End With
    ''create Name Label
    With aNameLabel[i]
      .Expand = True
      .Height = "2em"
      .Text = $sVitalsVar[i]["ExamName"]
      .Tag = i
    End With

    ''Value entry
    If $sVitalsVar[i]["DataType"] = "Quantitative" Then
      xlimit = modClinic.GetBothQuantiExamVal($sVitalsVar[i]["ExamName"], Trim(txtencid.Text))
      xcrit = $sVitalsVar[i]["CritValue"]
      If xcrit Then
        aCrlower = xlimit[0] - xcrit * (xlimit[1] - xlimit[0])
        aCrupper = xlimit[1] + xcrit * (xlimit[1] - xlimit[0])
        If aCrupper = 0 Then
          aCrupper = 500
        Endif
      Else
        aCrlower = 0
        aCrupper = 500
      Endif
      With aCheckAbn[i]
        .Width = "4em"
        .Height = "2em"
        .Text = "Abn"
        .Enabled = False
        .Tag = i
      End With
      With aQuantiKey[i]
        .Class = "toolButton hide-control"
        .Width = "2em"
        .Height = "2em"
        .Tag = i
        .Text = ""
        .Image = "icon:/32/pen"
      End With
      With aQuantiBox[i]
        .Width = "4em"
        .Height = "2em"
        .Tag = i
      End With
      With sButtonPlus[i]
        .Class = "transparent-control"
        .Width = "2em"
        .Height = "2em"
        .Image = "icon:/small/add"
        .Tag = i
      End With
      With sButtonMinus[i]
        .Class = "transparent-control"
        .Width = "2em"
        .Height = "2em"
        .Image = "icon:/small/remove"
        .Tag = i
      End With
      With aMinBox[i]
        .Width = "3em"
        .Height = "2em"
        If xlimit Then
          .Value = xlimit[0]
        Endif
        .Enabled = False
        .Tag = i
      End With
      With aMaxBox[i]
        .Width = "3em"
        .Height = "2em"
        If xlimit Then
          .Value = xlimit[1]
        Endif
        .Enabled = False
        .Tag = i
      End With

      With aWebBottom[i]
        .Arrangement = Arrange.Horizontal
        .Class = "less-container-margin"
        .Visible = $viewSlider
      End With
      With aValSlider[i]
        .Expand = True
        .Height = "1em"
        .Tag = i
      End With
      aValSlider[i].MinValue = aCrlower
      aValSlider[i].MaxValue = aCrupper

    Else
      With aCheckAbn[i]
        .Width = "4em"
        .Height = "2em"
        .Text = "Abn"
        .Tag = i
        If modBasic.$ClinFlagAbnormExam = "Disable" Then
          .Visible = False
        Else
          .Visible = True
        Endif
      End With

      Select $sVitalsVar[i]["OptionType"]
        Case "Single Selection"
          With aComboBox[i]
            .Width = "17em"
            .Height = "2em"
            .ReadOnly = False
            .List = Split($sVitalsVar[i]["OptionList"], ";")
            .Tag = i
          End With

        Case "Dichotomous"
          With aDichoValue[i]
            .Width = "17em"
            .Height = "2em"
            .List = Split($sVitalsVar[i]["OptionList"], ";")
            .Tag = i
          End With

        Case "Clinical Scale"
          With aQualiBox[i]
            .Width = "10em"
            .Height = "2em"
            .Tag = i
          End With
          With aQuantiBox[i]
            .Width = "5em"
            .Height = "2em"
            .Tag = i
          End With
          With aHelpButton[i]
            .Class = "toolButton"
            .Width = "2em"
            .Height = "2em"
            .Tag = i
            .Text = ""
            .Image = "icon:/small/info"
          End With

        Case Else
          With aQualiBox[i]
            .Width = "17em"
            .Height = "2em"
            .Tag = i
          End With

      End Select
    Endif

    With aBlankFrame[i]
      .Height = "1em"
    End With
  Next

End

Public Sub QuantiKey_Click()

  Dim i As Integer

  i = Last.Tag

  $hKeyBoard.SetValueBox(aQuantiBox[i])
  Me.Exec("$_(" & JS(aQuantiBox[i].Name & ":entry") & ").select()")

End

Public Sub PlusGroup_Click()

  Dim i As Integer

  i = Last.Tag
  aQuantiBox[i].Value = aQuantiBox[i].Value + 1

End

Public Sub MinusGroup_Click()

  Dim i As Integer

  i = Last.Tag
  aQuantiBox[i].Value = aQuantiBox[i].Value - 1

End

Public Sub ButtonBoxgroup_Click()

  Dim i As Integer
  Dim sVal As String[]
  Dim xopt As String[]

  i = Last.Tag
  If $sVitalsVar[i]["OptionList"] Then
    xopt = Split($sVitalsVar[i]["OptionList"], ";")
    If $sVitalsVar[i]["OptionType"] = "Clinical Scale" Then
      sVal = SubChoose(xopt, "Clinical Scale", $sVitalsVar[i]["ExamName"])
      If sVal Then
        aQuantiBox[i].Value = sVal[0]
        aQualiBox[i].Text = sVal[1]
      Endif
    Endif
  Endif

End

Public Sub SliderGroup_Change()

  Dim j As Integer

  j = Last.Tag
  If aQuantiBox[j] Then
    If $enableControl = True Then
      aQuantiBox[j].Value = aValSlider[j].Value
    Endif
  Endif

End

Public Sub QuantiGroup_Change()

  Dim j As Integer

  j = Last.Tag

  If aQuantiBox[j].Value Then
    If aMinBox[j].Value <> aMaxBox[j].Value Then
      If aQuantiBox[j].Value < aMinBox[j].Value Or If aQuantiBox[j].Value > aMaxBox[j].Value Then
        aCheckAbn[j].Value = True
      Else
        aCheckAbn[j].Value = False
      Endif
    Else
      aCheckAbn[j].Value = False
    Endif
  Else
    aCheckAbn[j].Value = False
  Endif

  If aCheckAbn[j].Value = True Then
    aCheckAbn[j].Foreground = Color.Red
  Else
    aCheckAbn[j].Foreground = Color.Default
  Endif

End

Public Sub btnsaveall_Click()

  Dim i As Integer

  For i = 0 To $sVitalsVar.Count - 1
    If i < 49 Then
      If $sVitalsVar[i]["DataType"] = "Quantitative" Then
        If aQuantiBox[i].Value Then
          modClinSub.AddQuantiData(Trim(txtencid.Text), "", $sVitalsVar[i]["ExamName"], $sVitalsVar[i]["OptionType"], aQuantiBox[i].Value, aCheckAbn[i].Value, "Essential Examinations", $sVitalsVar[i]["SysConstant"])
          aQuantiBox[i].Value = 0
          aQuantiBox[i].ReadOnly = True
        Endif

      Else
        If $sVitalsVar[i]["OptionType"] = "Single Selection" Then
          If aComboBox[i].Text Then
            modClinSub.AddQualiData(Trim(txtencid.Text), "", $sVitalsVar[i]["ExamName"], $sVitalsVar[i]["OptionType"], aComboBox[i].Text, aCheckAbn[i].Value, "Essential Examinations", $sVitalsVar[i]["SysConstant"])
            aComboBox[i].Text = ""
          Endif
        Else If $sVitalsVar[i]["OptionType"] = "Dichotomous" Then
          If aDichoValue[i].Value Then
            modClinSub.AddQualiData(Trim(txtencid.Text), "", $sVitalsVar[i]["ExamName"], $sVitalsVar[i]["OptionType"], aDichoValue[i].Value, aCheckAbn[i].Value, "Essential Examinations", $sVitalsVar[i]["SysConstant"])
          Endif
        Else If $sVitalsVar[i]["OptionType"] = "Clinical Scale" Then
          If aQualiBox[i].Text Then
            modClinSub.AddClinicExam(Trim(txtencid.Text), "", $sVitalsVar[i]["ExamName"], $sVitalsVar[i]["OptionType"], aQualiBox[i].Text, aQuantiBox[i].Value, aCheckAbn[i].Value, "Essential Examinations", $sVitalsVar[i]["SysConstant"], "Regular")     ''
            aQualiBox[i].Text = ""
            aQuantiBox[i].Value = 0
            aQuantiBox[i].ReadOnly = True
          Endif
        Else
          If aQualiBox[i].Text Then
            modClinSub.AddQualiData(Trim(txtencid.Text), "", $sVitalsVar[i]["ExamName"], $sVitalsVar[i]["OptionType"], aQualiBox[i].Text, aCheckAbn[i].Value, "Essential Examinations", $sVitalsVar[i]["SysConstant"])
            aQualiBox[i].Text = ""
            aQualiBox[i].ReadOnly = True
          Endif
        Endif

      Endif
    Endif
  Next
  Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information saved", modBasic.$BalloonDelay))

End

Public Sub btnclose_Click()

  Me.Close()

End
