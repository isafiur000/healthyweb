' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Public Sub _new()
  
  cmbcategory.List = modMedicine.GetPathoCategoryList("Syndrome")
  
End

Public Sub tlbtnadd_Click()
  
  Dim hForm As FmAddVariable1
  
  hform = New FmAddVariable1("flclass", "fldcategory", "Syndrome", "tblpathocategory")
  hform.ShowModal
  cmbcategory.List = modMedicine.GetPathoCategoryList("Syndrome")
  
End

Public Sub btnadd_Click()
  
  Dim res As Result
  
  If Not txtcode.Text Then
    txtcode.Text = "Other"
  Endif
  If cmbcategory.Text And If txtsyndrome.Text And If txtcode.Text Then
    res = modDatabase.$medConn.Create("tblsyndromes")
    res["fldsyndrome"] = Trim(txtsyndrome.Text)
    res["fldcategory"] = cmbcategory.Text
    res["fldsymcode"] = txtcode.Text
    res.Update
    ShowGridData(cmbcategory.Text)
    txtsyndrome.Text = ""
    txtcode.Text = ""
    txtsearch.Text = ""
    Me.Exec("Toastify({text: 'Information updated', duration: 3000}).showToast()")
  Endif
  
End

Public Sub btnrefresh_Click()
  
  ShowGridData(cmbcategory.Text)
  
End

Public Sub btndel_Click()
  
  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If Message.Question(("Are you sure?"), ("No"), ("Yes")) = 2 Then
      modDatabase.$medConn.Delete("tblsyndromes", "fldsyndrome=&1", $rData["fldsyndrome"])
      ShowGridData(cmbcategory.Text)
      txtsyndrome.Text = ""
      txtcode.Text = ""
      Me.Exec("Toastify({text: 'Information deleted', duration: 3000}).showToast()")
    Endif
  Endif 
  
End

Private Sub ShowGridData(sType As String)
  
  If sType Then
    $rData = modDatabase.$medConn.Exec("select fldsyndrome,fldsymcode from tblsyndromes where fldcategory=&1", sType)
    $aMyFields = New String[]
    modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
    With GridView1
      .Columns[0].Width = CStr(400 * modBasic.$AppWidthRatio) & "px"
      .Columns[1].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
      .Columns[0].Text = "Syndrome"
      .Columns[1].Text = "ICD Group"
    End With
  Endif
  
End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)
  
  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]
  
End

Public Sub btnaddicd_Click()
  
  Dim xx As String[]
  
  xx = ICDGridView("Select Disease")
  If xx Then
    txtsearch.Text = xx[0]
    txtcode.Text = xx[1]
  Endif
  
End

Public Sub btnclose_Click()
  
  Me.Close
  
End
