' Gambas class file

Private $typ As String
Private $tbl As String
Private $itemName As String

Public Sub _new(sTitle As String, xType As String, xItemName As String)

  $typ = xType
  Me.Title = sTitle
  $itemName = xItemName

  If $typ = "Service Grouping" Then
    cmbcategory.List = ["Diagnostic Tests", "General Services", "Procedures", "Equipment", "Radio Diagnostics", "Other Items", "Mapped Items", "Pharmacy"]
  Else If $typ = "Laboratory Grouping" Then
    cmbcategory.List = ["Diagnostic Tests"]
  Endif

  ShowFilledList()

End

Public Sub cmbcategory_Click()

  If cmbcategory.Text = "Pharmacy" Then
    cmbbillmode.List = ["Medicines", "Surgicals", "Extra Items"]
  Else
    cmbbillmode.List = modNonMedical.FillCashModeCombo()
    cmbbillmode.Add("%")
    cmbbillmode.Text = "%"
  Endif

End

Public Sub cmbbillmode_Click()

  chkall.Value = False
  LoadSelectedListsView()

End

Private Sub LoadSelectedListsView()

  If cmbcategory.Text And If cmbbillmode.Text Then
    If $typ = "Service Grouping" Then
      NonStockBillingItemListView(cmbcategory.Text, cmbbillmode.Text)
    Else If $typ = "Laboratory Grouping" Then
      NonStockBillingLabListView(cmbcategory.Text, cmbbillmode.Text)
    Endif
  Endif

End

Public Sub chkall_Click()

  If chkall.Value = True Then
    lstfirst.SelectAll()
  Else
    lstfirst.UnselectAll()
  Endif

End

Private Sub ShowFilledList()

  Dim sql As String
  Dim res As Result

  If $typ = "Service Grouping" Then
    $tbl = "tblreportgroup"
    sql = "select flditemname as col from " & $tbl & " where fldgroup=&1" ''flditemtype=&2 and
    res = modDatabase.$myConn.Exec(sql, $itemName) ''modNonMedical.GetBillItemCategoryFromCombo(cmbcategory.Text),
  Else If $typ = "Laboratory Grouping" Then
    $tbl = "tblservicecost"
    sql = "select flditemname as col from " & $tbl & " where fldreport=&1" ''flditemtype=&2 and
    res = modDatabase.$myConn.Exec(sql, $itemName)
  Endif
  modFillContainer.FillListBox(lstsecond, res, False)

End

Public Sub btnsave_Click()

  Dim res As Result
  Dim i As Integer

  For i = 0 To lstfirst.List.Count - 1
    If lstfirst.IsSelected(i) = True Then

      If $typ = "Service Grouping" Then
        If lstsecond.List.Exist(lstfirst.List[i]) = False Then
          res = modDatabase.$myConn.Create($tbl)
          res["fldgroup"] = $itemName
          res["flditemtype"] = cmbcategory.Text
          res["flditemname"] = lstfirst.List[i]
          res["fldactive"] = "Active"
          res.Update()
        Endif
      Else If $typ = "Laboratory Grouping" Then
        res = modDatabase.$myConn.Edit("tblservicecost", "flditemname=&1 and flditemtype=&2", lstfirst.List[i], cmbcategory.Text)
        res["fldreport"] = $itemName
        res.Update()
      Endif

    Endif
  Next
  ShowFilledList()
  LoadSelectedListsView()
  cmbcategory.SetFocus

End

Private Sub FillGridViewSelected(res As Result)

  lstfirst.Clear
  If res.Available Then
    For Each res
      If Len(res["col"]) Then
        If lstsecond.List.Exist(res["col"]) = False Then
          lstfirst.Add(res["col"])
        Endif
      Endif
    Next
  Endif

End

Private Sub NonStockBillingItemListView(sType As String, sBillMode As String)

  Dim res As Result

  If sType = "Pharmacy" Then
    If sBillMode = "Medicines" Then
      res = modDatabase.$myConn.Exec("select fldbrandid as col from tblmedbrand where fldbrandid NOT IN(select flditemname from tblreportgroup)")
    Else If sBillMode = "Surgicals" Then
      res = modDatabase.$myConn.Exec("select fldbrandid as col from tblsurgbrand where fldbrandid NOT IN(select flditemname from tblreportgroup)")
    Else If sBillMode = "Extra Items" Then
      res = modDatabase.$myConn.Exec("select fldbrandid as col from tblextrabrand where fldbrandid NOT IN(select flditemname from tblreportgroup)")
    Endif
  Else If sType = "Mapped Items" Then
    res = modDatabase.$myConn.Exec("select distinct(flditemname) as col from tblstockrate where fldbillingmode like &1 and flditemname NOT IN(select flditemname from tblreportgroup)", sBillMode)
  Else
    res = modDatabase.$myConn.Exec("select flditemname as col from tblservicecost where flditemtype=&1 and fldgroup like &2 and flditemname NOT IN(select flditemname from tblreportgroup)", sType, sBillMode) '("Diagnostic Tests","Procedures","General Services")
  Endif
  FillGridViewSelected(res)

End

Private Sub NonStockBillingLabListView(sType As String, sBillMode As String)

  Dim res As Result

  res = modDatabase.$myConn.Exec("select flditemname as col from tblservicecost where flditemtype=&1 and fldgroup like &2 and fldreport IS NULL", sType, sBillMode)
  FillGridViewSelected(res)

End

Public Sub btnclose_Click()

  Me.Close()

End
