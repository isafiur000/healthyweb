' Gambas class file

Private $DHUser As String
Private $DHPass As String
Private $DHURL As String
Private $DHLocation As String
Private $DHQuote As String
Private $DHAgent As String
Private $DHCaCert As String
Private $DHOutput As String

Public Sub _new(xVar As Collection, xURL As String)

  Dim aColl As Collection

  $DHUser = Trim(UnBase64(modSettings.ShowSettingFromFIle("FIND/UserName")))
  $DHPass = Trim(UnBase64(modSettings.ShowSettingFromFIle("FIND/Password")))
  $DHURL = Trim(modSettings.ShowSettingFromFIle("FIND/URL"))
  $DHLocation = Trim(modSettings.ShowSettingFromFIle("FIND/Location"))
  $DHAgent = Trim(modSettings.ShowSettingFromFIle("FIND/UserAgent"))
  $DHQuote = Trim(modSettings.ShowSettingFromFIle("FIND/QuotePass"))
  $DHCaCert = Trim(modSettings.ShowSettingFromFIle("FIND/CACertificate"))
  $DHOutput = ""

  aColl = xVar
  aColl.Add(Trim($DHLocation), "orgUnit")
  ExecuteAPI(aColl, xURL)

End

Private Sub ExecuteAPI(xColl As Collection, aURL As String)

  Dim xcmd As String
  Dim xauth As String
  Dim xcontent As String
  Dim xurl As String
  Dim xhead As String

  xurl = Trim($DHURL) &/ aURL
  If $DHQuote = "Yes" Then
    xauth = "'" & $DHUser & ":" & $DHPass & "'"
  Else
    xauth = $DHUser & ":" & $DHPass
  Endif
  xcontent = "Content-Type: application/json"

  xhead = Space(1)
  If $DHCaCert = "Disable" Then
    xhead = xhead & "-k" & Space(1)
  Endif
  If $DHAgent Then
    xhead = xhead & "-A " & $DHAgent & Space(1)
  Endif
  xcmd = "curl" & xhead & "-u " & xauth & " -H " & Quote(xcontent) & "  -X POST " & xurl & " -d '" & JSON.Encode(xColl) & "'"     ''
  modBasic.DebugAPIString("Execute " & Replace(xcmd, xauth, "***"))
  Shell xcmd To $DHOutput
  modBasic.DebugAPIString("Response: " & $DHOutput)

End

Public Function GetAPIResult() As String

  Return $DHOutput

End

Public Function GetAPIOutput() As String

  Dim aColl As Collection
  Dim xmsg As String

  If $DHOutput Then
    Try aColl = JSON.Decode($DHOutput)
    If aColl["response"]["status"] = "SUCCESS" Then
      xmsg = "SUCCESS"
    Else
      xmsg = $DHOutput
    Endif
  Else
    xmsg = $DHOutput
  Endif
  Return xmsg

End
