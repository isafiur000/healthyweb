' Gambas class file

Private $sValue As Variant[]
Private $sType As String
Private $encid As String

Public Sub Run(sType As String, encid As String, strExam As String, minval As Float, maxval As Float, strValue As Float) As Variant[]

  $sType = sType
  $encid = encid
  lblitem.Font = "bold"
  lblitem.Text = strExam
  txtmin.Value = minval
  txtmax.Value = maxval
  txtreport.Value = strValue
  modGeneralMain.GetOpenModalForm(Me)

  modSettings.ShowCheckBox(chkoutlier, "Examination/WarnOutliers")
  If txtmin.Value = txtmax.Value Then
    chkabnormal.Enabled = False
  Endif
  If modBasic.$TabletModeEnable = "Yes" Then
  Else
    txtreport.SetFocus
  Endif

  If Me.ShowModal() Then Return $sValue

End

Private Sub GetOutlierRange()

  Dim $critVal As Float

  If txtmin.Value <> txtmax.Value Then
    If $sType = "Exam" Then
      $critVal = modFixRadio.GetCritValueExam(lblitem.Text)
    Else If $sType = "Radio" Then
      $critVal = modFixRadio.GetCritValueRadio(lblitem.Text)
    Endif

    If $critVal Then
      txtcritlow.Value = txtmin.Value - $critVal * (txtmax.Value - txtmin.Value)
      txtcrithigh.Value = txtmax.Value + $critVal * (txtmax.Value - txtmin.Value)
    Endif
  Endif

End

Public Sub btnBrOK_Click()

  If chkoutlier.Value = True Then

    GetOutlierRange()
    If txtcritlow.Value Or If txtcrithigh.Value Then

      If txtreport.Value < txtcritlow.Value Or If txtreport.Value > txtcrithigh.Value Then
        If Message.Question(("Are you sure ?"), ("No"), ("Yes")) = 2 Then
          SetValues()
        Endif
      Else
        SetValues()
      Endif

    Else
      SetValues()
    Endif

  Else
    SetValues()
  Endif

  Wait
  Me.Close(True)

End

' Public Sub Form_KeyRelease()
'
'   If Key.Code = Key.Esc Then
'     Me.Close
'   Else If Key.Code = Key.F1 Then
'     btncalc_Click()
'   Else If Key.Code = Key["C"] And If Key.Control Then
'     btncalc_Click()
'   Endif
'
' End

Public Sub txtreport_Change()

  If txtreport.Value Then
    If txtmax.Value <> txtmin.Value Then
      If txtreport.Value < txtmin.Value Or txtreport.Value > txtmax.Value Then
        chkabnormal.Value = True
      Else
        chkabnormal.Value = False
      Endif
    Else
      chkabnormal.Value = False
    Endif
  Else
    chkabnormal.Value = False
  Endif

  If chkabnormal.Value = True Then
    chkabnormal.Foreground = Color.Red
  Else
    chkabnormal.Foreground = Color.Default
  Endif

End

Private Sub SetValues()

  Dim xx As Variant[]

  xx = New Variant[]
  If chknull.Value Then
    xx.Add(txtreport.Value)
    xx.Add(chkabnormal.Value)
    $sValue = xx
  Else
    If txtreport.Value Then
      xx.Add(txtreport.Value)
      xx.Add(chkabnormal.Value)
      $sValue = xx
    Endif
  Endif

End

Public Sub btncalc_Click()

  Dim xsyscon As String
  Dim xval As Float

  xsyscon = modAllExam.GetExamSysConstValue($sType, lblitem.Text)
  If xsyscon Then
    xval = modSysCons.GetConstantValue(xsyscon, $encid)
    If xval Then
      txtreport.Value = xval
    Endif
  Endif

End

Public Sub btnlast_Click()

  Dim xval As String
  Dim xsyscon As String

  xsyscon = modAllExam.GetExamSysConstValue($sType, lblitem.Text)
  If xsyscon Then
    xval = modReportVar.GetSysConstantValue($sType, xsyscon, $encid)
    If xval Then
      txtreport.Value = CFloat(xval)
    Endif
  Endif

End

Public Sub chkoutlier_Click()

  modSettings.EnterCheckSetting(chkoutlier, "Examination/WarnOutliers")

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub btnplus_Click()

  txtreport.Value = txtreport.Value + 1

End

Public Sub btnminus_Click()

  txtreport.Value = txtreport.Value - 1

End
