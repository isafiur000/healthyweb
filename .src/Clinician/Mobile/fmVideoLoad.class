' Gambas class file

Private $strType As String
Private $encid As String

Public Sub _new(strType As String, encid As String)

  $strType = strType
  $encid = encid
  modGeneralMain.GetOpenModalForm(Me)
  Me.Title = $strType & Space(1) & "FILES"

End

Private Sub DisplaySelectImg(sPath As String)

  If sPath Then
    If $strType = "VIDEO" Then
      MediaView1.Tag = sPath
      MediaView1.Video = modPrint.GetFileWebURL(sPath)
    Endif
  Endif

End

Public Sub btnOK_Click()

  Dim res As Result
  Dim sPath As String
  Dim xblobfile As String
  Dim xIntVal As String

  If txttitle.Text And If MediaView1.Tag Then
    If $strType = "VIDEO" Then
      sPath = MediaView1.Tag
    Endif

    If Exist(sPath) Then
      If MMain.$WebEntry = True Then
        xblobfile = modFTPSub.SendBlobToFTP(sPath, $encid, "Patient")
      Else
        If modMisc.BlobTarget("Patient") = "FTP" Then
          xblobfile = modFTPSub.SendBlobToFTP(sPath, $encid, "Patient")
        Else
          xblobfile = File.Load(sPath)
        Endif
      Endif

      If xblobfile Then
        res = modDatabase.$myConn.Create("tblpatimagedata")
        res["fldcateg"] = $strType
        res["fldencounterval"] = $encid
        If MMain.$SISHAppMode = "Portal" Then
          res["fldtitle"] = "[PATIENT] " & Trim(txttitle.Text)
        Else
          res["fldtitle"] = Trim(txttitle.Text)
        Endif
        If modMisc.BlobTarget("Patient") = "FTP" Then
          res["fldpic"] = ""
          res["fldlink"] = xblobfile
        Else
          res["fldpic"] = xblobfile
          res["fldlink"] = ""
        Endif
        res["fldkeyword"] = ""
        res["flddetail"] = txtdetail.Text
        res["fldhashcode"] = ""
        res["flduserid"] = modBasic.$lbluser
        res["fldtime"] = Now()
        res["fldcomp"] = modBasic.$compID
        res["fldsave"] = True
        res["flduptime"] = ""
        If MMain.$WebEntry = True Then
          xIntVal = modString.GetDateString(Now())
          res["fldid"] = CLong(xIntVal)
          res["fldrepoid"] = modMisc.GetWebIndexStr(xIntVal)
          res["fldrepodate"] = Now()
          res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
          res["fldhospcode"] = modBasic.$HospCode
        Endif
        res.Update()

        Me.Exec(Subst("Toastify({text: '&1', duration: &2}).showToast()", "Information saved", modBasic.$BalloonDelay))
        txttitle.Text = ""
        txtdetail.Text = ""
      Endif
    Endif

  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub btnOpen_Click()

  btnOpen.Upload()

End

Public Sub btnOpen_Progress()

  WebProgressBar1.Value = btnOpen.Progress

End

Public Sub btnOpen_Finish()

  Dim xPath As String

  Me.Exec("Toastify({text: 'Upload complete', duration: 3000}).showToast()")
  xPath = modPrint.GetCopyTempPath(btnOpen.Path)
  DisplaySelectImg(xPath)

End

Public Sub btnOpen_Abort(Reason As String)

  Message.Error("Unable to upload file.<br>Reason: " & Html(Reason))
  WebProgressBar1.Value = 0

End
