' Gambas class file

Private $sType As String
Private $itemList As String[]

Public Sub _new(sType As String, sVarList As String[], sItemList As String[])

  $sType = sType
  ListBox1.List = sVarList
  $itemList = sItemList

  Me.Title = sType

End

Public Sub ListBox1_Select()

  Dim res As Result

  ListBox2.Clear()
  If ListBox1.Text Then

    If MMain.$WebEntry = True Then
      If modBasic.$HospCode Then
        res = modDatabase.$myConn.Exec("select fldvalue as col from tblhmissetting where fldtype=&1 and fldcategory=&2 and fldhospcode like &3", $sType, ListBox1.Text, modBasic.$HospCode)
      Else
        res = modDatabase.$myConn.Exec("select DISTINCT(fldvalue) as col from tblhmissetting where fldtype=&1 and fldcategory=&2", $sType, ListBox1.Text)
      Endif
    Else
      res = modDatabase.$myConn.Exec("select fldvalue as col from tblhmissetting where fldtype=&1 and fldcategory=&2", $sType, ListBox1.Text)
    Endif
    ListBox2.List = modControlSub.GetDirectFillresult(res)

  Endif

End

Public Sub mnudelete_Click()

  If ListBox1.Text Then

    If MMain.$WebEntry = True Then
      If modBasic.$HospCode Then
        modDatabase.$myConn.Delete("tblhmissetting", "fldtype=&1 and fldcategory=&2 and fldvalue=&3 and fldhospcode like &4", $sType, ListBox1.Text, ListBox2.Text, modBasic.$HospCode)
      Endif
    Else
      modDatabase.$myConn.Delete("tblhmissetting", "fldtype=&1 and fldcategory=&2 and fldvalue=&3", $sType, ListBox1.Text, ListBox2.Text)
    Endif
    ListBox1_Select()

  Endif

End

Public Sub btnsave_Click()

  Dim res As Result
  Dim res1 As Result
  Dim xval As String
  Dim xIntVal As String

  If ListBox1.Text Then

    If MMain.$WebEntry = True Then
      If modBasic.$HospCode Then
        res1 = modDatabase.$myConn.Exec("select fldrepoid from tblhmissetting where fldtype=&1 and fldcategory=&2 and fldhospcode like &3", $sType, ListBox1.Text, modBasic.$HospCode)
      Endif
    Else
      res1 = modDatabase.$myConn.Exec("select fldid from tblhmissetting where fldtype=&1 and fldcategory=&2", $sType, ListBox1.Text)
    Endif
    If res1.Available Then
      For Each res1
        If MMain.$WebEntry = True And If modBasic.$HospCode Then
          modDatabase.$myConn.Delete("tblhmissetting", "fldrepoid=&1", res1["fldrepoid"])
        Else
          modDatabase.$myConn.Delete("tblhmissetting", "fldid=&1", res1["fldid"])
        Endif
      Next
    Endif
    For Each xval In ListBox2.List
      res = modDatabase.$myConn.Create("tblhmissetting")
      res["fldtype"] = $sType
      res["fldcategory"] = ListBox1.Text
      res["fldvalue"] = xval
      res["fldvarval"] = ""
      res["fldtime"] = Now()
      res["flduserid"] = modBasic.$lbluser
      res["xyz"] = False
      If MMain.$WebEntry = True Then
        xIntVal = modString.GetDateString(Now())
        res["fldid"] = CLong(xIntVal)
        res["fldrepoid"] = modApplication.GetMD5SumString(xval & ":" & xIntVal & ":" & Session.Id)
        res["fldrepodate"] = Now()
        res["fldrepomac"] = CGI["REMOTE_ADDR"] & ":" & CGI["REMOTE_PORT"]
        res["fldhospcode"] = modBasic.$HospCode
      Endif
      res.Update
    Next

    Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")
  Endif

End

Public Sub btnclear_Click()

  ListBox2.Clear()

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub btnaddinstr_Click()

  Dim aList As String[]
  Dim xx As String[]
  Dim sItem As String

  If ListBox1.Text Then
    aList = GetItemList()
    If aList.Count Then
      xx = GridListView("Select Components for " & ListBox1.Text, aList)
      If xx Then
        For Each sItem In xx
          ListBox2.Add(sItem)
        Next
      Endif
    Endif
  Endif

End

Private Function GetItemList() As String[]

  Dim xListAll As String[]
  Dim xList As String[]
  Dim xVaritem As String
  Dim res As Result

  xListAll = New String[]
  For Each xVaritem In ListBox1.List

    If MMain.$WebEntry = True Then
      If modBasic.$HospCode Then
        res = modDatabase.$myConn.Exec("select fldvalue as col from tblhmissetting where fldtype=&1 and fldcategory=&2 and fldhospcode like &3", $sType, xVaritem, modBasic.$HospCode)
      Endif
    Else
      res = modDatabase.$myConn.Exec("select fldvalue as col from tblhmissetting where fldtype=&1 and fldcategory=&2", $sType, xVaritem)
    Endif
    xListAll.Insert(modControlSub.GetDirectFillresult(res))

  Next

  xList = modString.GetRemainingArray($itemList, xListAll)
  Return xList

End

Public Sub btnmanual_Click()

  If txtmanual.Text Then
    ListBox2.Add(Trim(txtmanual.Text))
  Endif

End
