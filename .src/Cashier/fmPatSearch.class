' Gambas class file

Private $rData As Result
Private $aMyFields As String[]
Private $sType As String

Private $sValue As String

Private $con As Connection
Private $tblpatientinfo As String
Private $fldpatientval As String
Private $fldptnamefir As String
Private $fldptnamelast As String
Private $fldptsex As String
Private $fldptaddvill As String
Private $fldptadddist As String
Private $fldptcontact As String
Private $fldptbirday As String
Private $fldadmitfile As String
Private $fldptguardian As String
Private $fldrelation As String

Public Sub Run(sType As String) As String

  $sType = sType

  If MMain.$SISHAppMode = "HMIS" Then
    cmbsex.List = ["Female"]
    cmbsex.Text = "Female"
  Else
    cmbsex.List = ["Female", "Male", "Other"]
    cmbsex.Text = ""
  Endif
  rblocal.Value = True  ''Disabled due to bug in 3.18.2
  LoadLocalVaribles()

  If Me.ShowModal() Then Return $sValue

End

Private Sub LoadLocalVaribles()

  $con = modDatabase.$myConn
  $tblpatientinfo = "tblpatientinfo"
  $fldpatientval = "fldpatientval"
  $fldptnamefir = "fldptnamefir"
  $fldptnamelast = "fldptnamelast"
  $fldptsex = "fldptsex"
  $fldptaddvill = "fldptaddvill"
  $fldptadddist = "fldptadddist"
  $fldptcontact = "fldptcontact"
  $fldptbirday = "fldptbirday"
  $fldadmitfile = "fldadmitfile"
  $fldptguardian = "fldptguardian"
  $fldrelation = "fldrelation"

End

Private Sub LoadRemoteVaribles()

  Dim scon As Connection
  Dim xservice As String
  Dim xserver As String
  Dim xdatabase As String
  Dim xuser As String
  Dim xpass As String

  xservice = modSettings.ShowExtSettingFromFIle("Database/Service")
  xserver = modSettings.ShowExtSettingFromFIle("Database/Server")
  xdatabase = modSettings.ShowExtSettingFromFIle("Database/DB")
  xuser = modSettings.ShowExtSettingFromFIle("Database/User")
  xpass = modPassword.DecryptSimple(modSettings.ShowExtSettingFromFIle("Database/Password"))

  If xservice And If xserver And If xdatabase And If xuser Then
    scon = modDBApp.OpenConnection(xservice, xserver, xdatabase, xuser, xpass)                        ''
    If scon.Opened = False Then
      Message.Warning(("Unable to connect to extra database"), ("OK"))

    Else If scon.Opened = True Then
      $tblpatientinfo = modSettings.ShowExtSettingFromFIle("PatientTable/TableName")
      $fldpatientval = modSettings.ShowExtSettingFromFIle("PatientTable/PatientNo")
      $fldptnamefir = modSettings.ShowExtSettingFromFIle("PatientTable/Name")
      $fldptnamelast = modSettings.ShowExtSettingFromFIle("PatientTable/SurName")
      $fldptsex = modSettings.ShowExtSettingFromFIle("PatientTable/Sex")
      $fldptaddvill = modSettings.ShowExtSettingFromFIle("PatientTable/Address")
      $fldptadddist = modSettings.ShowExtSettingFromFIle("PatientTable/District")
      $fldptcontact = modSettings.ShowExtSettingFromFIle("PatientTable/Contact")
      $fldptbirday = modSettings.ShowExtSettingFromFIle("PatientTable/DOB")
      $fldadmitfile = modSettings.ShowExtSettingFromFIle("PatientTable/DiaryNo")
      $fldptguardian = modSettings.ShowExtSettingFromFIle("PatientTable/Guardian")
      $fldrelation = modSettings.ShowExtSettingFromFIle("PatientTable/Relation")
      $con = scon
      If $tblpatientinfo And If $fldpatientval And If $fldptnamefir And If $fldptnamelast And If $fldptsex And If $fldptaddvill And If $fldptadddist And If $fldptcontact And If $fldptbirday And If $fldadmitfile And If $fldptguardian And If $fldrelation Then
      Else
        LoadLocalVaribles()
        Message.Warning(("Incomplete table structures"), ("OK"))
      Endif

    Endif
  Endif

End

Public Sub btnsearch_Click()

  FillRegistrationGrid()

End

Private Sub FillRegistrationGrid()

  Dim sql As String
  Dim namefir As String
  Dim namelast As String
  Dim addfir As String
  Dim addlast As String
  Dim contact As String
  Dim sex As String

  Dim xstr1 As String
  Dim xstr2 As String
  Dim xstr3 As String
  Dim xstr4 As String
  Dim xstr5 As String
  Dim xstr6 As String

  If cmbsex.Text Then
    xstr1 = "lower(" & $fldptsex & ") like &1"
    sex = LCase(cmbsex.Text) & "%"
  Else
    xstr1 = "(" & $fldptsex & " like &1 or " & $fldptsex & " IS &7)"
    sex = "%"
  Endif

  If txtnamefir.Text Then
    xstr2 = "lower(" & $fldptnamefir & ") like &2"
    namefir = LCase(Trim(txtnamefir.Text)) & "%"
  Else
    xstr2 = "(" & $fldptnamefir & " like &2 or " & $fldptnamefir & " IS &7)"
    namefir = "%"
  Endif

  If txtnamelast.Text Then
    xstr3 = "lower(" & $fldptnamelast & ") like &3"
    namelast = LCase(Trim(txtnamelast.Text)) & "%"
  Else
    xstr3 = "(" & $fldptnamelast & " like &3 or " & $fldptnamelast & " IS &7)"
    namelast = "%"
  Endif

  If txtaddfir.Text Then
    xstr4 = "lower(" & $fldptaddvill & ") like &4"
    addfir = LCase(Trim(txtaddfir.Text)) & "%"
  Else
    xstr4 = "(" & $fldptaddvill & " like &4 or " & $fldptaddvill & " IS &7)"
    addfir = "%"
  Endif

  If cmbdistrict.Text Then
    xstr5 = "lower(" & $fldptadddist & ") like &5"
    addlast = LCase(Trim(cmbdistrict.Text)) & "%"
  Else
    xstr5 = "(" & $fldptadddist & " like &5 or " & $fldptadddist & " IS &7)"
    addlast = "%"
  Endif

  If txtcontact.Text Then
    xstr6 = "lower(" & $fldptcontact & ") like &6"
    contact = LCase(Trim(txtcontact.Text)) & "%"
  Else
    xstr6 = "(" & $fldptcontact & " like &6 or " & $fldptcontact & " IS &7)"
    contact = "%"
  Endif

  sql = Subst("select &1,&2,&3,&4,&5,&6,&7,&8,&9,&{10},&{11} from &{12}", $fldpatientval, $fldadmitfile, $fldptnamefir, $fldptnamelast, $fldptsex, $fldptbirday, $fldptaddvill, $fldptadddist, $fldptcontact, $fldptguardian, $fldrelation, $tblpatientinfo)
  sql = Subst(sql & " where &1 and &2 and &3 and &4 and &5 and &6", xstr1, xstr2, xstr3, xstr4, xstr5, xstr6)                                           ''
  $rData = $con.Exec(sql, sex, namefir, namelast, addfir, addlast, contact, Null)
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)
  With GridView1
    .Columns[0].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[1].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[3].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[4].Width = CStr(75 * modBasic.$AppWidthRatio) & "px"
    .Columns[5].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[6].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[7].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[8].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"
    .Columns[9].Width = CStr(150 * modBasic.$AppWidthRatio) & "px"
    .Columns[10].Width = CStr(125 * modBasic.$AppWidthRatio) & "px"

    .Columns[0].Text = "PatientNo"
    .Columns[1].Text = "Diary"
    .Columns[2].Text = "Name"
    .Columns[3].Text = "SurName"
    .Columns[4].Text = "Gender"
    .Columns[5].Text = "CurAge"
    .Columns[6].Text = "Address"
    .Columns[7].Text = "District"
    .Columns[8].Text = "Contact"
    .Columns[9].Text = "Guardian"
    .Columns[10].Text = "Relation"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  If Column = 5 Then
    Data.Text = modDate.GetAgeString($rData[$aMyFields[Column]], Now())
  Else
    Data.Text = $rData[$aMyFields[Column]]
  Endif

End

' Public Sub cmbsex_KeyRelease()
'
'   modFillContainer.AutoFillComboBox(cmbsex)
'   modFillContainer.RestrictComboToContent(cmbsex)
'
' End

Private Sub FillSearchCombo(TextBox1 As WebTextBox, sql As String)

  Dim res As Result

  res = $con.Exec(sql, LCase(TextBox1.Text) & "%")
  If res.Available Then
    TextBox1.Text = GridViewNew("Select", modControlSub.GetDirectFillresult(res), True)
  Endif

End

Public Sub btnselname_Click()

  Dim sql As String

  sql = Subst("select distinct(&1) as col from &2 where lower(&1)", $fldptnamefir, $tblpatientinfo) & " like &1"                                        ''
  FillSearchCombo(txtnamefir, sql)

End

Public Sub btnselsurname_Click()

  Dim sql As String

  sql = Subst("select distinct(&1) as col from &2 where lower(&1)", $fldptnamelast, $tblpatientinfo) & " like &1"                                       ''
  FillSearchCombo(txtnamelast, sql)

End

Public Sub btnseladdress_Click()

  Dim sql As String

  sql = Subst("select distinct(&1) as col from &2 where lower(&1)", $fldptaddvill, $tblpatientinfo) & " like &1"                                        ''
  FillSearchCombo(txtaddfir, sql)

End

Public Sub btnseldistrict_Click()

  Dim sql As String

  sql = Subst("select distinct(&1) as col from &2 where lower(&1)", $fldptadddist, $tblpatientinfo) & " like &1"                                       ''
  FillSearchCombo(cmbdistrict, sql)

End

''------------------------------- Other functions -----------------------------------
Public Sub btnfullrep_Click()

  modCHTMLReport.ExportGridToHTML(GridView1, "REGISTRATION REPORT", "SEARCH REPORT")

End

Public Sub btnsave_Click()

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    If $sType = "PatientNo" Then
      $sValue = $rData[$fldpatientval]
    Else If $sType = "Encounter" Then
      $sValue = modPatient.GetLastEncoutnerFromPatNo($rData[$fldpatientval])
    Endif
    Me.Close(True)
  Endif

End

''---------------------------------- menu ------------------------------------------------
Public Sub mnuphoto_Click()

  Dim hForm As FmPersonImage

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    hForm = New FmPersonImage("Patient", $rData[$fldpatientval], False)
    hForm.ShowModal
  Endif

End

Public Sub mnuenc_Click()

  Dim enc As String

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    enc = modPatient.GetLastEncoutnerFromPatNo($rData[$fldpatientval])
    Message.Info(("Last Encounter: ") & CStr(enc) & gb.NewLine & ("Last Visit: ") & modReportVar.GetDateTimeReport(modPatient.GetRecordDate(enc), gb.MediumDate), ("OK"))
  Endif

End

Public Sub mnualenct_Click()

  Dim res As Result
  Dim hForm As FRequest

  If GridView1.Selection.Count Then
    $rData.MoveTo(GridView1.Selection[0])
    res = modDatabase.$myConn.Exec("select fldencounterval,fldadmitlocat,fldregdate,fldadmission,flduserid from tblencounter where fldpatientval=&1", $rData[$fldpatientval])
    If res.Available Then
      hForm = New FRequest(res, "Patient No: " & $rData[$fldpatientval], "PatientSearch", False)
      hForm.ShowModal
    Endif
  Endif

End

''--------------------------------- variables -------------------------------------
Public Sub btnconnect_Click()

  If rblocal.Value = True Then
    LoadLocalVaribles()
  Else If rbremote.Value = True Then
    LoadRemoteVaribles()
  Endif

End

Public Sub btnclose_Click()

  Me.Close()

End
