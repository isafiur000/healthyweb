' Gambas class file

Private $rData As Result
Private $aMyFields As String[]

Private $sValue As Variant[]

Public Sub Run(sTitle As String) As Variant[]

  Me.Title = sTitle
  modGeneralMain.GetOpenModalForm(Me)
  chkleftmain.Value = modBasic.$SearchBothSide
  modSettings.ShowCheckBox(chkgroup, "ClinicForms/ICDGroups")
  chkboth.Value = True

  $sValue = New Variant[]
  FillICDGrid()
  If modBasic.$TabletModeEnable = "Yes" Then
  Else
    txtname.SetFocus
  Endif

  If Me.ShowModal() Then Return $sValue

End

Public Sub chkgroup_Click()

  modSettings.EnterCheckSetting(chkgroup, "ClinicForms/ICDGroups")

End

Public Sub chkleftmain_Click()

  modBasic.$SearchBothSide = chkleftmain.Value
  txtname.SetFocus

End

Public Sub FillICDGrid()

  Dim xstr As String
  Dim xgrp As String

  If chkboth.Value = True Then
    xstr = " and fldoldcode IS NOT NULL"
  Else
    xstr = ""
  Endif
  If chkgroup.Value = True Then
    xgrp = " and fldicdcode NOT LIKE &2"
  Else
    xgrp = ""
  Endif

  If txtcode.Text Then
    $rData = modDatabase.$icdConn.Exec("select fldicdnme,fldicdcode,fldoldcode from tblicddisease where lower(fldicdcode) like &1" & xstr & xgrp, LCase(txtcode.Text) & "%", "%.%")
  Else If txtname.Text Then
    If chkleftmain.Value = True Then
      $rData = modDatabase.$icdConn.Exec("select fldicdnme,fldicdcode,fldoldcode from tblicddisease where lower(fldicdnme) like &1" & xstr & xgrp, "%" & LCase(txtname.Text) & "%", "%.%")
    Else
      $rData = modDatabase.$icdConn.Exec("select fldicdnme,fldicdcode,fldoldcode from tblicddisease where lower(fldicdnme) like &1" & xstr & xgrp, LCase(txtname.Text) & "%", "%.%")
    Endif

  Else
    If chkgroup.Value = True Then
      If chkboth.Value = True Then
        $rData = modDatabase.$icdConn.Exec("select fldicdnme,fldicdcode,fldoldcode from tblicddisease where fldoldcode IS NOT NULL and fldicdcode NOT LIKE &1", "%.%")
      Else
        $rData = modDatabase.$icdConn.Exec("select fldicdnme,fldicdcode,fldoldcode from tblicddisease where fldicdcode NOT LIKE &1", "%.%")
      Endif
    Else
      If chkboth.Value = True Then
        $rData = modDatabase.$icdConn.Exec("select fldicdnme,fldicdcode,fldoldcode from tblicddisease where fldoldcode IS NOT NULL")
      Else
        $rData = modDatabase.$icdConn.Exec("select fldicdnme,fldicdcode,fldoldcode from tblicddisease")
      Endif
    Endif

  Endif
  $aMyFields = New String[]
  modGridView.ReadSmallData(GridView1, $rData, $aMyFields)

  With GridView1
    .Columns[0].Expand = True
    .Columns[1].Width = CStr(100 * modBasic.$AppWidthRatio) & "px"
    .Columns[2].Hidden = True

    .Columns[0].Text = "Particulars"
    .Columns[1].Text = "Code"
  End With

End

Public Sub GridView1_Data(Row As Integer, Column As Integer, Data As WebTableData)

  $rData.MoveTo(Row)
  modGridView.GridViewDecoration(Data, Row)
  Data.Text = $rData[$aMyFields[Column]]

End

Public Sub txtname_Change()

  txtcode.Text = ""
  If txtname.Text Then
    FillICDGrid()
  Endif

End

Public Sub txtcode_Change()

  txtname.Text = ""
  If txtcode.Text Then
    FillICDGrid()
  Endif

End

Public Sub txtname_Click()

  txtname.Text = ""
  FillICDGrid()

End

Public Sub btninsert_Click()

  Dim Row As Integer
  Dim xxx As String[]

  For Row = 0 To GridView1.Count - 1
    If GridView1.IsSelected(Row) = True Then
      $rData.MoveTo(Row)
      xxx = New String[]
      xxx.Add($rData["fldicdnme"])
      xxx.Add($rData["fldicdcode"])
      xxx.Add($rData["fldoldcode"])
      $sValue.Add(xxx)
    Endif
  Next
  Me.Exec("Toastify({text: 'Information saved', duration: 3000}).showToast()")

End

Public Sub btnok_Click()

  Me.Close(True)

End

Public Sub btnclose_Click()

  Me.Close()

End

Public Sub chkall_Click()

  If chkall.Value = True Then
    GridView1.SelectAll()
  Else If chkall.Value = False Then
    GridView1.UnselectAll()
  Endif

End
